NEW
  AUTO 3,1
*/--------------------------------------
* # Socket
* ## C / CSH
* `#include <sys/socket.h>`
* `int socket(int socket_family, int socket_type, int protocol);`
* ## ASM
* `>SS`
* `>PUSHWI socket_family`
* `>PUSHWI socket_type`
* `>PUSHWI protocol`
* `>LIBC Socket`
* `>SR`
* ## RETURN VALUE
*  CC = success
*  YA = sockfd
*\--------------------------------------
K.Socket		ldx #0

				ldy #4					AF_
				jsr RAMSW.GetStkYW

				bra K.2KMY
*/--------------------------------------
* # Bind
* ## C / CSH
* `#include <sys/socket.h>`
* `int bind(int socket, const struct sockaddr *address, socklen_t address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW address`
* `>PUSHWI address_len`
* `>LIBC Bind`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Bind			ldx #2
				ldy #4					socket
				bra K.2KM
*/--------------------------------------
* # Connect
* ## C / CSH
* `#include <sys/socket.h>`
* `int connect(int socket, const struct sockaddr *address, socklen_t address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW address`
* `>PUSHWI address_len`
* `>LIBC Connect`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Connect		ldx #4
				ldy #4					socket
				bra K.2KM
*/--------------------------------------
* # Listen
* ## C / CSH
* `#include <sys/socket.h>`
* `int listen(int socket, int backlog);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHWI backlog`
* `>LIBC Listen`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Listen		ldx #6
				ldy #2					socket
				bra K.2KM
*/--------------------------------------
* # Accept
* ## C / CSH
* `#include <sys/socket.h>`
* `int accept(int socket, struct sockaddr *restrict address, socklen_t *restrict address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW address`
* `>PUSHWI address_len`
* `>LIBC Accept`
* `>SR`
* ## RETURN VALUE
*  CC = success
*  YA = hFD
*  CS = error
*  YA = -1 (ERRNO)
*\--------------------------------------
K.Accept		ldx #8
				ldy #4					socket
				bra K.2KM
*/--------------------------------------
* # Shutdown
* ## C / CSH
* `#include <sys/socket.h>`
* `int shutdown(int socket, int how);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHWI how`
* `>LIBC Shutdown`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Shutdown		ldx #10
				ldy #2					socket
				bra K.2KM
*/--------------------------------------
* # Recv
* ## C / CSH
* `#include <sys/socket.h>`
* `ssize_t recv(int socket, void *buffer, size_t length, int flags);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW buffer`
* `>PUSHW length`
* `>PUSHW flags`
* `>LIBC Recv`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Recv			ldx #12
				ldy #6					socket
				bra K.2KM
*/--------------------------------------
* # RecvFrom
* ## C / CSH
* `#include <sys/socket.h>`
* `ssize_t recvfrom(int socket, void *restrict buffer, size_t length,`
* `					int flags, struct sockaddr *restrict address,`
* `					socklen_t *restrict address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW buffer`
* `>PUSHWI length`
* `>PUSHWI flags`
* `>PUSHW dest_addr`
* `>PUSHWI address_len`
* `>LIBC RecvFrom`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.RecvFrom		ldx #14
				ldy #6					socket
				bra K.2KM

K.RecvMsg		ldx #16
				ldy #6					socket
				bra K.2KM
*/--------------------------------------
* # Send
* ## C / CSH
* `#include <sys/socket.h>`
* `ssize_t send(int socket, const void *buffer, size_t length, int flags);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW message`
* `>PUSHW length`
* `>PUSHW flags`
* `>LIBC Send`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.Send			ldx #18
				ldy #6					socket
				bra K.2KM

K.SendMsg		ldx #20
				ldy #6					socket
				bra K.2KM
*/--------------------------------------
* # SendTo
* ## C / CSH
* `#include <sys/socket.h>`
* `ssize_t sendto(int socket, const void *message, size_t length,`
* `				  int flags, const struct sockaddr *dest_addr,`
* `				  socklen_t dest_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW message`
* `>PUSHWI length`
* `>PUSHWI flags`
* `>PUSHW dest_addr`
* `>PUSHWI address_len`
* `>LIBC SendTo`
* `>SR`
* ## RETURN VALUE
*  CC = success
*\--------------------------------------
K.SendTo		ldx #22
				ldy #10					socket
				bra K.2KM
*/--------------------------------------
* # GetPeerName
* ## C / CSH
* `#include <sys/socket.h>`
* `int getpeername(int socket, struct sockaddr *restrict address, socklen_t *restrict address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW address`
* `>PUSHWI address_len`
* `>LIBC GetPeerName`
* `>SR`
* ## RETURN VALUE
*  CC = success
*  CS = error
*\--------------------------------------
K.GetPeerName	ldx #24
				ldy #4					socket
				bra K.2KM
*/--------------------------------------
* # GetSockName
* ## C / CSH
* `#include <sys/socket.h>`
* `int getsockname(int socket, struct sockaddr *restrict address, socklen_t *restrict address_len);`
* ## ASM
* `>SS`
* `>PUSHW socket`
* `>PUSHW address`
* `>PUSHWI address_len`
* `>LIBC GetSockName`
* `>SR`
* ## RETURN VALUE
*  CC = success
*  CS = error
*\--------------------------------------
K.GetSockName	ldx #26
				ldy #4					socket
				bra K.2KM

K.GetSockOpt	ldx #28
				ldy #10					socket
				bra K.2KM

K.SetSockOpt	ldx #30
				ldy #10					socket
*--------------------------------------
K.2KM			jsr RAMSW.GetStkYW		Y = hFD

				lda pFDs+1,y
				beq K.2KM.9

				sta pFD+1
				lda pFDs,y
				sta pFD

				lda (pFD)				#S.FD.T
				cmp #S.FD.T.DSOCK
				beq K.2KM.FD

				cmp #S.FD.T.SSOCK
				bne K.2KM.9

K.2KM.FD		ldy #S.FD.SOCK.AF
				lda (pFD),y
				tay

K.2KMY			lda A2osX.pAFs+1,y		Y = AF_
				beq K.2KM.9

				pha

				lda A2osX.pAFs,y
				pha

				rts

K.2KM.9			lda #E.UNSUP
				sec
				rts
*--------------------------------------
K.2KMs			stx .2+1
				sty .3+1
				sta .4+1

				ldx #0

.1				lda A2osX.pAFs+1,x
				beq .7

				sta .5+2
				lda A2osX.pAFs,x
				sta .5+1

				phx

.2				ldx #$ff				SELF MODIFIED
.3				ldy #$ff				SELF MODIFIED
.4				lda #$ff				SELF MODIFIED

.5				jsr $FFFF				SELF MODIFIED

				plx

.7				inx						next AF_...
				inx
				cpx #8
				bcc .1

.9				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.socket
LOAD usr/src/sys/kernel.s
ASM
