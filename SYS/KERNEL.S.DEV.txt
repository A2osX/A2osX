NEW
PREFIX /A2OSX.BUILD
AUTO 4,1
*/--------------------------------------
* # GetDevByID.A
* ## IN: 
*  A = DevID
* ## OUT:
*  CC = OK, CS = ERROR
*  Y,A = DEVSLOT
*  note: X Unmodified
*\--------------------------------------
K.GetDevByID.A	cmp DevMgr.Count
				bcs K.GetDevDNF

				asl
				asl
				asl
				asl
				php
				clc
				adc	#Dev.Table
				tay
				
				plp
				lda #0
				adc /Dev.Table
				rts						CC		
*/--------------------------------------
* # GetDevByName.YA
* ## IN: 
*  Y,A = Ptr to device name (C-String)
* ## OUT:
*  CC = OK, CS = ERROR
*  X = DEVID
*  Y,A = DEVSLOT
*\--------------------------------------
K.GetDevByName.YA
				>STYA ZPPtr1
				>LDYAI Dev.Table+S.DEV.NAME
				>STYA ZPPtr2
				
				ldx #0
				
.1				ldy #$ff
				
.2				iny
				lda (ZPPtr1),y
				cmp (ZPPtr2),y
				bne .3

				ora (ZPPtr2),y 			Both char are 0 ?
				bne .2					no....loop
				
				lda ZPPtr2
				sec
				sbc #S.DEV.NAME
				tay
				lda ZPPtr2+1
				sbc /S.DEV.NAME
				clc
				rts
				
.3				lda ZPPtr2
				clc
				adc #S.DEV
				sta ZPPtr2
				bcc .4
				
				inc ZPPtr2+1
				
.4				inx
				cpx DevMgr.Count
				bne .1

K.GetDevDNF		lda #MLI.E.NODEV		CS from cpx/beq
				rts
*--------------------------------------
MAN
SAVE /A2OSX.SRC/SYS/KERNEL.S.DEV
LOAD /A2OSX.SRC/SYS/KERNEL.S
ASM
