NEW
  AUTO 3,1
*--------------------------------------
* DRV.Disk][
* X = IOCTL
* Y,A = PTR to S.IOCTL
*--------------------------------------
DRV.D2			cpx #IOCTL.FORMAT
				.HS 2C					BIT ABS
DRV.BLK			cpx #IOCTL.CONTROL
				bcs .9

				jmp (.1,x)

.1				.DA DRV.BLK.S			STATUS
				.DA DRV.GBLK			READBLOCK
				.DA DRV.GBLK			WRITEBLOCK
				.DA DRV.GBLK			FORMAT

.9				lda #MLI.E.BADCALL
*				sec
DRV.RTS			rts
*--------------------------------------
DRV.BLK.S		ldy #S.FD.DEV.SIZE
				lda (pFD),y
				tax

				iny
				lda (pFD),y
				tay
				bne .8

				txa
				bne .8

*				ldx #IOCTL.STATUS

				jsr DRV.GBLK
				bcs .9

.8				stx DRV.BLK.DIB+S.DIB.SIZE
				sty DRV.BLK.DIB+S.DIB.SIZE+1

				ldy #S.FD.DEV.T
				lda (pFD),y
				tay

				lda DRV.IDS,y
				tax
				lda DRV.IDS+1,y
				sta DRV.BLK.DIB+S.DIB.T

				ldy #0

.1				lda DRV.D2.IDS,x
				beq .2

				sta DRV.BLK.DIB+S.DIB.IDS+1,y
				inx
				iny
				bra .1

.2				sty DRV.BLK.DIB+S.DIB.IDS

				lda #C.SPACE

.3				cpy #16
				beq .4

				sta DRV.BLK.DIB+S.DIB.IDS+1,y
				iny
				bra .3

.4				>LDYAI DRV.BLK.DIB
				jmp DRV.STATUS.YA

.9				rts
*--------------------------------------
DRV.BLK.DIB		.DA #S.DIB.S.BLOCK+S.DIB.S.WRITE+S.DIB.S.READ
				.BS 3
				.BS 17					IDString
				.DA #S.DIB.T.BLK
				.DA #0
				.DA K.VER
*--------------------------------------
DRV.IDS			.DA #0,#S.DIB.T.D2
				.DA #DRV.RAM.IDS-DRV.D2.IDS,#S.DIB.T.BLK
				.DA #DRV.USER.IDS-DRV.D2.IDS,#S.DIB.T.BLK
				.DA #DRV.BLK.IDS-DRV.D2.IDS,#S.DIB.T.BLK
				.DA #DRV.RBLK.IDS-DRV.D2.IDS,#S.DIB.T.BLK
				.DA #0,#0				Smartport
				.DA #DRV.RSP.IDS-DRV.D2.IDS,#S.DIB.T.BLK
*--------------------------------------
DRV.D2.IDS		.CZ "Disk ][ 5.25"
DRV.RAM.IDS		.CZ "ProDOS /RAM Disk"
DRV.USER.IDS	.CZ "User Block Dev"
DRV.BLK.IDS		.CZ "ProDOS Block Dev"
DRV.RBLK.IDS	.CZ "Remapped BLK Dev"
DRV.RSP.IDS		.CZ "Remapped SP Dev"
*--------------------------------------
* IOCTL to BLK protocol converter
*--------------------------------------
DRV.GBLK		txa
				lsr
				sta ZP.CMDNUM

				ldy #S.FD.DEV.DEVID		Drive 1, or 2
				lda (pFD),y
				dec						O or 1
				lsr						in Carry
				php
				ldy #S.FD.DEV.BUSID
				lda (pFD),y
				asl
				asl
				asl
				asl
				asl
				plp
				ror						DSSS0000

				sta ZP.UNITNUM

				ldx #3

.1				lda K.S.IOCTL+S.IOCTL.BUFPTR,x
				sta ZP.BUFPTR,x
				dex
				bpl .1

				ldy #S.FD.DEV.pBUS
				lda (pFD),y
				sta .71+1
				iny
				lda (pFD),y
				sta .71+2

				ldx #9

.2				lda .70,x
				sta $110,x
				dex
				bpl .2

				>LDYAI $110
				>STYA RAMSW.2AUX

				jmp RAMSW.2AUXJSR
*--------------------------------------
.70				bit IO.RRAMWRAMBNK1		if Disk ][ at $D000
.71				jsr $FFFF
				bit IO.RRAMWRAMBNK2
				rts
*--------------------------------------
* Smartport	Driver
*--------------------------------------
DRV.SP			txa
				lsr
				sta .2
				tax
				lda DRV.SP.PCnt,x
				sta K.S.IOCTL+S.IOCTL.CNT

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				sta K.S.IOCTL+S.IOCTL.UNITNUM

				ldy #S.FD.DEV.pBUS
				lda (pFD),y
				sta .1+1
				iny
				lda (pFD),y
				sta .1+2

.1				jsr $ffff
.2				.BS 1
				.DA K.S.IOCTL

				rts

DRV.SP.PCnt		.HS 03030301030101010404
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.blk
LOAD usr/src/sys/kernel.s
ASM
