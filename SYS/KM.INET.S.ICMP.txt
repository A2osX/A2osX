NEW
  AUTO 3,1
*--------------------------------------
ICMP.IN			ldy #S.ICMP.TYPE
				lda (pFrameIn),y
				cmp #S.ICMP.TYPE.ECHOREP
				beq ICMP.IN.ECHOREP

				cmp #S.ICMP.TYPE.ECHOREQ
				bne ICMP.IN.EXIT
*--------------------------------------
ICMP.IN.ECHOREQ	ldy #S.IP.DST
				jsr IP.FindIPCFG
				bcs ICMP.IN.EXIT

				jsr ARP.AddFrompFrameIn

				ldy #S.ICMP.TYPE
				lda #S.ICMP.TYPE.ECHOREP
				sta (pFrameIn),y

				ldx #3

.2				lda pFrameIn,x
				sta pFrameOut,x
				dex
				bpl .2

				ldx #3
				ldy #S.IP.DST+3

.3				lda ARP.CACHE+S.ARPCACHE.IP,x
				sta (pFrameOut),y
				dey
				dex
				bpl .3

				clc						Queue if fail
				jmp FRM.SendIP

ICMP.IN.EXIT	jmp FRM.DiscardIn
*--------------------------------------
ICMP.IN.ECHOREP	jsr FRM.GetAddr

				ldy #S.ICMP.IDENTIFIER
				lda (pFrameIn),y
				sta FRM.SA.SRC+S.SA.IN.PORT+1
				sta FRM.SA.DST+S.SA.IN.PORT+1
				iny
				lda (pFrameIn),y
				sta FRM.SA.SRC+S.SA.IN.PORT
				sta FRM.SA.DST+S.SA.IN.PORT

				jsr SKT.Find
				bcs ICMP.IN.EXIT

				ldy #S.FD.SOCK.P
				lda (pSKT),y
				cmp #S.IP.PROTOCOL.ICMP
				bne ICMP.IN.EXIT

				jsr SKT.AddFrameInToQ
				bcs ICMP.IN.EXIT		Q full, discard...

				rts
*--------------------------------------
MAN
SAVE usr/src/sys/km.inet.s.icmp
LOAD usr/src/sys/km.inet.s
ASM
