NEW
  AUTO 3,1
*/--------------------------------------
* # Time
* Get System Time in Buffer
* ## C
* `void time (struct tm* timeptr);`
* ## ASM
* `>LDYA timeptr`
* `>LIBC time`
* ## RETURN VALUE
* S.TIME filled with System date/time
*\--------------------------------------
K.Time			>STYA FORPNT			timeptr

				>MLICALL MLI.GETTIME

				ldy #3

.1				lda MLI.DATE,y
				sta TIMEX.PT,y
				dey
				bpl .1

				bra TIME.PT2ST
*/--------------------------------------
* # PTime2Time
*  Convert ProDOS Time To S.TIME
* ## C
* `void PTime2Time (long ptime, const struct tm* timeptr );`
* ## ASM
* `>SS`
* `>PUSHL ptime`
* `>PUSHW timeptr`
* `>LIBC PTime2Time`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.PTime2Time	ldy #0
				jsr RAMSW.StkY2FORPNT	timeptr

				ldy #2					ptime

.1				jsr RAMSW.GetStkY
				sta TIMEX.PT-2,y
				iny
				cpy #6
				bcc .1

				bra TIME.PT2ST
*/--------------------------------------
* # CTime2Time
*  Convert CTime Time To S.TIME
* ## C
* `void CTime2Time (long ctime, const struct tm* timeptr );`
* ## ASM
* `>SS`
* `>PUSHL ctime`
* `>PUSHW timeptr`
* `>LIBC CTime2Time`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.CTime2Time	ldy #0
				jsr RAMSW.StkY2FORPNT	timeptr

				ldy #2					ptime

.1				jsr RAMSW.GetStkY
				sta ARG32-2,y			ARG = ctime
				iny
				cpy #6
				bcc .1

				ldx #X_CT2ST
				.HS 2C					BIT ABS
TIME.PT2ST		ldx #X_PT2ST

				jsr RAMSW.JMPX

				ldy #S.TIME-1

				sta IO.SETWRITEAUX

.1				lda TIMEX.ST,y
				sta (FORPNT),y
				dey
				bpl .1

				sta IO.CLRWRITEAUX

				rts
*/--------------------------------------
* # StrFTime
* ## C
* Convert S.TIME struct to CSTR
* `void strftime (char* str, const char* format, const struct tm* timeptr );`
* ## ASM
* `>SS`
* `>PUSHW str`
* `>PUSHW format`
* + %a : Abbreviated weekday name : Thu
* + %A : Full weekday name : Thursday
* + %b : Abbreviated month name : Aug
* + %B : Full month name : August
* + %d : Day of the month, zero-padded (01-31)
* + %H : Hour in 24h format (00-23)
* + %I : Hour in 12h format (01-12)
* + %m : Month as a decimal number (01-12)
* + %M : Minute (00-59)
* + %p : AM or PM designation
* + %S : Second (00-61)
* + %w : Weekday as a decimal number with Sunday as 0 (0-6)
* + %y : Year, last two digits (00-99)
* + %Y : Year four digits (2025)
* `>PUSHW timeptr`
* `>LIBC strftime`
* `>SR`
* ## RETURN VALUE
*   none. always succeed.
*\--------------------------------------
K.StrFTime		ldy #0					S.TIME
				jsr RAMSW.StkY2TXTPTR

				ldy #S.TIME-1

.10				jsr RAMSW.xTXTPTRgY
				sta TIMEX.ST,y
				dey
				bpl .10

				ldy #2					format
				jsr RAMSW.StkY2TXTPTR

				ldy #4					str
				jsr RAMSW.StkY2FORPNT

.1				jsr RAMSW.xTXTPTRgn
				beq .8

				cmp #'%'
				beq .2

				jsr X.xFORPNTpn
				bra .1

.2				jsr RAMSW.xTXTPTRgn
				beq .8

				ldx #K.StrFTime.Cnt-1

.3				cmp K.StrFTime.Tbl,x
				beq .4

				dex
				bpl .3

				bra .1

.4				cpx #7
				bcc .5

				jsr K.StrFTime.AtX
				bra .1

.5				txa
				asl
				tax
				jsr .6

				bra .1

.6				jmp (.7,x)

.7				.DA K.StrFTime.A
				.DA K.StrFTime.AA
				.DA K.StrFTime.B
				.DA K.StrFTime.BB
				.DA K.StrFTime.II
				.DA K.StrFTime.P
				.DA K.StrFTime.YY

.8				lda #0					Terminate C string
				jsr X.xFORPNTpn
				clc
				rts
*--------------------------------------
K.StrFTime.Tbl	.AS "aAbBIpYydHSwmM"
K.StrFTime.Cnt	.EQ *-K.StrFTime.Tbl
K.StrFTime.OfsX	.DA #S.TIME.YEAR,#S.TIME.DAY,#S.TIME.HOUR,#S.TIME.SECOND,#S.TIME.WDAY,#S.TIME.MONTH,#S.TIME.MINUTE
*--------------------------------------
K.StrFTime.A	sec						Short day of week, 3 chars...
				.HS	90					BCC
K.StrFTime.AA	clc						full DoW
				lda #TIME.DAY
				ldx /TIME.DAY
				ldy TIMEX.ST+S.TIME.WDAY
				bra K.StrFTime.STR

K.StrFTime.B	sec						Short Month, 3 chars....
				.HS	90					BCC
K.StrFTime.BB	clc						full Month....
				lda #TIME.MON
				ldx /TIME.MON
				ldy TIMEX.ST+S.TIME.MONTH

K.StrFTime.STR	sta ZPPtr4
				stx ZPPtr4+1

				ldx #15
				bcc .10

				ldx #3

.10				tya
				beq .2					Illegal value, print ???

.1				lda (ZPPtr4)
				sec
				adc ZPPtr4
				sta ZPPtr4
				bcc .11

				inc ZPPtr4+1

.11				dey
				bne .1
.2
*				ldy #0					Y is already 0

.3				iny
				lda (ZPPtr4),y
				jsr X.xFORPNTpn
				tya
				cmp (ZPPtr4)
				beq .8

				dex
				bne .3

.8				rts
*--------------------------------------
K.StrFTime.II	lda TIMEX.ST+S.TIME.HOUR
				cmp #12
				bcc K.StrFTime.addDecA

				sbc #12
				bra K.StrFTime.addDecA

K.StrFTime.P	lda TIMEX.ST+S.TIME.HOUR
				cmp #12
				bcc .1

				lda #'p'
				.HS 2C					bit abs
.1				lda #'a'
				jsr X.xFORPNTpn
				lda #'m'
				jmp X.xFORPNTpn
t
K.StrFTime.YY	lda TIMEX.ST+S.TIME.CENTURY
				jsr K.StrFTime.addDecA

				ldx #7					"y"

K.StrFTime.AtX	ldy K.StrFTime.OfsX-7,x
				lda TIMEX.ST,y
*--------------------------------------
K.StrFTime.addDecA
				ldx #$30

.1				cmp #10
				bcc .8

				sbc #10
				inx
				bra .1

.8				ora #$30

				pha
				txa

				jsr X.xFORPNTpn

				pla

				jmp X.xFORPNTpn
*--------------------------------------
TIME.DAY		.PS "???"
				.PS "Monday"
				.PS "Tuesday"
				.PS "Wednesday"
				.PS "Thursday"
				.PS "Friday"
				.PS "Saturday"
				.PS "Sunday"
*--------------------------------------
TIME.MON		.PS "???"
				.PS "January"
				.PS "February"
				.PS "March"
				.PS "April"
				.PS "May"
				.PS "June"
				.PS "July"
				.PS "August"
				.PS "September"
				.PS "October"
				.PS "November"
				.PS "December"
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.time
LOAD usr/src/sys/kernel.s
ASM
