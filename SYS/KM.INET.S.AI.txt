NEW
  AUTO 3,1
*--------------------------------------
* int getaddrinfo(const char *restrict nodename,
* 				  const char *restrict servname,
* 				  const struct addrinfo *restrict hints,
* 				  struct addrinfo **restrict res);
*--------------------------------------
* TXTPTR = hints (or NULL)
*--------------------------------------
AI.GetAddrInfo	ldy #6
				jsr K.GetStkYW			nodename

				>STYA TXTPTR

				lda TXTPTR+1
				beq .9

				jsr DNS.GetName
				bcs .99

				ldx #S.SA.IN-1

.1				stz SA.REMOTE,x
				dex
				bne .1

				lda #AF_INET
				sta SA.REMOTE

				jsr AI.ScanIP
				bcc .4					IP in SA.REMOTE

				jsr DNS.Query
				bcs .99

				ldy #0

.3				lda DNS.CACHE+S.DNSCACHE.IP,x
				sta SA.REMOTE+S.SA.IN.ADDR,y
				inx
				iny
				cpy #4
				bcc .3

.4				ldy #0
				jsr K.GetStkYW			res
				>STYA A1

				>LDYAI S.ADDRINFO+S.SA.IN
				>DAPI Malloc
				bcs .99

				>STYA A2

				sta IO.SETWRITEAUX

				phy
				ldy #1
				sta (A1),y
				pla
				sta (A1)

				ldy #S.ADDRINFO.AF
				lda #AF_INET
				sta (A2),y

				clc
				lda #S.ADDRINFO
				adc A2
				ldy #S.ADDRINFO.pSOCKADDR
				sta (A2),y
				sta A3
				lda /S.ADDRINFO
				adc A2+1
				iny
				sta (A2),y
				sta A3+1

				ldy #S.SA.IN-1

.7				lda SA.REMOTE,y
				sta (A3),y
				dey
				bpl .7

				sta IO.CLRWRITEAUX

				lda #0					exit with int = 0
				tay
				clc
				rts

.9				lda #GAI.E.FAIL
				sec
.99				rts
*--------------------------------------
* void freeaddrinfo(struct addrinfo *ai);
*--------------------------------------
AI.FreeAddrInfo





				clc
				rts
*--------------------------------------
AI.ScanIP		ldx #0
				ldy #0

.2				lda K.Buf512+S.DNS+1,y
				beq .4

				iny

				cmp #'9'+1
				bcs .3

				cmp #'0'
				bcc .3

				and #$0F
				pha

				lda SA.REMOTE+S.SA.IN.ADDR,x
				asl SA.REMOTE+S.SA.IN.ADDR,x	x2
				bcs .9

				asl SA.REMOTE+S.SA.IN.ADDR,x	x4
				bcs .9

*				clc
				adc SA.REMOTE+S.SA.IN.ADDR,x	x5
				bcs .9

				sta SA.REMOTE+S.SA.IN.ADDR,x

				asl SA.REMOTE+S.SA.IN.ADDR,x	x10
				bcs .9

				pla

*				clc
				adc SA.REMOTE+S.SA.IN.ADDR,x
				sta SA.REMOTE+S.SA.IN.ADDR,x
				bra .2

.3				cmp #'.'
				bne .9

*				lda SA.REMOTE+S.SA.IN.ADDR,x
*				beq .9

				inx
				cpx #4
				bcc .2

				lda K.Buf512+S.DNS+1,y
				beq .8

				cmp #C.SPACE
				beq .8

				cmp #':'
				bne .9

.8				clc
				rts

.4				cpx #3
				beq .8

*				lda SA.REMOTE+S.SA.IN.ADDR,x
*				bne .8

.9				sec
				rts
*--------------------------------------
AI.AddServInfo	jsr SKT.GetRemoteAddrTXTPTR

				lda SA.REMOTE+S.SA.IN.PORT+1
				bne .9

				lda SA.REMOTE+S.SA.IN.PORT
				cmp #UDP.PORT.DNS
				bne .9

				ldy #0

.1				lda DNSs,y
				beq .3

				ldx #0

.2				lda	DNSs,y
				cmp SA.REMOTE+S.SA.IN.ADDR,x
				bne .3

				iny
				inx
				cpx #4
				bne .2

				clc						Already known
				rts

.3				tya
				and #$C
				clc
				adc #4
				cmp #16
				tay
				bcc .1

				ldy #0

.4				lda DNSs,y
				beq .5

				tya
				clc
				adc #4
				cmp #16
				tay
				bcc .4

				lda #E.OOH
*				sec
				rts

.5				ldx #0

.6				lda SA.REMOTE+S.SA.IN.ADDR,x
				sta DNSs,y
				iny
				inx
				cpx #4
				bcc .6

				clc
				rts

.9				lda #SKT.E.BAD
				sec
				rts
*--------------------------------------
AI.AddNameInfo
				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/sys/km.inet.s.ai
LOAD usr/src/sys/km.inet.s
ASM
