NEW
  AUTO 3,1
*/--------------------------------------
* # StrVNew
* ## ASM
* `>LDYA size`
* `>KAPI StrVNew`
* ## RETURN VALUE
*\--------------------------------------
K.StrVNew		jsr K.Malloc
				bcs .9

				>STYA ZPPtr1			pSTRV

				pha

				sta IO.SETWRITEAUX
				lda #0
				sta (ZPPtr1)
				sta IO.CLRWRITEAUX

				pla						Y,A = pSTRV

*				clc

.9				rts
*/--------------------------------------
* # StrVSet
* ## ASM
* `>SS`
* `>PUSHW pSTRV`
* `>PUSHW id`
* `>PUSHW ptr`
* `>KAPI StrVSet`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.StrVSet		jsr STRV.GetParams
				bcs .7
*			>DEBUG

				ldy #$ff

.1				iny
				jsr RAMSW.xFORPNTgY		ptr
				bne .1

				tya

*				clc
				adc #4					LEN+IDlo+IDhi+\0
				bcs .9

				jsr RAMSW.xTXTPTRc		pSTRV
				bne .4

				sta IO.SETWRITEAUX		Same Length, replace
				bra .10

.9				lda #E.OOM
				sec
				rts

.4				jsr STRV.Delete

.7				ldy #$ff

.8				iny
				jsr RAMSW.xFORPNTgY
				bne .8

				tya

				clc
				adc #4
				bcs .9

				sta IO.SETWRITEAUX

				sta (TXTPTR)
				ldy #1
				lda ZPPtr1				id
				sta (TXTPTR),y
				iny
				lda ZPPtr1+1
				sta (TXTPTR),y

.10				jsr STRV.Skip3
				
				ldy #$ff

.11				iny
				jsr RAMSW.xFORPNTgY
				sta (TXTPTR),y
				bne .11

				iny
				sta (TXTPTR),y			StrV Ending \0

				sta IO.CLRWRITEAUX

				clc
				rts
*/--------------------------------------
* # StrVGet
* ## ASM
* `>SS`
* `>PUSHW pSTRV`
* `>PUSHW id`
* `>PUSHW ptr`
* `>KAPI StrVGet`
* `>SR`
* ## RETURN VALUE
*  CC: Y,A = Ptr
*  CS: A = E.NOKEY
*\--------------------------------------
K.StrVGet		jsr STRV.GetParams
				bcs .9

				jsr STRV.Skip3
				
				ldy #$ff

				sta IO.SETWRITEAUX

.5				iny
				jsr RAMSW.xTXTPTRgY
				sta (FORPNT),y
				bne .5

				sta IO.CLRWRITEAUX

				>LDYA FORPNT

				clc

.9				rts
*--------------------------------------
* PRIVATE
*--------------------------------------
STRV.GetParams	ldy #4					pSTRV
				jsr RAMSW.StkY2TXTPTR

				ldy #2
				jsr RAMSW.GetStkYW		id
				>STYA ZPPtr1

				ldy #0					ptr
				jsr RAMSW.StkY2FORPNT

.1				jsr RAMSW.xTXTPTRg		len
				beq .9

				ldy #1

				jsr RAMSW.xTXTPTRgY		id LO
				cmp ZPPtr1
				bne .2

				iny
				jsr RAMSW.xTXTPTRgY		id HI
				cmp ZPPtr1+1
				beq .8

.2				jsr STRV.Next
				bcc .1

.9				lda #E.NOKEY
				sec
				rts
				
.8				clc
				rts
*--------------------------------------
STRV.Delete		>LDYA TXTPTR
				>STYA ZPPtr2
*			>DEBUG
				jsr STRV.Next
				bcs .8

.1	 			jsr RAMSW.xTXTPTRg

				tax

				sta IO.SETWRITEAUX

				ldy #0

.2				jsr RAMSW.xTXTPTRgY
				sta (ZPPtr2),y
				iny
				dex
				bne .2

				sta IO.CLRWRITEAUX

				tya
				clc
				adc TXTPTR
				sta TXTPTR
				bcc .3

				inc TXTPTR+1

.3				clc
				tya
				adc ZPPtr2
				sta ZPPtr2
				bcc .4

				inc ZPPtr2+1

.4				jsr RAMSW.xTXTPTRg
				bne .1

.8				>LDYA ZPPtr2
				>STYA TXTPTR
				clc
				rts
*--------------------------------------
STRV.Next		jsr RAMSW.xTXTPTRg
				sec
				beq .8

				clc
				adc TXTPTR
				sta TXTPTR
				bcc .8

				inc TXTPTR+1
				clc

.8				rts
*--------------------------------------
STRV.Skip3		lda TXTPTR
				clc
				adc #3
				sta TXTPTR
				bcc .1

				inc TXTPTR+1

.1				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.strv
LOAD usr/src/sys/kernel.s
ASM
