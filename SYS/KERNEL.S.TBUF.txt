NEW
  AUTO 3,1
*/--------------------------------------
* # TBufInsL
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW LineNum`
* `>PUSHW DataPtr`
* `>KAPI TBufInsL`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.TBufInsL		ldy #0					DataPtr
				jsr RAMSW.StkY2TXTPTR

				ldy #2					LineNum
				jsr RAMSW.GetStkYW
				>STYA ZPPtr2

				ldy #4					pTBuf
				jsr TBUF.GetP			Y = 1

.1				iny
				lda (pIBlk),y
				bne .1

				dey						last used block

				sty iBlk

				lda (ZPPtr3),y
				sta iByte

				lda (pIBlk),y
				jsr BLIST.GetpDblkY

.2				jsr RAMSW.xTXTPTRgn
				beq .8

				tax

.3				ldy iBlk
				lda (ZPPtr3),y
				inc
				sta (ZPPtr3),y

				cpx #C.CR
				bne .4

				lda (ZPPtr4),y
				inc
				sta (ZPPtr4),y

.4				txa

				jsr BLIST.AddByte
				bcc .2

				rts

.8				ldy iBlk
				lda (ZPPtr3),y
				inc
				sta (ZPPtr3),y

				lda (ZPPtr4),y
				inc
				sta (ZPPtr4),y

				lda #C.CR
				jsr BLIST.AddByte
				bcs .9

				lda #0
				jmp BLIST.AddByte		A=0

.9				rts
*/--------------------------------------
* # TBufGetL
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW LineNum`
* `>PUSHW DataPtr`
* `>KAPI TBufGetL`
* `>SR`
* ## RETURN VALUE
*  CC:Y,A = Line length
*  CS:Y,A = Line count
*\--------------------------------------
K.TBufGetL		ldy #4					pTBuf
				jsr TBUF.GetP

				ldy #2					LineNum
				jsr RAMSW.GetStkYW
				jsr TBUF.FindL
				bcs .9

				ldy #0					DataPtr
				jsr RAMSW.StkY2FORPNT

				stz ZPPtr1
				stz ZPPtr1+1

.5				jsr TBUF.GetNByte
				cmp #C.CR
				beq .8

				jsr X.xFORPNTpn
				inc ZPPtr1
				bne .5

				inc ZPPtr1+1
				bra .5

.8				lda #0
				jsr X.xFORPNTpn

				>LDYA ZPPtr1			exit with line len

				clc

.9				rts
*/--------------------------------------
* # TBufSetL
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW LineNum`
* `>PUSHW DataPtr`
* `>KAPI TBufSetL`
* `>SR`
* ## RETURN VALUE
*  Y,A = Line length
*\--------------------------------------
K.TBufSetL 		ldy #4					pTBuf
				jsr TBUF.GetP

				ldy #2					LineNum
				jsr RAMSW.GetStkYW
				jsr TBUF.FindL
				bcs .9

				ldy #0					DataPtr
				jsr RAMSW.StkY2TXTPTR

				jsr TBUF.DelL

				lda TXTPTR+1
				beq .8

				jmp TBUF.InsL

.8				clc
				rts

.9				lda #E.NOKEY

*				sec

				rts
*/--------------------------------------
* # TBufGetB
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW DataOfs`
* `>PUSHW DataPtr`
* `>PUSHW DataLen`
* `>KAPI TBufGetB`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.TBufGetB		ldy #6					pTBuf
				jsr RAMSW.GetStkYW
				>STYA pIBlk

				ldy #4					DataOfs
				jsr RAMSW.StkY2FORPNT
				>STYA ZPSListDataOfs

				stz iByte

				lda #2
				sta iBlk


				clc
				rts
*/--------------------------------------
* # TBufInsB
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW DataOfs`
* `>PUSHW DataPtr`
* `>PUSHW DataLen`
* `>KAPI TBufInsB`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.TBufInsB

				clc
				rts
*/--------------------------------------
* # TBufDelB
* ## ASM
* `>SS`
* `>PUSHW pTBuf`
* `>PUSHW DataOfs`
* `>PUSHW DataLen`
* `>KAPI TBufDelB`
* `>SR`
* ## RETURN VALUE
*\--------------------------------------
K.TBufDelB

				clc
				rts
*/--------------------------------------
* # TBufNew
* ## ASM
* `>KAPI TBufNew`
* ## RETURN VALUE
*\--------------------------------------
K.TBufNew		lda #0
				jsr BLIST.New
				bcs .9

				>LDYAI 512				Block Len + CR count
				ldx #S.MEM.F.INIT0
				jsr MEM.Malloc.YAX		Kernel Memory
				bcs .9

				txa						hLBlk

				sta (pIBlk)

				>LDYA pIBlk

*				clc

.9				rts
*/--------------------------------------
* # TBufFree
* ## ASM
* `>LDYA pTBuf`
* `>KAPI TBufFree`
* ## RETURN VALUE
*\--------------------------------------
K.TBufFree		>STYA pIBlk

				lda (pIBlk)				hLBlk
				jsr MEM.FreeA

				>LDYA pIBlk

				jmp BLIST.Free
*--------------------------------------
TBUF.GetP		jsr RAMSW.GetStkYW
				>STYA pIBlk

				lda (pIBlk)				hLBlk
				jsr MEM.GetA
				jsr MEM.GetPtr
				>STYA ZPPtr3			LEN block
				inc
				>STYA ZPPtr4			CR block

				ldy #1
				lda (pIBlk),y
				sta SLIST.Bnk

				rts
*--------------------------------------
TBUF.FindL		>STYA ZPPtr2

				ldx #0					start at LineCnt = 0
				txa

				ldy #1					First DBlk - 1

.1				stx ZPPtr1
				sta ZPPtr1+1

				iny						Next DBlk

				lda (pIBlk),y
				beq .9

				lda (ZPPtr4),y			we have one more DBlk block
				clc
				adc ZPPtr1				add lines in this DBlk
				tax

				lda ZPPtr1+1
				adc #0					to AX=LineCnt

				cpx ZPPtr2
				pha
				sbc ZPPtr2+1			LineNum > LineCnt
				pla
				bcc .1					LineNum is not in this block

				sty iBlk
				jsr BLIST.GetpDblkY

				stz iByte

.2				lda ZPPtr1
				eor ZPPtr2
				bne .3

				lda ZPPtr1+1
				eor ZPPtr2+1
				beq .8

.3				jsr TBUF.GetNByte
				cmp #C.CR
				bne .3

				inc ZPPtr1
				bne .2

				inc ZPPtr1+1
				bra .2

.8				clc
				rts

.9				>LDYA ZPPtr1			not found, exit with line count
				sec
				rts
*--------------------------------------
TBUF.DelL		ldy iByte

.1				jsr BLIST.GetDBlkYB
				cmp #C.CR
				beq .4

				iny
				bne .1

				lda iByte
				beq .2

				ldy iBlk
				sta (ZPPtr3),y

				stz iByte
				jsr BLIST.2NBlk
				bcc TBUF.DelL

				rts

.2				jsr TBUF.FreeB
				bcc TBUF.DelL

				rts
*--------------------------------------
* Delete between iByte -- Y+1
*--------------------------------------
.4				lda iByte
				clc
				adc pDBlk
				sta ZPPtr1
				lda pDBlk+1
				adc #0
				sta ZPPtr1+1

				lda SLIST.Bnk
				sta (pRWReg)
				sta IO.SETWRITEAUX

				ldx #0					!CR deleted

.5				iny
				beq .6

				jsr BLIST.GetDBlkY
				sta (ZPPtr1)
				beq .6

				>INCW ZPPtr1

				cmp #C.CR
				bne .5

				dex
				bra .5

.6				sta IO.CLRWRITEAUX
				lda A2osX.ActBnk
				sta (pRWReg)

				tya
				ldy iBlk

				sta (ZPPtr3),y

				txa
				clc
				adc (ZPPtr4),y
				sta (ZPPtr4),y

				rts
*--------------------------------------
TBUF.InsL		lda TXTPTR+1
				pha

				jsr RAMSW.xTXTPTRl
				sty ZPPtr1				StrLen
				stx ZPPtr1+1

				pla
				sta TXTPTR+1

.1				jsr RAMSW.xTXTPTRgn
				beq .3

				tax

				ldy iBlk
				lda (ZPPtr3),y
				inc
				sta (ZPPtr3),y

				cpx #C.CR
				bne .2

				lda (ZPPtr4),y
				inc
				sta (ZPPtr4),y

.2				txa

				ldy iByte
				jsr BLIST.SetDBlkY
				inc iByte
				bne .1

				jsr TBUF.InsB
				bcc .1

				rts

.3				ldy iBlk
				lda (ZPPtr3),y
				inc
				sta (ZPPtr3),y

				lda (ZPPtr4),y
				inc
				sta (ZPPtr4),y

				lda #C.CR

				ldy iByte
				jsr BLIST.SetDBlkY
				inc iByte
				beq TBUF.InsB

				clc
				rts
*--------------------------------------
TBUF.InsB		ldy iBlk

.3				lda (pIBlk),y
				beq .4

				iny
				bra .3

.4				lda (pIBlk),y
				iny
				sta (pIBlk),y

				dey
				lda (ZPPtr3),y
				iny
				sta (ZPPtr3),y

				dey
				lda (ZPPtr4),y
				iny
				sta (ZPPtr4),y

				dey
				cpy iBlk
				bne .4

				lda #0
				sta (pIBlk),y
				sta (ZPPtr3),y
				sta (ZPPtr4),y

				jmp BLIST.AddBlk
*--------------------------------------
TBUF.FreeB		ldy iBlk
				lda (pIBlk),y

				jsr MEM.FreeAX

				ldy iBlk

.1				iny
				lda (pIBlk),y
				tax
				dey
				sta (pIBlk),y

				iny
				lda (ZPPtr3),y
				dey
				sta (ZPPtr3),y

				iny
				lda (ZPPtr4),y
				dey
				sta (ZPPtr4),y

				iny
				txa
				bne .1

				ldy iBlk
				jmp BLIST.GetpDblkY
*--------------------------------------
TBUF.GetNByte	ldy iByte
				jsr BLIST.GetDBlkYB
				bne .1

				stz iByte
				jsr BLIST.2NBlk
				bcc TBUF.GetNByte

				rts

.1				inc iByte
				bne .8

				pha
				jsr BLIST.2NBlk
				pla

.8				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.tbuf
LOAD usr/src/sys/kernel.s
ASM
