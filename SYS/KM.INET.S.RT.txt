NEW
  AUTO 3,1
*--------------------------------------
RT.GetRoute		>LDYAI K.ROUTE.MAX*S.ROUTE
				>DAPI Malloc
				bcs .9

				>STYA A2

				sta IO.SETWRITEAUX

				ldy #0

.1				lda ROUTEs,y
				sta (A2),y
				iny
				cpy #K.ROUTE.MAX*S.ROUTE
				bcc .1

				sta IO.CLRWRITEAUX

				>LDYA A2

				clc

.9				rts
*--------------------------------------
* TXPTR = S.ROUTE
*--------------------------------------
RT.SetRoute		ldy #0

.1				jsr A2osX.GetTXTPTR
				>INCW TXTPTR
				sta ROUTE,y
				iny
				cpy #S.ROUTE
				bcc .1

				ldx #8

.2				lda ROUTE+S.ROUTE.ADDR-1,x
				bne .3

				dex
				bne .2

				bra .4

.3				jsr RT.GetFree
				bcs .9

.4				ldy #0

.5				lda ROUTE,y
				sta ROUTEs,x
				inx
				iny
				cpy #S.ROUTE
				bcc .5

				clc

.9				rts
*--------------------------------------
RT.GetFree		ldx #S.ROUTE			skip default

.1				lda ROUTEs+S.ROUTE.IF,x
				beq .2

				txa
				clc
				adc #S.ROUTE
				tax
				cpx #S.ROUTE*K.ROUTE.MAX
				bcc .1

				lda #E.OOH
*				sec
				rts

.2				phx

				ldy #S.ROUTE

.3				stz ROUTEs,x
				inx
				dey
				bne .3

				plx

				clc
				rts
*--------------------------------------
RT.pFrameOut	ldx #S.ROUTE			skip DFLT

.1				lda ROUTEs+S.ROUTE.IF,x
				beq .7

				phx

				ldy #S.IP.DST

.2				lda (pFrameOut),y
				eor ROUTEs+S.ROUTE.ADDR,x
				and	ROUTEs+S.ROUTE.MASK,x
				bne .6

				inx
				iny
				cpy #S.IP.DST+4
				bcc .2

				plx
				phx

				lda ROUTEs+S.ROUTE.IF,x
				sta hIFOut
*--------------------------------------
				jsr IF.GetIPCFGA
				bcs .99

				ldy #S.IP.SRC
				lda (pFrameOut),y
				bne .8

				ldy #S.IPCFG.IP

.3				lda (pIPCFG),y
				pha
				iny
				cpy #S.IPCFG.IP+4
				bcc .3

				ldy #S.IP.SRC+4

.4				pla
				dey
				sta (pFrameOut),y
				cpy #S.IP.SRC
				bne .4

.8				plx
				lda ROUTEs+S.ROUTE.F,x
				bpl RT.pFrameOut.IF
				bmi RT.pFrameOut.GW
*--------------------------------------
.6				plx

.7				txa
				clc
				adc #S.ROUTE
				tax
				cpx #K.ROUTE.MAX*S.ROUTE
				bcc .1

				lda ROUTEs+S.ROUTE.IF	default route active ?
				beq .9

				sta hIFOut
				jsr IF.GetIPCFGA
				bcs .99

				ldx #0
				bra RT.pFrameOut.GW

.9				lda #SKT.E.NOCONN

*				sec

.99				rts
*--------------------------------------
RT.pFrameOut.IF	lda pFrameOut			Same network, query ARP for dest IP
				clc
				adc #S.IP.DST
				tay

				lda pFrameOut+1
				adc /S.IP.DST
				bra RT.pFrameOut.ARP
*--------------------------------------
RT.pFrameOut.GW >LDYA L.ROUTE.GW,x		X = Route index
*--------------------------------------
RT.pFrameOut.ARP
				jsr ARP.QueryYA
				bcs .9

				ldy #S.ETH.DSTMAC		X = ptr to ARP.CACHE

.1				lda ARP.CACHE+S.ARPCACHE.MAC,x
				sta (pFrameOut),y
				inx
				iny
				cpy #S.ETH.DSTMAC+6
				bcc .1

				clv

				clc

				rts

.9				cmp #SKT.E.PENDING
				bne .98

				clc
				bit .99

.98				sec

.99				rts
*--------------------------------------
MAN
SAVE usr/src/sys/km.inet.s.rt
LOAD usr/src/sys/km.inet.s
ASM
