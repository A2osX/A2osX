NEW
  AUTO 3,1
*--------------------------------------
* 32 = SIG
* 34 = Incoming
*--------------------------------------
K.GetIfCfg		ldx #36
				.HS 2C					BIT ABS
*--------------------------------------
K.SetIfCfg		ldx #38

				ldy #4
				jsr RAMSW.GetStkYW		AF_

				cmp #0
				bne .9

				jmp K.2KMY

.9				lda #E.UNSUP
				sec
				rts
*--------------------------------------
* int getaddrinfo(const char *restrict nodename,
* 				  const char *restrict servname,
* 				  const struct addrinfo *restrict hints,
* 				  struct addrinfo **restrict res);
*--------------------------------------
K.GetAddrInfo	ldy #2
				jsr RAMSW.GetStkYW		hints

				>STYA TXTPTR
				ora TXTPTR
				beq .1					hints = NULL

				jsr RAMSW.xTXTPTRg		S.ADDRINFO.AF
				tay
				jsr RAMSW.xTXTPTRng		Y,A = AF_

				ldx #40
				jmp K.2KMY
*--------------------------------------
.1				ldx #0

.2				lda A2osX.pAFs+1,x
				beq .7

				sta .3+2
				lda A2osX.pAFs,x
				sta .3+1

				phx
				ldx #40

.3				jsr $FFFF				SELF MODIFIED
				plx
				bcc .8

				cmp #GAI.E.AGAIN		pending request
				beq .9

.7				inx						next AF_...
				inx
				cpx #K.AF.MAX*2
				bcc .2

*				sec

.8				lda #0
				tay						return int = 0

.9				rts
*--------------------------------------
* void freeaddrinfo(struct addrinfo *ai);
*--------------------------------------
K.FreeAddrInfo	ldx #42

				clc
				rts
*--------------------------------------
* >LDYAI AF_
* >KAPI GetRoute
*--------------------------------------
K.GetRoute		ldx #44
				jmp K.2KMY
*--------------------------------------
K.SetRoute		>STYA TXTPTR

				jsr RAMSW.xTXTPTRg		S.ROUTE.AF
				tay

				ldx #46
				jmp K.2KMY
*--------------------------------------
K.IfUp			sty IO.hFD

				ldx #48
				jsr K.2KMs

				ldx #0

.1				lda hIFs,x
				cmp IO.hFD
				beq .9

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

				ldx #0

.2				lda hIFs,x
				beq .3

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .2

				lda #E.OOH
*				sec
				rts

.3				lda IO.hFD
				sta hIFs,x

				tay
				lda pFDs+1,y
				sta pFD+1
				sta pIFDRVs+1,x
				lda pFDs,y
				sta pIFDRVs,x
				sta pFD

				jsr DEV.GetPDrv
				jmp DEV.Open

.9				lda #E.INVH
*				sec
				rts
*--------------------------------------
K.IfDown		sty IO.hFD

				ldx #50
				jsr K.2KMs

				ldx #0

.1				lda hIFs,x
				cmp IO.hFD
				beq .2

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

				lda #E.INVH
*				sec
				rts

.2				stz hIFs,x

				lda pIFDRVs+1,x
				stz pIFDRVs+1,x
				sta pFD+1
				lda pIFDRVs,x
				sta pFD

				jmp DEV.Close
*--------------------------------------
* >LDYAI AF_
* KAPI ArpClear
*--------------------------------------
K.ArpClear		ldx #52
				jmp K.2KMY
*--------------------------------------
* >PUSHWI AF_
* >PUSHW PA
* >PUSHW HA
* KAPI ArpQuery
*--------------------------------------
K.ArpQuery		ldy #4					AF_
				jsr RAMSW.GetStkYW

				ldx #54
				jmp K.2KMY
*--------------------------------------
* >PUSHWI AF_
* >PUSHW PA
* >PUSHW HA
* KAPI ArpAdd
*--------------------------------------
K.ArpAdd		ldy #4					AF_
				jsr RAMSW.GetStkYW

				ldx #56
				jmp K.2KMY
*--------------------------------------
* >LDYAI AF_
* KAPI ArpGet
*--------------------------------------
K.ArpGet		ldx #58
				jmp K.2KMY
*--------------------------------------
* >LDYA pSA
* KAPI AddServInfo
*--------------------------------------
K.AddServInfo	ldx #60
				bra NETDB.YAX
*--------------------------------------
* >PUSHW pSA
* >PUSHW pName
* KAPI AddNameInfo
*--------------------------------------
K.AddNameInfo	ldy #2					pSA
				jsr RAMSW.GetStkYW
				ldx #62

NETDB.YAX		>STYA TXTPTR
				jsr RAMSW.xTXTPTRg		S.SA.AF
				tay
				jmp K.2KMY
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.netdb
LOAD usr/src/sys/kernel.s
ASM
