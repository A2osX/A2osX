NEW
  AUTO 3,1
*--------------------------------------
IF.GetCFG		ldy #0
				jsr K.GetStkYW			pIFCFG
				>STYA TXTPTR

				ldy #2					hFD
				jsr K.GetStkYW
				>STYA ZPPtr1

				lda ZPPtr1

				ldx #0

.1				cmp hIFs,x
				beq .2

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

				lda #E.INVH

*				sec

				rts

.2				>LDYA pIPCFGs,x
				>STYA ZPPtr2

				sta IO.SETWRITEAUX

				ldy #S.IPCFG-1

.3				lda (ZPPtr2),y
				sta (TXTPTR),y
				dey
				bpl .3

				sta IO.CLRWRITEAUX

				clc
				rts
*--------------------------------------
IF.SetCFG		ldy #0
				jsr K.GetStkYW			pIFCFG
				>STYA TXTPTR

				ldy #2					hFD
				jsr K.GetStkYW
				>STYA ZPPtr1

				lda ZPPtr1

				ldx #0

.1				cmp hIFs,x
				beq .4

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

				ldx #0

.2				lda hIFs,x
				beq .3

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .2

				lda #E.OOH

*				sec

				rts

.3				phx

				>LDYAI S.IPCFG
				>DAPI KMalloc

				plx

				bcs .9

				>STYA ZPPtr2
				>STYA pIPCFGs,x

				lda ZPPtr1
				sta hIFs,x

				bra .5
*--------------------------------------
.4				>LDYA pIPCFGs,x
				>STYA ZPPtr2

.5				ldy #0

.6				jsr A2osX.GetTXTPTR
				sta (ZPPtr2),y
				>INCW TXTPTR
				iny
				cpy #S.IPCFG
				bcc .6

				clc

.9				rts
*--------------------------------------
* Y,A = hFD
*--------------------------------------
IF.Up			sty hIFIn

				tya

				ldx #0

.1				cmp hIFs,x
				beq .2

				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

.9				lda #E.INVH

				sec

				rts

.2				>LDYA pIPCFGs,x
				beq .9

				>STYA pIPCFG

				ldy #S.IPCFG.IP
				lda (pIPCFG),y

				jsr RT.GetFree
				bcs .99

.3				lda hIFIn
				sta ROUTEs+S.ROUTE.IF,x

				lda #1
				sta ROUTEs+S.ROUTE.METRIC,x

				ldy #S.IPCFG.MASK

				phx

.4				lda (pIPCFG),y
				sta ROUTEs+S.ROUTE.MASK,x
				inx
				iny
				cpy #S.IPCFG.MASK+4
				bcc .4

				plx

				ldy #S.IPCFG.IP

.5				lda (pIPCFG),y
				and ROUTEs+S.ROUTE.MASK,x
				sta ROUTEs+S.ROUTE.ADDR,x
				inx
				iny
				cpy #S.IPCFG.IP+4
				bcc .5

				clc

.99				rts
*--------------------------------------
IF.Down			sty hIFIn

				ldx #S.ROUTE			skip default

.1				lda ROUTEs+S.ROUTE.IF,x
				beq .7

				cmp hIFIn
				bne .7

				bit ROUTEs+S.ROUTE.F,x
				bmi .7

				stz ROUTEs+S.ROUTE.IF,x

.7				txa
				clc
				adc #S.ROUTE
				tax
				cpx #S.ROUTE*K.ROUTE.MAX
				bcc .1

				clc
				rts
*--------------------------------------
IF.GetIPCFGA	ldx #0

.1				ldy pIPCFGs+1,x
				beq .7

				cmp hIFs,x
				bne .7

				sty pIPCFG+1
				ldy pIPCFGs,x
				sty pIPCFG

				clc
				rts

.7				inx
				inx
				cpx #K.IF.MAX*2
				bcc .1

				lda #SKT.E.NOCONN

*				sec

				rts
*--------------------------------------
MAN
SAVE usr/src/sys/km.inet.s.if
LOAD usr/src/sys/km.inet.s
ASM
