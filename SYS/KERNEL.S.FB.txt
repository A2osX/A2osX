NEW
  AUTO 3,1
*--------------------------------------
FB.Swap			ldy TERM.FB
				ldx FB.B,y
				bit $C000,x				IO.RRAMWRAMBNK2,IO.RRAMWRAMBNK1

				ldx #23

.1				phx

				lda TERM.BL,x
				sta ZPL1
				lda TERM.BH,x
				sta ZPL1+1

				ldy TERM.FB
				clc
				lda FB.BL,y
				adc FB.L,x
				sta .80+1
				sta .81+1
				lda FB.BH,y
				adc FB.H,x
				sta .80+2
				sta .81+2

				sta IO.SETREADAUX
				sta IO.SETWRITEAUX

				ldx #78
				jsr .8

				sta IO.CLRREADAUX
				sta IO.CLRWRITEAUX

				ldx #79
				jsr .8

				plx
				dex
				bpl .1

				bit IO.RRAMWRAMBNK2

				rts
*--------------------------------------
.8				ldy #39

.80				lda $ffff,x				SELF MODIFIED
				pha
				lda (ZPL1),y

.81				sta $ffff,x				SELF MODIFIED
				pla
				sta (ZPL1),y

				dex
				dex
				dey
				bpl .80

				rts
*--------------------------------------
FB.EDXY			sty ZPTmpX

				ldy TERM.FB
				lda FB.B,y
				tay
				lda $C000,y				IO.RRAMWRAMBNK2,IO.RRAMWRAMBNK1

.1				cpx ZPTmpX
				bcs .8

				ldy TERM.FB
				clc
				lda FB.BL,y
				adc FB.L,x
				sta ZPL1
				lda FB.BH,y
				adc FB.H,x
				sta ZPL1+1

				ldy #79

				lda #" "

.2				sta (ZPL1),y
				dey
				bpl .2

				inx
				bra .1

.8				bit IO.RRAMWRAMBNK2

				rts
*--------------------------------------
FB.CPLX			php

				ldy TERM.FB
				clc
				lda FB.BL,y
				adc FB.L,x
				sta ZPL2
				lda FB.BH,y
				adc FB.H,x
				sta ZPL2+1
				plp
				bcs .1

				inx
				.HS B0					BCS

.1				dex

				clc
				lda FB.BL,y
				adc FB.L,x
				sta ZPL1
				lda FB.BH,y
				adc FB.H,x
				sta ZPL1+1

				lda FB.B,y
				tay
				lda $C000,y				IO.RRAMWRAMBNK2,IO.RRAMWRAMBNK1

				ldy #79

.2				lda (ZPL1),y
				sta (ZPL2),y
				dey
				bpl .2

				bit IO.RRAMWRAMBNK2

				rts
*--------------------------------------
FB.SetCYX		sta .1+1				char
				sty .2+1				Line

				ldy TERM.FB
				clc
				lda FB.BL,y
				adc FB.L,x
				sta ZPL1
				lda FB.BH,y
				adc FB.H,x
				sta ZPL1+1

*				ldx FB.B,y
*				bit $C000,x

				lda FB.B,y
				tay
				lda $C000,y				IO.RRAMWRAMBNK2,IO.RRAMWRAMBNK1

.1				lda #$FF				SELF MODIFIED
.2				ldy #$FF				SELF MODIFIED
				sta (ZPL1),y

				bit IO.RRAMWRAMBNK2

				rts
*--------------------------------------
FB.BL			.DA #$D000
				.DA #$D780
				.DA #$D000
				.DA #$D780
				.DA #$DF00
				.DA #$E680
				.DA #$EE00
				.DA #$F580
FB.BH			.DA /$D000
				.DA /$D780
				.DA /$D000
				.DA /$D780
				.DA /$DF00
				.DA /$E680
				.DA /$EE00
				.DA /$F580
FB.B			.DA #IO.RRAMWRAMBNK2
				.DA #IO.RRAMWRAMBNK2
				.DA #IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1
*--------------------------------------
FB.L			.DA #0
				.DA #80
				.DA #160
				.DA #240
				.DA #320
				.DA #400
				.DA #480
				.DA #560
				.DA #640
				.DA #720
				.DA #800
				.DA #880
				.DA #960
				.DA #1040
				.DA #1120
				.DA #1200
				.DA #1280
				.DA #1360
				.DA #1440
				.DA #1520
				.DA #1600
				.DA #1680
				.DA #1760
				.DA #1840
*--------------------------------------
FB.H			.DA /0
				.DA /80
				.DA /160
				.DA /240
				.DA /320
				.DA /400
				.DA /480
				.DA /560
				.DA /640
				.DA /720
				.DA /800
				.DA /880
				.DA /960
				.DA /1040
				.DA /1120
				.DA /1200
				.DA /1280
				.DA /1360
				.DA /1440
				.DA /1520
				.DA /1600
				.DA /1680
				.DA /1760
				.DA /1840
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.fb
LOAD usr/src/sys/kernel.s
ASM
