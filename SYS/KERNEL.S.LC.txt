NEW
  AUTO 3,1
*/--------------------------------------
* # GetBufData
* ## ASM
* `>SS`
* `>PUSHW hBuf`
* `>PUSHW start`
* `>PUSHW len`
* `>PUSHW dst
* `>KAPI GetBufData`
* `>SR`
*
*\--------------------------------------
LC.RWBnk		.BS 1
LC.Bits			.BS 1
LC.Bnk			.BS 1
*--------------------------------------
K.GetBufData	ldy #6
				jsr RAMSW.GetStkYW
				jsr K.GetpLCBuf
				bcc .20

				rts

.20				sta LC.RWBnk
				stx LC.Bnk

				sty A1L+1				LC ram HI

				ldy #4
				jsr RAMSW.GetStkY		start LO
				sta A1L
				iny
				jsr RAMSW.GetStkY		start HI
				clc
				adc A1L+1
				sta A1L+1

				ldy #2
				jsr RAMSW.GetStkY		len LO
				eor #$ff
				sta ZPPtr4
				iny
				jsr RAMSW.GetStkY		len HI
				eor #$ff
				sta ZPPtr4+1

				ldy #0
				jsr RAMSW.GetStkYW
				>STYA A2L				dst

				ldx A2osX.ActBnk

				ldy LC.Bnk
				lda $C000,y

				lda LC.RWBnk
				sta (pRWReg)

				>LDYA A1L

				sei
				sta IO.SETALTZP

				>STYA A1L

				sta IO.CLRALTZP
				cli

				txa
				sta (pRWReg)

				sta IO.SETWRITEAUX

.1				inc ZPPtr4
				bne .2

				inc ZPPtr4+1
				beq .8

.2				lda LC.RWBnk
				sta (pRWReg)

				sei
				sta IO.SETALTZP

				lda (A1L)
				>INCW A1L

				sta IO.CLRALTZP
				cli

				pha
				txa
				sta (pRWReg)
				pla

				sta (A2L)				SELF MODIFIED
				>INCW A2L

				bra .1

.8				sta IO.CLRWRITEAUX

				bit IO.RRAMWRAMBNK2

				clc

.9				rts
*--------------------------------------
* Y,A = filename
*--------------------------------------
K.LoadBuf		jsr RAMSW.YA2PathBuf
*			>DEBUG
				jsr STAT.Stat
				bcs .99

				lda K.S.STAT+S.STAT.MODE.REG+1
				and #$F0
				bne .98

				lda K.S.STAT+S.STAT.SIZE+3
				ora	K.S.STAT+S.STAT.SIZE+2
				bne .98

				lda K.S.STAT+S.STAT.SIZE+1
				ldy K.S.STAT+S.STAT.SIZE
				beq .1

				inc

.1				bit #$7
				beq .2

*				clc
				adc #8

.2				lsr
				lsr
				lsr
				cmp #13
				bcs .98

				jsr K.GetBuf
				bcs .99

				sty LC.RWBnk
				sta LC.Bits

				lda #O.RDONLY
				sta IO.Flags
				stz IO.Type
				stz IO.AuxType
				stz IO.AuxType+1

				jsr STDIO.FOpen
				bcs .97

				jsr LC.Read
				bcs .96

				>LDYA pFILE
				jsr K.FClose

				ldy LC.RWBnk
				lda LC.Bits
				clc
				rts

.96				pha
				>LDYA pFILE
				jsr K.FClose
				pla

.97				pha

				ldy LC.RWBnk
				lda LC.Bits
				ora K.LC,y
				sta K.LC,y 				free buf

				pla
				.HS 2C					BIT ABS

.98				lda #MLI.E.INCFF
				sec
.99				rts
*--------------------------------------
LC.Read			ldx #$ff
				lda LC.Bits

.1				inx
				asl
				bcc .1

*				stz .5+1
				lda LC.Bufs,x
				sta .5+2

				lda LC.Bnks,x
				sta .20+1

				clc
				ror A2osX.IOOpt			MAIN

.2				>SS
				sta IO.SETWRITEAUX
				>PUSHW pFILE
				>PUSHWI K.Buf512
				>PUSHWI 512
				sta IO.CLRWRITEAUX

				jsr K.FRead.PFT
				>SR
				bcs .7

				eor #$ff
				pha
				tya
				eor #$ff
				tax

				lda /K.Buf512
				sta .4+2

				lda LC.RWBnk
				sta (pRWReg)

				pla

.20				bit $C000				SELF MODIFIED

				sei
				sta IO.SETALTZP
				pha

				ldy #0

.3				inx
				bne .4

				pla
				inc
				beq .6

				pha

.4				lda K.Buf512,y			SELF MODIFIED
.5				sta $D000,y				SELF MODIFIED
				iny
				bne .3

				inc .4+2
				inc .5+2
				bra .3

.6				sta IO.CLRALTZP
				cli

				bit IO.RRAMWRAMBNK2

				lda A2osX.ActBnk
				sta (pRWReg)
				bra .2

.7				asl	A2osX.IOOpt

				cmp #MLI.E.EOF
				bne .9

				clc
				rts

.9				sec
				rts
*/--------------------------------------
* # GetBuf
* ## ASM
* `>LDA Size`
* `>KAPI GetBuf`
* ## RETURN VALUE
*  Y,A = hBuf (Y=RWBnk, A = BitMask)
*\--------------------------------------
* A = 2kb page count {1..6] (max 6=12k)
*--------------------------------------
K.GetBuf		tax

				lda LC.BMs-1,x
				sta MEM.ReqS

				ldy #$ff

.1				iny

				lda K.LC,y
				beq .7					8 blocks used

				lda MEM.ReqS		find %11000000 or %00111000...

.2				sta MEM.ReqS+1

				and K.LC,y
				cmp MEM.ReqS+1
				beq .8

				lda MEM.ReqS+1
				lsr
				bcc .2

.7				cpy A2osX.MaxBnk
				bne .1

				lda #E.OOM
				sec
				rts

.8				lda MEM.ReqS+1
				eor K.LC,y
				sta K.LC,y				mark all bits as 0=used

				lda MEM.ReqS+1

				clc						Y=RWBnk, A = BitMask
				rts
*--------------------------------------
* Y,A = hBuf (Y=RWBnk, A = BitMask)
*--------------------------------------
K.FreeBuf		jsr LC.Check
				bcs .9

				ora K.LC,y
				sta K.LC,y

*				clc

.9				rts
*--------------------------------------
* IN:
*  Y,A = hBuf (Y=RWBnk, A = BitMask)
* OUT:
*  A=RWBnk, X=LCBnk, Y=BufH
*--------------------------------------
K.GetpLCBuf		jsr LC.Check
				bcs .9

				phy

				ldy #$ff

.1				iny
				asl
				bcc .1

				lda LC.Bufs,y
				ldx LC.Bnks,y
				tay

				pla

				clc

.9				rts
*--------------------------------------
LC.Check		tax						%11000000 or %00111000
				and K.LC,y				should be %00xxxxxx or %xx000xxx
				bne .9

				txa
				clc
				rts

.9				lda #E.INVH
*			>DEBUG
				sec
				rts
*--------------------------------------
LC.Bufs			.DA #$D0,#$D8
				.DA #$D0,#$D8
				.DA #$E0,#$E8
				.DA #$F0,#$F8

LC.Bnks			.DA #IO.RRAMWRAMBNK2,#IO.RRAMWRAMBNK2
				.DA #IO.RRAMWRAMBNK1,#IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1,#IO.RRAMWRAMBNK1
				.DA #IO.RRAMWRAMBNK1,#IO.RRAMWRAMBNK1

LC.BMs			.DA #%10000000
				.DA #%11000000
				.DA #%00111000
				.DA #%00111100
				.DA #%00111110
				.DA #%00111111
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.lc
LOAD usr/src/sys/kernel.s
ASM
