NEW
  AUTO 3,1
*--------------------------------------
CURBLNK.SPEED	.EQ 2
*--------------------------------------
				.DUMMY
				.OR ZPDRV+4
ZPpDCB			.BS 2
ZPL1			.BS 2
ZPL2			.BS 2

bActive			.BS 1
ZPTmpX			.BS 1
ZPTmpChar		.BS 1
CsiH			.BS 1
CsiPCnt			.BS 1
CsiP			.BS 4
CsiF			.BS 1
				.ED
*--------------------------------------
TERM.FB			.BS 1
TERM.bCurOn		.BS 1
TERM.CurSave	.BS 1
TERM.bL0		.BS 1
TERM.L0			.EQ	$DF00				AUXLC,B2
*--------------------------------------
TERM.DRV		ldy #S.FD.DEV.DEVID
				lda (pFD),y
				cmp A2osX.ASCREEN
				beq .1					CS

				clc

.1				ror bActive

				tay
				lda TERM.FBs,y
				sta TERM.FB

				jmp (.2,x)

.2				.DA TERM.STATUS
				.DA A2osX.BADCALL		READBLOCK
				.DA A2osX.BADCALL       WRITEBLOCK
				.DA A2osX.BADCALL       FORMAT
				.DA TERM.CONTROL
				.DA A2osX.BADCALL		INIT
				.DA TERM.OPEN
				.DA TERM.CLOSE
				.DA TERM.READ
				.DA TERM.WRITE
*--------------------------------------
TERM.STATUS		>LDYAI TERM.DIB

				jsr DRV.STATUS.YA
				bcs .4

				ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
				beq .9					CC

				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda TERM.DIB
				ora #S.DIB.S.OPENED
				sta (ZPpBuf)

				sta IO.CLRWRITEAUX

*				clc

				rts

.4				cpy #S.IOCTL.S.EOF
				sec
				bne .9					A = #MLI.E.BADCTL

				ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
				beq TERM.OPEN.E

				sta ZPpDCB+1
				dey
				lda (pFD),y
				sta ZPpDCB

				ldy #S.DCB.TTY.OUTTAIL
				lda (ZPpDCB),y
				iny
				eor (ZPpDCB),y			OUTHEAD
				bne .8

				bit bActive
				bpl .7

				jsr TERM.CheckOA
				bmi .7

				jsr TERM.CBLNK

				bit IO.KBD
				bmi .8

.7				lda #$ff				TRUE
				.HS 2C					BIT ABS
.8				lda #0					FALSE

				clc

.9				rts

TERM.OPEN.E		lda #MLI.E.OPEN
				sec
				rts
*--------------------------------------
TERM.OPEN		ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
				bne TERM.OPEN.E

				>LDYAI S.DCB.TTY
				jsr MEM.Malloc
				bcs .9

				>STYA ZPpDCB

				phy
				ldy #S.FD.DEV.pDCB+1
				sta (pFD),y
				dey
				pla
				sta (pFD),y

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				tax

				lda IO.hFD				in ZPTMP
				sta A2osX.SCRNDEVS,x

				jmp TERM.RESET

.9				rts
*--------------------------------------
TERM.CONTROL	bit bActive
				bmi .8

				ldx A2osX.ASCREEN
				cpx #9
				beq .7					GFX

				jsr TERM.CurHide
				jsr TERM.L0Off

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				tay						Y = this device
				lda TERM.FBs,y
				bmi .7

				ldx A2osX.ASCREEN
				sta TERM.FBs,x			move active screen to this buffer
				sta TERM.FB				for FB.Swap...

				lda #$ff
				sta TERM.FBs,y

				lda #0
				sta (pRWReg)

				sei
				sta IO.SETALTZP
				jsr FB.Swap
				sta IO.CLRALTZP
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

.7				sta IO.SETTEXT

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				sta A2osX.ASCREEN

.8				clc
				rts
*--------------------------------------
TERM.CLOSE		ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
				beq .9

				ldy #S.FD.DEV.pDCB
				lda (pFD),y
				pha
				iny
				lda (pFD),y
				ply

				jsr MEM.FreeYA

				ldy #S.FD.DEV.pDCB+1
				lda #0
				sta (pFD),y

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				tax

				stz A2osX.SCRNDEVS,x

				clc
				rts

.9				lda #MLI.E.IO
				sec
				rts
*--------------------------------------
TERM.READ		ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
*				beq TERM.OPEN.E

				sta ZPpDCB+1
				dey
				lda (pFD),y
				sta ZPpDCB

				jsr DRV.GetnCntpBuf

				stz K.S.IOCTL+S.IOCTL.BYTECNT
				stz K.S.IOCTL+S.IOCTL.BYTECNT+1

				bit bActive
				bpl .6

				jsr TERM.CBLNK

				jsr TERM.CheckOA
				bmi .6

				lda IO.KBD
				bpl .6

				sta IO.KBDSTROBE

				and #$7F
				cmp #C.ESC
				bne .1

				lda #3

.1				ldx #KeyMap.L-1

.2				cmp KeyMap,x
				beq .3

				dex
				bpl .2

				cmp #C.CR
				bne .21

				jsr TERM.COUT
				ldy #S.DCB.TTY.bLNM
				lda (ZPpDCB),y
				bpl .7

				lda #C.LF

.21				jsr TERM.COUT			no remap....send char....
				bra .7					and flush

.3				jsr TERM.ESCCSI

				lda CsiCodes,x
				jsr TERM.COUT
				bra .7

.6				ldy #S.DCB.TTY.OUTTAIL
				lda (ZPpDCB),y
				iny
				cmp (ZPpDCB),y			OUTHEAD
				beq .9					no char in output buffer...
*--------------------------------------
.7				ldx #0

.70				inc ZPCnt
				bne .71

				inc ZPCnt+1
				beq .8

.71				ldy #S.DCB.TTY.OUTTAIL
				lda (ZPpDCB),y
				iny						OUTHEAD
				cmp (ZPpDCB),y
				beq .8

				tay
				lda (ZPpDCB),y
				pha
				tya
				inc
				cmp #S.DCB.TTY.OUTBUF.MAX
				bne .72

				lda #S.DCB.TTY.OUTBUF

.72				ldy #S.DCB.TTY.OUTTAIL
				sta (ZPpDCB),y

				txa
				tay
				pla

				bit A2osX.IOOpt
				bpl .73

				sta IO.SETWRITEAUX

.73				sta (ZPpBuf),y

				sta IO.CLRWRITEAUX

				inx
				bra .70

.8				stx K.S.IOCTL+S.IOCTL.BYTECNT

				clc
				rts

.9				lda	#E.NODATA
*				sec
				rts
*--------------------------------------
TERM.WRITE		ldy #S.FD.DEV.pDCB+1
				lda (pFD),y
*				beq TERM.OPEN.E

				sta ZPpDCB+1
				dey
				lda (pFD),y
				sta ZPpDCB

				jsr DRV.GetnCntpBuf

				jsr TERM.CurHide

.1				inc ZPCnt
				bne .2

				inc ZPCnt+1
				beq .8

.2				ldy #S.DCB.TTY.M
				lda (ZPpDCB),y
				tax
				jsr .3

				inc ZPpBuf
				bne .1

				inc ZPpBuf+1
				bra .1

.3				jmp (.4,x)

.4				.DA TERM.CIN
				.DA TERM.ESC
				.DA TERM.CSI
				.DA TERM.IAC
				.DA TERM.G0
				.DA TERM.G1

.8				clc
				rts
*--------------------------------------
TERM.CIN		jsr RAMSW.GetZPpBuf

				cmp #IAC
				bne .1

				lda #S.DCB.TTY.M.IAC
				jmp TERM.SETM

.1				cmp #C.DEL
				beq TERM.DEL

				cmp #C.SPACE
				bcs .8

				ldx #CtrlChars.L-1

.2				cmp CtrlChars,x
				beq .3

				dex
				bpl .2

				clc
				rts

.3				txa
				asl
				tax
				jmp (.4,x)

.4				.DA TERM.ENQ
				.DA TERM.BS
				.DA TERM.LF
				.DA	TERM.FF
				.DA TERM.CR
				.DA TERM.SO
				.DA TERM.SI
				.DA TERM.SETM.ESC
*--------------------------------------
.8				jsr TERM.SetC
*--------------------------------------
TERM.FS			ldy #S.DCB.TTY.bDECAWM
				lda (ZPpDCB),y
				asl

				.HS 89					BIT IMM

TERM.CUF		clc

				php

				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				inc
				sta (ZPpDCB),y
				cmp #80
				bcc .7

				plp
				bcc .8

				jmp TERM.NEL

.7				plp

.8				clc
				rts
*--------------------------------------
TERM.CUB		sec						no wrap

				.HS 90					BCC

TERM.BS			clc						wrap

*				jsr TERM.BS1

*				clc
*				rts
*--------------------------------------
*TERM.BS1
				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				beq .1

				dec
				sta (ZPpDCB),y

				clc
				rts

.1				bcs .9					no wrap

				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				sec
				beq .9

				dec
				sta (ZPpDCB),y

				ldy #S.DCB.TTY.CH
				lda #79
				sta (ZPpDCB),y

				clc

.9				rts
*--------------------------------------
TERM.DEL		jsr TERM.BS
*				clc						wrap
*				jsr TERM.BS1
				bcs TERM.CR.8

				lda #C.SPACE
				jmp TERM.SetC
*--------------------------------------
TERM.CR			lda #0
				ldy #S.DCB.TTY.CH
				sta (ZPpDCB),y

TERM.CR.8		clc
				rts
*--------------------------------------
TERM.SO			sec
				.HS 90					BCC
*--------------------------------------
TERM.SI			clc
				ldy #S.DCB.TTY.bG0G1
				ror
				sta (ZPpDCB),y
				clc
				rts
*--------------------------------------
TERM.ESC		jsr RAMSW.GetZPpBuf		y = #S.DCB.TTY.M

				ldx #EscCodes.L-1

.2				cmp EscCodes,x
				beq .3

				dex
				bpl .2

				bra TERM.SETM.0

.3				phx
				jsr .4
				plx
				lda EscModes,x
				bra TERM.SETM

.4				txa
				asl
				tax
				jmp (.5,x)

.5				.DA TERM.DECSC			7
				.DA TERM.DECRC			8
				.DA TERM.IND			D
				.DA TERM.NEL			E
				.DA TERM.RI				M
				.DA TERM.RIS			c
				.DA TERM.RTS			[
				.DA TERM.RTS			(
				.DA TERM.RTS			)
*--------------------------------------
TERM.SETM.ESC	lda #S.DCB.TTY.M.ESC
				.HS 2C					BIT ABS
TERM.SETM.0		lda #0
TERM.SETM		ldy #S.DCB.TTY.M
				sta (ZPpDCB),y
				ldy #S.DCB.TTY.INBUF
				tya
				sta (ZPpDCB),y

				clc
				rts
*--------------------------------------
TERM.CSI		ldy #S.DCB.TTY.INBUF
				lda (ZPpDCB),y
				inc
				sta (ZPpDCB),y
				tay

				jsr RAMSW.GetZPpBuf

				sta (ZPpDCB),y
				cmp #64					End of Seq ?
				bcs .10

				cpy #S.DCB.TTY.INBUF.MAX-1	buffer full?
				bcs TERM.SETM.0

				rts
*--------------------------------------
.10				stz CsiH
				stz CsiPCnt
				stz CsiF

				lda #S.DCB.TTY.INBUF+1	EscSeq Ptr
				clc
				adc ZPpDCB
				sta ZPPtr2

				lda /S.DCB.TTY.INBUF+1
				adc ZPpDCB+1
				sta ZPPtr2+1

				lda (ZPPtr2)
				cmp #'?'
				bne .2

				sta CsiH

.1				>INCW ZPPtr2

.2				jsr MATH.Dec2ACC32
				bcs .5

*				clc
				tya
				adc ZPPtr2
				sta ZPPtr2
				bcc .20

				inc ZPPtr2+1

.20				lda #255
				ldx ACC32+1
				bne .3

				lda ACC32

.3				ldx CsiPCnt
				sta CsiP,x
				inc CsiPCnt

				lda (ZPPtr2)
				cmp #';'
				beq .1

				cmp #C.SPACE
				bne .5

				sta CsiF

				>INCW ZPPtr2

.5				jsr TERM.SETM.0

				lda (ZPPtr2)
				ldx #CsiCodes.L-1

.6				cmp CsiCodes,x
				beq .7

				dex
				bpl .6

				clc
				rts

.7				txa
				asl
				tax
				jmp (.8,x)

.8				.DA TERM.CUx			A
				.DA TERM.CUx			B
				.DA TERM.CUx			C
				.DA TERM.CUx			D
				.DA	TERM.CUP			H
				.DA TERM.ED				J
				.DA TERM.EL				K
				.DA	TERM.HVP			f
				.DA TERM.SM				h
				.DA TERM.RM				l
				.DA TERM.SGR			m
				.DA TERM.DSR			n
				.DA TERM.DECSCUSR		q
				.DA TERM.DECSTBM		r
*--------------------------------------
* LNM : ESC [ 20
* DECAWM : ESC [ ? 7
* CUR : ESC [ ? 25
*--------------------------------------
TERM.SM			sec
				.HS 90					BCC
*--------------------------------------
TERM.RM			clc

				ldx CsiPCnt
				beq .99

				dex
				bne .99

				php

				lda CsiP
				ldx CsiH
				bne .1

				cmp #20					bLNM
				bne .98

				ldy #S.DCB.TTY.bLNM
				bra .8
*--------------------------------------
.1				cpx #'?'
				bne .98

				cmp #7
				bne .2

				ldy #S.DCB.TTY.bDECAWM
				bra .8
*--------------------------------------
.2				eor #25
				bne .98

				ldy #S.DCB.TTY.bCURON
				plp
				ror						A = 0
				sta (ZPpDCB),y
				bmi .3

				jmp TERM.CurOff

.3				jmp TERM.CurOn

.8				plp
				ror
				sta (ZPpDCB),y

				clc
				rts

.98				plp

.99				clc
				rts
*--------------------------------------
TERM.SGR		lda CsiPCnt
				beq .10

				ldx #0

.1				lda CsiP,x
				bne .2

				jsr .10
				bra .7

.2				cmp #7
				bne .7

				jsr .11

.7				inx
				dec CsiPCnt
				bne .1

.8				clc
				rts

.10				sec
				.HS 90					BCC
.11				clc

				lda #0
				ror
				ldy #S.DCB.TTY.bNORMAL
				sta (ZPpDCB),y

				rts
*--------------------------------------
TERM.DSR		ldx CsiPCnt
				beq .8

				dex
				bne .8

				lda CsiP
				cmp #6
				bne .8

				jsr TERM.ESCCSI
				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				inc
				jsr TERM.10OUT
				lda #';'
				jsr TERM.COUT

				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				inc
				jsr TERM.10OUT
				lda #'R'
				jmp TERM.COUT

.8				clc
				rts
*--------------------------------------
*ESC [ 0 SP q Terminal default
*ESC [ 1 SP q Blinking Block
*ESC [ 2 SP q Steady Block
*ESC [ 3 SP q Blinking Underline
*ESC [ 4 SP q Steady Underline
*ESC [ 5 SP q Blinking Bar
*ESC [ 6 SP q Steady Bar
*--------------------------------------
TERM.DECSCUSR	lda CsiF
				cmp #C.SPACE
				bne .8

				lda CsiPCnt
				cmp #1
				bne .8

				ldy CsiP
				cpy #7
				bcs .8

				lda TERM.Curs,y

				ldy #S.DCB.TTY.CURCHAR
				sta (ZPpDCB),y

.8				clc
				rts
*--------------------------------------
TERM.DECSTBM	lda #0
				ldx #23

				ldy CsiPCnt
				beq .8

				cpy #2
				bne .9

				ldy CsiP
				beq .9

				dey
				cpy #24
				bcs .9

				tya

				ldy CsiP+1
				beq .9

				dey
				cpy #24
				bcs .9

				phy
				plx

.8				ldy #S.DCB.TTY.SCROLLTOP
				sta (ZPpDCB),y
				iny						S.DCB.TTY.SCROLLBOT
				txa
				sta (ZPpDCB),y

.9				clc
				rts
*--------------------------------------
TERM.CUx		ldy #1

				lda CsiPCnt
				beq .1

				dec
				bne .8

				ldy CsiP
				beq .8

.1				lda .80,x
				sta .3+1
				lda .80+1,x
				sta .3+2

.2				phy

.3				jsr $FFFF				SELF MODIFIED

				ply
				dey
				bne .2

.8				clc
				rts

.80				.DA	TERM.CUU
				.DA	TERM.CUD
				.DA TERM.CUF
				.DA TERM.CUB
*--------------------------------------
TERM.CUP
TERM.HVP		lda CsiPCnt
				beq TERM.HOME

				ldx #1
				lda CsiP
				beq .2

				ldx #24
				cmp #24
				bcs .2

				tax

.2				dex
				txa
				ldy #S.DCB.TTY.CV
				sta (ZPpDCB),y
				dec CsiPCnt
				beq .8

				ldx #1
				lda CsiP+1
				beq .3

				ldx #80
				cmp #80
				bcs .3

				tax

.3				dex
				txa
				ldy #S.DCB.TTY.CH
				sta (ZPpDCB),y

.8				clc
				rts
*--------------------------------------
TERM.HOME		lda #0
				ldy #S.DCB.TTY.CV
				sta (ZPpDCB),y

				ldy #S.DCB.TTY.CH
				sta (ZPpDCB),y

				clc
				rts
*--------------------------------------
TERM.G0			ldx #$80
				.HS 2C					BIT ABS
TERM.G1			ldx #$40

				ldy #S.DCB.TTY.bG0G1ALT

				jsr RAMSW.GetZPpBuf
				cmp #'0'
				beq .1

				txa
				eor #$ff
				and (ZPpDCB),y
				bra .8

.1				txa
				ora (ZPpDCB),y
.8				sta (ZPpDCB),y

				jmp TERM.SETM.0
*--------------------------------------
TERM.RIS		jsr TERM.RESET
				jmp TERM.ED2			Erase Screen
*--------------------------------------
TERM.ENQ		ldx #0

.1				lda TERM.ENQ.REP,x

				jsr TERM.COUT
				inx
				cpx #TERM.ENQ.REP.L
				bne .1

.8				clc
				rts
*--------------------------------------
TERM.LF			ldy #S.DCB.TTY.bLNM
				lda (ZPpDCB),y
				bpl TERM.IND

TERM.NEL		jsr TERM.CR

TERM.IND		sec						scroll
				.HS 90					BCC
TERM.CUD		clc						no scroll

				php
				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				ldy #S.DCB.TTY.SCROLLBOT
				cmp (ZPpDCB),y
				bcs .1

				inc
				ldy #S.DCB.TTY.CV
				sta (ZPpDCB),y

				plp

.8				clc
				rts

.1				plp
				bcc .8

				ldy #S.DCB.TTY.SCROLLTOP
				lda (ZPpDCB),y

				tax

.2				clc
				jsr TERM.CPLX

				txa
				ldy #S.DCB.TTY.SCROLLBOT
				cmp (ZPpDCB),y
				bne .2

				bra TERM.EL2
*--------------------------------------
TERM.EL			ldx CsiPCnt
				beq TERM.EL0			Erase EOL

				lda CsiP
				beq TERM.EL0			0:Erase EOL

				dec
				beq TERM.EL1			1:Erase SOL

				dec						2:Erase Line
				beq TERM.EL2

TERM.EL.8		clc
				rts
*--------------------------------------
TERM.EL1		ldy #S.DCB.TTY.CH		End
				lda (ZPpDCB),y
				cmp #80
				bcc TERM.EL2.1

TERM.EL2		lda #80					End

TERM.EL2.1		ldy #0					Start
				bra TERM.EL0.1

TERM.EL0		ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				tay						Start
				cpy #80
				bcs TERM.EL.8

				lda #80					End

TERM.EL0.1		sty .1+1				Save Start
				sta .2+1				Save End

				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				tax

.1				ldy #$ff				SELF MODIFIED
.2				cpy #$ff				SELF MODIFIED
				bcs TERM.EL.8

				lda #" "
				jsr TERM.SetCYX			X unmodified

				inc .1+1
				bra .1
*--------------------------------------
TERM.DECSC		ldy #S.DCB.TTY.CH

				jsr .1

				iny

.1				lda (ZPpDCB),y
				iny
				sta (ZPpDCB),y
				rts
*--------------------------------------
TERM.DECRC		ldy #S.DCB.TTY.CV.SAVE

				jsr .1

				dey

.1				lda (ZPpDCB),y
				dey
				sta (ZPpDCB),y
				rts
*--------------------------------------
TERM.RI			sec						scroll
				.HS 90					BCC
TERM.CUU		clc						no scroll
				php

				ldy #S.DCB.TTY.SCROLLTOP
				lda (ZPpDCB),y

				ldy #S.DCB.TTY.CV
				cmp (ZPpDCB),y
				bcs .1

				lda (ZPpDCB),y
				dec
				sta (ZPpDCB),y

				plp

.8				clc
				rts

.1				plp
				bcc .8

				ldy #S.DCB.TTY.SCROLLBOT
				lda (ZPpDCB),y

				tax

.2				sec
				jsr TERM.CPLX

				txa
				ldy #S.DCB.TTY.SCROLLTOP
				cmp (ZPpDCB),y
				bne .2

				bra TERM.EL2
*--------------------------------------
TERM.CBLNK		ldy #S.DCB.TTY.bCURON
				lda (ZPpDCB),y
				bpl TERM.RTS

				iny						#S.DCB.TTY.CURCHAR
				lda (ZPpDCB),y
				bpl TERM.CurShow

				lda A2osX.T16
				and #CURBLNK.SPEED
				beq TERM.CurHide

				bne TERM.CurShow

TERM.RTS		clc
				rts
*--------------------------------------
TERM.CurOn		lda bActive
				ldy #S.DCB.TTY.bCURON
				and (ZPpDCB),y
				bpl TERM.RTS

TERM.CurShow	bit TERM.bCurOn
				bmi TERM.RTS

				jsr TERM.GetC
				bcs TERM.RTS			Out of screen

				sta TERM.CurSave
				dec TERM.bCurOn

				ldy #S.DCB.TTY.CURCHAR
				lda (ZPpDCB),y
				and #$7F
				cmp #C.SPACE
				bne .1

				lda TERM.CurSave
				eor #$80
				bra .8

.1				bit TERM.CurSave
				bpl .8

				ora #$80

.8				pha
				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				tay
				pla
				jmp TERM.SetCY
*--------------------------------------
TERM.CurOff		lda bActive
				ldy #S.DCB.TTY.bCURON
				and (ZPpDCB),y
				bpl TERM.RTS

TERM.CurHide	bit TERM.bCurOn
				bpl TERM.RTS

				stz TERM.bCurOn
				lda TERM.CurSave
				jmp TERM.SetC
*--------------------------------------
TERM.10OUT		jsr MATH.A2STR10NP

				ldx #0

.1				lda FOUTBuf,x
				beq TERM.RTS

				inx
				jsr TERM.COUT
				bra .1
*--------------------------------------
TERM.ESCCSI		lda #C.ESC
				jsr TERM.COUT
				lda #'['
*--------------------------------------
TERM.COUT		pha						save char

				ldy #S.DCB.TTY.OUTHEAD
				lda (ZPpDCB),y
				pha						save actual HEAD
				inc
				cmp #S.DCB.TTY.OUTBUF.MAX
				bne .1

				lda #S.DCB.TTY.OUTBUF

.1				dey						OUTTAIL
				cmp (ZPpDCB),y			HEAD+1 = TAIL ?
				beq .9					CS, full!

				iny
				sta (ZPpDCB),y			new head
				ply						old head
				pla
				sta (ZPpDCB),y
				clc
				rts

.9				pla
				pla
				rts
*--------------------------------------
TERM.ED			ldx CsiPCnt
				beq TERM.ED0

.1				lda CsiP
				beq TERM.ED0			0...

				dec						1 ?
				beq TERM.ED1

				dec						2 ?
				beq TERM.ED2

				clc
				rts
*--------------------------------------
TERM.ED0		ldy #S.DCB.TTY.CV		Erase Cursor -> Bottom
				lda (ZPpDCB),y
				tax
				ldy #24
				bra TERM.EDXY

TERM.ED1		ldx #0					Erase Top -> Cursor
				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				tay
				bra TERM.EDXY

TERM.FF			jsr TERM.HOME

TERM.ED2		ldx #0					Erase Screen
				ldy #24

TERM.EDXY		lda #0
				sta (pRWReg)

				bit TERM.FB
				bpl .4

				sty ZPTmpX

.1				cpx ZPTmpX
				bcs .8

				lda TERM.BL,x
				sta ZPL1
				lda TERM.BH,x
				sta ZPL1+1

				lda #" "

				sta IO.SETWRITEAUX
				ldy #39

.2				sta (ZPL1),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				ldy #39

.3				sta (ZPL1),y
				dey
				bpl .3

				inx
				bra .1
*--------------------------------------
.4				sei
				sta IO.SETALTZP
				jsr FB.EDXY
				sta IO.CLRALTZP
				cli

.8				lda A2osX.ActBnk
				sta (pRWReg)

				rts
*--------------------------------------
TERM.IAC		ldy #S.DCB.TTY.INBUF
				lda (ZPpDCB),y
				inc
				sta (ZPpDCB),y
				tay

				jsr RAMSW.GetZPpBuf

				sta (ZPpDCB),y
				cpy #S.DCB.TTY.INBUF+1	CMD ?
				beq .8					yes, wait for additional bytes

				ldy #S.DCB.TTY.INBUF+1
				lda (ZPpDCB),y			get back CMD
				cmp #SB
				bne .1					not a SB/SE....stop with CMD/SUBCMD

				jsr RAMSW.GetZPpBuf

				cmp #SE
				bne .8					wait for ending SE....keep bIACMode

				bra TERM.IAC.SB

				ldy #S.DCB.TTY.INBUF+1
				lda (ZPpDCB),y			get back CMD

.1				cmp #IAC
				beq TERM.IAC.9

				sec
				sbc #WILL
				bcc TERM.IAC.9

				asl
				tax

				ldy #S.DCB.TTY.INBUF+2
				lda (ZPpDCB),y			Get SUBCMD in A

				jmp (.7,x)

.7				.DA TERM.IAC.RWILL
				.DA TERM.IAC.RWONT
				.DA TERM.IAC.RDO
				.DA TERM.IAC.RDONT

.8				clc
				rts
*--------------------------------------
TERM.IAC.SB		ldy #S.DCB.TTY.INBUF+2
				lda (ZPpDCB),y			Get SUBCMD in A

				cmp #TN.O.TTYPE
				bne TERM.IAC.9

				iny
				lda (ZPpDCB),y
				cmp #SB.SEND
				bne TERM.IAC.9

				ldx #0

.1				lda TERM.TTYPE,x
				jsr TERM.COUT
				inx
				cpx #TERM.TTYPE.L
				bne .1

TERM.IAC.9		jmp TERM.SETM.0
*--------------------------------------
TERM.IAC.RWILL	ldx #TERM.WILLDO.L-1

.1				cmp TERM.WILLDO,x
				beq TERM.IAC.SDO

				dex
				bpl .1

.8				bra TERM.IAC.SDONT
*--------------------------------------
TERM.IAC.RWONT	ldx #TERM.WILLDO.L-1

.1				cmp TERM.WILLDO,x
				beq TERM.IAC.SDO

				dex
				bpl .1

				bra TERM.IAC.SDONT
*--------------------------------------
TERM.IAC.RDO	ldx #TERM.DOWILL.L-1

.1				cmp TERM.DOWILL,x
				beq TERM.IAC.SWILL

				dex
				bpl .1

				bra TERM.IAC.SWONT
*--------------------------------------
TERM.IAC.RDONT	ldx #TERM.DOWILL.L-1

.1				cmp TERM.DOWILL,x
				beq TERM.IAC.SWILL

				dex
				bpl .1

				bra TERM.IAC.SWONT
*--------------------------------------
TERM.IAC.SWILL	ldx #WILL
				.HS 2C					BIT ABS
*--------------------------------------
TERM.IAC.SWONT	ldx #WONT
				.HS 2C					BIT ABS
*--------------------------------------
TERM.IAC.SDO	ldx #DO
				.HS 2C					BIT ABS
*--------------------------------------
TERM.IAC.SDONT	ldx #DONT
*--------------------------------------
				pha						push CMD
				phx
				lda #IAC
				jsr TERM.COUT
				pla
				jsr TERM.COUT
				pla
				jsr TERM.COUT
				jmp TERM.SETM.0
*--------------------------------------
TERM.CheckOA	lda IO.OPENAPPLE
				php
				bpl .5

				bit TERM.bL0
				bmi .8

				lda #0
				sta (pRWReg)

				sei
				sta IO.SETALTZP

				lda TERM.BL
				sta ZPL1
				lda TERM.BH
				sta ZPL1+1

				sta IO.SET80STORE
				sta IO.SETPAGE2

				ldy #39

.10				lda (ZPL1),y
				sta TERM.L0+40,y
				lda #C.SPACE
				sta (ZPL1),y
				dey
				bpl .10

				sta IO.CLRPAGE2
				sta IO.CLR80STORE

				ldy #39

.11				lda (ZPL1),y
				sta TERM.L0,y
				lda #C.SPACE
				sta (ZPL1),y
				dey
				bpl .11

				sta IO.CLRALTZP

				lda A2osX.ActBnk
				sta (pRWReg)

				cli

				ldx IO.hFD
				lda pNames,x
				sta TXTPTR
				lda pNames+1,x
				sta TXTPTR+1

				lda TERM.BL
				sta ZPL1
				lda TERM.BH
				sta ZPL1+1

				ldy #0

.1				lda (TXTPTR),y
				beq .3

				cmp #$40
				bcc .2

				cmp #$60
				bcs .2

				and #$1F				remap UPPERCASE

.2				phy
				jsr TERM.SetCY
				ply
				iny
				bra .1

.3				dec TERM.bL0

				plp
				rts

.5				jsr TERM.L0Off

.8				plp
				rts
*--------------------------------------
TERM.L0Off		bit TERM.bL0
				bpl .8

				lda #0
				sta (pRWReg)

				sei
				sta IO.SETALTZP

				lda TERM.BL
				sta ZPL1
				lda TERM.BH
				sta ZPL1+1

				sta IO.SETWRITEAUX

				ldy #39

.1				lda TERM.L0+40,y
				sta (ZPL1),y
				dey
				bpl .1

				sta IO.CLRWRITEAUX

				ldy #39

.2				lda TERM.L0,y
				sta (ZPL1),y
				dey
				bpl .2

				sta IO.CLRALTZP
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

				stz TERM.bL0

.8				rts
*--------------------------------------
TERM.CPLX		lda #0
				sta (pRWReg)

				bit TERM.FB
				bmi .4

				sei
				sta IO.SETALTZP
				jsr FB.CPLX
				sta IO.CLRALTZP
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

				rts
*--------------------------------------
.4				lda TERM.BL,x
				sta ZPL2
				lda TERM.BH,x
				sta ZPL2+1
				bcs .5

				inx
				.HS B0					BCS

.5				dex
				lda TERM.BL,x
				sta ZPL1
				lda TERM.BH,x
				sta ZPL1+1

				sei
				sta IO.SET80STORE
				sta IO.SETPAGE2

				jsr .6

				sta IO.CLRPAGE2
				sta IO.CLR80STORE
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

.6				ldy #39

.7				lda (ZPL1),y
				sta (ZPL2),y
				dey
				bpl .7

				rts
*--------------------------------------
TERM.SetC		pha
				ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				tax
				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				tay
				pla
*--------------------------------------
TERM.SetCYX		cpx #24
				bcs .8

				cpy #80
				bcs .8

				sty ZPTmpX

				cmp #$40
				bcc .1

				cmp #$60
				bcs .1

				and #$1F				remap UPPERCASE

.1				ldy #S.DCB.TTY.bNORMAL
				ora (ZPpDCB),y
				sta ZPTmpChar

				ldy #S.DCB.TTY.bG0G1	Select Active Font
				lda (ZPpDCB),y
				clc
				bpl .2

				sec

.2				lda #$80
				bcc .21

				lsr

.21				iny						#S.DCB.TTY.bG0G1ALT
				and (ZPpDCB),y
				beq .3					not Graphic mode

				ldy ZPTmpChar
				cpy #$E0				Normal lowercase ?
				bcc .3

				lda TERM.E0FF-$E0,y
				sta ZPTmpChar

.3				bit TERM.FB
				bmi .6

				lda #0
				sta (pRWReg)

				ldy ZPTmpX
				lda ZPTmpChar

				sei
				sta IO.SETALTZP
				jsr FB.SetCYX
				sta IO.CLRALTZP
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

.8				rts
*--------------------------------------
.6				lda TERM.BL,x
				sta ZPL1
				lda TERM.BH,x
				sta ZPL1+1

				bra TERM.SetCSCR
*--------------------------------------
TERM.SetCY		sta ZPTmpChar
				sty ZPTmpX

TERM.SetCSCR	lsr ZPTmpX
				ldy ZPTmpX
				bcs .2

				lda #0
				sta (pRWReg)

				sei
				sta IO.SETWRITEAUX

				lda ZPTmpChar
				sta (ZPL1),y

				sta IO.CLRWRITEAUX
				cli

				lda A2osX.ActBnk
				sta (pRWReg)

				rts

.2				lda ZPTmpChar
				sta (ZPL1),y

				rts
*--------------------------------------
TERM.GetC		ldy #S.DCB.TTY.CV
				lda (ZPpDCB),y
				cmp #24
				bcs .9					Out of screen

				tax
				lda TERM.BL,x
				sta ZPL1
				lda TERM.BH,x
				sta ZPL1+1

				ldy #S.DCB.TTY.CH
				lda (ZPpDCB),y
				cmp #80
				bcs .9					Out of screen

				lsr
				tay
				bcs .2

				lda #0
				sta (pRWReg)

				sei
				sta IO.SET80STORE
				sta IO.SETPAGE2
				lda (ZPL1),y
				sta IO.CLRPAGE2
				sta IO.CLR80STORE
				cli

				pha
				lda A2osX.ActBnk
				sta (pRWReg)
				pla
				bra .7

.2				lda (ZPL1),y

.7				cmp #$20
				bcs .8

*				clc
				adc #$40				remap $00-$1F uppercase

.8				clc

.9				rts
*--------------------------------------
TERM.RESET		ldy #S.DCB.TTY.M

.1				lda .8-S.DCB.TTY.M,y
				sta (ZPpDCB),y
				iny
				cpy #S.DCB.TTY.INBUF+1
				bcc .1

				clc
				rts

.8				.DA #0					MODE
				.DA #0					CH
				.DA #0					CH.SAVE
				.DA #0					CV
				.DA #0					CV.SAVE
				.DA #0					SCROLLTOP
				.DA #23					SCROLLBOT
				.DA #$80				bCURON
				.DA #" "				CURCHAR
				.DA #$80				bNORMAL
				.DA #$80				bDECAWM
				.DA #$80				bLNM
				.DA #0					bG0G1
				.DA #0					bG0G1ALT
				.DA #S.DCB.TTY.OUTBUF	OUTTAIL
				.DA #S.DCB.TTY.OUTBUF	OUTHEAD
				.DA #S.DCB.TTY.INBUF	INBUFFER
*--------------------------------------
KeyMap			.DA #11,#10,#21,#8		esc[A,B,C,D
KeyMap.L		.EQ *-KeyMap
*--------------------------------------
CtrlChars		.DA #C.ENQ,#C.BS,#C.LF,#C.FF,#C.CR,#C.SO,#C.SI,#C.ESC
CtrlChars.L		.EQ *-CtrlChars
*--------------------------------------
EscCodes		.AZ "78DEMc[()"
EscCodes.L		.EQ *-EscCodes
EscModes		.DA #0,#0,#0,#0,#0,#0,#S.DCB.TTY.M.CSI,#S.DCB.TTY.M.G0,#S.DCB.TTY.M.G1
*--------------------------------------
CsiCodes		.AS "ABCDHJKfhlmnqr"
CsiCodes.L		.EQ *-CsiCodes
*--------------------------------------
TERM.WILLDO		.DA #TN.O.BINARY,#TN.O.ECHO
				.DA #TN.O.TTYPE,#TN.O.NAWS
TERM.WILLDO.L	.EQ *-TERM.WILLDO
*--------------------------------------
TERM.DOWILL		.DA #TN.O.BINARY
				.DA #TN.O.TTYPE,#TN.O.NAWS
TERM.DOWILL.L	.EQ *-TERM.DOWILL
*--------------------------------------
TERM.TTYPE		.DA #IAC,#SB,#TN.O.TTYPE,#SB.IS
TERM.ENQ.REP	.AS "vt100"
TERM.ENQ.REP.L	.EQ *-TERM.ENQ.REP
				.DA #IAC,#SE
TERM.TTYPE.L	.EQ *-TERM.TTYPE
*--------------------------------------
*SB.IS.NAWS		.DA #IAC,#SB,#TN.O.NAWS,#SB.IS
*				.DA 80
*				.DA 24
*				.DA #IAC,#SE
*SB.IS.NAWS.L	.EQ *-SB.IS.NAWS
*--------------------------------------
TERM.DIB		.DA #S.DIB.S.WRITE+S.DIB.S.READ
				.DA #0,#0,#0
				.PS "A2osX VT100 term"
				.DA #S.DIB.T.CHAR
				.DA #0
				.DA K.VER
*--------------------------------------
TERM.Curs		.DA #" ",#" ",#' ',#"_",#'_',#"|",#'|'
*--------------------------------------
*					`  a  b  c  d  e  f  g  h  i  j  k  l  m  n  o
TERM.E0FF		.HS 5b.41.42.43.44.45.46.47.48.49.20.5e.5a.5a.4e.4f
*					p  q  r  s  t  u  v  w  x  y  z  {  |  }  ~  DEL
				.HS 50.5c.52.53.54.55.56.57.5a.59.5a.40.51.5d.4b.5f
*--------------------------------------
TERM.FBs		.HS FF					console
				.HS 0001020304050607	tty1-8
				.HS FF					gfx
*--------------------------------------
TERM.BL			.HS	00.80.00.80.00.80.00.80.28.A8.28.A8.28.A8.28.A8.50.D0.50.D0.50.D0.50.D0
TERM.BH			.HS	04.04.05.05.06.06.07.07.04.04.05.05.06.06.07.07.04.04.05.05.06.06.07.07
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.term
LOAD usr/src/sys/kernel.s
ASM
