NEW
  AUTO 3,1
*--------------------------------------
KeyID			.EQ *
iByte			.BS 1
iBlk			.BS 1
*--------------------------------------
BLIST.New		sta SLIST.Opt

				>LDYAI 178				opt,bank + 176 hMEMs
				ldx #S.MEM.F.INIT0
				jsr MEM.Malloc.YAX		Kernel Memory
				bcs .9

				>STYA pIBlk

				lda SLIST.Opt
				sta (pIBlk)

				jsr MEM.GetBestBnk
				ldy #1
				sta (pIBlk),y

				sta (pRWReg)

				>LDYAI 256
				jsr K.Malloc
				bcs .9

				>STYA pDBlk

				txa
				ldy #2
				sta (pIBlk),y

				lda #0
				sta IO.SETWRITEAUX
				sta (pDBlk)
				sta IO.CLRWRITEAUX

				lda A2osX.ActBnk
				sta (pRWReg)

				>LDYA pIBlk
*				clc
.9				rts
*--------------------------------------
BLIST.Free		>STYA pIBlk

				ldy #1
				lda (pIBlk),y

				sta (pRWReg)

				iny

.1				sty iBlk

				lda (pIBlk),y
				beq .8

				jsr MEM.FreeAX

				ldy iBlk
				iny
				bne .1

.8				lda A2osX.ActBnk
				sta (pRWReg)

				>LDYA pIBlk
				jmp MEM.FreeYA
*--------------------------------------
BLIST.GetNByte	ldy iByte
				jsr BLIST.GetDBlkYB

BLIST.2NByte	inc iByte
				bne .8

				pha
				jsr BLIST.2NBlk
				pla
.8				rts
*--------------------------------------
BLIST.AddByte	phy
				ldy iByte
				jsr BLIST.SetDBlkY
				inc iByte
				bne .8

				phx

				jsr BLIST.AddBlk
				
				plx
				ply
				rts

.8				ply

				clc

				rts
*--------------------------------------
BLIST.AddBlk	lda SLIST.Bnk
				sta (pRWReg)

				>LDYAI 256
				jsr K.Malloc
				bcs .9

				>STYA pDBlk

				txa

				inc iBlk
				ldy iBlk
				sta (pIBlk),y

				lda #0
				sta IO.SETWRITEAUX
				sta (pDBlk)
				sta IO.CLRWRITEAUX
				
*				clc

.9				pha
				lda A2osX.ActBnk
				sta (pRWReg)
				pla

BLIST.RTS		rts
*--------------------------------------
BLIST.SetBlkByte
				ldy iByte
				jsr BLIST.SetDBlkY
				inc iByte
				bne BLIST.RTS
*--------------------------------------
BLIST.2NBlk		inc iBlk
				ldy iBlk
*--------------------------------------
BLIST.GetpDblkY	lda SLIST.Bnk
				sta (pRWReg)

				lda (pIBlk),y
				sec
				beq .9

				jsr MEM.GetPtrX

				>STYA pDBlk

				clc

.9				pha
				lda A2osX.ActBnk
				sta (pRWReg)
				pla

				rts
*--------------------------------------
BLIST.SetDBlkY	pha
				lda SLIST.Bnk
				sta (pRWReg)
				pla

				sta IO.SETWRITEAUX
				sta (pDBlk),y
				sta IO.CLRWRITEAUX

				lda A2osX.ActBnk
				sta (pRWReg)

				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.blist
LOAD usr/src/sys/kernel.s
ASM
