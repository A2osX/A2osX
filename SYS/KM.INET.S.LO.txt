NEW
  AUTO 3,1
*--------------------------------------
				.DUMMY
				.OR ZPDRV+4

ZPIOCTL			.BS 2

				.ED
*--------------------------------------
LO.CS.START		cld
				jmp (.1,x)

.1				.DA LO.STATUS
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA LO.OPEN
				.DA LO.CLOSE
				.DA LO.READ
				.DA LO.WRITE

				.DA 0
*--------------------------------------
LO.STATUS		>STYA ZPIOCTL

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta ZPpBuf
				iny
				lda (ZPIOCTL),y
				sta ZPpBuf+1

				ldy #S.IOCTL.S
				lda (ZPIOCTL),y
				beq .1

				cmp #S.IOCTL.S.GETDIB
				bne .3

				ldy #S.DIB-1

				.HS 2C					BIT ABS

.1				ldy #3

				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda LO.DIB,y
				sta (ZPpBuf),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				clc
				rts

.3				cmp #S.IOCTL.S.GETDCB
				bne .9

				ldy #S.DCB.NIC-1

				bit A2osX.IOOpt
				bpl .4

				sta IO.SETWRITEAUX

.4				lda LO.DCB,y
				sta (ZPpBuf),y
				dey
				bpl .4

				sta IO.CLRWRITEAUX

				clc
				rts

.9				lda #MLI.E.BADCTL
				sec
				rts
*--------------------------------------
LO.OPEN			lda #S.DIB.S.OPENED
				bit LO.DIB+S.DIB.S
				bne LO.E.OPEN

				tsb LO.DIB+S.DIB.S

				clc
				rts

LO.E.OPEN		lda #MLI.E.OPEN
				sec
				rts
*--------------------------------------
LO.CLOSE		lda #S.DIB.S.OPENED
				bit LO.DIB+S.DIB.S
				beq LO.E.OPEN

				trb LO.DIB+S.DIB.S

				clc
				rts
*--------------------------------------
LO.READ			>STYA ZPIOCTL

				lda LO.Tail
				cmp LO.Head
				beq LO.RW.NODATA

				tax
				ldy #S.IOCTL.BUFPTR

.1				lda LO.Queue,x
				sta (ZPIOCTL),y
				inx
				iny
				cpy #S.IOCTL.BUFPTR+4
				bcc .1

				txa
				and #63
				sta LO.Tail

				clc
				rts
*--------------------------------------
LO.RW.NODATA	lda #E.NODATA
				sec
				rts
*--------------------------------------
LO.WRITE		>STYA ZPIOCTL

				lda LO.Head
				clc
				adc #4
				and #63
				cmp LO.Tail
				beq LO.RW.NODATA		Q full

				ldy #S.IOCTL.BYTECNT
				lda (ZPIOCTL),y
				pha
				iny
				lda (ZPIOCTL),y
				ply
				>STYA A4

				>DAPI KMalloc
				bcs .9

				>STYA A2

				lda LO.Head
				tax
				clc
				adc #4
				and #63
				sta LO.Head

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta A1
				iny
				lda (ZPIOCTL),y
				sta A1+1

				lda A2
				sta LO.Queue,x
				iny
				lda A2+1
				sta LO.Queue+1,x
				lda A4
				sta LO.Queue+2,x

				lda A4+1
				sta LO.Queue+3,x

				>DAPI MemCpy

				clc
.9				rts
*--------------------------------------
LO.CS.END		.EQ *
*--------------------------------------
LO.DIB			.DA #0
				.DA #0,#0,#0			size
				.PS "Local Loopback  "
				.DA #S.DIB.T.NIC
				.DA #0					Subtype=lo
				.DA K.VER
*--------------------------------------
LO.DCB			.DA #S.DCB.T.NIC
				.DA #S.DCB.NIC.F.LO+S.DCB.NIC.F.ARPOFFLOAD
				.DA #S.DCB.NIC.LINK.OK+S.DCB.NIC.LINK.FD
				.DA #S.DCB.NIC.SPEED.10
				.HS 000000000000		no MAC
				.DA #0,#0,#0,#0			no IPOFFLOAD
				.DA #0,#0,#0,#0			no IPOFFLOAD
				.DA #0,#0,#0,#0			no IPOFFLOAD
				.BS 2					spare
				.BS 48					Stats
*--------------------------------------
LO.Tail			.BS 1
LO.Head			.BS 1
LO.Queue		.BS 16*4
*--------------------------------------
LO.ID.END		.EQ *
*--------------------------------------
MAN
SAVE usr/src/sys/km.inet.s.lo
LOAD usr/src/sys/km.inet.s
ASM
