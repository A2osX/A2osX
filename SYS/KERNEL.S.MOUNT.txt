NEW
  AUTO 3,1
*--------------------------------------
* K.Mount
* ## ASM
* `>PUSHW mountpoint`
* `>PUSHW callback`
*
*  Out : A = hMount
*--------------------------------------
K.Mount			jsr SHARED.PullP2P1		callback,mountpoint
				
				ldx #1
				
.1				lda PFTs.pHI-1,x
				beq .2
				
				inx 
				cpx #K.PFT.MAX+1
				bne .1
				
				lda #E.OOH
				sec
				rts
				
.2				lda ZPPtr1
				sta PFTs.pLO-1,x
				lda ZPPtr1+1
				sta PFTs.pHI-1,x

				lda ZPPtr2
				sta PFTs.cbLO-1,x
				lda ZPPtr2+1
				sta PFTs.cbHI-1,x
				
				txa

				clc
				rts
*--------------------------------------
* K.UMount
*  In : A = hMount
*  Out : 
*--------------------------------------
K.UMount		tax

				lda PFTs.pHI-1,x
				beq .9

				stz PFTs.pHI-1,x
				clc
				rts
				
.9				lda #E.INVH
				sec
				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.mount
LOAD usr/src/sys/kernel.s
ASM
