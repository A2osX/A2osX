NEW
  AUTO 3,1
*/--------------------------------------
* # GetMemStat
*  Y,A = Ptr to 24 bytes buffer
* ## RETURN VALUE
*  Buffer filled with memory stats
*\--------------------------------------
K.GetMemStat	>STYA FORPNT

				ldx #11

.1				stz	STAT.Total,x
				dex
				bpl .1

				lda #0

.2				sta (pRWReg)
				pha
				jsr RAMSW.GetBnkStat

				jsr STAT.Add2Total

				pla
				cmp A2osX.MaxBnk
				inc
				bcc .2

				lda A2osX.ActBnk
				sta (pRWReg)

				sta IO.SETWRITEAUX

				ldy #S.MSTAT.MaxBnk
				lda A2osX.MaxBnk
				sta (FORPNT),y

				ldx #11
				ldy #S.MSTAT.UT+11

.6				lda STAT.Total,x
				sta (FORPNT),y
				dey
				dex
				bpl .6

				ldx #7
				ldy #S.MSTAT.KL+7

.7				lda MEM.LoMem,x
				sta (FORPNT),y
				dey
				dex
				bpl .7

*				sta IO.CLRWRITEAUX

				clc
				rts
*--------------------------------------
STAT.Add2Total	sec
				lda STAT.BnkStat+4
				sbc STAT.BnkStat+0
				sta STAT.BnkT
				lda STAT.BnkStat+5
				sbc STAT.BnkStat+1
				sta STAT.BnkT+1

				sec
				lda STAT.BnkStat+4
				sbc STAT.BnkStat+2
				sta STAT.BnkU
				lda STAT.BnkStat+5
				sbc STAT.BnkStat+3
				sta STAT.BnkU+1

				sec
				lda STAT.BnkT
				sbc STAT.BnkU
				sta STAT.BnkF
				lda STAT.BnkT+1
				sbc STAT.BnkU+1
				sta STAT.BnkF+1

				ldy #0
				ldx #0
				jsr STAT.Add2T

				ldy #2
				ldx #4
				jsr STAT.Add2T

				ldy #4
				ldx #8
*--------------------------------------
STAT.Add2T		clc
				lda STAT.BnkT,y
				adc STAT.Total,x
				sta STAT.Total,x

				lda STAT.BnkT+1,y
				adc STAT.Total+1,x
				sta STAT.Total+1,x

				bcc .8

				inc STAT.Total+2,x

*				bne .8

*				inc STAT.Total+3,x

.8				rts
*--------------------------------------
				.DUMMY
				.OR K.S.STAT
STAT.BnkStat	.BS 2					LoMem
				.BS 2                   Free
				.BS 2                   HiMem
				.BS 1                   LastSlot
				.BS 1					BnkID

STAT.BnkT		.BS 2
STAT.BnkU		.BS 2
STAT.BnkF		.BS 2

STAT.Total		.BS 4
				.BS 4
				.BS 4
				.ED
*--------------------------------------
K.GetFileByID	cmp #K.FILE.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pFILEs+1,x
				beq .9

				ldy	pFILEs,x

				>STYA ZPPtr1

				>LDYAI S.FILE
				jsr K.Malloc
				bcs .99

				>STYA ZPPtr2

				sta IO.SETWRITEAUX

				ldy #S.FILE-1

.1				lda	(ZPPtr1),y
				sta (ZPPtr2),y
				dey
				bpl .1

				>LDYA ZPPtr2

*				sta IO.CLRWRITEAUX

*				clc

				rts

.9				lda #E.INVH
				sec
.99				rts
*--------------------------------------
K.GetFDByID		cmp #K.FD.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pFDs+1,x
				beq .9

				ldy	pFDs,x

				>STYA ZPPtr1

				>LDYAI S.FD
				jsr K.Malloc
				bcs .99

				>STYA ZPPtr2

				sta IO.SETWRITEAUX

				ldy #S.FD-1

.1				lda	(ZPPtr1),y
				sta (ZPPtr2),y
				dey
				bpl .1

				>LDYA ZPPtr2

*				sta IO.CLRWRITEAUX

*				clc

				rts

.9				lda #E.INVH
				sec
.99				rts
*--------------------------------------
K.GetNameByID	cmp #K.FD.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pNames+1,x
				beq .9

				ldy	pNames,x

				>STYA ZPPtr1

				ldy #$ff

.10				iny
				lda	(ZPPtr1),y
				bne .10

				iny

				jsr K.Malloc
				bcs .99

				>STYA ZPPtr2

				sta IO.SETWRITEAUX

				ldy #$FF

.1				iny
				lda	(ZPPtr1),y
				sta (ZPPtr2),y
				bne .1

				>LDYA ZPPtr2

*				sta IO.CLRWRITEAUX

*				clc

				rts

.9				lda #E.INVH
				sec
.99				rts
*--------------------------------------
K.GetPSByID		cmp #K.PS.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pPSs+1,x
				beq .9

				ldy	pPSs,x

				jmp PS.GetYAX

.9				lda #E.INVH
				sec
				rts
*--------------------------------------
K.GetPSCmdLine	jsr PS.FindYA
				bcs .9

				>STYA ZPPtr1

				ldy #S.PS.Bnk
				lda (ZPPtr1),y
				sta A3L

				lda A2osX.ActBnk
				sta A3H

				ldy #S.PS.pCmdLine
				lda (ZPPtr1),y
				pha
				iny
				lda (ZPPtr1),y
				ply

				jmp MEM.MBDupYA

.9				rts
*--------------------------------------
K.GetpFILEByID	cmp #K.FILE.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pFILEs+1,x
				beq .9

				ldy	pFILEs,x

*				clc

				rts

.9				lda #E.INVH
				sec
				rts
*--------------------------------------
K.GetpFDByID	cmp #K.FD.MAX*2
				bcs .9

				tax
				lsr
				bcs .9

				lda pFDs+1,x
				beq .9

				ldy	pFDs,x

*				clc

				rts

.9				lda #E.INVH
				sec
				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.kapi
LOAD usr/src/sys/kernel.s
ASM
