PR#3
PREFIX /A2OSX.SRC
NEW
INC 1
AUTO 6
				.LIST OFF
*--------------------------------------
* S.LoadLibYA
*  in :
*   Y,A = PTR To Lib Name
* out :
*   A = hMem To loaded LIB
*--------------------------------------
S.LoadLibYA		>STYA S.LoadLib.Name	save libname for S.FileSearch
				>LDYAI ENV.LIB			push ENVNAME=LIB
				jsr S.GetEnvYA			get value for ENV=LIB
				bcs .99
				
				>PUSHYA					Push $LIB value

				>PUSHW S.LoadLib.Name
				
				jsr S.FileSearch		find libname in $LIB
				bcs .99

				stx S.LoadLib.hFullName
				
				jsr S.LoadBinYA			Y,A = filename full path
				bcs .98
				
				stx S.LoadLib.hMem
				>STYA pLib

				ldx #LIBMGR.LOAD
				jsr pLibJmp				Call LIB.LOAD function
				
				bcs .97				

				jsr .98					Cleanup...
				
				lda S.LoadLib.hMem
				clc
				rts
				
.97				pha
				lda S.LoadLib.hMem
				jsr S.FreeMemA
				pla
				
.98				pha
				lda S.LoadLib.hFullName
				jsr S.FreeMemA
				pla
				
				sec
.99				rts
*--------------------------------------
S.LoadLib.Name	.BS 2
S.LoadLib.hFullName	.BS 1
S.LoadLib.hMem	.BS 1
*--------------------------------------
* S.UnloadLibA
*  in :
*   A = hMem To Lib
* out :
*--------------------------------------
S.UnloadLibA	pha
				jsr S.GetMemByIDA
				>STYA ZPQuickPtr1
				ldy #S.MEM.REFCNT
				lda (ZPQuickPtr1),y		Get count of those referencing this lib
				dec						only one left ?	
				beq .1
				sta (ZPQuickPtr1),y
				pla
				clc
				rts
				
.1				ldy #S.MEM.PTR
				lda (ZPQuickPtr1),y
				pha
				iny
				lda (ZPQuickPtr1),y
				ply
				>STYA pLib
				
				ldx #LIBMGR.UNLOAD
				jsr pLibJmp				Call LIB.UNLOAD function
				pla
				jmp S.FreeMemA
*--------------------------------------
ENV.LIB			>PSTRING "LIB"
MAN
SAVE SYS/KERNEL.S.LIB
LOAD SYS/KERNEL.S
ASM
