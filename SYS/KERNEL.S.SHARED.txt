NEW
  AUTO 3,1
*--------------------------------------
SHARED.IsIDValid
				cmp #'.'
				beq SHARED.8

				cmp #'_'
				beq SHARED.8
SHARED.IsDigitOrL
				cmp #'z'+1
				bcs SHARED.9

				cmp #'a'
				bcs SHARED.8

				cmp #'Z'+1
				bcs SHARED.9

				cmp #'A'
				bcs SHARED.8

				jmp ZP.IsDigit

SHARED.8		clc

SHARED.9		rts
*--------------------------------------
SHARED.AddYp12P1
				sec
*				.HS 90					BCC
*SHARED.AddY2P1	clc
				tya
				adc ZPPtr1
				sta ZPPtr1
				bcc .8

				inc ZPPtr1+1

.8				rts
*--------------------------------------
SHARED.AddYp12P2
				sec
				.HS 90					BCC
SHARED.AddY2P2	clc
				tya
				adc ZPPtr2
				sta ZPPtr2
				bcc .8

				clc						keep CC as some JMP exit here
				inc ZPPtr2+1

.8				rts
*--------------------------------------
SHARED.PutCP2	sta (ZPPtr2)

				inc ZPPtr2
				bne .8

				inc ZPPtr2+1
.8				rts
*--------------------------------------
SHARED.StrLenYA	>STYA .1+1

				lda #0
				tay

.1				ldx $ffff,y
				beq .8

				iny
				bne .1

				inc
				inc .1+2
				bra .1

.8				rts
*--------------------------------------
SHARED.PathBuf2FORPNT
				lda FORPNT+1
				bne .1

				>LDYAI K.PATH.MAX
				jsr K.Malloc
				bcs .9

				>STYA FORPNT

.1				sta IO.SETWRITEAUX

				ldy #$ff

.2				iny
				lda K.PathBuf,y
				sta (FORPNT),Y
				bne .2

				sta IO.CLRWRITEAUX

				>LDYA FORPNT

				clc

.9				rts
*--------------------------------------
SHARED.GethFDpFDA
				sta IO.hFD
				tay
				lda pFDs+1,y
				beq .9

				sta pFD+1
				lda pFDs,y
				sta pFD

				clc
				rts

.9				lda #E.INVH
				sec
				rts
*--------------------------------------
SHARED.GethFDPathBuf
				lda pNames,y
				sta .2+1
				lda pNames+1,y
				sta .2+2

				ldy #$ff

.1				iny

.2				lda $FFFF,y				SELF MODIFIED
				sta K.PathBuf,y
				bne .1

				rts
*--------------------------------------
SHARED.ClrStat	ldx #S.STAT-1

.1				stz K.S.STAT,x
				dex
				bpl .1

				lda A2osX.FSID
				sta K.S.STAT+S.STAT.FSID

				lda #2
				sta K.S.STAT+S.STAT.BLKSIZE+1		= 512 bytes

				rts
*--------------------------------------
* Convert A to 2 hex digits in AX
*--------------------------------------
SHARED.A2HexAX	pha

				and #$0F
				jsr .8

				tax

				pla
				lsr
				lsr
				lsr
				lsr

.8				ora #$30
				cmp #'9'+1
				bcc .9

				adc #6

.9				rts
*--------------------------------------
SHARED.xFORPNTpn
				sta IO.SETWRITEAUX
				sta (FORPNT)
				sta IO.CLRWRITEAUX

				inc FORPNT
				bne .9

				inc FORPNT+1

.9				rts
*--------------------------------------
MAN
SAVE usr/src/sys/kernel.s.shared
LOAD usr/src/sys/kernel.s
ASM
