NEW
  AUTO 3,1
*--------------------------------------
ONLINE LDA BUFFER.BASES
       STA MISC.PARMS+3
       LDA #0
       STA MISC.PARMS+2
       STA MISC.PARMS+1
       JSR MLI.C5   (Might clobber DRIVER.PTR)
*---Now display the list---------
       LDA BUFFER.BASES
       STA DRIVER.PTR+1
       LDA #0
       STA DRIVER.PTR
.1     PHA
       TAY
       LDA (DRIVER.PTR),Y
       BEQ .5            ...END OF LIST
       PHA
       LDA #"S"
       JSR COUT
       PLA
       PHA
       LSR               ISOLATE SLOT NUMBER
       LSR
       LSR
       LSR
       AND #7
       ORA #"0"
       JSR COUT          PRINT SLOT NUMBER
       LDA #","
       JSR COUT
       LDA #"D"
       JSR COUT
       PLA
       PHA
       ASL               SET CARRY IF DRIVE 2
       LDA #"1"          ASSUME DRIVE 1
       ADC #0            CHANGE TO 2 IF TRUE
       JSR COUT
       LDA #" "     PRINT SPACE
       JSR COUT
       PLA          get dsssllll again
       AND #$0F     isolate length
       BEQ .3       no name, show error code
       TAX
       LDA #"/"
.2     JSR COUT
       INY          PRINT THE VOLUME OR FILE NAME
       LDA (DRIVER.PTR),Y
       ORA #$80
       DEX
       BPL .2
       LDA #"/"
       BNE .4       ...ALWAYS
.3     LDA #"("
       JSR COUT
       INY
       LDA (DRIVER.PTR),Y    GET ERROR CODE
       JSR PRBYTE
       LDA #")"
.4     JSR COUT
       JSR CROUT
*--------------------------------
.5     CLC               POINT TO NEXT VOLUME NAME
       PLA
       ADC #16
       BCC .1       STILL IN SAME PAGE
       CLC          NO ERROR!
       RTS
*--------------------------------------
MAN
SAVE usr/src/scmasm.31/sci.s.online
LOAD usr/src/scmasm.31/scmasm.s
ASM
