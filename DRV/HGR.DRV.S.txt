NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR $2000
				.TF drv/hgr.drv
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
				.INB inc/io.i
				.INB inc/gfx.i
*--------------------------------------
pRWReg			.EQ $10+14

A1				.EQ $3C
A2				.EQ $3E
A3				.EQ $40
A4				.EQ $42

TXTPTR			.EQ $B8
*--------------------------------------
DRV.DST			.EQ $4000
*--------------------------------------
				.DUMMY
				.OR ZPBIN

ZPPtr1			.BS 2
ZPPtr2			.BS 2

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp Dev.Detect			cld,jmp abs=DRV
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					DRV Layout Version 2
				.DA 0
				.DA CS.END
				.DA ID.END
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------
L.MSG.DETECT	.DA MSG.DETECT
L.DRV.CS.START	.DA DRV.CS.START
L.FD.DEV		.DA FD.DEV
L.FD.DEV.NAME	.DA FD.DEV.NAME
				.DA 0
*--------------------------------------
Dev.Detect		>LDYA L.MSG.DETECT
				>LIBC PutS

				php
				sei
				>LDYAI $4000+DRV.L
				sta IO.CLRWRITEAUX
				>STYA MEM.LoMem
				sta IO.SETWRITEAUX

				jsr DRV.Install

				sta IO.SETHIRES

				plp

				>SS
				>PUSHW L.FD.DEV
				>PUSHW L.FD.DEV.NAME
				>LIBC MKDev
				>SR

				rts
*--------------------------------------
DRV.Install		>LDYA L.DRV.CS.START
				>STYA ZPPtr1

				>LDYAI DRV.DST
				>STYA ZPPtr2

				lda /DRV.L
				eor #$ff
				pha

				lda #DRV.L
				eor #$ff
				tax

				ldy #0

				sta IO.CLRWRITEAUX

.1				inx
				bne .2

				pla
				inc
				beq .3

				pha

.2				lda (ZPPtr1),y
				sta (ZPPtr2),y

				iny
				bne .1

				inc ZPPtr1+1
				inc ZPPtr2+1
				bra .1

.3				sta IO.SETWRITEAUX

				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.DETECT		.AZ "Apple IIe/IIc HGR Driver."
*--------------------------------------
*			Device Header (16 Bytes)
*--------------------------------------
FD.DEV			.DA #S.FD.T.CDEV
				.DA #0					HANDLER
				.DA #$F					BUSID
				.DA #9					DEVID
				.DA 0					BUSPTR
				.DA DRV.DST				DRVPTR
				.DA 0					DCBPTR
				.DA 0					BUFPTR
FD.DEV.NAME		.AZ "/dev/gfx"
*--------------------------------------
*			Driver Code
*--------------------------------------
DRV.CS.START	.PH DRV.DST

DRV.START		.INB usr/src/drv/hgr.drv.s.core
				.INB usr/src/drv/hgr.drv.g
				.INB usr/src/shared/x.hgr.g
*--------------------------------------
DIB				.DA #0
				.DA #0,#0,#0
				.PS "Apple II HGR"
				.BS 4
				.DA #S.DIB.T.GFX
				.DA #0
				.DA K.VER
*--------------------------------------
DCB				.DA #S.DCB.T.GFX
				.BS 1					DEV.ID
				.DA #0					S.DCB.GFX.S default to 0
				.DA #S.CB.M.C8			F
				.DA 40					W
				.DA 192					H
*--------------------------------------
CB.Cache		.BS S.CB
BM.Cache		.BS S.BM
BM.SrcRWBnk		.BS 1
BM.SrcLCBnk		.BS 1
FON.Cache		.BS S.FON
FONT.SrcRWBnk	.BS 1
FONT.SrcLCBnk	.BS 1
FONT.Base		.BS 1
FONT.ByteW		.BS 1
FONT.GlyphBytes	.BS 1
*--------------------------------------
DRV.END			.EP
*--------------------------------------
				.LIST ON
DRV.L			.EQ DRV.END-DRV.START
				.LIST OFF
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
MAN
SAVE usr/src/drv/hgr.drv.s
ASM
