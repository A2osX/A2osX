NEW
  AUTO 3,1
*--------------------------------------
LBUF.C1			.BS 1
LBUF.C1.MASK	.BS 1	Bits to CLR in VMEM : 11100000 00000000 00111111
LBUF.C1.DATA	.BS 1	Bits to SET/ORA/XOR : 000ccccc cccccccc cc000000
LBUF.C2			.BS 1							 C1				   C2
LBUF.C2.MASK	.BS 1
LBUF.C2.DATA	.BS 1
*--------------------------------------
LBUF.SetBounds1X1
				>LDYA CB.Cache+S.CB.X1
*--------------------------------------
LBUF.SetBounds1YA
				jsr DIVMOD7YA			A=DIV, X=MOD
				sta LBUF.C1

				lda C1.MASK,x
				sta LBUF.C1.MASK
				lda C1.DATA,x
				sta LBUF.C1.DATA
				rts
*--------------------------------------
LBUF.SetBounds2X1W
				lda CB.Cache+S.CB.X1
				clc
				adc CB.Cache+S.CB.SrcW
				pha

				lda CB.Cache+S.CB.X1+1
				adc CB.Cache+S.CB.SrcW+1

				ply
				bne .1

				dec

.1				dey
*--------------------------------------
LBUF.SetBounds2YA
				jsr DIVMOD7YA			A=DIV, X=MOD
				sta LBUF.C2
				cmp LBUF.C1
				beq .1

				lda C2.MASK,x
				sta LBUF.C2.MASK
				lda C2.DATA,x
				sta LBUF.C2.DATA
				rts

.1				lda C2.MASK,x			C1=C2, go combine in C1
				tsb	LBUF.C1.MASK
				lda C2.DATA,x
				tsb LBUF.C1.DATA
				rts
*--------------------------------------
LBUF.DrawAtY	lda HGR.L,y				setup line Base Ptr
				sta ZPpHGR
				lda HGR.H,y
				sta ZPpHGR+1

				sta IO.SET80STORE

				lda CB.Cache+S.CB.OP
				and #$F
				tax
				jmp (.1,x)

.1				.DA $ffff
				.DA LBUF.DrawAtY.XOR
				.DA LBUF.DrawAtY.SET
				.DA LBUF.DrawAtY.ORA
				.DA $ffff
				.DA LBUF.DrawAtY.AND.XOR
				.DA LBUF.DrawAtY.AND.SET
				.DA LBUF.DrawAtY.AND.ORA
*--------------------------------------
LBUF.DrawAtY.SET
LBUF.DrawAtY.AND.SET
				ldx LBUF.C1				x C1->C2

				>X2PageY

				lda (ZPpHGR),y			Clear screen at C1
				and LBUF.C1.MASK		with MASK bits
				sta GBYTE1

				lda LBUF.DATA,x			get DATA bits
				and LBUF.C1.DATA		set ONLY bits starting at X1 mod 7
				ora GBYTE1				ora with screen bits
				sta (ZPpHGR),y			update screen

				inx
				cpx LBUF.C2
				beq .2					C1=C2, go setup C2

				bcs .8					C1+1 > C2, we are done...

.1				>X2PageY

				lda LBUF.DATA,x			set all bytes between C1+1 & C2-1
				sta (ZPpHGR),y

				inx
				cpx LBUF.C2
				bcc .1

.2				>X2PageY

				lda (ZPpHGR),y			Get C2
				and LBUF.C2.MASK		clear ONLY bits ending at X2 mod 7
				sta GBYTE1

				lda LBUF.DATA,x			get DATA bits
				and LBUF.C2.DATA		set ONLY bits ending at X2 mod 7
				ora GBYTE1
				sta (ZPpHGR),y			update screen

.8				sta IO.CLRPAGE2
				sta IO.CLR80STORE
				clc
				rts
*--------------------------------------
LBUF.DrawAtY.XOR
LBUF.DrawAtY.AND.XOR
				lda #$51				EOR (Indirect),Y
				bra LBUF.DrawAtY.XXX
*--------------------------------------
LBUF.DrawAtY.ORA
				lda #$11				ORA (Indirect),Y
LBUF.DrawAtY.XXX
				sta .10
				sta .20
				sta .30

				ldx LBUF.C1				x C1->C2

				>X2PageY

				lda LBUF.DATA,x			get DATA bits
				and LBUF.C1.DATA		set ONLY bits starting at X1 mod 7

.10			eor (ZPpHGR),y
				sta (ZPpHGR),y			update screen
				inx
				cpx LBUF.C2
				beq .2					C1=C2, go setup C2

				bcs .8					C1+1 > C2, we are done...

.1				>X2PageY

				lda LBUF.DATA,x			set all bytes between C1+1 & C2-1
.20			eor (ZPpHGR),y
				sta (ZPpHGR),y

				inx
				cpx LBUF.C2
				bne .1

.2				>X2PageY

				lda LBUF.DATA,x			get DATA bits
				and LBUF.C2.DATA		set ONLY bits ending at X2 mod 7

.30			eor (ZPpHGR),y
				sta (ZPpHGR),y			update screen

.8				sta IO.CLRPAGE2
				sta IO.CLR80STORE
				clc
				rts
*--------------------------------------
LBUF.DrawAtY.AND.ORA
				ldx LBUF.C1				x C1->C2

				>X2PageY

				lda LBUF.MASK,x			get MASK bits
				ora LBUF.C1.MASK		clear ONLY bits starting at X1 mod 7
			and (ZPpHGR),y
				sta GBYTE1

				lda LBUF.DATA,x
				and LBUF.C1.DATA
			ora GBYTE1
				sta (ZPpHGR),y			update screen

				inx
				cpx LBUF.C2
				beq .2					C1=C2, go setup C2

				bcs .8					C1+1 > C2, we are done...

.1				>X2PageY

				lda (ZPpHGR),y
			and LBUF.MASK,x
			ora LBUF.DATA,x				set all bytes between C1+1 & C2-1

				sta (ZPpHGR),y			update screen

				inx
				cpx LBUF.C2
				bne .1

.2				>X2PageY

				lda LBUF.MASK,x			get MASK bits
				ora LBUF.C2.MASK
			and (ZPpHGR),y
				sta GBYTE1

				lda LBUF.DATA,x			get DATA bits
				and LBUF.C2.DATA		set ONLY bits ending at X2 mod 7
			ora GBYTE1
				sta (ZPpHGR),y			update screen

.8				sta IO.CLRPAGE2
				sta IO.CLR80STORE
				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.lbuf
LOAD usr/src/drv/dhgr.drv.s
ASM
