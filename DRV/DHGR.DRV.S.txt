NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR $2000
				.TF drv/dhgr.drv
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/kernel.i
				.INB inc/mli.i
				.INB inc/mli.e.i
				.INB inc/io.i
				.INB inc/gfx.i
				.INB inc/gfx.eve.i
*--------------------------------------
DRV.DST			.EQ $4000
*--------------------------------------
				.DUMMY
				.OR ZPBIN

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp Dev.Detect			cld,jmp abs=DRV
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					DRV Layout Version 2
				.DA 0
				.DA CS.END
				.DA ID.END
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------
L.MSG.DETECT	.DA MSG.DETECT
L.MSG.HWCheck	.DA MSG.DETECT.IIC
				.DA MSG.DETECT.EVE
				.DA MSG.DETECT.80C
L.DRV.CS.START	.DA DRV.CS.START
L.FD.DEV		.DA FD.DEV
L.FD.DEV.NAME	.DA FD.DEV.NAME
				.DA 0
*--------------------------------------
Dev.Detect		>LDYA L.MSG.DETECT
				>LIBC PutS

				jsr DRV.HWCheck
				>LDYA L.MSG.HWCheck,x
				>LIBC PutS

				php
				sei
				>LDYAI $4000+DRV.L
				sta IO.CLRWRITEAUX
				>STYA MEM.LoMem
				sta IO.SETWRITEAUX

*				>LDYAI $4000
*				>STYA MEM.LoMem

				jsr DRV.Install

				sta IO.SETHIRES

				plp

				>SS
				>PUSHW L.FD.DEV
				>PUSHW L.FD.DEV.NAME
				>LIBC MKDev
				>SR

				rts
*--------------------------------------
DRV.HWCheck		ldx #0

*				stz DCB+S.DCB.GFX.S		//c : 80c Mode

				lda A2osX.HWT
				cmp #A2osX.HWT.IIc
				bcs .8

				sei
				sta IO.SET80STORE
				sta IO.SETPAGE2
				ldx $400				Save Aux $400
				lda #$ff				Make sure !=1 for comparing later
				sta $400

				sta IO.CLRPAGE2
				ldy $400				Save Main $400
				lda #$01				Select Foreground/BKgrnd Colors
				sta TXT16.ON			Activate 16 color mode
				sta $400				Store something in Main
				sta IO.SETPAGE2
				eor $400				read back AUX, If EVE, must be F/BG colors
				bne .2

				dec DCB+S.DCB.GFX.S		0=80C,$ff=EVE

.2				stx $400				Set back Aux $400
				sta IO.CLRPAGE2
				sty $400				Set back Main $400
				sta TXT16.OFF
				sta IO.CLR80STORE
				cli

				ldx #2

				bit DCB+S.DCB.GFX.S
				bmi .8

				inx
				inx

.8				rts
*--------------------------------------
DRV.Install		>LDYA L.DRV.CS.START
				>STYA ZPPtr1

				>LDYAI DRV.DST
				>STYA ZPPtr2

				lda /DRV.L
				eor #$ff
				pha

				lda #DRV.L
				eor #$ff
				tax

				ldy #0

				sta IO.CLRWRITEAUX

.1				inx
				bne .2

				pla
				inc
				beq .3

				pha

.2				lda (ZPPtr1),y
				sta (ZPPtr2),y

				iny
				bne .1

				inc ZPPtr1+1
				inc ZPPtr2+1
				bra .1

.3				sta IO.SETWRITEAUX

				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.DETECT		.AZ "Apple IIe/IIc DHGR Driver."
MSG.DETECT.IIC	.AZ "Apple //c,IIgs : 'LCM Feline/80c' Mode."
MSG.DETECT.EVE	.AZ "Apple //e : 'LCM Eve' Board Detected."
MSG.DETECT.80C	.AZ "No Specific H/W Found, 'Video7 80c' Mode."
*--------------------------------------
*			Device Header (16 Bytes)
*--------------------------------------
FD.DEV			.DA #S.FD.T.CDEV
				.DA #0					HANDLER
				.DA #$F					BUSID
				.DA #9					DEVID
				.DA 0					BUSPTR
				.DA DRV.DST				DRVPTR
				.DA 0					DCBPTR
				.DA 0					BUFPTR
FD.DEV.NAME		.AZ "/dev/gfx"
*--------------------------------------
*			Driver Code
*--------------------------------------
DRV.CS.START	.PH DRV.DST

DRV.START		.INB usr/src/drv/dhgr.drv.s.core
				.INB usr/src/drv/dhgr.drv.s.blt
				.INB usr/src/drv/dhgr.drv.s.font
				.INB usr/src/drv/dhgr.drv.s.lbuf
				.INB usr/src/drv/dhgr.drv.s.line
				.INB usr/src/drv/dhgr.drv.s.pix
				.INB usr/src/drv/dhgr.drv.s.rect
*				.INB usr/src/drv/dhgr.drv.s.osd
				.INB usr/src/drv/dhgr.drv.g
				.INB usr/src/shared/x.hgr.g
				.INB usr/src/shared/x.dhgr.g
*--------------------------------------
DIB				.DA #0
				.DA #0,#0,#0
				.PS "Apple II DHGR"
				.BS 3
				.DA #S.DIB.T.GFX
				.DA #0
				.DA K.VER
*--------------------------------------
DCB				.DA #S.DCB.T.GFX
				.BS 1					DEV.ID
				.DA #0					S.DCB.GFX.S default to 0
				.DA #S.CB.M.MONO+S.CB.M.C16	F
				.DA 560					W
				.DA 192					H
*--------------------------------------
CB.Cache		.BS S.CB
BM.Cache		.BS S.BM
FON.Cache		.BS S.FON

LBUF.MASK		.BS 81					81 because of sta LBUF.DATA+1,x!!!
LBUF.DATA		.BS 81
*--------------------------------------
DRV.END			.EP
*--------------------------------------
				.LIST ON
DRV.L			.EQ DRV.END-DRV.START
				.LIST OFF
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s
ASM
