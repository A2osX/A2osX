NEW
  AUTO 3,1
*--------------------------------------
*				.MA X2PageY
*				txa
*				lsr
*				ldy #IO.SETPAGE2		col 0,2,4...in AUX mem
*				bcc :1					col 1,3,5...in MAIN mem
*
*				dey						CLRPAGE2
*
*:1				sta $C000,y
*
*				tay
*				.EM
*--------------------------------------
				.MA X2PageY
				ldy X2Page,x			IO.SETPAGE2,IO.CLRPAGE2
				sta $C000,y
				ldy X2Y,x
				.EM
*--------------------------------------
*				.MA X2BankY
*				ldy X2Bank,x			IO.SETWRITEAUX,IO.CLRWRITEAUX
*				sta $C000,y
*				ldy X2Y,x
*				.EM
*--------------------------------------
				.DUMMY
				.OR ZPDRV+4

ZPIOCTL			.BS 2
ZPpHGR			.BS 2

ZPBMShiftPtr	.BS 2					BLT
*ZPBMSavePtr	.BS 2					BLT
ZPBMMaskPtr		.BS 2					BLT
ZPBMDataPtr		.BS 2					BLT
ZPScrShiftPtr	.BS 2					BLT

ZPDstBMPtr		.BS 2					OSD

*ZPFontPtr		.BS 2					FONT
*ZPTablePtr		.BS 2					FONT
ZPGlyphPtr		.BS 2					FONT

*ZPTmpPtr		.BS 2					OSD
GWORD			.BS 2
GBYTE1			.BS 1
*GBYTE2			.BS 1					OSD

				.ED
*--------------------------------------
DRV				cld
				jmp (.1,x)

.1				.DA STATUS
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA CONTROL
				.DA A2osX.BADCALL
				.DA OPEN
				.DA CLOSE
				.DA READ
				.DA WRITE
*--------------------------------------
STATUS			>STYA ZPIOCTL

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta ZPpBuf
				iny
				lda (ZPIOCTL),y
				sta ZPpBuf+1

				ldy #S.IOCTL.S
				lda (ZPIOCTL),y
				beq .1

				cmp #S.IOCTL.S.GETDIB
				bne STATUS.DCB

				ldy #S.DIB-1
				.HS 2C					bit abs

.1				ldy #3

				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda DIB,y
				sta (ZPpBuf),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				clc
				rts

STATUS.DCB		cmp #S.IOCTL.S.GETDCB
				bne STATUS.9

				ldy #S.DCB.GFX-1
				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda DCB,y
				sta (ZPpBuf),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				clc
				rts

STATUS.9		lda #MLI.E.BADCTL
				sec
				rts
*--------------------------------------
OPEN			tax						DEV.ID in A

				lda #S.DIB.S.OPENED
				bit DIB+S.DIB.S
				bne CLOSE.IOE

				lda A2osX.SCRNDEVS+9
				beq .1

				lda #E.OOH
				sec
				rts

.1				stx A2osX.SCRNDEVS+9
				stx DCB+S.DCB.GFX.DEVID

				lda #S.DIB.S.OPENED
				tsb DIB+S.DIB.S

				jsr ClrScr
*--------------------------------------
CONTROL			lda #9
				cmp A2osX.ASCREEN
				beq .8

				sta A2osX.ASCREEN

				ldy #0

				bit DCB+S.DCB.GFX.S
				bpl .1					//c,EVE mode

				ldy #CONTROL.EVE-CONTROL.80C

.1				ldx CONTROL.80C,y
				beq .8					Ending 0

				sta $C000,x

				iny
				bra .1

.8				clc
				rts

CLOSE.IOE		lda #MLI.E.IO
				sec
				rts
*--------------------------------------
CLOSE			lda #S.DIB.S.OPENED

				bit DIB+S.DIB.S
				beq CLOSE.IOE

				trb DIB+S.DIB.S

*				stz A2osX.SCRNDEVS+9

				clc
				rts
*--------------------------------------
READ			>STYA ZPIOCTL

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				cmp A2osX.ASCREEN
				bne .9

				bit IO.OPENAPPLE
				bmi .9

				lda IO.KBD
				bpl .9

				sta IO.KBDSTROBE

				and #$7F
				tax

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta ZPpBuf
				iny
				lda (ZPIOCTL),y
				sta ZPpBuf+1
				iny
				lda #1
				sta (ZPIOCTL),y
				dec
				iny
				sta (ZPIOCTL),y
				txa
				sta IO.SETWRITEAUX
				sta (ZPpBuf)
				sta IO.CLRWRITEAUX

				clc
				rts

.9				lda	#E.NODATA
				sec
				rts
*--------------------------------------
WRITE			>STYA ZPIOCTL

				ldy #S.IOCTL.BYTECNT
				lda (ZPIOCTL),y

				tax

				dey
				lda (ZPIOCTL),y
				sta ZPpBuf+1
				dey
				lda (ZPIOCTL),y
				sta ZPpBuf

				ldy #0

.1				jsr A2osX.GetpBuf
				sta CB.Cache,y
				>INCW ZPpBuf

				iny
				dex
				bpl .1

				lda CB.Cache
				asl
				tax

				bcs .2

				jmp (.7-2,x)

.2				brk
				jmp (.8-2,x)

.7				.DA SETPIXEL
				.DA GETPIXEL
				.DA HLINE
				.DA VLINE
				.DA FILLRECT
				.DA BITBLT
				.DA GETRECTBUFSIZE
				.DA FONT.DrawText
				.DA FONT.GetTextW

.8				.DA 0
*--------------------------------------
* IN:
* Y = LO
* A = HI
* OUT:
* A = DIV
* X = MOD
*--------------------------------------
DIVMOD7YA		dec
				bmi .2

				clc
				beq .1

				lda DIV7.512,y
				adc #$49
				ldx MOD7.512,y
				rts

.1				lda DIV7.256,y
				adc #$24
				ldx MOD7.256,y
				rts

.2				lda DIV7.0,y
				ldx MOD7.0,y
				rts
*--------------------------------------
* IN:
* A = num1 (8)
* X = num2 (8)
* OUT:
* A = (A * X (8)
*--------------------------------------
AMultX			stx GBYTE1

				sta GWORD

				lda #0					Result
				bra .3

.1				clc
				adc GWORD

.2				asl GWORD

.3				lsr GBYTE1
				bcs .1

				bne .2

				rts
*--------------------------------------
* IN:
* Y,A = num1 (16)
* X = num2 (8)
* OUT:
* Y,A = (Y,A) * X
*--------------------------------------
YAMultX			stx GBYTE1

				sty GWORD
				sta GWORD+1

				ldy #0					Result LO
				tya						Result HI
				bra .3

.1				pha
				tya
				clc
				adc GWORD
				tay
				pla
				adc GWORD+1

.2				asl GWORD
				rol GWORD+1

.3				lsr GBYTE1
				bcs .1

				bne .2

				rts
*--------------------------------------
ClrScr			lda #0
				sta (pRWReg)

				lda #$55

				ldx #0

*				sta IO.SETHIRES

.1				ldy HGR.L,x
				sty ZPPtr1
				ldy HGR.H,x
				sty ZPPtr1+1

				sei
				sta IO.SETWRITEAUX
				jsr .2

				eor #$7F

				sta IO.CLRWRITEAUX
				cli
				jsr .2

				inx
				cpx #192
				bne .1

				lda A2osX.ActBnk
				sta (pRWReg)

				rts

.2				ldy #39

.3				sta (ZPPtr1),y
				dey
				bpl .3

				rts
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.core
LOAD usr/src/drv/dhgr.drv.s
ASM
