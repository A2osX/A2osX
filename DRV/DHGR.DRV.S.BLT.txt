NEW
  AUTO 3,1
*--------------------------------------
* BitBlt
*  S.CB.X1 : Destination X (screen)
*  S.CB.Y1 : Destination Y (screen)
*  S.CB.DstX : X1 in bitmap/mask
*  S.CB.DstY : Y1 in bitmap/mask
*  S.CB.SrcW : width in bitmap/mask
*  S.CB.SrcH : height in bitmap/mask
*  S.CB.pSrc : pointer to S.BM
*  S.CB.pDst : pointer to Save/restore Buffer
*--------------------------------------
BLT.CMASK		.BS 1
BLT.ScrColIdx	.BS 1
BLT.ScrBitOfs	.BS 1

BLT.BMBitOfs0	.BS 1
BLT.BMBitOfsL	.BS 1

BLT.BMByteOfs0	.BS 1
BLT.BMByteOfsL	.BS 1

BLT.SrcRWBnk	.BS 1

BLT.BMMaskPtr	.BS 2
BLT.BMDataPtr	.BS 2
*--------------------------------------
BLT.GetDByteY	.EQ $110
*--------------------------------------
BLT.110			sta IO.SETREADAUX
				lda (ZPBMDataPtr),y
				sta IO.CLRREADAUX
				rts

BLT.110.L		.EQ *-BLT.110
*--------------------------------------
BLT.Setup110	ldy #BLT.110.L

.1				lda	BLT.110,y
				sta $110,y
				dey
				bpl .1

				rts
*--------------------------------------
BITBLT			jsr LBUF.SetBounds1X1
				stx BLT.ScrBitOfs

				jsr LBUF.SetBounds2X1W

				lda CB.Cache+S.CB.SrcH
				sta GBYTE1
*--------------------------------------
				lda CB.Cache+S.CB.OP
				bit #S.CB.OP.RESTORE
				beq BITBLT.0

				>LDYA CB.Cache+S.CB.pDst
				>STYA ZPpBuf

				lda pRWReg
				sta .4+1

				ldy CB.Cache+S.CB.Y1

.2				lda HGR.L,y				setup line Base Ptr
				sta ZPpHGR
				lda HGR.H,y
				sta ZPpHGR+1

				phy

				ldx LBUF.C1

.3				jsr A2osX.GetpBuf

.4				stz $C000				SELF MODIFIED

*				>X2BankY

				ldy X2Bank,x			IO.SETWRITEAUX,IO.CLRWRITEAUX
				sta $C000,y
				ldy X2Y,x

				sta (ZPpHGR),y

				lda A2osX.ActBnk
				sta (pRWReg)

				>INCW ZPpBuf

				cpx LBUF.C2
				inx
				bcc .3

				ply
				iny
				dec GBYTE1
				bne .2

*				sta IO.CLRWRITEAUX

BITBLT.8		clc

BITBLT.RTS		rts
*--------------------------------------
BITBLT.0		bit #S.CB.OP.SAVE
				beq BITBLT.1

				>LDYA CB.Cache+S.CB.pDst
				>STYA ZPpBuf

				lda pRWReg
				sta .3+1
				sta .4+1

				sta IO.SETWRITEAUX

				ldy CB.Cache+S.CB.Y1

.2				lda HGR.L,y				setup line Base Ptr
				sta ZPpHGR
				lda HGR.H,y
				sta ZPpHGR+1

				phy

				ldx LBUF.C1

.3				stz $C000				SELF MODIFIED

				sta IO.SET80STORE
				>X2PageY
				lda (ZPpHGR),y
				sta IO.CLRPAGE2
				sta IO.CLR80STORE

				ldy A2osX.ActBnk

.4				sty $C000				SELF MODIFIED

				sta (ZPpBuf)

				>INCW ZPpBuf

				cpx LBUF.C2
				inx
				bcc .3

				ply
				iny
				dec GBYTE1
				bne .2

				sta IO.CLRWRITEAUX

				lda CB.Cache+S.CB.OP
*--------------------------------------
BITBLT.1		and #$0F
				beq BITBLT.8

				lda CB.Cache+S.CB.hSrc+1
				beq .2

				ldy CB.Cache+S.CB.hSrc

				>DAPI GetpLCBuf
				bcs BITBLT.RTS

				sta BLT.SrcRWBnk
				sta (pRWReg)
				bit $C000,x

				sei
				sta IO.SETALTZP

				stz ZPBMDataPtr
				sty ZPBMDataPtr+1

				ldy #S.BM-1

.1				lda (ZPBMDataPtr),y
				sta BM.Cache,y
				dey
				bpl .1

				jsr BLT.SetupPtrs

				>LDYAI BLT.Get7LC
				bra BITBLT.3
*--------------------------------------
.2				>LDYA CB.Cache+S.CB.pSrc
				ldx A2osX.ActBnk

				sei
				sta IO.SETALTZP

				>STYA ZPBMDataPtr
				stx BLT.SrcRWBnk

				jsr BLT.Setup110

				ldy #S.BM-1

.3				jsr BLT.GetDByteY
				sta BM.Cache,y
				dey
				bpl .3

				jsr BLT.SetupPtrs
				bra BITBLT.2
*--------------------------------------
BITBLT.FONT		sei
				sta IO.SETALTZP

				>STYA ZPBMDataPtr
				stx BLT.SrcRWBnk

				jsr BLT.Setup110
*--------------------------------------
BITBLT.2		>LDYAI BLT.Get7RAM
*--------------------------------------
BITBLT.3		>STYA BITBLT.LOOP0.1+1
				>STYA BITBLT.LOOP0.3+1
				>STYA BITBLT.LOOPx.1+1
				>STYA BITBLT.LOOPx.5+1

				lda BM.Cache+S.BM.F
				eor #S.BM.F.BBP1
				cmp #1
				lda #0
				ror
				sta BLT.CMASK

				>LDYA CB.Cache+S.CB.SrcY
				ldx BM.Cache+S.BM.RowBytes
				jsr YAMultX
				pha
				tya
				clc
				adc ZPBMDataPtr
				sta BLT.BMDataPtr
				pla

				pha
				adc ZPBMDataPtr+1
				sta BLT.BMDataPtr+1

				tya
				adc ZPBMMaskPtr
				sta BLT.BMMaskPtr

				pla
				adc ZPBMMaskPtr+1
				sta BLT.BMMaskPtr+1

				lda CB.Cache+S.CB.SrcX
				pha
				and #7
				sta BLT.BMBitOfs0
				pla
				lsr CB.Cache+S.CB.SrcX+1
				ror
				lsr CB.Cache+S.CB.SrcX+1
				ror
				lsr CB.Cache+S.CB.SrcX+1
				ror
				sta BLT.BMByteOfs0

				lda BM.Cache+S.BM.H
				cmp CB.Cache+S.CB.SrcH
				bcs .5

				sta CB.Cache+S.CB.SrcH

.5				ldx BLT.ScrBitOfs
				beq BITBLT.LOOP0

				jmp BITBLT.LOOPx

*------------  Shift 0

BITBLT.LOOP0	lda CB.Cache+S.CB.OP
				bit #S.CB.OP.MASK
				beq BITBLT.LOOP0.2

				>LDYA BLT.BMMaskPtr
				>STYA ZPBMDataPtr

				lda BLT.BMBitOfs0
				sta BLT.BMBitOfsL
				lda BLT.BMByteOfs0
				sta BLT.BMByteOfsL
				ldx LBUF.C1
				stx BLT.ScrColIdx

BITBLT.LOOP0.1	jsr BLT.Get7LC			we have 0xxxxxxxx in A

				ldx BLT.ScrColIdx
				sta LBUF.MASK,x
				inc BLT.ScrColIdx
				cpx LBUF.C2
				bne BITBLT.LOOP0.1
*--------------------------------------
BITBLT.LOOP0.2	>LDYA BLT.BMDataPtr
				>STYA ZPBMDataPtr

				lda BLT.BMBitOfs0
				sta BLT.BMBitOfsL
				lda BLT.BMByteOfs0
				sta BLT.BMByteOfsL
				ldx LBUF.C1
				stx BLT.ScrColIdx

BITBLT.LOOP0.3	jsr BLT.Get7LC			we have 0xxxxxxxx in A

				ora BLT.CMASK
				ldx BLT.ScrColIdx
				sta LBUF.DATA,x
				inc BLT.ScrColIdx
				cpx LBUF.C2
				bne BITBLT.LOOP0.3

				sta IO.CLRALTZP
				lda #0
				sta (pRWReg)

				ldy CB.Cache+S.CB.Y1
				jsr LBUF.DrawAtY

				lda BLT.SrcRWBnk
				sta (pRWReg)
				sta IO.SETALTZP

				inc CB.Cache+S.CB.Y1
				dec CB.Cache+S.CB.SrcH
				beq .8

				lda CB.Cache+S.CB.OP
				bit #S.CB.OP.MASK
				beq .4

				lda BLT.BMMaskPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta BLT.BMMaskPtr
				bcc .4

				inc BLT.BMMaskPtr+1

.4				lda BLT.BMDataPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta BLT.BMDataPtr
				bcc .5

				inc BLT.BMDataPtr+1

.5				jmp BITBLT.LOOP0

.8				jmp BITBLT.Exit
*--------------------------------------
BITBLT.LOOPx	ldy SHL7L-1,x			X = 1->6
				sty ZPScrShiftPtr
				ldy SHL7H-1,x			X = 1->6
				sty ZPScrShiftPtr+1

				lda Shift7BitsCol1,x	get #%00001111, BM bits that go in Col1
				sta BITBLT.LOOPx.2+1
				sta BITBLT.LOOPx.6+1

				lda Shift7BitsCol2,x	get #%01110000, BM bits that go in Col2
				sta BITBLT.LOOPx.3+1
				sta BITBLT.LOOPx.7+1

*------------  Shift 1->6				Comment : SCRBitOfs=X=3

BITBLT.LOOPx.0	lda CB.Cache+S.CB.OP	X = BLT.ScrBitOfs
				bit #S.CB.OP.MASK
				beq BITBLT.LOOPx.4

				>LDYA BLT.BMMaskPtr
				>STYA ZPBMDataPtr

				lda BLT.BMBitOfs0
				sta BLT.BMBitOfsL
				lda BLT.BMByteOfs0
				sta BLT.BMByteOfsL
				ldx LBUF.C1
				stx BLT.ScrColIdx
				stz LBUF.MASK,x			make sure C1 not ORed with trash in "ora LBUF.DATA,x"

BITBLT.LOOPx.1	jsr BLT.Get7LC			we have c6543210 in A, destination : COL=3210xxx, COL+1=xxxx654
				pha						save BM byte for 2nd col

BITBLT.LOOPx.2	and #$ff				SELF MODIFIED : MASK WITH #%00001111, keep col1 bits only
				tay
				lda (ZPScrShiftPtr),y	make 0000dddd Shift left 3 :  0dddd000
				ldx BLT.ScrColIdx		Get actual COL index
				ora LBUF.MASK,x			Light proper bits : 0dddd???
				sta LBUF.MASK,x			store  0xdddd???

				pla						Get back BM Byte

BITBLT.LOOPx.3	and #$ff				SELF MODIFIED : #%01110000 get only col2 bites
				tay
				lda (ZPScrShiftPtr),y	shift right 4 (=shift left 3!!!) : 00000ddd
				sta LBUF.MASK+1,x
				inc BLT.ScrColIdx
				cpx LBUF.C2
				bne BITBLT.LOOPx.1
*--------------------------------------
BITBLT.LOOPx.4	>LDYA BLT.BMDataPtr
				>STYA ZPBMDataPtr

				lda BLT.BMBitOfs0
				sta BLT.BMBitOfsL
				lda BLT.BMByteOfs0
				sta BLT.BMByteOfsL
				ldx LBUF.C1
				stx BLT.ScrColIdx
				stz LBUF.DATA,x			make sure C1 not ORed with trash in "ora LBUF.DATA,x"

BITBLT.LOOPx.5	jsr BLT.Get7LC			we have c6543210 in A, destination : COL=3210xxx, COL+1=xxxx654
				pha						save BM byte for 2nd col

BITBLT.LOOPx.6	and #$ff				SELF MODIFIED : MASK WITH #%00001111, keep col1 bits only
				tay
				lda (ZPScrShiftPtr),y	make 0000dddd Shift left 3 :  0dddd000
				ora BLT.CMASK

				ldx BLT.ScrColIdx		Get actual COL index
				ora LBUF.DATA,x			Light proper bits : 0dddd???
				sta LBUF.DATA,x			store  0xdddd???

				pla						Get back BM Byte

BITBLT.LOOPx.7	and #$ff				SELF MODIFIED : #%01110000 get only col2 bites
				tay
				lda (ZPScrShiftPtr),y	shift right 4 (=shift left 3!!!) : 00000ddd
				ora BLT.CMASK
				sta LBUF.DATA+1,x
				inc BLT.ScrColIdx
				cpx LBUF.C2
				bne BITBLT.LOOPx.5

				sta IO.CLRALTZP
				lda #0
				sta (pRWReg)

				ldy CB.Cache+S.CB.Y1
				jsr LBUF.DrawAtY

				lda BLT.SrcRWBnk
				sta (pRWReg)
				sta IO.SETALTZP

				inc CB.Cache+S.CB.Y1
				dec CB.Cache+S.CB.SrcH
				beq BITBLT.Exit

				lda CB.Cache+S.CB.OP
				bit #S.CB.OP.MASK
				beq .1

				lda BLT.BMMaskPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta BLT.BMMaskPtr
				bcc .1

				inc BLT.BMMaskPtr+1

.1				lda BLT.BMDataPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta BLT.BMDataPtr
				bcc .2

				inc BLT.BMDataPtr+1

.2				jmp BITBLT.LOOPx.0
*--------------------------------------
BITBLT.Exit		sta IO.CLRALTZP

*				sta IO.CLRPAGE2			make sure to leave IO.CLRPAGE2

				cli

				bit IO.RRAMWRAMBNK2

				lda A2osX.ActBnk
				sta (pRWReg)

				clc
				rts
*--------------------------------------
BLT.SetupPtrs	clc
				lda ZPBMDataPtr
				adc BM.Cache+S.BM.MASK.OFS
				sta ZPBMMaskPtr
				lda ZPBMDataPtr+1
				adc BM.Cache+S.BM.MASK.OFS+1
				sta ZPBMMaskPtr+1

*				clc
				lda ZPBMDataPtr
				adc #S.BM
				sta ZPBMDataPtr
				bcc .8

				inc ZPBMDataPtr+1

.8				rts
*--------------------------------------
* BM Data/Mask bits :
* 76543210 76543210
*    ^
*  BMBitOfsL (0->7)
* LBUF DATA/MASK Bits
* c6543210 c6543210
*      ^
*--------------------------------------
BLT.Get7LC		ldy BLT.BMByteOfsL

				ldx BLT.BMBitOfsL
				beq .1					OFS=0, done! and no need to advance ZPBMDataPtr

				dex						OFS 0->6
				beq .2					OFS was 1....done and go to next byte, OFS=0

				lda SHR7L-1,x		X = 1->6
				sta ZPBMShiftPtr
				lda SHR7H-1,x		X = 1->6
				sta ZPBMShiftPtr+1

				lda (ZPBMDataPtr),y
				lsr						OFS=1->7, shift one (Range 0->127)
				and Mask7BitsH,x		Get only left upper X bits
				tay						xxxxx000
				lda (ZPBMShiftPtr),y	shift'em RIGHT 000xxxxx
				pha

				inc BLT.BMByteOfsL		Next BM data byte

				ldy BLT.BMByteOfsL
				lda (ZPBMDataPtr),y		get next byte
				and Mask7BitsL,x		only first Y bits 000000yy
				tay

				pla						get back right part bits 000xxxxx

				ora (ZPBMShiftPtr),y	shift right 000000yy and ORA with 000xxxxx=0yyxxxxx
				dec BLT.BMBitOfsL		X = 1->6, ADD 7 MOD 8.....

				rts

.1				lda #7
				sta BLT.BMBitOfsL		Was 0, add 7 bits

				lda (ZPBMDataPtr),y
				and #%01111111			Get only 7 needed bits

				rts

.2				lda (ZPBMDataPtr),y
				lsr						OFS=1->7, shift one (Range 0->127)

				stz BLT.BMBitOfsL		LSR did already 0xxxxxxx, Was 1, add 7 bits, MOD 8=0

				inc BLT.BMByteOfsL		go to next byte in BM line

				rts
*--------------------------------------
BLT.Get7RAM		ldy BLT.BMByteOfsL

				ldx BLT.BMBitOfsL
				beq .1

				dex
				beq .2

				lda SHR7L-1,x
				sta ZPBMShiftPtr
				lda SHR7H-1,x
				sta ZPBMShiftPtr+1

				jsr BLT.GetDByteY
				lsr
				and Mask7BitsH,x
				tay
				lda (ZPBMShiftPtr),y
				pha

				inc BLT.BMByteOfsL

				ldy BLT.BMByteOfsL
				jsr BLT.GetDByteY
				and Mask7BitsL,x
				tay

				pla

				ora (ZPBMShiftPtr),y
				dec BLT.BMBitOfsL

				rts

.1				lda #7
				sta BLT.BMBitOfsL

				jsr BLT.GetDByteY
				and #%01111111

				rts

.2				jsr BLT.GetDByteY
				lsr

				stz BLT.BMBitOfsL

				inc BLT.BMByteOfsL

				rts
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.blt
LOAD usr/src/drv/dhgr.drv.s
ASM
