NEW
  AUTO 3,1
*--------------------------------------
* FILLRECT
*	1 : OP (XOR,SET,ORA)
*   2 : Mode
*	3 : Color
*   4 : X1
*   6 : Y1
*   8 : X2
*  10 : Y2
*--------------------------------------
FILLRECT		lda CB.Cache+S.CB.pPat+1
				bne FILLRECT.PAT

				lda #0
				sta (pRWReg)

				jsr HLINE.I

				ldy CB.Cache+S.CB.Y1
				cpy CB.Cache+S.CB.Y2
				beq .8

.2				iny
				phy
				jsr LBUF.DrawAtY
				ply
				cpy CB.Cache+S.CB.Y2
				bne .2

.8				lda A2osX.ActBnk
				sta (pRWReg)

				clc
				rts
*--------------------------------------
FILLRECT.PAT	>DEBUG


				clc
				rts
*--------------------------------------
*   4 : X1
*   6 : Y1
*   8 : X2
*  10 : Y2
*  18 : pDst = Size in Bytes to save this RECT
*--------------------------------------
GETRECTBUFSIZE	>LDYA CB.Cache+S.CB.X1
				jsr DIVMOD7YA
				sta .1+1				A = X1 div 7 = COL1

				>LDYA CB.Cache+S.CB.X2
				jsr DIVMOD7YA

				inc						A = (X2 div 7) + 1 = COL2

				sec

.1				sbc #$ff				SELF MODIFIED

				tax

				lda CB.Cache+S.CB.Y2
				sec
				sbc CB.Cache+S.CB.Y1

				inc						+1 because Y2 = Y1 + (H - 1)

				tay
				lda #0

				jsr YAMultX				Y,A = Size in bytes

				pha
				phy

				ldy #S.IOCTL.BYTECNT+1
				sta (ZPIOCTL),y
				dey
				pla
				sta (ZPIOCTL),y			Update IOCTL for UNISTD write()
				tay
				pla						Y,A = size for IOCTL

				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.rect
LOAD usr/src/drv/dhgr.drv.s
ASM
