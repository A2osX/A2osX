NEW
  AUTO 3,1
*--------------------------------------
				.DUMMY
				.OR ZPDRV+4

ZPIOCTL			.BS 2

ZPpHGR			.BS 2
ZPpBM			.BS 2
ZPpBMMask		.BS 2
ZPpBMData		.BS 2

ZPpFont			.BS 2					FONT
ZPpGlyph		.BS 2					FONT

GWORD			.BS 2
GBYTE1			.BS 1

				.ED
*--------------------------------------
pFD				.EQ 8
*--------------------------------------
DRV				cld
				jmp (.1,x)

.1				.DA STATUS
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA A2osX.BADCALL
				.DA CONTROL
				.DA A2osX.BADCALL
				.DA OPEN
				.DA CLOSE
				.DA READ
				.DA WRITE
*--------------------------------------
STATUS			>STYA ZPIOCTL

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta ZPpBuf
				iny
				lda (ZPIOCTL),y
				sta ZPpBuf+1

				ldy #S.IOCTL.S
				lda (ZPIOCTL),y
				beq .1

				cmp #S.IOCTL.S.GETDIB
				bne STATUS.DCB

				ldy #S.DIB-1
				.HS 2C					bit abs

.1				ldy #3

				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda DIB,y
				sta (ZPpBuf),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				clc
				rts

STATUS.DCB		cmp #S.IOCTL.S.GETDCB
				bne STATUS.9

				ldy #S.DCB.GFX-1
				bit A2osX.IOOpt
				bpl .2

				sta IO.SETWRITEAUX

.2				lda DCB,y
				sta (ZPpBuf),y
				dey
				bpl .2

				sta IO.CLRWRITEAUX

				clc
				rts

STATUS.9		lda #MLI.E.BADCTL
				sec
				rts
*--------------------------------------
OPEN			tax						DEV.ID in A

				lda #S.DIB.S.OPENED
				bit DIB+S.DIB.S
				bne CLOSE.IOE

				lda A2osX.SCRNDEVS+9
				beq .1

				lda #E.OOH
				sec
				rts

.1				stx A2osX.SCRNDEVS+9
				stx DCB+S.DCB.GFX.DEVID

				lda #S.DIB.S.OPENED
				tsb DIB+S.DIB.S

				jsr ClrScr
*--------------------------------------
CONTROL			lda #9
				cmp A2osX.ASCREEN
				beq .8

				sta A2osX.ASCREEN

				sta IO.CLRTEXT

				lda IO.RDIOUDIS
				sta IO.SETIOUDIS
				sta IO.CLRDHIRES
				bmi .8

				sta IO.CLRIOUDIS

.8				clc
				rts

CLOSE.IOE		lda #MLI.E.IO
				sec
				rts
*--------------------------------------
CLOSE			lda #S.DIB.S.OPENED

				bit DIB+S.DIB.S
				beq CLOSE.IOE

				trb DIB+S.DIB.S

*				stz A2osX.SCRNDEVS+9

				clc
				rts
*--------------------------------------
READ			>STYA ZPIOCTL

				ldy #S.FD.DEV.DEVID
				lda (pFD),y
				cmp A2osX.ASCREEN
				bne .9

				bit IO.OPENAPPLE
				bmi .9

				lda IO.KBD
				bpl .9

				sta IO.KBDSTROBE

				and #$7F
				tax

				ldy #S.IOCTL.BUFPTR
				lda (ZPIOCTL),y
				sta ZPpBuf
				iny
				lda (ZPIOCTL),y
				sta ZPpBuf+1
				iny
				lda #1
				sta (ZPIOCTL),y
				dec
				iny
				sta (ZPIOCTL),y
				txa
				sta IO.SETWRITEAUX
				sta (ZPpBuf)
				sta IO.CLRWRITEAUX

				clc
				rts

.9				lda	#E.NODATA
				sec
				rts
*--------------------------------------
WRITE			>STYA ZPIOCTL

				ldy #S.IOCTL.BYTECNT
				lda (ZPIOCTL),y

				tax

				dey
				lda (ZPIOCTL),y
				sta ZPpBuf+1
				dey
				lda (ZPIOCTL),y
				sta ZPpBuf

				ldy #0

.1				jsr A2osX.GetpBuf
				sta CB.Cache,y
				>INCW ZPpBuf

				iny
				dex
				bne .1

				lda CB.Cache
				asl
				tax
				jmp (.8-2,x)

.8				.DA SETPIXEL
				.DA GETPIXEL
				.DA HLINE
				.DA VLINE
				.DA FILLRECT
				.DA BITBLT
				.DA GETRECTBUFSIZE
				.DA DRAWTEXT
*--------------------------------------
SETPIXEL
*--------------------------------------
GETPIXEL
*--------------------------------------
HLINE
*--------------------------------------
VLINE
*--------------------------------------
FILLRECT
*--------------------------------------
BITBLT			>LDYA CB.Cache+S.CB.hSrc
				>DAPI GetpLCBuf
				bcs *

				sta BM.SrcRWBnk
				sta (pRWReg)
				stx BM.SrcLCBnk
				bit $C000,x

				sei
				sta IO.SETALTZP

				stz ZPpBM
				sty ZPpBM+1

				ldy #S.BM-1

.1				lda (ZPpBM),y
				sta BM.Cache,y
				dey
				bpl .1

				lda #S.BM
				clc
				adc ZPpBM
				sta ZPpBMData
				lda /S.BM
				adc ZPpBM+1
				sta ZPpBMData+1

				lda BM.Cache+S.BM.MASK.OFS
				clc
				adc ZPpBM
				sta ZPpBMMask
				lda BM.Cache+S.BM.MASK.OFS+1
				adc ZPpBM+1
				sta ZPpBMMask+1

				sta IO.CLRALTZP
				cli

				lda CB.Cache+S.CB.OP
				bit #S.CB.OP.RESTORE
				beq .2



.8				sta IO.RRAMWRAMBNK2

				clc
				rts
*--------------------------------------
.2				bit #S.CB.OP.SAVE
				beq .7

				>LDYA CB.Cache+S.CB.pDst
				>STYA ZPpBuf

				sta IO.SETWRITEAUX


				sta IO.CLRWRITEAUX

				lda CB.Cache+S.CB.OP

.7				and #$0F
				beq .8

				tax
				jmp (.80,x)

.80				.DA BITBLT.Exit
				.DA BITBLT.XOR
				.DA BITBLT.SET
				.DA BITBLT.ORA
				.DA BITBLT.Exit
				.DA BITBLT.ANDXOR
				.DA BITBLT.ANDSET
				.DA BITBLT.ANDORA
*--------------------------------------
BITBLT.XOR
*--------------------------------------
BITBLT.ORA
*--------------------------------------
BITBLT.ANDXOR
*--------------------------------------
BITBLT.SET		sei
				sta IO.SETALTZP

				lda CB.Cache+S.CB.SrcY

* TODO: CB.Cache+S.CB.SrcY*BM.Cache+S.BM.RowBytes.....

.1				ldy CB.Cache+S.CB.Y1
				lda CB.Cache+S.CB.X1
				clc
				adc HGR.L,y
				sta ZPpHGR
				lda HGR.H,y
				sta ZPpHGR+1

				ldy CB.Cache+S.CB.SrcX
				ldx CB.Cache+S.CB.SrcW
				bne .2

				ldx BM.Cache+S.BM.W

.2

.3				lda (ZPpBMData),y
				sta (ZPpHGR),y
				iny
				dex
				bne .3

				lda BM.Cache+S.BM.RowBytes
				clc
				adc ZPpBMData
				sta ZPpBMData
				bcc .4

				inc ZPpBMData+1

.4				inc CB.Cache+S.CB.Y1
				dec BM.Cache+S.BM.H
				beq BITBLT.Exit

				dec CB.Cache+S.CB.SrcH
				bne .1

*--------------------------------------
BITBLT.ANDSET
*--------------------------------------
BITBLT.ANDORA
*--------------------------------------
BITBLT.Exit

				sta IO.CLRALTZP
				cli

				sta IO.RRAMWRAMBNK2

				lda A2osX.ActBnk
				sta (pRWReg)

				clc
				rts
*--------------------------------------
GETRECTBUFSIZE	clc
				rts
*--------------------------------------
DRAWTEXT		>LDYA CB.Cache+S.CB.pTxt
				>STYA TXTPTR

				>LDYA CB.Cache+S.CB.hSrc
				>DAPI GetpLCBuf
				bcs *

				sta FONT.SrcRWBnk
				sta (pRWReg)
				stx FONT.SrcLCBnk
				bit $C000,x

				sei
				sta IO.SETALTZP

				stz ZPpFont
				sty ZPpFont+1
				sty FONT.Base

				ldy #S.FON-1

.1				lda (ZPpFont),y
				sta FON.Cache,y
				dey
				bpl .1

				sta IO.CLRALTZP
				cli

				lda CB.Cache+S.CB.Y1
				clc
				adc FON.Cache+S.FON.PixH
				sta CB.Cache+S.CB.Y2

				lda FON.Cache+S.FON.PixW
				beq .4					varable W

				lsr
				lsr
				lsr
				tax
				lda FON.Cache+S.FON.PixW
				and #7
				beq .2

				inx

.2				stx FONT.ByteW

				clc
				lda #0

.3				adc	FON.Cache+S.FON.PixH
				dex
				bne .3

				sta FONT.GlyphBytes

.4				lda A2osX.ActBnk
				sta (pRWReg)

				jsr A2osX.GetTXTPTR
				beq .8

				>INCW TXTPTR

				jsr DRAWTEXT.GetGlyph

*				clc

				adc #S.FON
				sta .6+1
				txa
				adc FONT.Base
				sta .6+2

				lda FONT.SrcRWBnk
				sta (pRWReg)

				sei
				sta IO.SETALTZP

				ldx CB.Cache+S.CB.Y1

.5				ldy CB.Cache+S.CB.X1
				lda CB.Cache+S.CB.X1+1
				beq .50

				lda DIV71,y
				bra .51

.50				lda DIV70,y

.51
*				clc

				adc HGR.L,x
				sta ZPpHGR
				lda HGR.H,x
				sta ZPpHGR+1

				ldy #0					cols

.6				lda $FFFF				SELF MODIFIED

				>INCW .6+1

				eor CB.Cache+S.CB.COLOR
				sta (ZPpHGR),y
				iny
				cpy FONT.ByteW
				bcc .6

				inx
				cpx CB.Cache+S.CB.Y2
				bcc .5

				sta IO.CLRALTZP
				cli

				lda CB.Cache+S.CB.X1
				clc
				adc FON.Cache+S.FON.PixW
				sta CB.Cache+S.CB.X1
				bcc .4

				inc CB.Cache+S.CB.X1+1

				bra .4

.8				sta IO.RRAMWRAMBNK2

				clc
				rts
*--------------------------------------
DRAWTEXT.GetGlyph
				cmp FON.Cache+S.FON.Last
				bcc .1

				bne .2

.1				sec
				sbc FON.Cache+S.FON.First
				bcs .3

.2				lda FON.Cache+S.FON.Default

.3				sta GWORD
				stz GWORD+1

				ldx FONT.GlyphBytes
				stx GBYTE1

				lda #0
				tax
				bra .6

.4				clc
				adc GWORD
				pha
				txa
				adc GWORD+1
				tax
				pla

.5				asl GWORD
				rol GWORD+1

.6				lsr GBYTE1
				bcs .4

				bne .5

*				clc

				rts
*--------------------------------------
* IN:
* Y = LO
* A = HI
* OUT:
* A = DIV
* X = MOD
*--------------------------------------
*DIVMOD7YA		dec
*				bmi .2
*
*				clc
*				beq .1
*
*				lda DIV7.512,y
*				adc #$49
*				ldx MOD7.512,y
*				rts
*
*.1				lda DIV7.256,y
*				adc #$24
*				ldx MOD7.256,y
*				rts
*
*.2				lda DIV7.0,y
*				ldx MOD7.0,y
*				rts
*--------------------------------------
* IN:
* Y,A = num1 (16)
* X = num2 (8)
* OUT:
* Y,A = (Y,A) * X
*--------------------------------------
YAMultX			stx GBYTE1

				sty GWORD
				sta GWORD+1
				ldy #0					Result LO
				tya						Result HI
				bra .3

.1				pha
				tya
				clc
				adc GWORD
				tay
				pla
				adc GWORD+1

.2				asl GWORD
				rol GWORD+1

.3				lsr GBYTE1
				bcs .1

				bne .2
				rts
*--------------------------------------
ClrScr			ldx #0
				txa

*				sta IO.SETHIRES

.1				ldy HGR.L,x
				sty ZPPtr1
				ldy HGR.H,x
				sty ZPPtr1+1

.2				ldy #39

.3				sta (ZPPtr1),y
				dey
				bpl .3

				inx
				cpx #192
				bne .1

				rts
*--------------------------------------
MAN
SAVE usr/src/drv/hgr.drv.s.core
LOAD usr/src/drv/hgr.drv.s
ASM
