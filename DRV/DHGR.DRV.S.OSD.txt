NEW
  AUTO 3,1
*--------------------------------------
* Y,A = Ptr to S.CB in MAIN
*--------------------------------------
OSD				ldy CB.Cache+S.CB.pDst
				lda CB.Cache+S.CB.pDst+1
				bne .1				Ptr To Aux

				tya					hBM in Aux
				beq .4				no Src BM

				>KAPI Malloc

.1				>STYA ZPDstBMPtr

				ldx #0

.2				lda (ZPDstBMPtr)
				sta DstBM.Cache,x
				inc ZPDstBMPtr
				bne .3

				inc ZPDstBMPtr+1

.3				inx
				cpx #S.BM
				bne .2

.4				ldx	CB.Cache+S.CB.CMD
				jmp (.8-2,x)
*--------------------------------------
.8				.DA OSD.SETPIXEL
				.DA OSD.GETPIXEL
				.DA OSD.HLINE
				.DA OSD.VLINE
				.DA OSD.FILLRECT
				.DA OSD.BITBLT
				.DA OSD.GETRECTBUFSIZE
				.DA OSD.DRAWTEXT
				.DA OSD.GETTEXTSIZE
*--------------------------------------
OSD.SETPIXEL	>LDYA CB.Cache+S.CB.Y1
				ldx DstBM.Cache+S.BM.RowBytes
				jsr YAMultX
				pha
				tya
				clc
				adc ZPDstBMPtr
				sta ZPDstBMPtr
				pla
				adc ZPDstBMPtr+1
				sta ZPDstBMPtr+1
				lda CB.Cache+S.CB.M
				bit #S.CB.M.C16
				bne OSD.SETPIXEL.C16

OSD.SETPIXEL.M	lda CB.Cache+S.CB.X1
				pha
				and #$7					MOD 7
				tax
				pla
				lsr CB.Cache+S.CB.X1
				ror
				lsr CB.Cache+S.CB.X1
				ror
				lsr CB.Cache+S.CB.X1
				ror
				clc
				adc ZPDstBMPtr
				sta ZPDstBMPtr
				lda CB.Cache+S.CB.X1
				adc ZPDstBMPtr+1
				sta ZPDstBMPtr+1

				lda CB.Cache+S.CB.OP
*				cmp #S.CB.OP.XOR
				beq .2
				lda CB.Cache+S.CB.COLOR
				bne .1
				lda (ZPDstBMPtr)
				and Mono8.NMasks,x
				sta (ZPDstBMPtr)
				rts

.1				lda (ZPDstBMPtr)
				ora Mono.Masks,x
				sta (ZPDstBMPtr)
				rts
.2				lda (ZPDstBMPtr)
				eor Mono.Masks,x
				sta (ZPDstBMPtr)
				rts
OSD.SETPIXEL.C16
*--------------------------------------
OSD.GETPIXEL
*--------------------------------------
OSD.HLINE
*--------------------------------------
OSD.VLINE
*--------------------------------------
OSD.FILLRECT
*--------------------------------------
* BitBlt
*  S.CB.X1 : SrcX1 in bitmap/mask
*  S.CB.Y1 : SrcY1
*  S.CB.SrcW : width
*  S.CB.SrcH : height

*  S.CB.DstX : Destination X (screen)
*  S.CB.DstY : Destination Y (screen)

*  S.CB.SrcPtr : pointer to S.BM
*  S.CB.SrcPtr+1 = 0, hMem in S.CB.SrcPtr
*  S.CB.DstPtr : pointer to Save Buffer
*--------------------------------------
OSD.BITBLT

*--------------------------------------
* GETRECTBUFSIZE
* In:
*  S.CB.SrcW
*  S.CB.SrcH
* Out:
*  S.CB.DstPtr : Buffer Size (in bytes)
*--------------------------------------
OSD.GETRECTBUFSIZE

OSD.GETRECTBUFSIZE.RTS
				rts
*--------------------------------------
* DRAWTEXT
* In:
*  S.CB.S.CB.FONT : hFont
*  S.CB.SrcPtr : Ptr to Text
* Out:
*  A=hBM
*--------------------------------------
ZPTablePtr		.BS 2
*--------------------------------------
OSD.DRAWTEXT	sec
				.HS 90					BCC
*--------------------------------------
* GETTEXTSIZE
* In:
*  S.CB.S.CB.FONT : hFont
*  S.CB.SrcPtr : Ptr to Text
* Out:
*  S.CB.SrcW = Width
*  S.CB.SrcH = Height
*--------------------------------------
OSD.GETTEXTSIZE	clc
				php
				>LDYA CB.Cache+S.CB.hSrc
				>STYA ZPFontPtr

				ldy #S.FON-1

.1				lda (ZPFontPtr),y
				sta FON.Cache,y
				dey
				bpl .1

				lda ZPFontPtr
				clc
				adc #S.FON
				sta ZPTablePtr
				lda ZPFontPtr+1
				adc /S.FON
				sta ZPTablePtr+1

.2				>LDYA CB.Cache+S.CB.pTxt
				>STYA TXTPTR
				stz CB.Cache+S.CB.SrcW
				stz CB.Cache+S.CB.SrcW+1

				lda FON.Cache+S.FON.PixH
				sta CB.Cache+S.CB.SrcH
				stz CB.Cache+S.CB.SrcH+1

.3				jsr OSD.TXTPTRgn
				beq .4

				jsr OSD.FON.GetChar
				lda (ZPCharPtr)			Get Char PixelW
				sec
				adc CB.Cache+S.CB.SrcW
				sta CB.Cache+S.CB.SrcW
				bcc .3
				inc CB.Cache+S.CB.SrcW+1
				bra .3

.4				plp
				bcs .5

				clc
				rts

.5				lda CB.Cache+S.CB.SrcW
				ldx CB.Cache+S.CB.SrcW+1
				ldy CB.Cache+S.CB.SrcH

				sta DstBM.Cache+S.BM.W
				stx DstBM.Cache+S.BM.W+1
				sty DstBM.Cache+S.BM.H
				stz DstBM.Cache+S.BM.H+1

				lda #S.BM.F.BBP1
				sta DstBM.Cache+S.BM.F

				jsr OSD.BM.Create
				bcs OSD.GETRECTBUFSIZE.RTS

				sta OSD.DRAWTEXT.END+1		save hBM, ZPDstBMPtr=BMData
				>LDYA CB.Cache+S.CB.pTxt
				>STYA TXTPTR

				stz GWORD				reset Col index in BM
				stz GWORD+1				reset Bit index in Col

OSD.DRAWTEXT.LOOP
				jsr OSD.TXTPTRgn
				beq OSD.DRAWTEXT.END

				jsr OSD.FON.GetChar
				lda (ZPCharPtr)			Char PixW
				sta GBYTE1

				jsr OSD.CHAR.GetNext	Char ByteW
				sta GBYTE2
				>LDYA ZPDstBMPtr
				>STYA ZPTmpPtr

				lda GWORD+1				Get Bit Index for shifting
				jsr OSD.DRAWTEXT.JMP

				lda GBYTE1				Get Char PixW
				sec						+1 for char spacing
				adc GWORD+1				Add Bit index in Col
				pha
				and #7					mod 8
				sta GWORD+1				Update Bit index in Col
				pla
				lsr
				lsr
				lsr						div 8
				clc
				adc GWORD				Update Col Index in BM
				sta GWORD
				bra OSD.DRAWTEXT.LOOP

OSD.DRAWTEXT.END
				lda #$FF				SELF MODIFIED
				sta CB.Cache+S.CB.pSrc	Save hBM in Src CB
				stz CB.Cache+S.CB.pSrc+1

				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD
				clc
				rts
*--------------------------------------
OSD.DRAWTEXT.JMP
				asl
				tax
				jmp (.1,x)
.1				.DA OSD.DRAWTEXT.SHIFT0
				.DA OSD.DRAWTEXT.SHIFT1
				.DA OSD.DRAWTEXT.SHIFT26
				.DA OSD.DRAWTEXT.SHIFT26
				.DA OSD.DRAWTEXT.SHIFT26
				.DA OSD.DRAWTEXT.SHIFT26
				.DA OSD.DRAWTEXT.SHIFT26
				.DA OSD.DRAWTEXT.SHIFT7
*--------------------------------------
OSD.DRAWTEXT.SHIFT0
				lda FON.Cache+S.FON.PixH
.6				pha						save Height counter

				ldx GBYTE2				init Width counter
				ldy GWORD				get col index in BM
.7				jsr OSD.CHAR.GetNext

				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
				iny

				dex
				bne .7

				lda ZPTmpPtr
				clc
				adc DstBM.Cache+S.BM.RowBytes
				sta ZPTmpPtr
				bcc .10

				inc ZPTmpPtr+1

.10				pla						get back Height counter
				dec
				bne .6

				rts
*--------------------------------------
OSD.DRAWTEXT.SHIFT1
				lda FON.Cache+S.FON.PixH
.1				pha						save Height counter

				ldx GBYTE2				init Width counter
				ldy GWORD				get col index in BM

.2				jsr OSD.CHAR.GetNext
				asl						8th bit in ->carry
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
				iny

				bcc .3

				lda #1
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
.3				dex
				bne .2

				lda ZPTmpPtr			Next line...
				clc
				adc DstBM.Cache+S.BM.RowBytes
				sta ZPTmpPtr
				bcc .4

				inc ZPTmpPtr+1

.4				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
* X = ShiftL x2 (Range 4..10)
*--------------------------------------
ColIndex		.BS 1
*--------------------------------------
OSD.DRAWTEXT.SHIFT26
				lda SHIFT8.L-4,x
				sta .3+1
				lda SHIFT8.L-3,x
				sta .3+2

				lda SHIFT8.L-2,x
				sta .4+1
				lda SHIFT8.L-1,x
				sta .4+2

				ldx GWORD+1				ShiftL Index
				lda FON.Cache+S.FON.PixH

.1				pha						save Height counter

				lda GWORD
				sta ColIndex
				lda GBYTE2

.2				pha						save Width counter
				jsr OSD.CHAR.GetNext
				pha

				and First.Masks,x		x = 2->6, for X=3 : A=000xxxxx
				tay						Range 0..127
.3				lda $FFFF,y				SELF MODIFIED	ShiftL3 A=xxxxx000

				ldy ColIndex			get col index in BM
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
				inc ColIndex

				pla
				and Last.Masks,x		x = 2->6, for X=3 : A=xxx00000
				lsr						Range 0..127 !! A=0xxx0000
				tay						but SHIFTL(X+1)   A=00000xxx

.4				lda $FFFF,y				SELF MODIFIED

				ldy ColIndex			get col index in BM
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y

				pla						get back Witdh counter
				dec
				bne .2

				lda ZPTmpPtr
				clc
				adc DstBM.Cache+S.BM.RowBytes
				sta ZPTmpPtr
				bcc .5

				inc ZPTmpPtr+1

.5				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
OSD.DRAWTEXT.SHIFT7
				lda FON.Cache+S.FON.PixH

.1				pha						save Height counter

				ldx GBYTE2				init Width counter
				ldy GWORD				get col index in BM
.2				jsr OSD.CHAR.GetNext
				lsr						1st bit in carry, for COL 1
				bcc .3

				pha						Other 7 bits for COL 2
				lda #$80
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
				pla						get back bit 1->8 already shifted Left 7 (= Shift right 1)

.3				iny
				eor (ZPTmpPtr),y
				sta (ZPTmpPtr),y
				dex
				bne .2

				lda ZPTmpPtr
				clc
				adc DstBM.Cache+S.BM.RowBytes
				sta ZPTmpPtr
				bcc .4

				inc ZPTmpPtr+1

.4				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
OSD.CHAR.GetNext
				>INCW ZPCharPtr
				lda (ZPCharPtr)
				rts
*--------------------------------------
OSD.FON.GetChar	cmp FON.Cache+S.FON.Last
				bcc .1

				bne .2

.1				sec
				sbc FON.Cache+S.FON.First
				bcs .3

.2				lda FON.Cache+S.FON.Default

.3				asl
				php
				clc
				adc ZPTablePtr
				sta ZPCharPtr
				lda #0
				adc ZPTablePtr+1
				sta ZPCharPtr+1
				plp
				bcc .4

				inc ZPCharPtr+1

.4				lda (ZPCharPtr)
				clc
				adc ZPFontPtr
				pha
				ldy #1
				lda (ZPCharPtr),y
				adc ZPFontPtr+1
				sta ZPCharPtr+1
				pla
				sta ZPCharPtr
				rts
*--------------------------------------
OSD.BM.Create	lda DstBM.Cache+S.BM.W
				ldx DstBM.Cache+S.BM.W+1
				bit #7
				beq .1

				and #$F8
				clc
				adc #8
				bcc .1

				inx

.1				sta DstBM.Cache+S.BM.RowBytes
				txa

				lsr
				ror DstBM.Cache+S.BM.RowBytes
				lsr
				ror DstBM.Cache+S.BM.RowBytes
				lsr
				ror DstBM.Cache+S.BM.RowBytes
				>LDYA DstBM.Cache+S.BM.H
				ldx DstBM.Cache+S.BM.RowBytes
				jsr YAMultX					Compute BM total bytes
				pha
				eor #$ff
				sta ZPTmpPtr+1
				tya
				eor #$ff
				sta ZPTmpPtr
				tya
				clc
				adc #S.BM
				tay
				pla
				adc /S.BM
				>KAPI Malloc
				bcs .9

				>STYA ZPDstBMPtr

				ldy #S.BM-1

.2				lda DstBM.Cache,y
				sta (ZPDstBMPtr),y
				dey
				bpl .2

				lda ZPDstBMPtr
*				clc
				adc #S.BM
				sta ZPDstBMPtr
				sta .4+1
				lda ZPDstBMPtr+1
				adc /S.BM
				sta ZPDstBMPtr+1
				sta .4+2

				ldy #0

				lda CB.Cache+S.CB.OP
				and #S.CB.OP.INVERSE
				beq .3

				lda #$ff

.3				inc ZPTmpPtr
				bne .4

				inc ZPTmpPtr+1
				beq .8

.4				sta $ffff,y
				iny
				bne .3

				inc .4+2
				bra .3

.8				txa						hBM

				clc
.9				rts
*--------------------------------------
OSD.TXTPTRgn	jmp ($a000)
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.osd
LOAD usr/src/drv/dhgr.drv.s
ASM
