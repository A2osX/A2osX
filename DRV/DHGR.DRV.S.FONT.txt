NEW
  AUTO 3,1
*--------------------------------------
FONT.bDraw		.BS 1

FONT.TxtRWBnk	.BS 1					48k (TXTPTR)

FONT.SrcRWBnk	.BS 1					LC (FONT) (BM)
FONT.SrcLCBnk	.BS 1					$D000 (FONT)

FONT.ByteOfs	.BS 1
FONT.BitOfs		.BS 1

FONT.Table		.BS 2

FONT.GlyphSize	.BS 1					0 if variable width

FONT.GlyphBitW	.BS 1
FONT.GlyphByteW	.BS 1
*--------------------------------------
FONT.GetTXTPTR	.EQ $110
FONT.GetDByteY	.EQ $119
*--------------------------------------
FONT.110		sta IO.SETREADAUX
				lda (TXTPTR)
				sta IO.CLRREADAUX
				rts

				sta IO.SETREADAUX
				eor (ZPBMDataPtr),y
				sta IO.CLRREADAUX
				rts

FONT.110.L		.EQ *-FONT.110
*--------------------------------------
FONT.Setup110	ldy #FONT.110.L

.1				lda	FONT.110,y
				sta $110,y
				dey
				bpl .1

				rts
*--------------------------------------
FONT.DrawText	sec
				.HS 90					BCC
FONT.GetTextW	clc

				ror FONT.bDraw

				lda A2osX.ActBnk
				sta FONT.TxtRWBnk

				>LDYA CB.Cache+S.CB.pTxt
				>STYA TXTPTR

				jsr FONT.Setup110

				>LDYA CB.Cache+S.CB.hFont
				>DAPI GetpLCBuf
				bcs *

				sta FONT.SrcRWBnk
				sta (pRWReg)
				stx FONT.SrcLCBnk
				bit $C000,x

				sei
				sta IO.SETALTZP

				sty .1+2				FONT LC HI byte
				sty FONT.Table+1
				lda #S.FON
				sta FONT.Table

				ldx #S.FON-1

.1				lda $FF00,x				SELF MODIFIED
				sta FON.Cache,x
				dex
				bpl .1

				lda FON.Cache+S.FON.PixW
				beq .15					Variable width

				sta FONT.GlyphBitW
				bit #7
				php
				lsr
				lsr
				lsr
				plp
				beq .10

				inc

.10				sta FONT.GlyphByteW

				ldx FON.Cache+S.FON.PixH

				jsr AMultX

.15				sta FONT.GlyphSize

				jsr FONT.Setup110

				stz BM.Cache+S.BM.W
				stz BM.Cache+S.BM.W+1

				lda FON.Cache+S.FON.PixH
				sta BM.Cache+S.BM.H
				stz BM.Cache+S.BM.H+1
*--------------------------------------
				ldx FON.Cache+S.FON.PixW
				beq .35					variable width font

.2				jsr FONT.GetChar
				beq .4

				txa
				clc
				adc BM.Cache+S.BM.W
				sta BM.Cache+S.BM.W
				bcc .2

				inc BM.Cache+S.BM.W+1
				bra .2
*--------------------------------------
.35				jsr FONT.GetChar
				beq .4

				jsr FONT.GetGlyph

				lda (ZPGlyphPtr)		Get Glyph PixW
				sec						+1 for spacing

				adc BM.Cache+S.BM.W
				sta BM.Cache+S.BM.W
				bcc .35

				inc BM.Cache+S.BM.W+1
				bra .35
*--------------------------------------
.4				sta IO.CLRALTZP
				cli

				bit IO.RRAMWRAMBNK2

				bit FONT.bDraw
				bmi FONT.Draw

				clc

				ldy #S.IOCTL.BYTECNT
				lda BM.Cache+S.BM.W
				sta (ZPIOCTL),y
				pha

				iny
				lda BM.Cache+S.BM.W+1
				sta (ZPIOCTL),y			Update IOCTL for UNISTD write()

				ply						Y,A = W for IOCTL
*--------------------------------------
FONT.DrawText.Exit
				pha

				lda A2osX.ActBnk
				sta (pRWReg)

				pla
				rts
*--------------------------------------
FONT.Draw		lda FONT.SrcRWBnk		same RWBnk as FONT
				sta (pRWReg)

				jsr FONT.GetBMBuf
				bcs FONT.DrawText.Exit

				ldx FONT.SrcLCBnk
				bit $C000,x

				>LDYA CB.Cache+S.CB.pTxt
				>STYA TXTPTR

				sei
				sta IO.SETALTZP

				ldy #FONT.110.L

.30				lda	FONT.110,y
				sta $110,y
				dey
				bpl .30

				stz FONT.ByteOfs
                stz FONT.BitOfs

				lda FON.Cache+S.FON.PixW
				beq .41
*--------------------------------------
.40				jsr FONT.GetChar
				beq .5

				jsr FONT.GetGlyph

				sta IO.SETWRITEAUX
				jsr FONT.DrawText.JMP
				sta IO.CLRWRITEAUX

				lda FON.Cache+S.FON.PixW
				clc
				adc FONT.BitOfs
				pha
				and #7					mod 8
				sta FONT.BitOfs
				pla
				lsr
				lsr
				lsr						div 8
				clc
				adc FONT.ByteOfs
				sta FONT.ByteOfs
				bra .40
*--------------------------------------
.41				jsr FONT.GetChar
				beq .5

				jsr FONT.GetGlyph

				lda (ZPGlyphPtr)		Get Glyph PixelW
				sta FONT.GlyphBitW
				>INCW ZPGlyphPtr

				lda (ZPGlyphPtr)		Get Glyph ByteW
				sta FONT.GlyphByteW
				>INCW ZPGlyphPtr

				sta IO.SETWRITEAUX
				jsr FONT.DrawText.JMP
				sta IO.CLRWRITEAUX

				lda FONT.GlyphBitW
				sec
				adc FONT.BitOfs
				pha
				and #7					mod 8
				sta FONT.BitOfs
				pla
				lsr
				lsr
				lsr						div 8
				clc
				adc FONT.ByteOfs
				sta FONT.ByteOfs
				bra .41
*--------------------------------------
.5				sta IO.CLRALTZP
				cli

				jsr LBUF.SetBounds1X1
				stx BLT.ScrBitOfs
				jsr LBUF.SetBounds2X1W

				>LDYA CB.Cache+S.CB.pSrc
				ldx FONT.SrcRWBnk

				jsr BITBLT.FONT

*				bit IO.RRAMWRAMBNK2		Done by BitBlt Exit

				lda FONT.SrcRWBnk
				sta (pRWReg)

				>LDYA CB.Cache+S.CB.pSrc
				>DAPI Free

				lda A2osX.ActBnk
				sta (pRWReg)

*				clc

				rts
*--------------------------------------
* Read in AUXZPLC:SrcRWBnk (ZPGlyphPtr)
* R/W  in AUXRAM :SrcRWBnk (ZPBMDataPtr)
*--------------------------------------
FONT.DrawText.JMP
				>LDYA CB.Cache+S.CB.pSrc
				>STYA ZPBMDataPtr		BM line 0

				lda FONT.BitOfs
				asl
				tax
				jmp (.1,x)

.1				.DA FONT.DrawText0
				.DA FONT.DrawText1
				.DA FONT.DrawText26
				.DA FONT.DrawText26
				.DA FONT.DrawText26
				.DA FONT.DrawText26
				.DA FONT.DrawText26
				.DA FONT.DrawText7
*--------------------------------------
FONT.DrawText0	lda FON.Cache+S.FON.PixH
				sta A1

.1				ldx FONT.GlyphByteW
				ldy FONT.ByteOfs

.2				lda (ZPGlyphPtr)
				>INCW ZPGlyphPtr

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y
				iny
				dex
				bne .2

				lda ZPBMDataPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta ZPBMDataPtr
				bcc .3

				inc ZPBMDataPtr+1

.3				dec	A1					H
				bne .1

				rts
*--------------------------------------
FONT.DrawText1	lda FON.Cache+S.FON.PixH

.1				pha						save Height counter

				ldx FONT.GlyphByteW
				ldy FONT.ByteOfs

.2				lda (ZPGlyphPtr)
				>INCW ZPGlyphPtr

				asl						8th bit in ->carry

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

				iny

				bcc .3

				lda #1

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

.3				dex
				bne .2

				lda ZPBMDataPtr			Next line...
				clc
				adc BM.Cache+S.BM.RowBytes
				sta ZPBMDataPtr
				bcc .4

				inc ZPBMDataPtr+1

.4				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
FONT.DrawText26	lda SHL8-4,x
				sta A3
				lda SHL8-3,x
				sta A3+1

				lda SHL8-2,x
				sta A4
				lda SHL8-1,x
				sta A4+1

				ldx FONT.BitOfs
				lda FON.Cache+S.FON.PixH

.1				pha						save Height counter

				lda FONT.ByteOfs
				sta A2
				lda FONT.GlyphByteW

.2				pha						save Width counter
				lda (ZPGlyphPtr)

				and First.Masks,x		x = 2->6, for X=3 : A=000xxxxx
				tay						Range 0..127
				lda (A3),y				ShiftL3 A=xxxxx000

				ldy A2					get col index in BM

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

				inc A2

				lda (ZPGlyphPtr)
				>INCW ZPGlyphPtr

				and Last.Masks,x		x = 2->6, for X=3 : A=xxx00000
				lsr						Range 0..127 !! A=0xxx0000
				tay						but SHIFTL(X+1)   A=00000xxx

				lda (A4),y

				ldy A2					get col index in BM

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

				pla						get back Witdh counter
				dec
				bne .2

				lda ZPBMDataPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta ZPBMDataPtr
				bcc .5

				inc ZPBMDataPtr+1

.5				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
FONT.DrawText7	lda FON.Cache+S.FON.PixH

.1				pha						save Height counter

				ldx FONT.GlyphByteW
				ldy FONT.ByteOfs

.2				lda (ZPGlyphPtr)
				>INCW ZPGlyphPtr

				lsr						1st bit in carry, for COL 1
				bcc .3

				pha						Other 7 bits for COL 2
				lda #$80

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

				pla						get back bit 1->8 already shifted Left 7 (= Shift right 1)

.3				iny

				jsr FONT.GetDByteY
				sta (ZPBMDataPtr),y

				dex
				bne .2

				lda ZPBMDataPtr
				clc
				adc BM.Cache+S.BM.RowBytes
				sta ZPBMDataPtr
				bcc .4

				inc ZPBMDataPtr+1

.4				pla						get back Height counter
				dec
				bne .1

				rts
*--------------------------------------
FONT.GetChar	sta IO.CLRALTZP
				lda FONT.TxtRWBnk
				sta (pRWReg)

				jsr FONT.GetTXTPTR
				pha

				>INCW TXTPTR

				lda FONT.SrcRWBnk
				sta (pRWReg)

				pla

				sta IO.SETALTZP
				rts
*--------------------------------------
FONT.GetGlyph	cmp FON.Cache+S.FON.Last
				bcc .1

				bne .2

.1				sec
				sbc FON.Cache+S.FON.First
				bcs .3

.2				lda FON.Cache+S.FON.Default

.3				ldx	FONT.GlyphSize
				bne .4

				asl
				php

				clc
				adc FONT.Table
				sta A1
				lda #0
				adc FONT.Table+1

				plp
				adc #0
				sta A1+1

				lda (A1)
				sta ZPGlyphPtr

				ldy #1
				lda (A1),y
*				clc
				adc FONT.Table+1
				sta ZPGlyphPtr+1

				rts

.4				tay
				lda #0

				jsr YAMultX
				pha
				tya

*				clc

				adc FONT.Table
				sta ZPGlyphPtr
				pla
				adc FONT.Table+1
				sta ZPGlyphPtr+1

				rts
*--------------------------------------
FONT.GetBMBuf	lda #S.BM.F.BBP1
				sta BM.Cache+S.BM.F

				lda BM.Cache+S.BM.W
				sta CB.Cache+S.CB.SrcW
				sta BM.Cache+S.BM.RowBytes

				lda BM.Cache+S.BM.W+1
				sta CB.Cache+S.CB.SrcW+1
				lsr
				ror BM.Cache+S.BM.RowBytes
				lsr
				ror BM.Cache+S.BM.RowBytes
				lsr
				ror BM.Cache+S.BM.RowBytes

				lda BM.Cache+S.BM.W
				and #7
				beq .1

				inc BM.Cache+S.BM.RowBytes

.1				>LDYA BM.Cache+S.BM.H

				>STYA CB.Cache+S.CB.SrcH
				ldx BM.Cache+S.BM.RowBytes
				jsr YAMultX					Compute BM total bytes

				>STYA ZPCnt

				>DAPI Malloc
				bcs .9

				>STYA CB.Cache+S.CB.pSrc
				>STYA ZPpBuf

				lda ZPCnt
				eor #$ff
				tax
				lda ZPCnt+1
				eor #$ff
				sta ZPCnt+1

				lda CB.Cache+S.CB.COLOR

				sta IO.SETWRITEAUX

				ldy #0

.2				inx
				bne .3

				inc ZPCnt+1
				beq .8

.3				sta (ZPpBuf),y
				iny
				bne .2

				inc ZPpBuf+1
				bra .2

.8				sta IO.CLRWRITEAUX

.9				rts
*--------------------------------------
MAN
SAVE usr/src/drv/dhgr.drv.s.font
LOAD usr/src/drv/dhgr.drv.s
ASM
