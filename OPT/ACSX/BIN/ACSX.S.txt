NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR $2000
				.TF	opt/acsx/bin/acsx
*---------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/gfx.i
*---------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hFDGFX			.BS 2
pBasepath		.BS 2
pIOBuf			.BS 2
pFilename		.BS 2

pDIC			.BS 2
pMAP			.BS 2
pMSG			.BS 2
pLOC			.BS 2
pOBJ			.BS 2

pGame			.BS 2
pActiveLOC		.BS 2
pTxtBuf			.BS 2

pLineBuf		.BS 2

CH				.BS 1
CV				.BS 1
bCurOn			.BS 1
bCurState		.BS 1

Idx				.BS 1
bEditor			.BS 1

ZS.END			.ED
*---------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.SIG.T10TH
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #0					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*---------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.LIBTUI		.DA LIBTUI
*---------------------------------------
J.GAME.Run		.DA GAME.Run1
				.DA GAME.Run2
				.DA GAME.Run3
				.DA GAME.Run4
				.DA GAME.Run5
*---------------------------------------
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.OK		.DA MSG.OK
L.MSG.KO        .DA MSG.KO
L.MSG.LOADING	.DA MSG.LOADING
L.MSG.USERCMD	.DA MSG.USERCMD
L.MSG.INIT		.DA MSG.INIT
L.MSG.RESET		.DA MSG.RESET

L.DEVNAME.GFX	.DA DEVNAME.GFX
L.FONTFILE		.DA FONTFILE
L.FONTFILEB		.DA FONTFILEB

L.CB.TOPBAR		.DA CB.TOPBAR
L.CB.TEXT		.DA CB.TEXT
L.CB.CHAR		.DA CB.CHAR
L.CB.PIC		.DA CB.PIC

L.PRJFILES		.DA PRJ.DIC
                .DA PRJ.MAP
                .DA PRJ.MSG
                .DA PRJ.LOC
                .DA PRJ.OBJ

L.PRJ.Lxxx		.DA PRJ.Lxxx

				.DA 0
*---------------------------------------
CS.INIT			clc
				rts

				>LDYA L.LIBTUI
				>KAPI LoadLib
				bcs .99

				>STYA LIBTUI

*				clc

.99				rts
*---------------------------------------
CS.RUN			jsr GetArgs
				bcs .99

				>LDYAI 256
				>LIBC Malloc
				bcs .99

				>STYA pIOBuf

				jsr SCR.Init
				bcs .99

				ldy #0

.1				sty Idx

				jsr LoadFileY
				bcs .9

				ldy Idx
				iny
				iny
				cpy #10
				bcc .1

				jsr GFX.Init
				bcs .9

				jsr GAME.Init
				bcs .9

				jsr GAME.RUN
				bcs .9

				lda #0

.9				pha

				>LDYA L.MSG.RESET
				>LIBC PutS

				pla
				sec
				rts

.8				lda #0
				sec
.99				rts
*---------------------------------------
CS.SIG			rts
*---------------------------------------
CS.QUIT			jsr GAME.Quit

				ldx #8

.1				stx Idx

				>LDYA pDIC,x
				beq .2

				>LIBC Free

.2				ldx Idx
				dex
				dex
				bpl .1

				>LDYA pIOBuf
				beq .5

				>LIBC Free

.5				>LDYA CB.TOPBAR+S.CB.hSrc
				beq .6

				>KAPI FreeBuf

.6				>LDYA CB.TEXT+S.CB.hSrc
				beq .7

				>KAPI FreeBuf

.7				ldy hFDGFX
				beq .8

				lda hFDGFX+1

				>LIBC Close

.8				>LDYA LIBTUI
				beq .80

				>KAPI UnloadLib

.80				clc
				rts
*--------------------------------------
GetArgs			inc Idx
				lda Idx
				>KAPI ArgV
				bcs .8

				>STYA R1

				lda (R1)
				cmp #'-'
				bne .4

				ldy #1
				lda (R1),y

				ldx #OptionVars-OptionList-1

.1				cmp OptionList,x
				beq .2

				dex
				bpl .1

				bra .9

.2				iny
				lda (R1),y
				bne .9

				ldy OptionVars,x

				lda #$ff
				sta 0,y

				bra GetArgs

.4				lda pBasepath+1
				bne .9

				>LDYA R1
				>STYA pBasepath
				bra GetArgs

.8				lda pBasepath+1
				beq .9

				clc
				rts

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

				lda #E.SYN
				sec
				rts
*--------------------------------------
LoadFileY		sty R2

				lda L.PRJFILES,y
				sta R1
				lda L.PRJFILES+1,y
				sta R1+1

				jsr pIOBufInit
				jsr pIOBufCatR1

				>SS
				>PUSHW L.MSG.LOADING
				>PUSHW pIOBuf
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs PrintERR

				>LDYA pIOBuf
				>LIBC LoadTxtFile
				bcs PrintERR

				ldx R2
				>STYA pDIC,x
				
*				clc				
*--------------------------------------
PrintERR		bcs .1
				
				>LDYA L.MSG.OK
				>LIBC PutS

				clc
				rts

.1				pha
				tay
				>SS
				>PUSHW L.MSG.KO
				tya
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				>SR
				pla
				sec
				rts
*--------------------------------------
SCR.Init		>SS
				>PUSHW L.MSG.INIT
				>PUSHW A2osX.KVER
				>PUSHW pBasepath
				>PUSHBI 4
				>LIBC PrintF
				>SR

				rts
*--------------------------------------
GAME.Init		>LDYAI 768
				>LIBC Malloc
				bcs .9

				>STYA pGame
				>STYA R1

				ldx #3

				lda #0
				tay

.1				sta (R1),y
				iny
				bne .1

				inc R1+1
				dex
				bne .1

				lda #1
				sta (pGame)

				>LDYAI 41*24
				>LIBC Malloc
				bcs .9

				>STYA pTxtBuf
				>STYA CB.TOPBAR+S.CB.pTxt

				jsr TXT.ClrScr

				clc

.9				rts
*--------------------------------------
GAME.Run		lda (pGame)
				>CMP.G GAME.ActiveLOC
				beq .1

				>LDYA CB.PIC+S.CB.hSrc
				beq .11
				
				>KAPI FreeBuf
				
				stz CB.PIC+S.CB.hSrc+1
				
.11				lda (pGame)
				jsr GAME.GetLOC
				bcs .99

				jsr GAME.Paint
				bcs .99

				jsr GAME.GetLOCDesc
				bcs .10

				jsr TXT.PrintMultiLineYA

.10				jsr TXT.Scroll

.1				jsr GAME.GetCmdLine
				bcs .99

				lda CH
				cmp #1
				bne .2

				>LDA.G GAME.bTextMode
				eor #$FF
				sta (pData),y
				
				jsr GAME.Paint
				bcs .99
				
				bra .1

.2				jsr GAME.ParseCmdLine

				jsr GAME.ScanDIC
				bcs .9
				
				lda R3
				beq .9
				
				cmp #6
				bcs .9
				
				asl
				tax
				jsr .7
				
				bcc GAME.Run
				
.9

				bra .10
				
.7				jmp	(J.GAME.Run-2,x)		

.99				rts
*--------------------------------------
* Nav Verbs
*--------------------------------------
GAME.Run1		sec
				rts
*--------------------------------------
* Nav Directions
*--------------------------------------
GAME.Run2		ldx R3+1
				jsr GAME.GetMAPx
			
				lda (R2)

				cmp #','
				beq .9

				>LDYA R2
				jsr GAME.GetMAPYA
				bcs .99
				
				sta (pGame)
			
				clc
				rts
			
.9				lda #2
				jsr GAME.GetMSG
				jsr TXT.PrintMultiLineYA
			
			
				sec
.99				rts
*--------------------------------------
* Verbs
*--------------------------------------
GAME.Run3
*--------------------------------------
* Objects
*--------------------------------------
GAME.Run4
*--------------------------------------
* LOC Objects
*--------------------------------------
GAME.Run5		sec
				rts
*--------------------------------------
GAME.Paint		>LDA.G GAME.bTextMode
				bmi .8
				
				>LDYA L.CB.TOPBAR
				jsr GFX.WriteYA
				bcs .9

				lda CB.PIC+S.CB.hSrc+1
				bne .1
				
				jsr GAME.GetLOCPic

.1				>LDYA L.CB.PIC
				jmp GFX.WriteYA
			
.8				jmp GFX.PaintAll

.9				rts
*--------------------------------------
GAME.GetCmdLine	ldx #23
				stx CV
				jsr TXT.GetLineX
				>STYA pLineBuf

				stz CH

				lda #'>'
				sta (pLineBuf)
				jsr GFX.PrintCharA

				inc CH

.2				jsr GFX.CurOn

.3				>SLEEP
				jsr GFX.CurBlink
				jsr GFX.GetC
				bcs .7

				cmp #3					CTRL-C
				beq .9

				cmp #127
				beq .6

				cmp #C.BS
				beq .6

				cmp #C.CR
				clc
				beq .8

				cmp #C.SPACE
				bcc .3

				cmp #'a'
				bcc .4

				cmp #'z'+1
				bcs .4

				eor #$20

.4				ldy CH
				cpy #39
				bcs .3

				sta (pLineBuf),y

				jsr GFX.CurOff

				jsr GFX.PrintChar
				inc CH
				bra .2
*--------------------------------------
.6				ldy CH
				cpy #1
				beq .3

				jsr GFX.CurOff

				dec CH
				ldy CH
				lda #C.SPACE
				sta (pLineBuf),y
				jsr GFX.PrintCharA

				bra .2
*--------------------------------------
.7				cmp #E.NODATA
				beq .3

				sec

.8				php
				pha
				jsr GFX.CurOff
				pla
				plp

.9				rts
*--------------------------------------
GAME.ParseCmdLine
				>LDYA pIOBuf
				>STYA R1

				ldy #0

.1				iny
				cpy CH
				beq .8

				lda (pLineBuf),y
				cmp #C.SPACE
				beq .1

.2				sta (R1)
				>INCW R1

				iny
				cpy CH
				beq .8

				lda (pLineBuf),y
				cmp #C.SPACE
				bne .2

				sta (R1)
				>INCW R1

.3				iny
				cpy CH
				beq .8

				lda (pLineBuf),y
				cmp #C.SPACE
				beq .3

.4				sta (R1)
				>INCW R1

				iny
				cpy CH
				beq .8

				lda (pLineBuf),y
				cmp #C.SPACE
				bne .4

.8				lda #0
				sta (R1)

				rts
*--------------------------------------
GAME.ScanDIC	>SS
				>PUSHW L.MSG.USERCMD
				>PUSHW pIOBuf
				>PUSHBI 2
				>LIBC PrintF
				>SR

				stz R3
				stz R3+1
				stz R4
				stz R4+1

				>LDYA pIOBuf
				>STYA R1

				>LDYA pDIC
				>STYA R2

.1				ldy #$ff

.2				iny
				lda (R2),y
				cmp (R1),y
				beq .2

				cmp #','
				bne .6

				tya
				beq .7


				
				iny
				lda (R2),y
				and #$0F
				sta R3
				iny
				iny

				lda (R2),y
				and #$0F
				sta R3+1
				iny
				iny


				clc
				jmp PrintERR

.6				ldy #0

.7				iny
				lda (R2),y
				cmp #C.LF
				bne .7

*				sec						skip C.LF

				tya
				adc R2
				sta R2
				bcc .8

				inc R2+1

.8				lda (R2)
				bne .1

				lda #E.INVKEY
				sec
.99				jmp PrintERR
*--------------------------------------
GAME.GetLOC		jsr GAME.GetMAPy
				bcs .9

				ldx #11					Title
				jsr GAME.GetMAPx

				ldy #$ff

.1				iny
				lda (R2),y
				sta (pTxtBuf),y
				jsr IsSep
				bcc .1

				lda #C.SPACE

.2				sta (pTxtBuf),y
				iny
				cpy #40
				bcc .2

				ldx #12					Exits
				jsr GAME.GetMAPx

				ldx #40
				ldy #$FF
				
.33				dex
				iny
				lda (R2),y
				jsr IsSep
				bcc .33

				txa
				tay

.4				lda (R2)
				>INCW R2
				iny
				sta (pTxtBuf),y
				cpy #39
				bcc .4

				txa
				tay

				ldx #5

.3				lda STR.Exits,x
				dey
				sta (pTxtBuf),y
				dex
				bpl .3

				clc

.9				rts
*--------------------------------------
* YA = Lxxx
*--------------------------------------
GAME.GetMAPYA	>STYA R2

				>LDYA pMAP
				>STYA pActiveLOC

				ldx #0

.1				lda (pActiveLOC)
				beq .9

				ldy #$ff

.2				iny
				
				lda (R2),y
				jsr IsSep
				bcs .3
				
				cmp (pActiveLOC),y
				beq .2
				
				bne .6
				
.3				lda (pActiveLOC),y
				cmp #':
				beq .8

.6				iny
				lda (pActiveLOC),y
				beq .9

				cmp #C.CR
				bne .6
				
				inx

				iny
				lda (pActiveLOC),y
				cmp #C.LF
				bne .7

				iny

.7				tya
				clc
				adc pActiveLOC
				sta pActiveLOC
				bcc .1

				inc pActiveLOC+1
				bra .1

.8				txa
				clc
				rts

.9				lda #E.INVKEY
				sec
				rts
*--------------------------------------
GAME.GetMAPy	sta R2
				stz R2+1

				>LDYA pMAP
				>STYA pActiveLOC

.1				lda (pActiveLOC)
				beq .9

				lda R2
				cmp R2+1
				beq .8

				inc R2+1

				ldy #$ff

.2				iny
				lda (pActiveLOC),y
				beq .9

				cmp #C.CR
				bne .2

				iny
				lda (pActiveLOC),y
				cmp #C.LF
				bne .3

				iny

.3				tya
				clc
				adc pActiveLOC
				sta pActiveLOC
				bcc .1

				inc pActiveLOC+1
				bra .1

.8				clc
				rts

.9				lda #E.INVKEY
				sec
				rts
*--------------------------------------
GAME.GetMAPx	txa
				beq .8

				>LDYA pActiveLOC
				>STYA R2

.1				ldy #$ff

.2				iny
				lda (R2),y
				jsr IsSep
				bne .2

				tya
				sec
				adc R2
				sta R2

				lda #0
				adc R2+1
				sta R2+1
				dex
				bne .1

.8				clc
				rts
*--------------------------------------
GAME.GetLOCPic	jsr pIOBufInit
				>LDYA pActiveLOC
				jsr pIOBufCatYA

*				>SS
*				>PUSHW pIOBuf
*				>PUSHBI O.RDONLY
*				>PUSHBI $CB				PIX
*				>PUSHWI 0
*				>LIBC LoadFile
*				>SR
*				bcs .9

				>LDYA pIOBuf
				>KAPI LoadBuf
				bcs .9

				>STYA CB.PIC+S.CB.hSrc

.9				rts
*--------------------------------------
GAME.GetLOCDesc	>LDYA pLOC
				>STYA R1

.1				lda (R1)
				beq .9

				ldy #$ff

.2				iny
				lda (R1),y
				cmp (pActiveLOC),y
				bne .5

				cmp #':'
				bne .2

				iny
				tya
				clc
				adc R1
				tay
				lda R1+1
				adc #0

				clc
				rts

.5				cmp #C.CR
				beq .7

.6				iny
				lda (R1),y
				beq .9

				cmp #C.CR
				bne .6

.7				tya
				inc						skip C.CR
				sec						skip C.LF
				adc R1
				sta R1
				bcc .1

				inc R1+1
				bra .1

.9				lda #E.INVKEY
				sec
				rts
*--------------------------------------
GAME.GetMSG		tax
				>LDYA pMSG
				>STYA R1

				txa
				beq .8
				
.1				ldy #$ff
				
.2				iny
				lda (R1),y
				jsr IsSep
				bcc .2

				tya
				sec
				adc R1
				sta R1
				bcc .3
				
				inc R1+1
				
.3				dex
				bne .1
				
.8				lda R1
				clc
				adc #5
				tay
				
				lda R1+1
				adc #0

				clc
				rts				
*--------------------------------------
GAME.Quit		>LDYA pTxtBuf
				beq .1

				>LIBC Free

.1				>LDYA CB.PIC+S.CB.hSrc
				beq .2
				
				>KAPI FreeBuf

.2				>LDYA pGame
				beq .8

				>LIBC Free

.8				rts
*--------------------------------------
TXT.ClrScr		ldx #0
				lda #C.SPACE
				jsr TXT.ClrLineAX

				inx
				lda #'='
				jsr TXT.ClrLineAX

.1				inx
				lda #C.SPACE
				jsr TXT.ClrLineAX
				cpx #23
				bne .1

				rts
*--------------------------------------
TXT.ClrLineAX	pha

				jsr TXT.GetLineX
				>STYA R4

				ldy #40
				lda #0
				sta (R4),y

				pla

				dey

.1				sta (R4),y
				dey
				bpl .1

				rts
*--------------------------------------
TXT.PrintMultiLineYA
				>STYA R1

.10				jsr TXT.Scroll

				ldx #23
				jsr TXT.GetLineX
				>STYA pLineBuf

				stz CH

.1				lda (R1)
				cmp #C.CR
				beq .8

				ldy #$ff

.2				iny
				lda (R1),y
				cmp #C.SPACE
				bne .2

				tya
				sec
				adc CH
				cmp #41
				bcc .4

				jsr GFX.PrintLine
				bra .10

.4				ldy CH

				sta CH

.5				lda (R1)
				sta (pLineBuf),y
				>INCW R1
				iny
				cmp #C.SPACE
				bne .5

				bra .1

.8				jsr GFX.PrintLine

				rts
*--------------------------------------
TXT.Scroll		ldx #2

.1				jsr TXT.GetLineX
				>STYA R3

				inx

				jsr TXT.GetLineX
				>STYA R4

				ldy #39

.2				lda (R4),y
				sta (R3),y
				dey
				bpl .2

				cpx #23
				bne .1

				ldy #39
				lda #C.SPACE

.3				sta (R4),y
				dey
				bpl .3

				jsr GFX.PaintAll

				clc

				rts
*--------------------------------------
TXT.GetLineX	clc
				lda pTxtbuf
				adc TXT.L,x
				tay
				lda pTxtBuf+1
				adc TXT.H,x
				rts
*--------------------------------------
GFX.Init		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC Open
				>SR
				bcs .9

				>STYA hFDGFX

				>LDYA L.FONTFILEB
				>KAPI LoadBuf
				bcs .9

				>STYA CB.TOPBAR+S.CB.hSrc

				>LDYA L.FONTFILE
				>KAPI LoadBuf
				bcs .9

				>STYA CB.TEXT+S.CB.hSrc
				>STYA CB.CHAR+S.CB.hSrc

				>LDYA pData				GAME.CharBuf
				>STYA CB.CHAR+S.CB.pTxt

.9				rts
*--------------------------------------
GFX.GetC		>SS
				>PUSHW hFDGFX
				>PUSHW pData			GAME.CharBuf
				>PUSHWI 1
				>LIBC Read
				>SR
				bcs .9

				lda (pData)				GAME.CharBuf

*				clc

.9				rts
*--------------------------------------
GFX.CurBlink	bit bCurOn
				bpl .8

				lda A2osX.T16
				and #2
				bne .1

				bit bCurState
				bpl .8

				bra GFX.CurHide

.1				bit bCurState
				bmi .8

				bra GFX.CurShow

.8				rts
*--------------------------------------
GFX.CurOn		dec bCurOn

				bit bCurState
				bmi .8

				jsr GFX.CurShow

.8				rts
*--------------------------------------
GFX.CurOff		stz bCurOn

				bit bCurState
				bpl .8

				jsr GFX.CurHide

.8				rts
*--------------------------------------
GFX.CurShow		dec bCurState

				lda #127
				bra GFX.PrintCharA
*--------------------------------------
GFX.CurHide		stz bCurState

GFX.PrintChar	ldy CH
				lda (pLineBuf),y

GFX.PrintCharA	sta (pData)				GAME.CharBuf

				ldy CH
				lda TXT.M7L,y
				sta CB.CHAR+S.CB.X1
				lda TXT.M7H,y
				sta CB.CHAR+S.CB.X1+1

				>LDYA L.CB.CHAR
				jsr GFX.WriteYA
				rts
*--------------------------------------
GFX.PaintAll	ldx #20

				>LDA.G GAME.bTextMode
				beq .3

				ldx #0

.3				stx CV

				jsr GFX.PrintLine

				ldx CV
				inx
				cpx #24
				bcc .3

				clc

				rts
*--------------------------------------
GFX.PrintLine	lda CV
				tax
				asl
				asl
				asl
				sta CB.TEXT+S.CB.Y1

				jsr TXT.GetLineX
				>STYA CB.TEXT+S.CB.pTxt

				>LDYA L.CB.TEXT
*--------------------------------------
GFX.WriteYA		>STYA R4
				>SS
				>PUSHW hFDGFX
				>PUSHW R4

				>PUSHBI 0
				lda (R4)
				and #$7F
				tax
				lda CB.CmdLen-1,x
				>PUSHA

				>LIBC Write
				>SR
				rts
*--------------------------------------
pIOBufInit		ldy #$ff

.1				iny
				lda (pBasepath),y
				sta (pIOBuf),y
				bne .1

				tya
				clc
				adc pIOBuf
				sta pFilename
				lda pIOBuf+1
				adc #0
				sta pFilename+1

				rts
*---------------------------------------
pIOBufCatYA		>STYA R1
*---------------------------------------
pIOBufCatR1		ldy #$ff

.1				iny
				lda (R1),y
				sta (pFilename),y
				beq .8

				eor #':'
				bne .1

				sta (pFilename),y

.8				rts
*---------------------------------------
IsSep			cmp #C.CR
				beq .9

				cmp #':'
				beq .9

				cmp #','
				beq .9

				clc
				rts

.9				lda #0
				rts
*---------------------------------------
CS.END			.EQ *
*--------------------------------------
LIBTUI			.AZ "libtui"
*---------------------------------------
DEVNAME.GFX		.AZ "/dev/gfx"
FONTFILE		.AZ "/opt/gui/fonts/a27x8"
FONTFILEB		.AZ "/opt/gui/fonts/a27x8b"
*--------------------------------------
CB.CmdLen		.DA #S.CB.Y1+2			SETPIXEL
				.DA #S.CB.Y1+2			GETPIXEL
				.DA #S.CB.X2+2			HLINE
				.DA #S.CB.Y2+2			VLINE
				.DA #S.CB.Y2+2			FILLRECT
				.DA #S.CB.hSrc+2		BITBLT
				.DA #S.CB.hSrc+2		GETRECTBUFSIZE
				.DA #S.CB.pTxt+2		DRAWTEXT
				.DA #S.CB.pTxt+2		GETTEXTSIZE
*--------------------------------------
CB.TOPBAR		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
				.DA 0					X1
				.DA 0					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0					SrcW
				.DA 0					SrcH
				.DA 0
				.DA 0
			.BS 2						hSrc
			.BS	2						pTxt
*--------------------------------------
CB.TEXT			.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
				.DA 0					X1
			.BS 2						Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2						hSrc
			.BS 2						pTxt
*--------------------------------------
CB.CHAR			.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
			.BS 2						X1
				.DA 184					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2						hSrc
			.BS 2						pTxt
*--------------------------------------
CB.PIC			.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.COLOR+S.CB.OP.SET
				.DA #S.CB.M.C256
				.DA #0
				.DA 0					X1
				.DA 9					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 40					SrcW
				.DA 149					SrcH
				.DA 0					pSrc
				.DA 0					pDst
			.BS 2					hSrc
*--------------------------------------
CB.OBJ			.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.COLOR+S.CB.OP.ORA+S.CB.OP.MASK
				.DA #S.CB.M.C256
				.DA #0
			.BS 2						X1
			.BS 2						Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
			.BS 2						SrcW
			.BS 2						SrcH
			.BS 2						pSrc
				.DA 0					pDst
				.DA 0					hSrc
*--------------------------------------
OptionList		.AS "Ee"
OptionVars		.DA #bEditor
				.DA #bEditor
*--------------------------------------
MSG.USAGE		.CS "Usage : ACSX /project/dir/\r\n"
				.CZ "   -E : Editor mode\r\n"
MSG.OK			.CZ "[OK]"
MSG.KO			.CS "[%h]"
MSG.CRLF		.CZ "\r\n"
*--------------------------------------
MSG.LOADING		.CZ "Loading:%s..."
MSG.USERCMD		.CZ "UserCmd:%s..."
*--------------------------------------
MSG.INIT		.CS "\ec"
				.CS "\e[7m\e[37;40m"	top bar color
				.CS "A2osX-%d.%02d ACSX (Adventure Construction Set):%s\r\n"
				.CS "\e[0m\e[93;44m"
				.CS "\e[2;24r"			scroll region
				.CZ "\e[?7l"			Disable Line Wrap
MSG.RESET		.CZ "\ec\e[?7h"			Enable Line Wrap
*---------------------------------------
PRJ.DIC			.AZ "dic"
PRJ.MAP			.AZ "map"
PRJ.MSG			.AZ "msg"
PRJ.LOC			.AZ "loc"
PRJ.OBJ			.AZ "obj"
PRJ.Lxxx		.CZ "%sL%03d"
*---------------------------------------
STR.Exits		.AS "EXITS:"
*---------------------------------------
TXT.L			.DA #41*00
				.DA #41*01
				.DA #41*02
				.DA #41*03
				.DA #41*04
				.DA #41*05
				.DA #41*06
				.DA #41*07
				.DA #41*08
				.DA #41*09
				.DA #41*10
				.DA #41*11
				.DA #41*12
				.DA #41*13
				.DA #41*14
				.DA #41*15
				.DA #41*16
				.DA #41*17
				.DA #41*18
				.DA #41*19
				.DA #41*20
				.DA #41*21
				.DA #41*22
				.DA #41*23

TXT.H			.DA /41*00
				.DA /41*01
				.DA /41*02
				.DA /41*03
				.DA /41*04
				.DA /41*05
				.DA /41*06
				.DA /41*07
				.DA /41*08
				.DA /41*09
				.DA /41*10
				.DA /41*11
				.DA /41*12
				.DA /41*13
				.DA /41*14
				.DA /41*15
				.DA /41*16
				.DA /41*17
				.DA /41*18
				.DA /41*19
				.DA /41*20
				.DA /41*21
				.DA /41*22
				.DA /41*23
*---------------------------------------
TXT.M7L			.DA #7*00
				.DA #7*01
				.DA #7*02
				.DA #7*03
				.DA #7*04
				.DA #7*05
				.DA #7*06
				.DA #7*07
				.DA #7*08
				.DA #7*09
				.DA #7*10
				.DA #7*11
				.DA #7*12
				.DA #7*13
				.DA #7*14
				.DA #7*15
				.DA #7*16
				.DA #7*17
				.DA #7*18
				.DA #7*19
				.DA #7*20
				.DA #7*21
				.DA #7*22
				.DA #7*23
				.DA #7*24
				.DA #7*25
				.DA #7*26
				.DA #7*27
				.DA #7*28
				.DA #7*29
				.DA #7*30
				.DA #7*31
				.DA #7*32
				.DA #7*33
				.DA #7*34
				.DA #7*35
				.DA #7*36
				.DA #7*37
				.DA #7*38
				.DA #7*39
*---------------------------------------
TXT.M7H			.DA /7*00
				.DA /7*01
				.DA /7*02
				.DA /7*03
				.DA /7*04
				.DA /7*05
				.DA /7*06
				.DA /7*07
				.DA /7*08
				.DA /7*09
				.DA /7*10
				.DA /7*11
				.DA /7*12
				.DA /7*13
				.DA /7*14
				.DA /7*15
				.DA /7*16
				.DA /7*17
				.DA /7*18
				.DA /7*19
				.DA /7*20
				.DA /7*21
				.DA /7*22
				.DA /7*23
				.DA /7*24
				.DA /7*25
				.DA /7*26
				.DA /7*27
				.DA /7*28
				.DA /7*29
				.DA /7*30
				.DA /7*31
				.DA /7*32
				.DA /7*33
				.DA /7*34
				.DA /7*35
				.DA /7*36
				.DA /7*37
				.DA /7*38
				.DA /7*39
*---------------------------------------
ID.END			.EQ *
*---------------------------------------
				.DUMMY
				.OR 0
GAME.CharBuf	.BS 2					for ending \0 (COUT)
GAME.bTextMode	.BS 1
GAME.ActiveLOC	.BS 1

DS.END			.ED
*---------------------------------------
MAN
SAVE usr/src/opt/acsx/bin/acsx.s
ASM
