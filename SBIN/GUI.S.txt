NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF sbin/gui
*--------------------------------------
				.INB inc/io.i
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/gfx.i
				.INB inc/libgui.i
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pScreen			.BS 2
pDesktop		.BS 2					pWND

pSysBar			.BS 2					pWND

pSysBut			.BS 2					pPANE
pSysWNDs		.BS 2					pPANE
pSysClk			.BS 2					pPANE

pSysMenu		.BS 2

pMsgBox			.BS 2

SYSBAR.Y1		.BS 1
SYSBAR.H		.BS 1

SYSBUT.W		.EQ *
SYSWNDs.X1		.BS 2
SYSWNDs.W		.BS 2
SYSCLK.X1		.BS 2
SYSCLK.W		.BS 2

b1sTick			.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.LIBGUI		.DA LIBGUI
L.SysBar.2osX	.DA SysBar.2osX
L.SysBar.Clk	.DA SysBar.Clk
L.SysMenu		.DA SysMenu
L.SysMenu.Programs	.DA SysMenu.Programs
L.SysMenu.Settings	.DA SysMenu.Settings
L.SysBut.fEnter	.DA CS.SysBut.fEnter
L.SysBut.fLeave	.DA CS.SysBut.fLeave
L.SysBut.fPaint	.DA CS.SysBut.fPaint
L.SysClk.fPaint	.DA CS.SysClk.fPaint
J.MSG.IDs		.DA CS.RUN.MSG.T.KEY
				.DA CS.RUN.MSG.T.ID
				.DA CS.RUN.MSG.T.WND
				.DA CS.RUN.MSG.T.XY
L.ABOUT.T		.DA ABOUT.T
L.ABOUT.M       .DA ABOUT.M
L.GUITEST1		.DA GUITEST1
L.GUITEST2		.DA GUITEST2
L.MSG			.DA MSG
				.DA 0
*--------------------------------------
MSG.ID.ABOUT	.EQ 2
MSG.ID.SEARCH	.EQ 8
MSG.ID.RUN		.EQ 10

MSG.ID.TEST1	.EQ 42
MSG.ID.TEST2	.EQ 44
*--------------------------------------
CS.INIT			>LDYA L.LIBGUI
				>KAPI LoadLib
				bcs .9

				>STYA LIBGUI

*				clc

				rts

.9				stz LIBGUI+1

CS.INIT.9		rts
*--------------------------------------
CS.RUN			>LDYA L.SysMenu.Programs
				>STYA SysMenu.1
				>LDYA L.SysMenu.Settings
				>STYA SysMenu.2

				>SS
				>PUSHBI WND.ID.SCREEN
				>LIBCALL LIBGUI,WND.Get
				>SR

				>STYA pScreen

				>SS
				>PUSHBI PTR.T.WAIT
				>LIBCALL LIBGUI,PTR.Set
				>SR

				>LIBCALL LIBGUI,PTR.Show

				jsr CS.Setup
				bcs CS.INIT.9

				>SS
				>PUSHBI PTR.T.ARROW
				>LIBCALL LIBGUI,PTR.Set
				>SR

*				jsr CS.RUN.About
*--------------------------------------
CS.RUN.Loop		>SLEEP

				>SS
				>PUSHW L.MSG
				>LIBCALL LIBGUI,MSG.Get
				>SR
				bcs .9

				ldx MSG+S.MSG.T
				beq CS.RUN.Loop			#S.MSG.Y.IDLE

				jsr .8
				bcc CS.RUN.Loop

.9				>DEBUG
				rts

.8				jmp (J.MSG.IDs-2,x)
*--------------------------------------
CS.RUN.MSG.T.KEY
				clc
				rts
*--------------------------------------
CS.RUN.MSG.T.ID	lda MSG+S.MSG.ID
				cmp #MSG.ID.ABOUT
				bne .1
*			>DEBUG
				jmp CS.RUN.About

.1				cmp #MSG.ID.TEST1
				bne .2

				>LDYA L.GUITEST1
				jmp CS.RUN.Exec

.2				cmp #MSG.ID.TEST2
				bne .8

				>LDYA L.GUITEST2
				jmp CS.RUN.Exec

.8				clc
				rts
*--------------------------------------
CS.RUN.MSG.T.WND
				rts
				clc
*--------------------------------------
CS.RUN.MSG.T.XY	clc
				rts
*--------------------------------------
*--------------------------------------
*--------------------------------------
*--------------------------------------
CS.Setup		>SS
				>PUSHWI FON.ID.SYS
				>PUSHEA.G SYSFON
				>LIBCALL LIBGUI,FON.GetProps
				>SR
				bcs .9

				>LDA.G SYSFON+S.FON.PixH
				clc
				adc #2
				sta SYSBAR.H

				jsr CS.SetupDesktop
				bcs .9

				jsr CS.SetupSysBar
				bcs .9

				>SS
				>PUSHW pScreen
				>LIBCALL LIBGUI,WND.ShowAll
				>SR

.9				rts
*--------------------------------------
* guiNewWnd(pParentW, wFlags, iX1, iY1, iW, iH)
*--------------------------------------
CS.SetupDesktop	>SS
				>PUSHW pScreen			pParentW
				>PUSHWZ					wFlags
				>PUSHWZ					iX1
				>PUSHWZ					iY1

				ldy #S.OBJ.W+1
				>PUSHB (pScreen),y
				dey
				>PUSHB (pScreen),y

				>PUSHBI 0				iH+1
				ldy #S.OBJ.H
				lda (pScreen),y
				sec
				sbc SYSBAR.H
				sta SYSBAR.Y1
				>PUSHA					iH

				>LIBCALL LIBGUI,WND.New
				>SR
				bcs .9

				>STYA pDesktop

				>SS
				>PUSHBI WND.ID.DESKTOP
				>PUSHW pDesktop
				>LIBCALL LIBGUI,WND.Set
				>SR
				bcs .9

				>SS
				>PUSHW pDesktop
				>PUSHBI S.OBJ.BGCOLOR
				>PUSHW PREFS.DesktopColor
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

.9				rts
*--------------------------------------
CS.SetupSysBar	>SS
				>PUSHW pScreen			pParentW
				>PUSHWZ					wFlags
				>PUSHWZ					iX1

				>PUSHBI 0
				>PUSHB SYSBAR.Y1		iY1

				ldy #S.OBJ.W+1			iW
				>PUSHB (pScreen),y
				dey
				>PUSHB (pScreen),y

				>PUSHBI 0				iH+1
				>PUSHB SYSBAR.H			iH

				>LIBCALL LIBGUI,WND.New
				>SR
				bcs .9

				>STYA pSysBar

				>SS
				>PUSHYA
				>PUSHBI S.OBJ.BGCOLOR
				>PUSHWI	C16.D.GREEN
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				jsr CS.SetupSysBut
				bcs .9

				jsr CS.SetupSysClk
				bcs .9

				jsr CS.SetupSysWNDs
				bcs .9





*				clc

.9				rts
*--------------------------------------
* guiGetTextWidth(pFON, pTXT)
*--------------------------------------
CS.SetupSysBut	>SS
				>PUSHWI FON.ID.SYSB
				>PUSHW L.SysBar.2osX
				>LIBCALL LIBGUI,FON.GetTextW
				>SR
				bcs .9

				tya
*				clc

				adc #25					4 + BM width(16) + 1 + "2osX" + 4
				sta SYSBUT.W

				>SS
				>PUSHW pSysBar			pParentW
				>PUSHWZ					wFlags
				>PUSHWZ					iX1
				>PUSHWZ					iY1
				>PUSHBI 0
				>PUSHB SYSBUT.W
				>PUSHBI 0
				>PUSHB SYSBAR.H
				>LIBCALL LIBGUI,PANE.New
				>SR
.9				bcs .99

				>STYA pSysBut

				jsr CS.SysBut.SetProp

*				clc

.99				rts
*--------------------------------------
CS.SysBut.SetProp
				>SS
				>PUSHYA
				>PUSHBI S.OBJ.BGCOLOR
				lda PREFS.SysBarColor
				eor #$ff
				>PUSHA
				>PUSHB PREFS.SysBarColor
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				>SS
				>PUSHW pSysBut
				>PUSHBI S.OBJ.fEnter
				>PUSHW L.SysBut.fEnter
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				>SS
				>PUSHW pSysBut
				>PUSHBI S.OBJ.fLeave
				>PUSHW L.SysBut.fLeave
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				>SS
				>PUSHW pSysBut
				>PUSHBI S.OBJ.fPaint
				>PUSHW L.SysBut.fPaint
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				rts
*--------------------------------------
* NewMenu(pPARENT, pMENU, iX1, iY1)
* ShowMenu(pWND, pMENU)
*--------------------------------------
CS.SysBut.fEnter
*				ldy #S.OBJ.pNext+1
*				lda (pSysBut),y
*				bne .8

				>SS
				>PUSHW pDesktop
				>PUSHW L.SysMenu
				>PUSHWZ					iX1
				>PUSHBI 0				iY1
				>PUSHB SYSBAR.Y1
				>LIBCALL LIBGUI,MENU.New
				>SR
				bcs .9

				>STYA pSysMenu

*				phy
*				ldy #S.OBJ.pNext+1
*				sta (pSysBut),y
*				dey
*				pla
*				sta (pSysBut),y

				>SS
				>PUSHW pSysMenu
				>LIBCALL LIBGUI,MENU.Show
				>SR


.8				clc

.9				rts
*--------------------------------------
CS.SysBut.fLeave


.8				clc

.9				rts
*--------------------------------------
* drawBitmap (pWND, bOP, bM, pBitmap, iX1, iY1)
* drawtext (pWND, bOP, pFONT, iX1, iY1, pTEXT)
*--------------------------------------
CS.SysBut.fPaint
				>SS
				>PUSHW pSysBut
				>PUSHBI S.CB.OP.MASK+S.CB.OP.ORA
				>PUSHBI S.CB.M.C16
				>PUSHWI BM.ID.APPLE
				>PUSHWI 4
				>PUSHWI 1
				>LIBCALL LIBGUI,DRAW.BitMap
				>SR
				bcs .9

				>SS
				>PUSHW pSysBut
				>PUSHBI S.CB.OP.XOR
				>PUSHWI FON.ID.SYSB
				>PUSHWI 21
				>PUSHWI 2
				>PUSHW L.SysBar.2osX
				>LIBCALL LIBGUI,DRAW.Text
				>SR
.9				rts
*--------------------------------------
CS.SetupSysClk	>SS
				>PUSHWI FON.ID.FIXED
				>PUSHW L.SysBar.Clk
				>LIBCALL LIBGUI,FON.GetTextW
				>SR
				bcs .9

				tya
*				clc
				adc #2					1 + "00:00" + 1
				sta SYSCLK.W

				ldy #S.OBJ.W
				lda (pScreen),y
				sec
				sbc SYSCLK.W
				sta SYSCLK.X1
				iny
				lda (pScreen),y
				sbc #0
				sta SYSCLK.X1+1

				>SS
				>PUSHW pSysBar			pParentW
				>PUSHWZ					wFlags
				>PUSHW SYSCLK.X1		iX1
				>PUSHWZ					iY1
				>PUSHBI 0
				>PUSHB SYSCLK.W
				>PUSHBI 0
				>PUSHB SYSBAR.H
				>LIBCALL LIBGUI,PANE.New
				>SR
.9				bcs .99

				>STYA pSysClk

				>SS
				>PUSHYA
				>PUSHBI S.OBJ.BGCOLOR
				>PUSHW PREFS.SysBarColor
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

				>SS
				>PUSHW pSysClk
				>PUSHBI S.OBJ.fPaint
				>PUSHW L.SysClk.fPaint
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

.99				rts
*--------------------------------------
CS.SysClk.fPaint
				>SS
				>PUSHW pSysClk
				>PUSHBI S.CB.OP.XOR
				>PUSHWI FON.ID.FIXED
				>PUSHWI 1
				>PUSHWI 2
				>PUSHW L.SysBar.Clk
				>LIBCALL LIBGUI,DRAW.Text
				>SR
				rts
*--------------------------------------
CS.SetupSysWNDs	sec
				lda SYSCLK.X1
				sbc SYSWNDs.X1
				sta SYSWNDs.W
				lda SYSCLK.X1+1
				sbc SYSWNDs.X1+1
				sta SYSWNDs.W+1

				>SS
				>PUSHW pSysBar			pParentW
				>PUSHWZ					wFlags
				>PUSHW SYSWNDs.X1		iX1
				>PUSHWZ					iY1
				>PUSHW SYSWNDs.W		iW
				>PUSHBI 0				iH
				>PUSHB SYSBAR.H
				>LIBCALL LIBGUI,PANE.New
				>SR
				bcs .9

				>STYA pSysWNDs

				>SS
				>PUSHYA
				>PUSHBI S.OBJ.BGCOLOR
				>PUSHW PREFS.SysBarColor
				>LIBCALL LIBGUI,OBJ.SetProp
				>SR

.9				rts
*--------------------------------------
* NewMenu(pPARENT, pMENU, x, y)
*--------------------------------------
CS.RUN.SysMenu	>SS
				>PUSHW pSysBut
				>PUSHW L.SysMenu
				>PUSHW 0
				>PUSHBI 0
				>PUSHB SYSBAR.Y1
				>LIBCALL LIBGUI,MENU.New
				>SR
				bcs .9

				>STYA pSysMenu


.9				rts
*--------------------------------------
CS.RUN.About	>SS
				>PUSHW pDesktop
				>PUSHW L.ABOUT.T
                >PUSHWI BM.ID.INFO
				>PUSHW L.ABOUT.M
				>PUSHWI 63				BUT.ID.OK
				>LIBCALL LIBGUI,MBOX.MsgBox
				>SR
				bcs .9

				>STYA pMsgBox

				>SS
				>PUSHW pMsgBox
				>LIBCALL LIBGUI,WND.Show
				>SR

.1				>SLEEP

				>SS
				>PUSHW L.MSG
				>LIBCALL LIBGUI,MSG.Get
				>SR
				bcs .1

				lda MSG+S.MSG.T
				cmp #S.MSG.T.ID
				beq .8

				cmp #S.MSG.T.WND
				bne .1

				lda MSG+S.MSG.WND
				cmp #S.MSG.WND.CLOSE
				bne .1

.8				>SS
				>PUSHW pMsgBox
				>LIBCALL LIBGUI,WND.Destroy
				>SR

.9				rts
*--------------------------------------
CS.RUN.Exec		>SS
				>PUSHYA
				>PUSHWI 0
				>LIBC ExecL
				>SR
			bcs *
				rts
*--------------------------------------
CS.SIG			sec
				ror b1sTick

				>LIBCALL LIBGUI,PTR.Update

.9				sec
				rts
*--------------------------------------
CS.QUIT			>LDYA LIBGUI
				beq .8

				>KAPI UnloadLib

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
LIBGUI			.AZ "libgui"
*--------------------------------------
PREFS.DesktopColor	.DA C16.D.BLUE
PREFS.SysBarColor	.DA #C.WHITE,#C.WHITE
*--------------------------------------
SysBar.2osX		.AZ	"2osX"
SysBar.Clk		.AZ	"00:00"
*--------------------------------------
SysMenu			.DA #S.MITEM.T.ITEM
				.DA #MSG.ID.ABOUT
				.DA #KEYMOD.CTRL+KEYMOD.OA+KEYMOD.CA,#'A'
				.DA BM.ID.APPLE			pICON
				.AZ "About..."

				.DA #S.MITEM.T.SEP

				.DA #S.MITEM.T.SUBMENU
SysMenu.1		.DA SysMenu.Programs
				.DA 0					pICON
				.AZ "Programs"

				.DA #S.MITEM.T.SUBMENU
SysMenu.2		.DA SysMenu.Settings
				.DA 0					pICON
				.AZ "Settings"

				.DA #S.MITEM.T.SEP

				.DA #S.MITEM.T.ITEM
				.DA #MSG.ID.SEARCH
				.DA #0,#0				KEYMOD+KEY
				.DA 0					pICON
				.AZ "Search..."

				.DA #S.MITEM.T.ITEM
				.DA #MSG.ID.RUN
				.DA #KEYMOD.CTRL+KEYMOD.OA,#'R'
				.DA 0					pICON
				.AZ "Run..."

				.DA #0
*--------------------------------------
SysMenu.Programs
				.DA #S.MITEM.T.ITEM
				.DA #MSG.ID.TEST1
				.DA #0,#0
				.DA BM.ID.APPLE			pICON
				.AZ "Test ASM"

				.DA #S.MITEM.T.ITEM
				.DA #MSG.ID.TEST2
				.DA #0,#0
				.DA BM.ID.APPLE			pICON
				.AZ "Test C"

				.DA #0
*--------------------------------------
SysMenu.Settings
				.DA #S.MITEM.T.ITEM
				.DA #62					ID
				.DA #0,#0
				.DA 0					pICON
				.AZ "Colors..."

				.DA #S.MITEM.T.ITEM
				.DA #64					ID
				.DA #0,#0
				.DA 0					pICON
				.AZ "SubItem2..."

				.DA #0
*--------------------------------------
*--------------------------------------
ABOUT.T			.AZ "About GUI"
ABOUT.M			.CS "MsgBox demo with all possible buttons.\r\n"
				.CS "...Line 2...\r\n"
				.CZ "...Line 3.\r\n"
GUITEST1		.AZ "/root/asmtest/testgui"
GUITEST2		.AZ "/root/ctest/testgui"
*--------------------------------------
MSG				.BS S.MSG
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
SYSFON			.BS S.FON
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/sbin/gui.s
ASM
