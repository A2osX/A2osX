NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF sbin/telnetd
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/kernel.i
				.INB inc/eth.i
				.INB inc/ip.i
				.INB inc/net.telnet.i
*--------------------------------------
TIMEOUT.MAX		.EQ 200					20 sec.
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hSrvSocket		.BS 2
hCltSocket		.BS 2

TimeOut			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.SA.LOCAL		.DA SA.LOCAL
L.MSG.INITOK	.DA MSG.INITOK
L.MSG.SKTERR	.DA MSG.SKTERR
L.MSG.INCOMING	.DA MSG.INCOMING
L.NOD.Template	.DA NOD.Template
L.CMD.Template	.DA CMD.Template
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #1
				>KAPI ArgV
				bcs .1

				>LIBC AToI
				>STYA SA.LOCAL+S.SA.IN.PORT

.1				>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_STREAM
				>PUSHWZ
				>LIBC Socket
				>SR
				bcs .9

				>STYA hSrvSocket

				>SS
				>PUSHW hSrvSocket
				>PUSHW L.SA.LOCAL
				>PUSHWI S.SA.IN
				>LIBC Bind
				>SR
				bcs .9

				>SS
				>PUSHW hSrvSocket
				>PUSHWI 16				backlog
				>LIBC Listen
				>SR
				bcs .9

				>LDYA L.MSG.INITOK
				>LIBC PutS
				bcc CS.RUN.LOOP

.9				pha
				>LDYA L.MSG.SKTERR
				>LIBC PutS
				pla
				sec
				rts
*--------------------------------------
CS.RUN.LOOP		>SLEEP

				>SS
				>PUSHW hSrvSocket
				>PUSHEA.G SA.CLNT
				>PUSHWI S.SA.IN
				>LIBC Accept
				>SR
				bcs CS.RUN.LOOP

				>STYA hCltSocket
				
				jsr CS.RUN.CLIENT

				bcc CS.RUN.LOOP

				>SS
				>PUSHW hCltSocket
				>PUSHWI 0				how
				>LIBC Shutdown
				>SR
				bra CS.RUN.LOOP
*--------------------------------------
CS.RUN.CLIENT	>SLEEP					give some time for TCPIP SYN/ACK

				>SS
				>PUSHW L.MSG.INCOMING

				>PUSHB hCltSocket

				ldx #3
				ldy #SA.CLNT+S.SA.IN.ADDR

.1				>PUSHB (pData),y
				iny
				dex
				bpl .1

				>PUSHW.G SA.CLNT+S.SA.IN.PORT

				>PUSHBI 7
				>LIBC PrintF
				>SR

				>SS
				>PUSHEA.G NodBuf
				>PUSHW L.NOD.Template
				>PUSHB hCltSocket
				>PUSHBI 1
				>LIBC SPrintF
				>SR

CS.RUN.CLIENT1	>SS
				>PUSHEA.G NodBuf
				>PUSHWZ
				>PUSHW hCltSocket
				>LIBC MKNod
				>SR
				bcs .9

				>SS
				>PUSHEA.G CmdBuf
				>PUSHW L.CMD.Template
				>PUSHEA.G NodBuf
				>PUSHBI 2
				>LIBC SPrintF
				>SR
				bcs .9

				>SS
				>PUSHEA.G CmdBuf
				>PUSHWI 0
				>LIBC ExecL
				>SR

.9				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				rts
*--------------------------------------
CS.QUIT			ldy hSrvSocket
				beq .8

				lda #0

				>LIBC Shutdown

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.INITOK		.AZ "TELNETD:Init Ok, Listening."
MSG.SKTERR		.AZ "TELNETD:Listen Error."
MSG.INCOMING	.CZ "TELNETD:Incoming Connection [SKT=%h] From : %d.%d.%d.%d:%D\r\n"
*--------------------------------------
NOD.Template	.AZ "/dev/tty%h"
CMD.Template	.AZ "/sbin/getty -E %s /sbin/login"
*--------------------------------------
SA.LOCAL		.DA AF_INET				S.SA.IN.AF
				.DA #0,#0,#0,#0			S.SA.IN.ADDR
				.DA TCP.PORT.TELNET
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
SA.CLNT			.BS S.SA.IN
NodBuf			.BS 11
CmdBuf			.BS 65

DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/sbin/telnetd.s
ASM
