PR#3
PREFIX /A2OSX.SRC
NEW
INC 1
AUTO 6
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF /A2OSX.BOOT/SBIN/HTTPD
*--------------------------------------
				.INB /A2OSX.DEV/INC/MACROS.I
				.INB /A2OSX.DEV/INC/A2OSX.I
				.INB /A2OSX.DEV/INC/LIBTCPIP.I
*--------------------------------------
TIMEOUT.MAX		.EQ 30					30 sec.
*--------------------------------------
ZPIPCfgPtr		.EQ ZPBIN
ZPSktPtr		.EQ ZPBIN+2
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #1					BIN Layout Version 1
				.DA 0
				.DA CS.END-CS.START		Code Length To Relocate
				.DA DS.END-DS.START		Data Segment to Allocate
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------				
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.DOEVENT
				.DA	CS.QUIT
L.LIBTCPIP		.DA LIBTCPIP
L.Socket		.DA Socket
L.MSG.TCPWAIT	.DA MSG.TCPWAIT
L.MSG.INITOK	.DA MSG.INITOK
L.MSG.TCPIPERR	.DA MSG.TCPIPERR
L.MSG.SKTERR	.DA MSG.SKTERR
L.MSG.ABORT		.DA MSG.ABORT
L.MSG.INCOMING	.DA MSG.INCOMING
				.DA 0				
*--------------------------------------
CS.INIT			>LDYA L.LIBTCPIP
				>SYSCALL LoadLibYA
				sta hLIBTCPIP

				lda (pPs)
				ora #S.PS.F.EVENT		Now accept events
				sta (pPs)
				clc
				rts
*--------------------------------------
CS.RUN			jsr Init.Timeout

				>LDYA L.MSG.TCPWAIT
				>SYSCALL CPrintFYA
				
.1				>LIBCALL hLIBTCPIP,LIBTCPIP.GET.IPCFG	is TCPIP loaded ?
				bcs .99
				>STYA ZPIPCfgPtr
				
				lda (ZPIPCfgPtr)		Configured ?
				bmi CS.RUN.INIT
				
				>SYSCALL Sleep
				jsr Wait.Timeout
				bcc .1
				>SYSCALL GetC
				bcs .1
				cmp #$03
				bne .1
				jmp CS.RUN.ABORT
				
.99				>LDYA L.MSG.TCPIPERR
				>SYSCALL CPrintFYA
				lda #SYSMGR.ERRSYN
				sec	
				rts
				
CS.RUN.INIT		ldx #3
				ldy #S.IPCFG.IP+3
.1				lda (ZPIPCfgPtr),y
				sta Socket.Src.Addr,x
				dey
				dex
				bpl .1
				
				>PUSHW L.Socket
				>LIBCALL hLIBTCPIP,LIBTCPIP.SKT.NEW
				bcs .9
				txa
				ldy #hSocket
				sta (pData),y
				
				>LDYA L.MSG.INITOK
				>SYSCALL CPrintFYA

.11				>SYSCALL Sleep
				>SYSCALL GetC
				bcs .12
				cmp #03
				beq CS.RUN.ABORT
				
.12				ldy #hSocket
				lda (pData),y
				
				>LIBCALL hLIBTCPIP,LIBTCPIP.SKT.ACCEPTA
				bcs .11
				>LIBCALL hLIBTCPIP,LIBTCPIP.SKT.GETA
				>STYA ZPSktPtr
				
				ldx #3
				ldy #S.SOCKET.DST.ADDR+3
				
.2				>PUSHB (ZPSktPtr),y
				dey
				dex
				bpl .2
				
				>LDYA L.MSG.INCOMING
				>SYSCALL CPrintFYA
				bra .11
				
.9				>LDYA L.MSG.SKTERR
				>SYSCALL CPrintFYA
				lda #SYSMGR.ERRSYN
				sec	
				rts
				
CS.RUN.ABORT	>LDYA L.MSG.ABORT
				>SYSCALL CPrintFYA
				lda #0
				sec	
				rts
*--------------------------------------
CS.DOEVENT		sec
				rts
*--------------------------------------
CS.QUIT			ldy #hSocket
				lda (pData),y
				>LIBCALL hLIBTCPIP,LIBTCPIP.SKT.CLOSEA
				
				lda hLIBTCPIP
				>SYSCALL UnloadLibA
				clc
				rts
*--------------------------------------
Init.Timeout	ldy #TimeOut
				lda #TIMEOUT.MAX
				sta (pData),y
				rts
*--------------------------------------
Wait.TimeOut	sec
				ldy #TimeOut
				lda (pData),y
				beq .9
				ldy #bCTRLC
				lda (pData),y
				bmi .9
				clc
.9				rts
*--------------------------------------
CS.END
*--------------------------------------
LIBTCPIP		>PSTR "libtcpip.o"
MSG.TCPWAIT		>CSTR "HTTPD:Waiting for TCP/IP initializing...\n"
MSG.INITOK		>CSTR "HTTPD:Init Ok, Listening.\n"
MSG.TCPIPERR	>CSTR "HTTPD:TCP/IP Not initialized properly\n"
MSG.SKTERR		>CSTR "HTTPD:Listen Error\n"
MSG.ABORT		>CSTR "HTTPD:User Aborted\n"
MSG.INCOMING	>CSTR "HTTPD:Incoming Connection From : %d.%d.%d.%d\n"	
hLIBTCPIP		.BS 1
*--------------------------------------
Socket			.DA #S.SOCKET.SOCK.STREAM+S.SOCKET.STATUS.LISTEN
				.DA #S.SOCKET.SO.ACCEPTCONN
				.DA 0
				.BS 1
Socket.Src.Addr	.BS 4
Socket.Src.Port	.DA 80
Socket.Dst.Addr	.BS 4
Socket.Dst.Port	.BS 2
				.BS 16
*--------------------------------------
				.DUMMY
				.OR 0
DS.START
TimeOut			.BS 1
bCTRLC			.BS 1
hSocket			.BS 1
DS.END
				.ED
*--------------------------------------
MAN
SAVE SBIN/HTTPD.S
ASM
