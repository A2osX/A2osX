NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF sbin/httpd
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
				.INB inc/net.http.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
TIMEOUT.MAX		.EQ 30					30 sec.
CONN.MAX		.EQ 16
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pFile			.BS 2
pLineBuf		.BS 2
pLineBufPtr		.BS 2

hSrvSocket		.BS 2
hCltSocket		.BS 2
pSrvName		.BS 2
pRootdir		.BS 2

REQ.pBuf		.BS 2
REQ.pBufPtr		.BS 2
REQ.Len			.BS 2
REQ.nLen		.BS 2

REQ.KeepAlive	.BS 1
REQ.MimeType	.BS 1
REP.pFile		.BS 2
REP.pBuf		.BS 2
REP.pBufPtr		.BS 2

TimeOut			.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.ETCHTTPDCONF	.DA ETCHTTPDCONF
L.KEYWORDS.CONF	.DA KEYWORDS.CONF
J.KEYWORDS.CONF	.DA CS.RUN.CONF.SERVERNAME
				.DA CS.RUN.CONF.LISTEN
				.DA CS.RUN.CONF.DOCUMENTROOT
L.SA.LOCAL		.DA SA.LOCAL
L.MSG.INITCONF	.DA MSG.INITCONF
L.MSG.INITOK	.DA MSG.INITOK
L.MSG.SKTERR	.DA MSG.SKTERR
L.MSG.INCOMING	.DA MSG.INCOMING
L.MSG.REQ		.DA MSG.REQ
L.MSG.FILE		.DA MSG.FILE
L.MSG.SKTCLOSE	.DA MSG.SKTCLOSE
L.MSG.OK		.DA MSG.OK
L.KEYWORDS.REQ	.DA KEYWORDS.REQ
J.KEYWORDS.REQ	.DA CS.RUN.REQ.GET
L.INDEX.HTML	.DA INDEX.HTML
L.MIME.TYPES	.DA MIME.TYPES
T.MIME			.DA MIME.HTML
				.DA MIME.TXT
				.DA MIME.TTF
				.DA MIME.ICO
				.DA MIME.PNG
				.DA MIME.JPG
L.MIME.DEFAULT	.DA MIME.DEFAULT
L.HTTP.200		.DA HTTP.200
L.HTTP.404		.DA HTTP.404
				.DA 0
*--------------------------------------
CS.INIT			clc
CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			>LDYAI 256
				>LIBC Malloc
				bcs CS.INIT.RTS

				>STYA pLineBuf

				>SS
				>PUSHW L.MSG.INITCONF
				>PUSHW L.ETCHTTPDCONF
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs CS.INIT.RTS

				jsr CS.RUN.Conf
				bcs CS.INIT.RTS

				>LDYA L.MSG.OK
				>LIBC PutS

				jsr CS.RUN.Socket
				bcs CS.INIT.RTS

				>SS
				>PUSHW L.MSG.INITOK
				>PUSHW SA.LOCAL+S.SA.IN.PORT
				>PUSHW pRootdir
				>PUSHBI 4
				>LIBC PrintF
				>SR
*--------------------------------------
CS.RUN.LOOP		>SLEEP
				jsr CS.RUN.SERVER
				bcs CS.RUN.ERR

				bra CS.RUN.LOOP

CS.RUN.ERR		>LDYA L.MSG.SKTERR
				>LIBC PutS
				lda #E.SYN
				sec
CS.RUN.ERR.RTS	rts
*--------------------------------------
CS.RUN.Conf		>SS
				>PUSHW L.ETCHTTPDCONF
				>PUSHBI	O.RDONLY+O.TEXT
				>PUSHBI S.FI.T.TXT
				>PUSHWZ
				>LIBC FOpen
				>SR
				bcs CS.RUN.ERR.RTS

				>STYA pFile

.1				>LDYA pLineBuf
				>STYA pLineBufPtr

				>SS
				>PUSHW pLineBuf
				>PUSHWI 255
				>PUSHW pFile
				>LIBC FGetS
				>SR
				bcs .8

				lda (pLineBufPtr)
				beq .1

				cmp #'#'
				beq .1

				>LDYA L.KEYWORDS.CONF
				>STYA R1

				ldx #0

.2				ldy #0

.3				lda (R1),y
				cmp	(pLineBufPtr),y
				bne .4

				iny
				cmp #C.SPACE
				bne .3

				jsr CS.RUN.CONF.JMP
				bcc .1

				bra .97

.4				inx
				inx

				ldy #0

.5				iny
				lda (R1),y
				cmp #C.SPACE
				bne .5

				tya
				sec
				adc R1
				sta R1
				bcc .6
				inc R1+1

.6				lda	(R1)
				bne .2

.97				lda #E.SYN

.98				pha
				jsr .8
				pla
				sec
.99				rts

.8				>LDYA pFile
				>LIBC FClose
				rts
*--------------------------------------
CS.RUN.CONF.JMP	tya
				clc
				adc pLineBufPtr
				sta R1
				tay
				lda #0
				adc pLineBufPtr+1
				sta R1+1

				jmp (J.KEYWORDS.CONF,x)
*--------------------------------------
CS.RUN.CONF.SERVERNAME
				>LIBC StrDup
				bcs .9

				>STYA pSrvName

.9				rts
*--------------------------------------
CS.RUN.CONF.LISTEN
				>LIBC AToI
				bcs .9

				>STYA SA.LOCAL+S.SA.IN.PORT

.9				rts
*--------------------------------------
CS.RUN.CONF.DOCUMENTROOT
				>SS
				>PUSHYA
				>PUSHWZ					Allocate
				>LIBC RealPath
				>SR
				bcs .9

				>STYA pRootdir

.9				rts
*--------------------------------------
CS.RUN.Socket	>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_SEQPACKET
				>PUSHWZ
				>LIBC Socket
				>SR
				bcs .9

				>STYA hSrvSocket

				>SS
				>PUSHW hSrvSocket
				>PUSHW L.SA.LOCAL
				>PUSHWI S.SA.IN
				>LIBC Bind
				>SR
				bcs .9

				>SS
				>PUSHW hSrvSocket
				>PUSHWI 16				backlog
				>LIBC Listen
				>SR

.9
CS.RUN.Socket.RTS
				rts
*--------------------------------------
CS.RUN.SERVER	>SS
				>PUSHW hSrvSocket
				>PUSHEA.G SA.REMOTE
				>PUSHWI S.SA.IN
				>LIBC Accept
				>SR
				bcs .8

				>STYA hCltSocket

				>SS
				>PUSHW L.MSG.INCOMING

				>PUSHB hCltSocket

				ldx #3
				ldy #SA.REMOTE+S.SA.IN.ADDR

.1				>PUSHB (pData),y
				iny
				dex
				bpl .1

				>PUSHBI 5
				>LIBC PrintF
				>SR

				jsr CS.RUN.CLIENT

				>SS
				>PUSHW L.MSG.SKTCLOSE
				>PUSHB hCltSocket
				>PUSHBI 1
				>LIBC PrintF
				>SR

				>LDYA hCltSocket
				>LIBC Shutdown

.8				clc
.9
CS.RUN.SERVER.RTS
				rts
*--------------------------------------
CS.RUN.CLIENT	>LDYAI K.TCP.MSS
				>LIBC Malloc
				bcs CS.RUN.SERVER.RTS

				>STYA REQ.pBuf

.10				>STZ.G REQ.FullPath
				stz REQ.KeepAlive

				stz REP.pFile+1

.1				>SLEEP

				>SS
				>PUSHW hCltSocket
				>PUSHW REQ.pBuf
				>PUSHWI K.TCP.MSS
				>PUSHWZ					flags
				>LIBC Recv
				>SR
				bcc .2

				cmp #E.NODATA
				beq .1

				jmp .99
*--------------------------------------
.2				jsr CS.RUN.REQ.GetReq

.3				jsr CS.RUN.REQ.GetLine
				bcs .4

				>LDYA pLineBufPtr
				>STYA R2

				>LDYA L.KEYWORDS.REQ
				jsr CS.RUN.GET.KW
				bcs .3

				jsr CS.RUN.REQ.JMP
				bcc .3

				bcs .98

.4				>LDYA REQ.pBuf
				>LIBC Free

				stz REQ.pBuf+1

				>LDA.G REQ.FullPath
				beq .9

				jsr CS.RUN.OpenFile
				bcs .9

				jsr CS.RUN.SendFile

				jsr CS.RUN.CloseFile

				bcs .98

				bra .8
*--------------------------------------
.9				>SS
				>PUSHW hCltSocket
				>PUSHW L.HTTP.404
				>PUSHWI HTTP.404.len
				>PUSHWZ					flags
				>LIBC Send
				>SR

.8				bit REQ.KeepAlive
				bpl .98

				jmp .10

.98				>LDYA REQ.pBuf
				beq .99

				>LIBC Free

.99				rts
*--------------------------------------
CS.RUN.REQ.GetReq
				>STYA REQ.Len
				eor #$ff
				sta REQ.nLen+1
				tya
				eor #$ff
				sta REQ.nLen

				>LDYA REQ.pBuf
				>STYA REQ.pBufPtr

				>SS
				>PUSHW L.MSG.REQ
				>PUSHB hCltSocket
				>PUSHW REQ.Len
				>PUSHBI 3
				>LIBC PrintF
				>SR

				rts
*--------------------------------------
CS.RUN.REQ.GetLine
				ldy #0

.1				inc REQ.nLen
				bne .2

				inc REQ.nLen+1
				beq .9

.2				lda (REQ.pBufPtr)
				inc REQ.pBufPtr
				bne .21

				inc REQ.pBufPtr+1

.21				cmp #C.CR
				bne .4

				inc REQ.nLen
				bne .3
				inc REQ.nLen+1
				beq .9

.3				lda (REQ.pBufPtr)
				inc REQ.pBufPtr
				bne .31

				inc REQ.pBufPtr+1

.31				eor #C.LF
				bne .9

				sta (pLineBufPtr),y		Y,A = StrLen
				clc
				rts

.4				sta (pLineBufPtr),y
				iny
				bra .1

.9				sec
				rts
*--------------------------------------
CS.RUN.OpenFile	>SS
				>PUSHW L.MSG.FILE
				>PUSHB hCltSocket
				>PUSHEA.G REQ.FullPath
				>PUSHBI 3
				>LIBC PrintF
				>SR

				>SS
				>PUSHEA.G REQ.FullPath
				>PUSHEA.G REQ.Stat
				>LIBC Stat
				>SR
				bcs .9

				>SS
				>PUSHEA.G REQ.FullPath
				>PUSHBI	O.RDONLY
				>PUSHBI 0				type
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA REP.pFile

				>LEA.G REQ.FullPath
				>STYA R2

				ldx #$ff
				ldy #$ff

.1				iny
				lda (R2),y
				beq .2

				cmp #'.'
				bne .1

				tya
				tax
				bra .1

.2				txa
				bpl .3

				lda #MIME.DEFAULT.ID
				bra .8

.3				sec

				adc R2
				sta R2
				bcc .4

				inc R2+1

.4				>LDYA L.MIME.TYPES
				jsr CS.RUN.GET.KW

				txa

.8				sta REQ.MimeType

				clc
.9				rts
*--------------------------------------
CS.RUN.SendFile	>LDYAI K.TCP.MSS
				>LIBC Malloc
				bcs .9

				>STYA REP.pBuf

				>SS
				>PUSHW REP.pBuf
				>PUSHW L.HTTP.200

				ldx REQ.MimeType
				>PUSHW T.MIME,x
				>PUSHL.G REQ.Stat+S.STAT.SIZE
				>PUSHBI 6
				>LIBC SPrintF
				>SR
				bcs .9

				jsr CS.RUN.SendBuf		Y,A = car count

.1				jsr CS.RUN.ReadBuf
				bcs .8

				jsr CS.RUN.SendBuf
				bcs .9

				bra .1

.8				cmp #MLI.E.EOF
				sec
				bne .9

				clc

.9				rts
*--------------------------------------
CS.RUN.CloseFile
				php
				pha
				>LDYA REP.pFile
				beq .9

				>LIBC FClose

.9				pla
				plp
				rts
*--------------------------------------
CS.RUN.ReadBuf	>SS
				>PUSHW REP.pFile
				>PUSHW REP.pBuf
				>PUSHWI 1024
				>LIBC FRead
				>SR
				rts
*--------------------------------------
CS.RUN.SendBuf	>SS
				pha
				>PUSHW hCltSocket
				>PUSHW REP.pBuf
				pla
				>PUSHYA
				>PUSHWZ					flags
				>LIBC Send
				>SR
				rts
*--------------------------------------
CS.RUN.REQ.JMP	jmp (J.KEYWORDS.REQ,x)
*--------------------------------------
CS.RUN.REQ.GET	ldy #0

.1				iny
				lda (R2),y

				eor #C.SPACE
				bne .1

				sta (R2),y

				lda (R2)

				cmp #'/'
				bne .4

				>SS
				>PUSHEA.G REQ.FullPath
				>PUSHW pRootdir
				>LIBC StrCpy
				>SR

				>SS
				>PUSHEA.G REQ.FullPath

				ldy #1
				lda (R2),y
				bne .2

				>LDYA L.INDEX.HTML
				bra .3

.2				>LDYA R2

.3				>PUSHYA
				>LIBC StrCat
				>SR
				rts

.4				>PUSHW R2
				>PUSHEA.G REQ.FullPath
				>LIBC RealPath
.9				rts
*--------------------------------------
CS.RUN.REQ.Connection
				lda (R2)
				eor #'k'
				beq .1


				lda #$ff

.1				sta REQ.KeepAlive

				clc
				rts
*--------------------------------------
CS.RUN.GET.KW	>STYA R1

				ldx #0

.2				ldy #$ff

.3				iny
				lda (R1),y
				cmp	(R2),y
				bne .4

				cmp #0
				beq .33

				cmp #C.SPACE
				bne .3

.33				tya
				sec
				adc R2
				sta R2
				bcc .8
				inc R2+1

.8				clc
				rts

.4				inx
				inx

				ldy #0

.5				iny
				lda (R1),y
				bne .5

				tya
				sec
				adc R1
				sta R1
				bcc .6
				inc R1+1

.6				lda	(R1)
				bne .2

				sec
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				rts
*--------------------------------------
CS.QUIT			ldy hSrvSocket
				beq .8

				lda #0

				>LIBC Shutdown

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
ETCHTTPDCONF	.AZ "/etc/httpd.conf"
*--------------------------------------
KEYWORDS.CONF	.AS "ServerName "
				.AS "Listen "
				.AS "DocumentRoot "
				.DA #0
*--------------------------------------
MSG.INITCONF	.CZ "HTTPD:Reading %s file..."
MSG.INITOK		.CZ "HTTPD:Listening on port %D, root dir: %s.\r\n"
MSG.SKTERR		.CZ "HTTPD:Skt Bind Error."
MSG.INCOMING	.CZ "HTTPD:Skt %h Opened From %d.%d.%d.%d\r\n"
MSG.REQ			.CZ "HTTPD:Skt %h Request %D bytes\r\n"
MSG.FILE		.CZ "HTTPD:Skt %h File %s\r\n"
MSG.SKTCLOSE	.CZ "HTTPD:Skt %h Close\r\n"
MSG.OK			.CZ "OK"
*--------------------------------------
KEYWORDS.REQ	.AS "GET "
				.AS "Connection: "
				.DA #0
*--------------------------------------
INDEX.HTML		.AS "/index."			html
*--------------------------------------
MIME.TYPES		.AZ "html"
				.AZ "txt"
				.AZ "ttf"
				.AZ "ico"
				.AZ "png"
				.AZ "jpg"
				.DA #0
*--------------------------------------
MIME.HTML		.AZ "text/html"
MIME.TXT		.AZ "text/plain"
MIME.TTF		.AZ "font/truetype"
MIME.ICO 		.AZ "image/vnd.microsoft.icon"			"image/x-icon"
MIME.PNG		.AZ "image/png"
MIME.JPG		.AZ "image/jpeg"
MIME.DEFAULT	.AZ "application/octet-stream"
MIME.DEFAULT.ID	.EQ 12
*--------------------------------------
HTTP.200		.AS "HTTP/1.1 200 OK"
				.DA #C.CR,#C.LF
				.AS "Server: A2osX-HTTPD 1.00"
				.DA #C.CR,#C.LF
				.AS "Content-Type: %s"
				.DA #C.CR,#C.LF
				.AS "Content-Length: %u"
				.DA #C.CR,#C.LF
				.DA #C.CR,#C.LF
				.DA #0
*--------------------------------------
HTTP.404		.AS "HTTP/1.1 404 Not Found"
				.DA #C.CR,#C.LF
				.AS "Server: A2osX-HTTPD 1.00"
				.DA #C.CR,#C.LF
				.AS "Content-Type: text/html"
				.DA #C.CR,#C.LF
				.AS "Content-Length: 47"
				.DA #C.CR,#C.LF
				.AS "Connection: Close"
				.DA #C.CR,#C.LF
				.DA #C.CR,#C.LF
				.AS "<HTML><BODY><B>404:NOT FOUND<B></BODY></HTML>"
				.DA #C.CR,#C.LF
HTTP.404.len	.EQ *-HTTP.404
*--------------------------------------
SA.LOCAL		.DA AF_INET				S.SA.IN.AF
				.DA #0,#0,#0,#0			S.SA.IN.ADDR
				.DA TCP.PORT.HTTP
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
REQ.FullPath	.BS MLI.MAXPATH+1
REQ.Stat		.BS S.STAT
SA.REMOTE		.BS S.SA.IN
DS.END
				.ED
*--------------------------------------
MAN
SAVE usr/src/sbin/httpd.s
ASM
