NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF sbin/login
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPBufPtr		.BS 2

ZPRetryCnt		.BS 1
ZPbValidUser	.BS 1
ZPUID			.BS 1

ZPGetLinePtr	.BS 2
ZPbGetLineSecret	.BS 1
ZPbEsc			.BS 1
ZPGetLineLen	.BS 1
ZPGetLineMax	.BS 1

ZPpNewSession	.BS 2
ZPpOldSession	.BS 2

ZPpFile			.BS 2

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #16					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.LOGIN		.DA MSG.LOGIN
L.MSG.PASSWORD	.DA MSG.PASSWORD
L.MSG.BAD		.DA MSG.BAD
L.MSG.BS		.DA MSG.BS
L.MSG.CRLF		.DA MSG.CRLF
L.ETCNOAUTH		.DA ETCNOAUTH
L.ETCISSUE		.DA ETCISSUE
L.ETCMOTD		.DA ETCMOTD
L.SHELL			.DA SHELL
				.DA 0
*--------------------------------------
CS.QUIT
*--------------------------------------
CS.INIT			clc
CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			>LDYA L.ETCISSUE
				jsr CS.RUN.DumpFile

				bit A2osX.bPW
				bmi CS.RUN.AUTH
*--------------------------------------
				>LDYA L.ETCNOAUTH
				jsr CS.RUN.DumpFile

				jsr CS.RUN.EXECPS
				bcs .9

				ldy #S.PS.ERRNO
				jsr A2osX.GetPSy

				sec

.9				rts
*--------------------------------------
CS.RUN.AUTH		lda #3
				sta ZPRetryCnt

.1				>LDYA L.MSG.LOGIN
				jsr CS.RUN.printf
				bcs .9

				>LEA.G Username
				ldx #16
				clc
				jsr CS.RUN.GetLine
				bcs .9

				>LDYA L.MSG.PASSWORD
				jsr CS.RUN.printf
				bcs .9

				>LDYA pData					>LEA.G Password
				ldx #32
				sec
				jsr CS.RUN.GetLine
				bcs .9

				>LDYA L.MSG.CRLF
				>LIBC PutS
				bcs .9

				>PUSHW pData				>PUSHEA.G Password
				>PUSHW pData				>PUSHEA.G Password

				>LIBC MD5

				jsr CS.RUN.EXEC
				bcc .9

				>LDYA L.MSG.BAD
				>LIBC PutS
				bcs .9

				dec ZPRetryCnt
				bne .1

				lda #E.IUSR
				sec
.9				rts
*--------------------------------------
CS.RUN.EXEC		>PUSHEA.G Username
				>PUSHW pData				>PUSHEA.G Password

				jsr CS.RUN.EXECPS

				ldy #S.PS.ERRNO
				jsr A2osX.GetPSy

				sec

.9				rts
*--------------------------------------
CS.RUN.EXECPS	>LDYA L.ETCMOTD
				jsr CS.RUN.DumpFile

				>SS
				>PUSHW L.SHELL
				>PUSHBI 0
				>PUSHBI S.PS.F.HOLD
				>LIBC ExecL
				>SR
				bcs .9

				>SLEEP
*				clc
.9				rts
*--------------------------------------
CS.RUN.GetLine	>STYA ZPGetLinePtr
				stx ZPGetLineMax
				ror ZPbGetLineSecret

				stz ZPGetLineLen
				stz ZPbEsc

.1				>LIBC GetChar
				bcs .9

				bit ZPbEsc
				bpl .11

*				clc
				ror ZPbEsc
				cmp #'D'
				bne .1

				bra .3

.11				cmp #C.DEL
				beq .3

				cmp #C.SPACE
				bcc .2

				ldy ZPGetLineLen
				cpy ZPGetLineMax
				beq .1

				sta (ZPGetLinePtr),y
				inc ZPGetLineLen
				bit ZPbGetLineSecret
				bmi .1

				>LIBC PutChar
				bra .1

.2				cmp #3					Ctrl-C
				beq .9

				cmp #C.CR
				beq .8

				cmp #C.ESC
				bne .1

*				sec
				ror ZPbEsc
				bra .1

.3				ldy ZPGetLineLen
				beq .1

				dec ZPGetLineLen
				bit ZPbGetLineSecret
				bmi .1

				>LDYA L.MSG.BS
				jsr CS.RUN.printf
				bra .1

.8				ldy ZPGetLineLen
				lda #0
				sta (ZPGetLinePtr),y
				clc
.9				rts
*--------------------------------------
CS.RUN.printf	>SS
				>PUSHYA
				>PUSHBI 0
				>LIBC PrintF
				>SR
CS.RUN.RTS		rts
*--------------------------------------
CS.RUN.DumpFile	>SS
				>PUSHYA
				>PUSHBI	O.RDONLY+O.TEXT
				>PUSHBI S.FI.T.TXT
				>PUSHWZ				Aux type
				>LIBC FOpen
				>SR
				bcs CS.RUN.RTS

				>STYA ZPpFile

				>LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPBufPtr

.1				>SS
				>PUSHW ZPBufPtr
				>PUSHWI	255
				>PUSHW ZPpFile
				>LIBC FGetS
				>SR
				bcs .8

				>SS
				>PUSHW ZPBufPtr
				>PUSHW ZPBufPtr
				>LIBC Expand
				>SR

				>LDYA ZPBufPtr
				>LIBC PutS
				bra .1

.8 				>LDYA ZPBufPtr
				>LIBC Free

*				clc

.9				php
				pha
				>LDYA ZPpFile
				>LIBC FClose
				pla
				plp
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.LOGIN		.CZ "\r\nlogin:"
MSG.PASSWORD	.CZ "\r\npassword:"
*--------------------------------------
MSG.BAD			.AZ "Bad user or password"
MSG.BS			.DA #C.BS,#C.SPACE,#C.BS
MSG.CRLF		.HS 00
ETCNOAUTH		.AZ "/etc/noauth"
ETCISSUE		.AZ "/etc/issue"
ETCMOTD			.AZ "/etc/motd"
SHELL			.AZ "${SHELL}"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
Password		.BS 33
Username		.BS 17
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/sbin/login.s
ASM
