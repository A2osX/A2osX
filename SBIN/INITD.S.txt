NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF sbin/initd
*--------------------------------------
INITDDBG		.EQ 1
ROOTMODE		.EQ 0
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pBuf			.BS 2
pFile			.BS 2
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.CTRLR		.DA MSG.CTRLR
				.DO INITDDBG=1
L.MSG.START		.DA MSG.START
L.MSG.EXEC		.DA MSG.EXEC
L.MSG.OK		.DA MSG.OK
L.MSG.KO		.DA MSG.KO
				.FIN
L.ETCINIT		.DA ETCINIT
L.CMDS			.DA BINSH
				.DA BINMEM
				.DA BINPS
				.DA BINLSOF
				.DA BINSHUTDOWN
				.DA 0
*--------------------------------------
CS.INIT
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.RUN			.DO INITDDBG=1
				>LDYA L.MSG.START
				>LIBC PutS
				.FIN

				.DO ROOTMODE=0

				ldy #S.PS.PID
				jsr A2osX.GetPSy
				cmp #1
				bne .9

				ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply

				>LIBC FEOF
				bcs .99

				tya
				bne .1

				>LIBC GetChar
				bcs .99

				cmp #18					CTRL-R for Root mode
				bne .1

				.FIN

				>LDYA L.MSG.CTRLR
				>LIBC PutS

				ldy #0
				jsr CS.RUN.EXEC.Y
				bcs .99

				>SLEEP
*--------------------------------------
.1				jsr CS.RUN.ETCINIT
				bcs .99

				>SS
				>PUSHWI 4				/dev/tty1
				>PUSHBI IOCTL.CONTROL
				>PUSHWZ
				>LIBC IOCTL
				>SR
*--------------------------------------
.2				>LIBC GetChar
				bcs .99

				ldy #4

.3				cmp CTRLCHARS,y
				beq .7

				dey
				bpl .3

				bra .2

.7				tya
				asl
				tay
				jsr CS.RUN.EXEC.Y
				bra .2

.9				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN.ETCINIT	>LDYAI 256
				>LIBC Malloc
				>STYA pBuf

				>SS
				>PUSHW L.ETCINIT
				>PUSHBI	O.RDONLY+O.TEXT
				>PUSHBI S.FI.T.TXT
				>PUSHWZ
				>LIBC FOpen
				>SR
				bcc .10

				pha

				.DO INITDDBG=1

				jsr CS.RUN.ERRMSG

				.FIN

				jsr .81

				pla
				sec
				rts

.10				>STYA pFile

.1				>SS
				>PUSHW pBuf
				>PUSHWI 256
				>PUSHW pFile

				>LIBC FGetS
				>SR
				bcs .80

				lda (pBuf)
				cmp #'#'
				beq .1

				.DO INITDDBG=1
				>SS
				>PUSHW L.MSG.EXEC
				>PUSHW pBuf
				>PUSHBI 2
				>LIBC PrintF
				>SR
				.FIN

				>SS
				>PUSHW pBuf
				>PUSHBI 0
				>PUSHBI S.PS.F.NOHUP+S.PS.F.HOLD
				>LIBC ExecL
				>SR

				.DO INITDDBG=1
				bcs .2

				>LDYA L.MSG.OK
				>LIBC PutS

				.FIN

				>SLEEP
				jmp .1

				.DO INITDDBG=1

.2				jsr CS.RUN.ERRMSG
				jmp .1

				.FIN

.80				>LDYA pFile
				>LIBC FClose

.81				>LDYA pBuf
				>LIBC Free

				rts
*--------------------------------------
CS.RUN.EXEC.Y	.DO INITDDBG=1

				phy
				>SS
				>PUSHW L.MSG.EXEC
				>PUSHW L.CMDS,y
				>PUSHBI 2
				>LIBC PrintF
				>SR
				ply

				.FIN

				>SS
				>PUSHW L.CMDS,y
				>PUSHBI 0
				>PUSHBI S.PS.F.HOLD
				>LIBC ExecL
				>SR

				.DO INITDDBG=1
				bcs CS.RUN.ERRMSG

				>LDYA L.MSG.OK
				>LIBC PutS
				.FIN

				rts
*--------------------------------------
				.DO INITDDBG=1

CS.RUN.ERRMSG	pha
				tay
				>SS
				>PUSHW L.MSG.KO
				tya
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				>SR
				pla
				sec
				rts

				.FIN
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.CTRLR		.AZ "INITD:Entering ROOT mode..."

				.DO INITDDBG=1

MSG.START		.AZ "INITD:Starting..."
MSG.EXEC		.CZ 'INITD:Exec "%s" ...'
MSG.OK			.CZ "[OK]"
MSG.KO			.CZ "[%h]\r\n"

				.FIN
*--------------------------------------
CTRLCHARS		.DA #19
				.DA #13
				.DA #16
				.DA #12
				.DA #4
*--------------------------------------
ETCINIT			.AZ "/etc/init"
*--------------------------------------
BINSH			.AZ "/bin/sh"
BINMEM			.AZ "/bin/mem"
BINPS			.AZ "/bin/ps"
BINLSOF			.AZ "/bin/lsof"
BINSHUTDOWN		.AZ "/bin/shutdown"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/sbin/initd.s
ASM
