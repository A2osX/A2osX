NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF root/asmtest/testhgr
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/mli.i
				.INB inc/gfx.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hFDGFX			.BS 2
hFont			.BS 2
hFontB			.BS 2

Filename		.BS 2
hBuf			.BS 2

bVBL			.BS 1

Cnt				.BS 1

xDir			.BS 1
yDir			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.DEVNAME.GFX	.DA DEVNAME.GFX
L.MSG.LOAD		.DA MSG.LOAD

L.FONTFILEB		.DA FONTFILEB
L.FONTFILE		.DA FONTFILE
L.MARILYNFILE	.DA MARILYNFILE
L.PRODOSFILE	.DA PRODOSFILE
L.LOGOFILE		.DA LOGOFILE

L.CB.RECT1		.DA CB.RECT1
L.CB.RECT2		.DA CB.RECT2
L.CB.MARILYN	.DA CB.MARILYN
L.CB.PRODOS		.DA CB.PRODOS
L.CB.Apple		.DA CB.Apple
L.CB.LOGO		.DA CB.LOGO
L.CB.TEXT1		.DA CB.TEXT1
L.CB.TEXT2		.DA CB.TEXT2

L.BM.Apple		.DA BM.Apple
L.MSG.TEXT1		.DA MSG.TEXT1
L.MSG.TEXT2		.DA MSG.TEXT2

				.DA 0
*--------------------------------------
CS.INIT			clc
CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			jsr LoadResources
				bcs CS.INIT.RTS

				jsr GFX.Open
				bcs CS.INIT.RTS
		
				>LDYA hFontB
				>STYA CB.TEXT1+S.CB.hSrc

				>LDYA L.MSG.TEXT1
				>STYA CB.TEXT1+S.CB.pTxt

				>LDYA L.CB.TEXT1
				jsr GFX.Write.YA

				>LDYA hFont
				>STYA CB.TEXT2+S.CB.hSrc

				>LDYA L.MSG.TEXT2
				>STYA CB.TEXT2+S.CB.pTxt

				>LDYA L.CB.TEXT2
				jsr GFX.Write.YA

			lda #0
			sec
			rts
				
				
				>LDYA L.CB.RECT1
				jsr GFX.Write.YA
				
				lda #16
				sta Cnt

.1				>LDYA L.CB.RECT2
				jsr GFX.Write.YA

				inc CB.RECT2+S.CB.COLOR

				lda CB.RECT2+S.CB.X1
				clc
				adc #28
				sta CB.RECT2+S.CB.X1
				bcc .2

				inc CB.RECT2+S.CB.X1+1

.2				lda CB.RECT2+S.CB.X2
				clc
				adc #28
				sta CB.RECT2+S.CB.X2
				bcc .3

				inc CB.RECT2+S.CB.X2+1
				
.3				dec Cnt
				bne .1
				
				>LDYA L.CB.MARILYN
				jsr GFX.Write.YA

				>LDYA L.CB.PRODOS
				jsr GFX.Write.YA

				>LDYA L.BM.Apple
				>STYA CB.Apple+S.CB.pSrc

				lda #16
				sta Cnt

.4				>LDYA L.CB.Apple
				jsr GFX.Write.YA
				
				lda CB.Apple+S.CB.X1
				clc
				adc #28
				sta CB.Apple+S.CB.X1
				bcc .5
				
				inc CB.Apple+S.CB.X1+1
				
.5				dec Cnt
				bne .4

				jsr CS.RUN.LOGO.SHOW
				bcs .9

				lda #0
				sec
.9
CS.RUN.RTS		rts
*--------------------------------------
CS.RUN.LOGO.SHOW
*				lda #S.CB.CMD.GETRECTBUFSIZE
*				sta CB.LOGO+S.CB.CMD

				>LDYA L.CB.LOGO
				jsr GFX.Write.YA
				bcs CS.RUN.RTS

				>LIBC Malloc
				bcs CS.RUN.RTS

				>STYA CB.LOGO+S.CB.pDst

				lda #S.CB.CMD.BITBLT
				sta CB.LOGO+S.CB.CMD

.1				lda #S.CB.OP.SET+S.CB.OP.COLOR+S.CB.OP.SAVE
				sta CB.LOGO+S.CB.OP

				>LDYA L.CB.LOGO
				jsr GFX.Write.YA

				ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply

				>LIBC FEOF
				bcs CS.RUN.RTS

				tya
				beq .80

.2			>SLEEP

				lda bVBL
				beq .2

				stz bVBL

				lda #S.CB.OP.RESTORE
				sta CB.LOGO+S.CB.OP

				>LDYA L.CB.LOGO

				jsr GFX.Write.YA

				bit xDir
				bmi .4

				lda CB.LOGO+S.CB.X1
				clc
				adc #4
				tay

				lda CB.LOGO+S.CB.X1+1
				adc #0
				pha
				cpy #244
				sbc /244
				pla
				bcc .5

.3				lda xDir
				eor #$ff
				sta xDir
				bra .6

.4				lda CB.LOGO+S.CB.X1
				sec
				sbc #4
				tay
				lda CB.LOGO+S.CB.X1+1
				sbc #0
				bcc .3

.5				sty CB.LOGO+S.CB.X1
				sta CB.LOGO+S.CB.X1+1

.6				lda CB.LOGO+S.CB.Y1
				bit yDir
				bmi .8

				inc
				sta CB.LOGO+S.CB.Y1
				cmp #144
				bne .1

.7				lda yDir
				eor #$ff
				sta yDir
				jmp .1

.8				sec
				sbc #1
				sta CB.LOGO+S.CB.Y1
				beq .7

				jmp .1

.80				>LIBC GetChar

				>LDYA CB.LOGO+S.CB.pDst
				>LIBC Free

				rts
*--------------------------------------
CS.SIG			sta bVBL

.9				sec
				rts
*--------------------------------------
CS.QUIT			>LDYA hFont
				beq .1

				>KAPI FreeBuf

.1				>LDYA hFontB
				beq .2

				>KAPI FreeBuf

.2				>LDYA CB.MARILYN+S.CB.hSrc
				beq .3

				>KAPI FreeBuf

.3				>LDYA CB.LOGO+S.CB.hSrc
				beq .4

				>KAPI FreeBuf

.4				>LDYA CB.TEXT2+S.CB.hSrc
				beq .5

				>KAPI FreeBuf

.5				>LDYA CB.TEXT1+S.CB.hSrc
				beq .6

				>KAPI FreeBuf

.6				>LDYA CB.PRODOS+S.CB.hSrc
				beq .7

				>KAPI FreeBuf

.7				>LDYA hFDGFX
				beq .8

				>LIBC Close

.8				clc
				rts
*--------------------------------------
LoadResources	>LDYA L.FONTFILE
				jsr .1
				bcs .9

				>STYA hFont

				>LDYA L.FONTFILEB
				jsr .1
				bcs .9

				>STYA hFontB

			clc
			rts
				
				>LDYA L.MARILYNFILE
				jsr .1
				bcs .9

				>STYA CB.MARILYN+S.CB.hSrc

				>LDYA L.PRODOSFILE
				jsr .1
				bcs .9

				>STYA CB.PRODOS+S.CB.hSrc

				>LDYA L.LOGOFILE
				jsr .1
				bcs .9

				>STYA CB.LOGO+S.CB.hSrc

.9				rts
*--------------------------------------
.1				>STYA Filename
				>KAPI LoadBuf
				bcs .9

				>STYA hBuf

				>SS
				>PUSHW L.MSG.LOAD
				>PUSHW Filename
				>PUSHB hBuf
				>PUSHB hBuf+1
				>PUSHBI 4
				>LIBC PrintF
				>SR

				>LDYA hBuf

				rts
*--------------------------------------
GFX.Open		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC Open
				>SR
				bcs .9

				>STYA hFDGFX

.9				rts
*--------------------------------------
GFX.Write.YA	>STYA R1

				>SS
				>PUSHW hFDGFX
				>PUSHW R1

				>PUSHBI 0
				lda (R1)
				and #$7F
				tax
				lda CB.CmdLen,x
				>PUSHA

				>LIBC Write
				>SR
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
DEVNAME.GFX		.AZ "/dev/gfx"
MSG.LOAD		.CZ "Loaded %s, hBuf=%h:%b\r\n"
FONTFILEB		.AZ "/opt/gui/fonts/a27x8b"
FONTFILE		.AZ "/opt/gui/fonts/a27x8"
MARILYNFILE		.AZ "/root/bmp/marilyn"
PRODOSFILE		.AZ "/root/bmp/prodosm"
LOGOFILE		.AZ "/A2osX.logo"
*--------------------------------------
MSG.TEXT1		.AZ "HGR Driver (a27x8b Font, Normal)"
MSG.TEXT2		.AZ "1234567890 (a27x8 Font, Inverse)"
*--------------------------------------
CB.RECT1		.DA #S.CB.CMD.FILLRECT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.C16
				.DA #11					Color
				.DA 28
				.DA 14
				.DA 559-28
				.DA 191-14

CB.RECT2		.DA #S.CB.CMD.FILLRECT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.C16
				.DA #0					Color
				.DA 56					X1
				.DA 191-19				Y1
				.DA 56+27				X2
				.DA 191-9				Y2

CB.MARILYN		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.SET+S.CB.OP.COLOR
				.DA #S.CB.M.MONO
				.DA #0
				.DA 56					X1
				.DA 10					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 140					SrcW
				.DA 150					SrcH
				.DA 0					pSrc
				.DA 0					pDst
				.BS 2					hSrc

CB.PRODOS		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.COLOR
				.DA #S.CB.M.C16
				.DA #0
				.DA 204					X1
				.DA 130					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 320					SrcW
				.DA 40					SrcH
				.DA 0					pSrc
				.DA 0					pDst
				.BS 2					hSrc

CB.Apple		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.COLOR
				.DA #S.CB.M.C16
				.DA #0
				.DA 64					X1
				.DA 174					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 16					SrcW
				.DA 7					SrcH
				.BS 2					pSrc
				.DA 0					pDst
				.DA 0					hSrc

CB.TEXT1		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #255				INVERSE
				.DA 7					X1
				.DA 1					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.BS 2					hSrc
				.BS 2					pTxt

CB.TEXT2		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
				.DA 7					X1
				.DA 184					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.BS 2					hSrc
				.BS 2					pTxt
*--------------------------------------
CB.LOGO			.DA #S.CB.CMD.GETRECTBUFSIZE
				.DA #S.CB.OP.SET+S.CB.OP.COLOR+S.CB.OP.SAVE
				.DA #S.CB.M.C16
				.DA #0
				.DA 200					X1
				.DA 50					Y1
				.DA 200+319				X2 For GETRECTBUFSIZE
				.DA 50+39				Y2 For GETRECTBUFSIZE
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 320					SrcW
				.DA 40					SrcH
				.DA 0					pSrc
				.DA 0					pDst
				.BS 2					hSrc
*--------------------------------------
CB.CmdLen		.DA #S.CB.Y1+1			SETPIXEL
				.DA #S.CB.Y1+1			GETPIXEL
				.DA #S.CB.X2+1			HLINE
				.DA #S.CB.Y2+1			VLINE
				.DA #S.CB.Y2+1			FILLRECT
				.DA #S.CB.hSrc+1		BITBLT
				.DA #S.CB.hSrc+1		GETRECTBUFSIZE
				.DA #S.CB.pTxt+1		DRAWTEXT
				.DA #S.CB.pTxt+1		GETTEXTSIZE
*--------------------------------------
BM.Apple		.DA #S.BM.F.BBP4		F
				.DA #2					RowBytes
				.DA 16					W
				.DA 7					H
				.DA BM.Apple.AND-BM.Apple
				.HS 0006				green (8)
				.HS 6006				green (8)
				.HS EEEE				yellow (15)
				.HS	CC0C				orange (14)
				.HS 9909				magenta (10)
				.HS 8888				violet (11)
				.HS 1001				Dark blue (4)
BM.Apple.AND	.HS FFF0
				.HS 0FF0
				.HS 0000
				.HS 00F0
				.HS 00F0
				.HS 0000
				.HS 0FF0
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE root/asmtest/testhgr.s
ASM
