NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF root/asmtest/gtest
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/gfx.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hFDGFX			.BS 2
hFont			.BS 2
hFontB			.BS 2
hFontT			.BS 2

Filename		.BS 2
hBuf			.BS 2

bVBL			.BS 1

Cnt				.BS 1

xDir			.BS 1
yDir			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T10TH
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.DEVNAME.GFX	.DA DEVNAME.GFX
L.MSG.LOAD		.DA MSG.LOAD

L.FONTFILEB		.DA FONTFILEB
L.FONTFILE		.DA FONTFILE
L.FONTFILET		.DA FONTFILET
L.MARILYNFILE	.DA MARILYNFILE
L.PRODOSFILE	.DA PRODOSFILE
L.LOGOFILE		.DA LOGOFILE

L.CB.RECT1		.DA CB.RECT1
L.CB.RECT2		.DA CB.RECT2
L.CB.MARILYN	.DA CB.MARILYN
L.CB.PRODOS		.DA CB.PRODOS
L.CB.Apple		.DA CB.Apple
L.CB.LOGO		.DA CB.LOGO
L.CB.TEXT1		.DA CB.TEXT1
L.CB.TEXT2		.DA CB.TEXT2
L.CB.TEXT3		.DA CB.TEXT3

L.BM.Apple		.DA BM.Apple
L.MESSAGE1		.DA MESSAGE1
L.MESSAGE2		.DA MESSAGE2
L.MESSAGE3		.DA MESSAGE3

				.DA 0
*--------------------------------------
CS.INIT			clc
CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			jsr LoadResources
				bcs CS.INIT.RTS

				jsr GFX.Open
				bcs CS.INIT.RTS

				>LDYA L.CB.RECT1
				jsr GFX.Write.YA

				lda #16
				sta Cnt

.1				>LDYA L.CB.RECT2
				jsr GFX.Write.YA

				inc CB.RECT2+S.CB.COLOR

				lda CB.RECT2+S.CB.X1
				clc
				adc #28
				sta CB.RECT2+S.CB.X1
				bcc .2

				inc CB.RECT2+S.CB.X1+1

.2				lda CB.RECT2+S.CB.X2
				clc
				adc #28
				sta CB.RECT2+S.CB.X2
				bcc .3

				inc CB.RECT2+S.CB.X2+1

.3				dec Cnt
				bne .1

				>LDYA L.CB.MARILYN
				jsr GFX.Write.YA

				>LDYA L.CB.PRODOS
				jsr GFX.Write.YA

				>LDYA L.BM.Apple
				>STYA CB.Apple+S.CB.pSrc

				lda #16
				sta Cnt

.4				>LDYA L.CB.Apple
				jsr GFX.Write.YA

				lda CB.Apple+S.CB.X1
				clc
				adc #28
				sta CB.Apple+S.CB.X1
				bcc .5

				inc CB.Apple+S.CB.X1+1

.5				dec Cnt
				bne .4

				>LDYA hFontB
				>STYA CB.TEXT1+S.CB.hFont

				>LDYA L.MESSAGE1
				>STYA CB.TEXT1+S.CB.pTxt

				>LDYA L.CB.TEXT1
				jsr GFX.Write.YA
				bcs .9

				>LDYA hFont
				>STYA CB.TEXT2+S.CB.hFont

				>LDYA L.MESSAGE2
				>STYA CB.TEXT2+S.CB.pTxt

				>LDYA L.CB.TEXT2
				jsr GFX.Write.YA
				bcs .9

				jsr CS.RUN.TERM.SHOW
				bcs .9

				jsr CS.RUN.LOGO.SHOW
				bcs .9

				lda #0
				sec
.9
CS.RUN.RTS		rts
*--------------------------------------
CS.RUN.TERM.SHOW
				>LDYA hFontT
				>STYA CB.TEXT3+S.CB.hFont

				>LDYA L.MESSAGE3
				>STYA CB.TEXT3+S.CB.pTxt

				lda #16
				sta Cnt

.1				>LDYA L.CB.TEXT3
				jsr GFX.Write.YA
				bcs .9

*				clc

				lda CB.TEXT3+S.CB.Y1
				adc #6
				sta CB.TEXT3+S.CB.Y1

				lda CB.TEXT3+S.CB.pTxt
				clc
				adc #19
				sta CB.TEXT3+S.CB.pTxt
				lda CB.TEXT3+S.CB.pTxt+1
				adc #0
				sta CB.TEXT3+S.CB.pTxt+1

				dec Cnt
				bne .1

				clc

.9				rts
*--------------------------------------
CS.RUN.LOGO.SHOW
*				lda #S.CB.CMD.GETRECTBUFSIZE
*				sta CB.LOGO+S.CB.CMD

				>LDYA L.CB.LOGO
				jsr GFX.Write.YA
				bcs CS.RUN.RTS

				>LIBC Malloc
				bcs CS.RUN.RTS

				>STYA CB.LOGO+S.CB.pDst

				lda #S.CB.CMD.BITBLT
				sta CB.LOGO+S.CB.CMD

.1				lda #S.CB.OP.SET+S.CB.OP.COLOR+S.CB.OP.SAVE
				sta CB.LOGO+S.CB.OP

				>LDYA L.CB.LOGO
				jsr GFX.Write.YA

				jsr GFX.GetC
				bcc .80

				cmp #E.NODATA
				sec
				bne .80

.2			>SLEEP

				lda bVBL
				beq .2

				stz bVBL

				lda #S.CB.OP.RESTORE
				sta CB.LOGO+S.CB.OP

				>LDYA L.CB.LOGO
				jsr GFX.Write.YA

				bit xDir
				bmi .4

				lda CB.LOGO+S.CB.X1
				clc
				adc #4
				tay

				lda CB.LOGO+S.CB.X1+1
				adc #0
				pha
				cpy #244
				sbc /244
				pla
				bcc .5

.3				lda xDir
				eor #$ff
				sta xDir
				bra .6

.4				lda CB.LOGO+S.CB.X1
				sec
				sbc #4
				tay
				lda CB.LOGO+S.CB.X1+1
				sbc #0
				bcc .3

.5				sty CB.LOGO+S.CB.X1
				sta CB.LOGO+S.CB.X1+1

.6				lda CB.LOGO+S.CB.Y1
				bit yDir
				bmi .8

				inc
				sta CB.LOGO+S.CB.Y1
				cmp #152
				bne .1

.7				lda yDir
				eor #$ff
				sta yDir
				jmp .1

.8				sec
				sbc #1
				sta CB.LOGO+S.CB.Y1
				beq .7

				jmp .1

.80				php
				pha
				>LDYA CB.LOGO+S.CB.pDst
				>LIBC Free
				pla
				plp
				rts
*--------------------------------------
CS.SIG			sta bVBL

.9				sec
				rts
*--------------------------------------
CS.QUIT			>LDYA hFontT
				beq .10

				>KAPI FreeBuf

.10				>LDYA hFont
				beq .1

				>KAPI FreeBuf

.1				>LDYA hFontB
				beq .2

				>KAPI FreeBuf

.2				>LDYA CB.MARILYN+S.CB.hSrc
				beq .3

				>KAPI FreeBuf

.3				>LDYA CB.LOGO+S.CB.hSrc
				beq .4

				>KAPI FreeBuf

.4				>LDYA CB.PRODOS+S.CB.hSrc
				beq .7

				>KAPI FreeBuf

.7				>LDYA hFDGFX
				beq .8

				>LIBC Close

.8				clc
				rts
*--------------------------------------
LoadResources	>LDYA L.FONTFILEB
				jsr .1
				bcs .9

				>STYA hFontB

				>LDYA L.FONTFILE
				jsr .1
				bcs .9

				>STYA hFont

				>LDYA L.FONTFILET
				jsr .1
				bcs .9

				>STYA hFontT

				>LDYA L.MARILYNFILE
				jsr .1
				bcs .9

				>STYA CB.MARILYN+S.CB.hSrc

				>LDYA L.PRODOSFILE
				jsr .1
				bcs .9

				>STYA CB.PRODOS+S.CB.hSrc

				>LDYA L.LOGOFILE
				jsr .1
				bcs .9

				>STYA CB.LOGO+S.CB.hSrc

.9				rts
*--------------------------------------
.1				>STYA Filename
				>KAPI LoadBuf
				bcs .9

				>STYA hBuf

				>SS
				>PUSHW L.MSG.LOAD
				>PUSHW Filename
				>PUSHB hBuf
				>PUSHB hBuf+1
				>PUSHBI 4
				>LIBC PrintF
				>SR

				>LDYA hBuf

				rts
*--------------------------------------
GFX.Open		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC Open
				>SR
				bcs .9

				>STYA hFDGFX

.9				rts
*--------------------------------------
GFX.Write.YA	>STYA R1

				>SS
				>PUSHW hFDGFX
				>PUSHW R1

				>PUSHBI 0
				lda (R1)
				and #$7F
				tax
				lda CB.CmdLen-1,x
				>PUSHA

				>LIBC Write
				>SR
				rts
*--------------------------------------
GFX.GetC		>SS
				>PUSHW hFDGFX
				>PUSHW pData			CHARBUF
				>PUSHWI 1
				>LIBC Read
				>SR
				bcs .9

				lda (pData)				CHARBUF

*				clc

.9				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
DEVNAME.GFX		.AZ "/dev/gfx"
MSG.LOAD		.CZ "Loaded %s, hBuf=%h:%b\r\n"
FONTFILEB		.AZ "/opt/gui/fonts/sysx7b"
FONTFILE		.AZ "/opt/gui/fonts/sysx7"
FONTFILET		.AZ "/opt/gui/fonts/term6x6"
MARILYNFILE		.AZ "/root/bmp/marilyn"
PRODOSFILE		.AZ "/root/bmp/prodosm"
LOGOFILE		.AZ "/A2osX.logo"
*--------------------------------------
MESSAGE1		.AZ " DHGR Driver Test ABCDEFabcdef0123456789 (SYSX7B Font, Inverse) "
MESSAGE2		.AZ " 1234567890 abcdefghijklmnopqrstuvwxyz { + - * / } (SYSX7 Font, Normal) "
MESSAGE3		.HS 20.200102030405060708090a0b0c0d0e0f.20.00
				.HS 20.101112131415161718191a1b1c1d1e1f.20.00
				.HS 20.202122232425262728292a2b2c2d2e2f.20.00
				.HS 20.303132333435363738393a3b3c3d3e3f.20.00
				.HS 20.404142434445464748494a4b4c4d4e4f.20.00
				.HS 20.505152535455565758595a5b5c5d5e5f.20.00
				.HS 20.606162636465666768696a6b6c6d6e6f.20.00
				.HS 20.707172737475767778797a7b7c7d7e7f.20.00
				.HS 20.808182838485868788898a8b8c8d8e8f.20.00
				.HS 20.909192939495969798999a9b9c9d9e9f.20.00
				.HS 20.a0a1a2a3a4a5a6a7a8a9aaabacadaeaf.20.00
				.HS 20.b0b1b2b3b4b5b6b7b8b9babbbcbdbebf.20.00
				.HS 20.c0c1c2c3c4c5c6c7c8c9cacbcccdcecf.20.00
				.HS 20.d0d1d2d3d4d5d6d7d8d9dadbdcdddedf.20.00
				.HS 20.e0e1e2e3e4e5e6e7e8e9eaebecedeeef.20.00
				.HS 20.f0f1f2f3f4f5f6f7f8f9fafbfcfdfeff.20.00
*--------------------------------------
CB.RECT1		.DA #S.CB.CMD.FILLRECT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.C16
				.DA #11					Color
				.DA 28
				.DA 14
				.DA 559-28
				.DA 191-14

CB.RECT2		.DA #S.CB.CMD.FILLRECT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.C16
				.DA #0					Color
				.DA 56					X1
				.DA 191-19				Y1
				.DA 56+27				X2
				.DA 191-9				Y2

CB.MARILYN		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.SET		S.CB.OP.SET+S.CB.OP.COLOR
				.DA #S.CB.M.MONO
				.DA #0
				.DA 56					X1
				.DA 10					Y1
				.DA 0
				.DA 0
				.DA 0					pDst
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 140					SrcW
				.DA 150					SrcH
			.BS 2						hSrc
				.DA 0					pSrc

CB.PRODOS		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.COLOR
				.DA #S.CB.M.C16
				.DA #0
				.DA 204					X1
				.DA 130					Y1
				.DA 0
				.DA 0
				.DA 0					pDst
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 320					SrcW
				.DA 40					SrcH
			.BS 2						hSrc
				.DA 0					pSrc

CB.Apple		.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.COLOR
				.DA #S.CB.M.C16
				.DA #0
				.DA 64					X1
				.DA 174					Y1
				.DA 0
				.DA 0
				.DA 0					pDst
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 16					SrcW
				.DA 7					SrcH
			.BS 2						hSrc
				.DA 0					pSrc

CB.TEXT1		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #255				INVERSE
				.DA 7					X1
				.DA 1					Y1
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2						hFont
				.DA 0
			.BS 2						pTxt

CB.TEXT2		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0
				.DA 7					X1
				.DA 184					Y1
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2						hFont
				.DA 0
			.BS 2						pTxt

CB.TEXT3		.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0
				.DA 280					X1
				.DA 20					Y1
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2						hFont
				.DA 0
			.BS 2						pTxt
*--------------------------------------
CB.LOGO			.DA #S.CB.CMD.GETRECTBUFSIZE
				.DA #S.CB.OP.SET+S.CB.OP.COLOR+S.CB.OP.SAVE
				.DA #S.CB.M.C16
				.DA #0
				.DA 200					X1
				.DA 50					Y1
				.DA 200+319				X2 For GETRECTBUFSIZE
				.DA 50+39				Y2 For GETRECTBUFSIZE
			.BS 2						pDst
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 320					SrcW
				.DA 40					SrcH
				.DA 0
			.BS 2						pSrc
*--------------------------------------
CB.CmdLen		.DA #S.CB.Y1+1			SETPIXEL
				.DA #S.CB.Y1+1			GETPIXEL
				.DA #S.CB.X2+1			HLINE
				.DA #S.CB.Y2+1			VLINE
				.DA #S.CB.Y2+1			FILLRECT
				.DA #S.CB.pSrc+1		BITBLT
				.DA #S.CB.pDst+1		GETRECTBUFSIZE
				.DA #S.CB.pTxt+1		DRAWTEXT
				.DA #S.CB.pTxt+1		GETTEXTW
*--------------------------------------
BM.Apple		.DA #S.BM.F.BBP4		F
				.DA #2					RowBytes
				.DA 16					W
				.DA 7					H
				.DA BM.Apple.AND-BM.Apple
				.HS 0006				green (8)
				.HS 6006				green (8)
				.HS EEEE				yellow (15)
				.HS	CC0C				orange (14)
				.HS 9909				magenta (10)
				.HS 8888				violet (11)
				.HS 1001				Dark blue (4)
BM.Apple.AND	.HS FFF0
				.HS 0FF0
				.HS 0000
				.HS 00F0
				.HS 00F0
				.HS 0000
				.HS 0FF0
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
CHARBUF			.BS 1
DS.END			.ED
*--------------------------------------
MAN
SAVE root/asmtest/gtest.s
ASM
