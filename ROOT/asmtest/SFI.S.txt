NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF root/asmtest/sfi
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/io.i
				.INB inc/gfx.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hFDGFX			.BS 2
hFont			.BS 2

pInBuf			.BS 2
hInFILE			.BS 2

Cnt				.BS 2
PicLen			.BS 2

pHGR			.BS 2
Pass			.BS 1
Repeat			.BS 1
Byte			.BS 1
Byte2			.BS 1

pOutBuf			.BS 2
pOutFilename	.BS 2
pOutFILE		.BS 2

bVBL			.BS 1
idx				.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.DEVNAME.GFX	.DA DEVNAME.GFX
L.FONTFILE		.DA FONTFILE

L.BETA.FILENAME	.DA	BETA.FILENAME
L.GAMMA.FILENAME	.DA	GAMMA.FILENAME
L.LOGO.FILENAME	.DA	LOGO.FILENAME
L.PIX.FILENAME	.DA	PIX.FILENAME

L.OUTDIR		.DA OUTDIR

L.OUTDIC		.DA OUTDIC
L.OUTMAP		.DA OUTMAP
L.OUTMSG		.DA OUTMSG
L.OUTOBJ		.DA OUTOBJ
L.OUTLOC		.DA OUTLOC
L.OUTPLOC		.DA OUTPLOC
L.OUTANIM		.DA OUTANIM
L.OUTPOBJ		.DA OUTPOBJ

L.MSG.DIC		.DA MSG.DIC
L.MSG.MAP		.DA MSG.MAP
L.MSG.MSG		.DA MSG.MSG
L.MSG.OBJ		.DA MSG.OBJ
L.MSG.LOC		.DA MSG.LOC

L.PIC.INDEX		.DA PIC.INDEX

L.MSG.OPIC		.DA MSG.OPIC
L.MSG.LPIC		.DA MSG.LPIC
L.MSG.LANI		.DA MSG.LANI

L.CB.TEXT		.DA CB.TEXT
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.BETA.FILENAME
				jsr fOpenYA
				bcs .9

				jsr GetDIC
				bcs .9

				jsr GetMSG
				bcs .9

				jsr GetMAP
				bcs .9

				jsr GetDesc
				bcs .9

				jsr fInClose

				jsr GFX.Init
				bcs .9

				jsr GetLOGO
				bcs .9

				>LDYA L.GAMMA.FILENAME
				jsr fOpenYA
				bcs .9

				jsr GetOBJPics
				bcs .9

				jsr GetLOGOAnim
				bcs .9

				jsr fInClose

				>LDYA L.PIX.FILENAME
				jsr fOpenYA
				bcs .9

				jsr GetLOCPics
				bcs .9

				jsr fInClose

				lda #0
				sec
.9
CS.RUN.RTS		rts
*--------------------------------------
CS.SIG			sta bVBL

.9				sec
				rts
*--------------------------------------
CS.QUIT			jsr fInClose

.1				>LDYA hFont
				beq .7

				>KAPI FreeBuf

.7				ldy hFDGFX
				beq .8

				lda hFDGFX+1

				>LIBC Close

.8				clc
				rts
*--------------------------------------
GetDIC			>SS
				>PUSHW hInFILE
				>PUSHWZ
				>PUSHWI $5000-$4000
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .99

				>LDYAI $AA0
				>LIBC Malloc
				bcs .99

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI $AA0
				>LIBC FRead
				>SR
				bcs .98

				>LDYA L.OUTDIC
				jsr fCreateYA
				bcs .98

.1				jsr GetDicX
				bcs .97

				lda R3
				clc
				adc #8
				sta R3
				bcc .7

				inc R3+1

.7				lda (R3)
				cmp #$FF
				bne .1

				clc

.97				jsr fOutClose

.98				jsr pInBufFree

.99				rts
*--------------------------------------
GetDicX			ldy #0

.1				lda (R3),y
				cmp #$A0
				beq .2

				and #$7F
				sta (pData),y
				iny
				cpy #5
				bne .1

.2				lda #0
				sta (pData),y

				>SS
				>PUSHW pOutFILE
				>PUSHW L.MSG.DIC
				>PUSHW pData

				ldy #5
				lda (R3),y
				>PUSHA
				iny
				lda (R3),y
				>PUSHA
				iny
				lda (R3),y
				>PUSHA
				>PUSHBI 5
				>LIBC FPrintF
				>SR

				rts
*--------------------------------------
GetMSG			>LDYA L.OUTMSG
				jsr fCreateYA
				bcs .99

				stz Cnt

				stz Byte

.1				jsr GetMSGX
				bcs .9

				inc Byte
				inc Byte
				lda Byte
				cmp #10
				bcc .1

				clc

.9				jsr fOutClose

.99				rts
*--------------------------------------
GetMSGX			>SS
				>PUSHW hInFILE
				>PUSHWZ
				ldx Byte
				>PUSHW MSG.Ofs,x
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .99

				ldx Byte
				>LDYA MSG.Len,x
				>LIBC Malloc
				bcs .99

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				ldx Byte
				>PUSHW MSG.Len,x
				>LIBC FRead
				>SR
.99				bcs .9

				stz Byte2

.1				ldy #0

.2				lda (R3)
				beq .8

				>INCW R3

				eor #$55
				and #$7F
				cmp #$0D
				bne .3

				lda #C.SPACE

.3				sta (pData),y
				iny
				bra .2

.8				sta (pData),y

				>INCW R3

				>SS
				>PUSHW pOutFILE
				>PUSHW L.MSG.MSG
				>PUSHB Cnt
				>PUSHW pData
				>PUSHBI 3
				>LIBC FPrintF
				>SR

				inc Cnt

				inc Byte2
				lda Byte2
				cmp #50
				bcc .1

				clc

				jsr pInBufFree

.9				rts
*--------------------------------------
GetMAP			>SS
				>PUSHW hInFILE
				>PUSHWZ
				>PUSHWI $82D8-$4000
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .99

				>LDYAI $58D
				>LIBC Malloc
				bcs .99

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI $58D
				>LIBC FRead
				>SR
				bcs .98

				>LDYA L.OUTMAP
				jsr fCreateYA
				bcs .98

				stz Cnt

.1				jsr GetMAPX
				bcs .97

				inc Cnt
				lda Cnt
				cmp #43
				bcc .1

				clc

.97				jsr fOutClose

.98				jsr pInBufFree

.99				rts
*--------------------------------------
GetMAPX			>SS
				>PUSHW pOutFILE
				>PUSHW L.MSG.MAP
				>PUSHB Cnt

				stz idx

				ldy #0

.1				lda (R3)
				>INCW R3

				eor #$55
				cmp #255
				beq .11

				tax
				lda #'L'
				jsr AX2IOBuf

.11				lda #','
				jsr A2IOBuf

				iny
				cpy #6					N,S,E,W,U,D,
				bcc .1

				lda #0
				jsr A2IOBuf

				>PUSHW pData

				>LDYA R3				Exits
				>STYA R4

				ldy #0

.2				lda (R3)
				beq .3

				eor #$55
				and #$7F
				sta (R3)
				eor #$0D
				bne .20

				sta (R3)

.20				>INCW R3
				iny
				bra .2

.3				>INCW R3				skip \0

				ldy #7

.30				dey
				lda (R4),y
				eor #C.SPACE
				bne .31

				sta (R4),y
				bra .30

.31				>PUSHW R3				Title
				>PUSHW R4				Exits

.4				lda (R3)
				beq .5

				eor #$55
				and #$7F
				sta (R3)
				eor #$0D
				bne .40

				sta (R3)

.40				>INCW R3
				bra .4

.5				>INCW R3

				>PUSHBI 7
				>LIBC FPrintF
				>SR

				rts
*--------------------------------------
AX2IOBuf		jsr A2IOBuf

				txa

				stz R1
				stz R1+1

				ldx #8

				sed

.1				asl
				pha
				lda R1
				adc R1
				sta R1
				lda R1+1
				adc R1+1
				sta R1+1
				pla
				dex
				bne .1

				cld

				lda R1+1
				jsr .2

				lda R1
				lsr
				lsr
				lsr
				lsr
				jsr .2

				lda R1
				and #$0F
.2				ora #$30
*--------------------------------------
A2IOBuf			phy
				ldy idx
				sta (pData),y
				inc idx
				ply
				rts
*--------------------------------------
GetDesc			>SS
				>PUSHW hInFILE
				>PUSHWZ
				>PUSHWI $8880-$4000
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .99

				>LDYAI $EA6+216			+108 Ptrs
				>LIBC Malloc
				bcs .99

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI $EA6+216
				>LIBC FRead
				>SR
				bcs .98

				stz Cnt

				jsr GetOBJDesc
				bcs .98

				jsr GetLOCDesc

.98				jsr pInBufFree

.99				rts
*--------------------------------------
GetOBJDesc		>LDYA L.OUTOBJ
				jsr fCreateYA
				bcs .99

.1				jsr SetDescR3

				ldy #0

.2				>INCW R3				skip len

				lda (R3)
				beq .3

				eor #$55
				sta (pData),y
				iny
				bra .2

.3				>INCW R3
				lda (R3)
				beq .8

				lda #','
				sta (pData),y
				iny
				bra .2

.8				sta (pData),y

				>SS
				>PUSHW pOutFILE
				>PUSHW L.MSG.OBJ
				>PUSHB Cnt
				>PUSHW pData
				>PUSHBI 3
				>LIBC FPrintF
				>SR
				bcs .98

				inc Cnt
				lda Cnt
				cmp #49
				bcc .1

				clc

.98				jsr fOutClose

.99				rts
*--------------------------------------
GetLOCDesc		>LDYA L.OUTLOC
				jsr fCreateYA
				bcs .99

.1				jsr SetDescR3

				ldy #0

.2				lda (R3)
				beq .8

				>INCW R3

				eor #$55
				and #$7F
				cmp #$0D
				bne .3

				lda #C.SPACE

.3				sta (pData),y
				iny
				bra .2

.8				sta (pData),y

				>INCW R3

				>SS
				>PUSHW pOutFILE
				>PUSHW L.MSG.LOC
				lda Cnt
				sec
				sbc #48
				>PUSHA
				>PUSHW pData
				>PUSHBI 3
				>LIBC FPrintF
				>SR
				bcs .98

				>INCW R3

				inc Cnt
				lda Cnt
				cmp #108
				bcc .1

				clc

.98				jsr fOutClose

.99				rts
*--------------------------------------
SetDescR3		lda Cnt
				asl
				tay
				lda (pInBuf),y
				sec
				sbc #$895A-218
				sta R3
				iny
				lda (pInBuf),y
				sbc /$895A-218
				sta R3+1

				lda R3
				clc
				adc pInBuf
				sta R3
				lda R3+1
				adc pInBuf+1
				sta R3+1
				rts
*--------------------------------------
GetLOGO			>LDYAI 8192
				>LIBC Malloc
				bcs .99

				>STYA pInBuf
				>STYA R3

				>LDYA L.LOGO.FILENAME
				jsr fOpenYA
				bcs .98

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI 8192
				>LIBC FRead
				>SR
				bcs .97

				jsr fInClose

				ldx #32

				ldy #0
				sty R4

				lda #$20
				sta R4+1

				sta IO.CLRWRITEAUX

.1				lda (R3),y
				sta (R4),y
				iny
				bne .1

				inc R3+1
				inc R4+1
				dex
				bne .1

				sta IO.SETWRITEAUX

				jsr pInBufFree
			>DEBUG
				clc
				rts

.97				jsr fInClose

.98				jsr pInBufFree

.99				rts
*--------------------------------------
GetOBJPics		>SS
				>PUSHW hInFILE
				>PUSHWZ
				>PUSHWI $A7CD-$9800
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .9

				>LDYAI 2875
				>LIBC Malloc
				bcs .9

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI 2875
				>LIBC FRead
				>SR
				bcs .9

				stz Cnt

.1				jsr ClrScrW
				jsr GetOBJPic
				jsr DrawOBJPic
				jsr PrintOBJStat

				ldy #1
				lda (R3),y
				clc
				adc R3
				tax
				iny
				lda (R3),y
				adc R3+1
				sta R3+1
				stx R3
*			>DEBUG
				lda (R3)
				cmp #$FF
				bne .1

				jsr pInBufFree

				clc

.9				rts
*--------------------------------------
GetOBJPic		ldx #S.BM-1

.1				stz BM,x
				dex
				bne .1

				lda R3
				clc
				adc #3
				sta R4
				lda R3+1
				adc #0
				sta R4+1

.2				lda (R4)
				>INCW R4
				cmp #$FF
				beq .8

				sta pHGR

				lda (R4)
				>INCW R4
				sta pHGR+1

				jsr pHGR2XY









.8				rts
*--------------------------------------
DrawOBJPic		lda R3
				clc
				adc #3
				sta R4
				lda R3+1
				adc #0
				sta R4+1

.1				lda (R4)
				cmp #$FF
				beq .8

				sta pHGR

				ldy #1
				lda (R4),y
				sta pHGR+1
				iny

				lda (R4),y
				and (pHGR)
				sta Byte
				iny

				lda (R4),y
				sta Byte2
				iny

				lda (R4),y
				tax
				iny

				lda (R4),y
				ora Byte
				iny
				bra .5

.4				lda (R4),y
				iny

.5				dex
				beq .6

				sta (pHGR)
				inc pHGR
				bne .4

.6				sta Byte
				lda (pHGR)
				and Byte2
				ora Byte
				sta (pHGR)
				tya
				clc
				adc R4
				sta R4
				bcc .1

				inc R4+1
				bra .1

.8				rts
*--------------------------------------
PrintOBJStat	>SS
				>PUSHW pData
				>PUSHW L.MSG.OPIC
				>PUSHB (R3)
				ldy #2
				>PUSHB (R3),y
				dey
				>PUSHB (R3),y
				>PUSHB CB.OBJ+S.CB.X1
				>PUSHB CB.OBJ+S.CB.Y1
				>PUSHB BM+S.BM.W
				>PUSHB BM+S.BM.H
				>PUSHBI 7
				>LIBC SPrintF
				>SR

				>LDYA pData
				ldx #160
				jsr GFX.PrintYAX

				rts
*--------------------------------------
GetLOGOAnim		>SS
				>PUSHW hInFILE
				>PUSHWZ
				>PUSHWI $B6FF-$9800
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .9

				>LDYAI 38+38+3
				>LIBC Malloc
				bcs .9

				>STYA pInBuf
				>STYA R3

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHWI 38+38+3
				>LIBC FRead
				>SR
				bcs .9
				
				
				
				
				
				
				
				

				jsr pInBufFree

				clc

.9				rts				
*--------------------------------------
GetLOCPics		stz Cnt

.1				jsr GetLOCPic
				bcs .9

				inc Cnt
				lda Cnt
				cmp #53
				bcc .1

				clc

.9				rts
*--------------------------------------
GetLOCPic		lda Cnt
				asl
				tax
				sec
				lda PIC.INDEX+2,x
				sbc PIC.INDEX,x
				sta PicLen
				lda PIC.INDEX+3,x
				sbc PIC.INDEX+1,x
				asl PicLen
				rol
				sta PicLen+1

				>LDYA PicLen
				>LIBC Malloc
				bcs .9

				>STYA pInBuf

				>SS
				>PUSHW hInFILE
				>PUSHW pInBuf
				>PUSHW PicLen
				>LIBC FRead
				>SR
				bcs .9

				jsr GetPicBuf
				bcs .9

				jsr ClrScrB
				jsr UnpaknDrawPic
				bcs .9

				jsr PrintPICStat
				bcs .9

				jsr SavePic
				bcs .9

				ldy #2
				lda (pInBuf),y
				iny
				ora (pInBuf),y
				beq .8
				
				jsr GetPicAnim
				bcs .9

				lda Cnt+1
				beq .8
				
				jsr PrintANIStat
				bcs .9

*				jsr PicAnimate

.8				jsr pOutBufFree
				jsr pInBufFree

			>DEBUG
			
*				clc

.9				rts
*--------------------------------------
ClrScrB			sec
				.HS 90					BCC
ClrScrW			clc

				lda #0
				sbc #0

				ldx #0

				sta IO.CLRWRITEAUX

.1				ldy HGR.L,x
				sty pHGR
				ldy HGR.H,x
				sty pHGR+1

				ldy #39

.2				sta (pHGR),y
				dey
				bpl .2

				inx
				cpx #192
				bcc .1

				sta IO.SETWRITEAUX

				rts
*--------------------------------------
GetPicBuf		>LDYAI 40*149+S.BM
				>LIBC Malloc
				bcs .9

				>STYA pOutBuf

				ldy #S.BM-1

.1				lda BM.PIC,y
				sta (pOutBuf),y
				dey
				bpl .1

.9				rts
*--------------------------------------
UnpaknDrawPic	lda pInBuf
				clc
				adc #4
				sta R3
				lda pInBuf+1
				adc #0
				sta R3+1

				ldx #1
				stx Pass
				lda HGR.L+9,x
				sta pHGR
				lda HGR.H+9,x
				sta pHGR+1

				clc
				lda PIC.L,x
				adc pOutBuf
				sta R4
				lda PIC.H,x
				adc pOutBuf+1
				sta R4+1

				stz Repeat

				ldy #0

.1				lda (R3)
				bne .3

				>INCW R3

				lda (R3)
				sta Repeat

				>INCW R3

				lda (R3)
				sta Byte

.2				lda Byte
				dec Repeat

.3				sta IO.CLRWRITEAUX
				sta (pHGR),y
				sta IO.SETWRITEAUX
				sta (R4),y
				inx
				inx
				cpx	#149
				bcc .5

				iny
				cpy #40
				bcc .4

				dec Pass
				bmi .8

				ldy #0

.4				ldx Pass

.5				lda HGR.L+9,x
				sta pHGR
				lda HGR.H+9,x
				sta pHGR+1

				clc
				lda PIC.L,x
				adc pOutBuf
				sta R4
				lda PIC.H,x
				adc pOutBuf+1
				sta R4+1

				lda Repeat
				bne .2

				inc R3
				bne .1

				inc R3+1
				bra .1

.8				clc
				rts
*--------------------------------------
SavePic			lda Cnt
				jsr fCreatePicA
				bcs .9

				>SS
				>PUSHW pOutFILE
				>PUSHW pOutBuf
				>PUSHWI 40*149+S.BM
				>LIBC fWrite
				>SR

.9				jmp fOutClose
*--------------------------------------
GetPicAnim		stz Cnt+1

				stz R4
				stz R4+1

				jsr GetPicAnimR3

.1				lda (R3)
				ldy #1
				ora (R3),y
				beq .5

				inc Cnt+1

.2				iny
				lda (R3),y
				bne .2

				tya						skip HGRl,HGRh+Bytes+00
				sec
				adc R3
				sta R3
				bcc .3

				inc R3+1

.3				tya						Add X,Y+L+Bytes
				sec
				adc R4
				sta R4
				bcc .4

				inc R4+1

.4				lda (R3)
				ldy #1
				ora (R3),y
				bne .2

				lda #2					skip 00 00
				clc
				adc R3
				sta R3
				bcc .1

				inc R3+1
				bra .1
*--------------------------------------
.5				lda Cnt+1
				beq .80

				jsr GetPicAnimR3

.6				lda (R3)
				sta pHGR
				ldy #1
				lda (R3),y
				sta pHGR+1
				ora pHGR
				beq .80
			>DEBUG
.7				iny
				lda (R3),y
				beq .8

				sta IO.CLRWRITEAUX
				sta (pHGR)
				sta IO.SETWRITEAUX
				inc pHGR
				bne .7
				
				inc pHGR+1
				bra .7

.8				tya
				sec
				adc R3
				sta R3
				bcc .9

				inc R3+1

.9				lda (R3)
				sta pHGR
				ldy #1
				lda (R3),y
				sta pHGR+1
				ora pHGR
				bne .7

				lda #2					skip 00 00
				clc
				adc R3
				sta R3
				bcc .6

				inc R3+1
				bra .6

.80				clc
				rts
*--------------------------------------
GetPicAnimR3	lda (pInBuf)
				clc
				adc pInBuf
				sta R3
				ldy #1
				lda (pInBuf),y
				adc pInBuf+1
				sta R3+1

				rts
*--------------------------------------
PicAnimate		lda (pInBuf)
				clc
				adc pInBuf
				sta R3
				ldy #1
				lda (pInBuf),y
				adc pInBuf+1
				sta R3+1

				lda (R3)
				ora (R3),y
				beq .8

.1				lda (R3)
				>INCW R3
				sta pHGR

				lda (R3)
				>INCW R3
				sta pHGR+1
				ora pHGR
				beq .7

				ldy #0

.2				lda (R3),y
				beq .3

				tax

				lda (pHGR),y
				sta (R3),y

				txa
				sta (pHGR),y
				iny
				bra .2

.3				tya
				sec
				adc R3
				sta R3
				bcc .1

				inc R3+1
				bra .1
*--------------------------------------
.7				jsr GFX.GetC
				bcc .9

				cmp #E.NODATA
				beq PicAnimate

				sec

				rts
*--------------------------------------
.8				jsr GFX.GetC
				bcc .9

				cmp #E.NODATA
				beq .8

				sec

.9				rts
*--------------------------------------
PrintPICStat	>SS
				>PUSHW pData
				>PUSHW L.MSG.LPIC
				>PUSHB Cnt
				>PUSHW PicLen

				ldy #1
				lda (pInBuf),y
				>PUSHA
				lda (pInBuf)
				>PUSHA

				ldy #3
				lda (pInBuf),y
				>PUSHA

				dey
				lda (pInBuf),y
				>PUSHA

				>PUSHBI 7
				>LIBC SPrintF
				>SR

				>LDYA pData
				ldx #0
				jmp GFX.PrintYAX
*--------------------------------------
PrintANIStat	>SS
				>PUSHW pData
				>PUSHW L.MSG.LANI
				>PUSHB Cnt
				>PUSHB Cnt+1

				>PUSHW R4

				>PUSHBI 4
				>LIBC SPrintF
				>SR

				>LDYA pData
				ldx #160
				jmp GFX.PrintYAX
*--------------------------------------
pHGR2XY			stz CB.OBJ+S.CB.X1
				stz CB.OBJ+S.CB.Y1

				lda pHGR
				and #$7F

.1				cmp #80
				bcc .2

				sbc #80
				bra .3

.2				cmp #40
				bcc .3

				sbc #40

.3				sta CB.OBJ+S.CB.X1

				lda pHGR
				sec
				sbc CB.OBJ+S.CB.X1
				sta pHGR

				ldy #192

.5				dey
				lda pHGR+1
				cmp HGR.H,y
				bne .6

				lda pHGR
				cmp HGR.L,y
				beq .7

.6				tya
				bne .5

.7				sty CB.OBJ+S.CB.Y1

				rts
*--------------------------------------
pInBufFree		php
				pha
				>LDYA pInBuf
				beq .8

				>LIBC Free

				stz pInBuf+1

.8				pla
				plp
				rts
*--------------------------------------
fOpenYA			>SS
				>PUSHYA
				>PUSHBI	O.RDONLY
				>PUSHBI 0				Type
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA hInFILE

.9				rts
*--------------------------------------
fInClose		php
				pha
				>LDYA hInFILE
				beq .8

				>LIBC FClose

				stz hInFILE+1

.8				pla
				plp
				rts
*--------------------------------------
fCreatePicA		sta R1

				>LDYAI 256
				>LIBC Malloc
				bcc .1

				rts

.1				>STYA pOutFilename

				>SS
				>PUSHW pOutFilename
				>PUSHW L.OUTPLOC
				>PUSHW L.OUTDIR
				>PUSHB R1
				>PUSHBI 3
				>LIBC SPrintF
				>SR
				bcs .9

				>SS
				>PUSHW pOutFilename
				>PUSHBI	O.CREATE+O.WRONLY
				>PUSHBI $CB				PIX
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pOutFILE

.9				php
				pha

				>LDYA pOutFilename
				>LIBC Free

				pla
				plp
.99				rts
*--------------------------------------
fCreateYA		>STYA R1

				>LDYAI 256
				>LIBC Malloc
				bcs .99

				>STYA pOutFilename

				ldy #$ff

.1				iny
				lda OUTDIR,y
				sta (pOutFilename),y
				bne .1

.2				lda (R1)
				sta (pOutFilename),y
				beq .3

				>INCW R1
				iny
				bra .2

.3				>SS
				>PUSHW pOutFilename
				>PUSHBI	O.CREATE+O.WRONLY
				>PUSHBI 4				TXT
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pOutFILE

.9				php
				pha

				>LDYA pOutFilename
				>LIBC Free

				pla
				plp
.99				rts
*--------------------------------------
fOutClose		php
				pha

				>LDYA pOutFILE
				>LIBC FClose

				pla
				plp
				rts
*--------------------------------------
pOutBufFree		php
				pha

				>LDYA pOutBuf
				beq .8

				>LIBC Free

				stz pOutBuf+1

.8				pla
				plp
				rts
*--------------------------------------
GFX.Init		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC Open
				>SR
				bcs .9

				>STYA hFDGFX

				>LDYA L.FONTFILE
				>KAPI LoadBuf
				bcs .9

				>STYA hFont
				>STYA CB.TEXT+S.CB.hSrc

.9				rts
*--------------------------------------
GFX.GetC		>SLEEP

				>SS
				>PUSHW hFDGFX
				>PUSHWI 1
				>PUSHW pData
				>LIBC Read
				>SR
				bcs .9

				lda (pData)

*				clc

.9				rts
*--------------------------------------
GFX.PrintYAX	>STYA CB.TEXT+S.CB.pTxt

				stx CB.TEXT+S.CB.Y1

				>LDYA L.CB.TEXT
				>STYA R1
*--------------------------------------
				>SS
				>PUSHW hFDGFX
				>PUSHW R1

				>PUSHBI 0
				lda (R1)
				and #$7F
				tax
				lda CB.CmdLen-1,x
				>PUSHA

				>LIBC Write
				>SR
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
DEVNAME.GFX		.AZ "/dev/gfx"
FONTFILE		.AZ "/opt/gui/fonts/a27x8b"
*--------------------------------------
MSG.Ofs			.DA $5B06-$4000
				.DA $60C4-$4000
				.DA $6684-$4000
				.DA $6DC6-$4000
				.DA $7864-$4000
*--------------------------------------
MSG.Len			.DA $550
				.DA $55C
				.DA $6DC
				.DA $A3A
				.DA $9E0
*--------------------------------------
PIC.INDEX		.DA	$0000
				.DA $0411
				.DA $0792
				.DA $0AF2
				.DA $0D43
				.DA $10E4
				.DA $13D5
				.DA $1626
				.DA $1A41
				.DA $1F11
				.DA $2213
				.DA $23C8
				.DA $2669
				.DA $2814
				.DA $2A52
				.DA $2D91
				.DA $2F20
				.DA $3110
				.DA $3379
				.DA $3601
				.DA $3912
				.DA $3BBF
				.DA $4033
				.DA $42D9
				.DA $44BF
				.DA $4810
				.DA $49FB
				.DA $4C4D
				.DA $4EF6
				.DA $5186
				.DA $54D5
				.DA $5734
				.DA $5895
				.DA $5C13
				.DA $5E17
				.DA $60B7
				.DA $62D2
				.DA $646B
				.DA $673E
				.DA $68B1
				.DA $6C8E
				.DA $70ED
				.DA $73FF
				.DA $757A
				.DA $794A
				.DA $7B80
				.DA $7D62
				.DA $8012
				.DA $817D
				.DA $8462
				.DA $876C
				.DA $88B8
				.DA $8B38
				.DA $8FAA
*--------------------------------------
BETA.FILENAME	.AZ "/SF/BETA"
GAMMA.FILENAME	.AZ "/SF/GAMMA"
LOGO.FILENAME	.AZ "/SF/LOGO"
PIX.FILENAME	.AZ "/SF/PIX"
*--------------------------------------
OUTDIR			.AZ "/var/opt/acsx/SF/"
OUTDIC			.AZ "dic"
OUTMAP			.AZ "map"
OUTMSG			.AZ "msg"
OUTOBJ			.AZ "obj"
OUTLOC			.AZ "loc"
OUTPLOC			.AZ "%sL%03d"
OUTANIM			.AZ "%sA%03d"
OUTPOBJ			.AZ "%sO%03d"
*--------------------------------------
MSG.DIC			.CZ "%s,%d,%d,L%03d\r\n"
MSG.MAP			.CZ "L%03d:%s,,,,%s,%s\r\n"
MSG.MSG			.CZ "M%03d:%s\r\n"
MSG.OBJ			.CZ "O%03d:%s\r\n"
MSG.LOC			.CZ "L%03d:%s\r\n"
*--------------------------------------
MSG.OPIC		.CZ "O%03d,L=%4D,X=%3d,Y=%3d,W=%3d,H=%3d"
MSG.LPIC		.CZ "L%03d,L=%4D,P=%4D,F=%h%h"
MSG.LANI		.CZ "A%03d,Cnt=%2d, repacked size:%4D"
*--------------------------------------
BM				.DA #S.BM.F.BBP8
				.BS S.BM-1
*--------------------------------------
BM.PIC			.DA #S.BM.F.BBP8
				.DA #40
				.DA 40
				.DA 149
				.DA 0
*--------------------------------------
CB.TEXT			.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
			.BS 2					X1
			.BS 2					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
			.BS 2					hSrc
			.BS 2					pTxt
*--------------------------------------
CB.OBJ			.DA #S.CB.CMD.BITBLT
				.DA #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.COLOR
				.DA #S.CB.M.C256
				.DA #0
			.BS 2					X1
			.BS 2					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
			.BS 2					SrcW
			.BS 2					SrcH
			.BS 2					pSrc
				.DA 0					pDst
				.DA 0					hSrc
*--------------------------------------
				.INB root/asmtest/sfi.g
				.INB usr/src/shared/x.hgr.g
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
IOBuf			.BS 256
DS.END			.ED
*--------------------------------------
MAN
SAVE root/asmtest/sfi.s
ASM
