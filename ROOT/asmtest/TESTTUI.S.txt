NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF root/asmtest/testtui
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/libtui.i
*--------------------------------------
EV.SAVE			.EQ $C0
EV.QUIT			.EQ $C1
EV.CHECKBOX		.EQ $C2
EV.RADIO		.EQ $C3
EV.LBOX1.CHANGED	.EQ $C4
EV.LBOX1.PRESSED	.EQ $C5
EV.OK			.EQ $CE
EV.CANCEL		.EQ $CF
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPtr1			.BS 2

pCTX			.BS 2
pSCRN			.BS 2
pTBOX1			.BS 2
pRADIO			.BS 2
pDLGS			.BS 2
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.LIBTUI		.DA LIBTUI
*--------------------------------------
L.SCRN.Title	.DA SCRN.Title
L.SCRN.Status	.DA SCRN.Status

L.TBOX1.Title	.DA TBOX1.Title
L.TBOX1.Status	.DA TBOX1.Status

L.LBOX1.List	.DA LBOX1.List
L.LABEL.Label	.DA LABEL.Label
L.TLINE.Default	.DA TLINE.Default
L.RADIO1.List	.DA RADIO1.List
L.CBOX1.Label	.DA CBOX1.Label
L.BUTQ.Label	.DA BUTQ.Label
L.BUTS.Label	.DA BUTS.Label
L.DLGS.Title	.DA DLGS.Title
L.BUT.OK		.DA BUT.OK
L.BUT.Cancel	.DA BUT.Cancel
				.DA 0
*--------------------------------------
CS.INIT			>LDYA L.LIBTUI
				>KAPI LoadLib
				bcs CS.INIT.RTS

				>STYA LIBTUI

*				clc

CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			>LIBCALL LIBTUI,Init
				bcs CS.INIT.RTS
			
				>STYA pCTX

				jsr CS.SCRN.Init
				bcs CS.INIT.RTS

				jsr CS.TBOX1.Init
				bcs CS.INIT.RTS

				jsr CS.LBOX1.Init
				bcs CS.INIT.RTS

				jsr CS.TLINE.Init
				bcs CS.INIT.RTS

				jsr CS.RADIO.Init
				bcs CS.INIT.RTS

				jsr CS.CBOX.Init
				bcs CS.INIT.RTS

				jsr CS.BUT.Init
				bcs CS.INIT.RTS

				jsr CS.SCRN.Draw		Will Call SCRN.Draw

				>SS
				>PUSHW pTBOX1
				>LIBCALL LIBTUI,Activate
				>SR
*--------------------------------------
CS.RUN.LOOP		>SS
				>PUSHW pSCRN
				>LIBCALL LIBTUI,Exec
				>SR
				bcs .9

				cmp #EV.RESIZE
				bne .1

				jsr CS.SCRN.Draw
				bcc CS.RUN.LOOP
				
				rts

.1				cmp #EV.SAVE
				bne .2

				jsr CS.RUN.DLGSAVE
				bcc CS.RUN.LOOP
				
				rts

.2				cmp #EV.CHECKBOX
				bne .3

				>LDA.G CBOX1.Var
				bne .20
				
				>PUSHW pRADIO
				>LIBCALL LIBTUI,Hide
				bcc CS.RUN.LOOP
				
				rts
				
.20				>PUSHW pRADIO
				>LIBCALL LIBTUI,Show
				bcc CS.RUN.LOOP
				
				rts

.3				cmp #EV.LBOX1.CHANGED
				bne .8
				
				jsr CS.RUN.SetStatus
				bcc CS.RUN.LOOP

.9				rts

.8				eor #EV.QUIT
				bne CS.RUN.LOOP

				sec

				rts
*--------------------------------------
CS.RUN.SetStatus
				>SS
				>PUSHW pSCRN
				>PUSHWI S.OBJ.pStatus
				>PUSHW.G LBOX1.Var
				>LIBCALL LIBTUI,SetProp
				>SR
				rts
*--------------------------------------
CS.SCRN.Init	>SS
				>PUSHW pCTX
				>PUSHWI S.OBJ.F.bTitle+S.OBJ.F.bStatus
				>LIBCALL LIBTUI,NewScrn
				>SR
				bcs .9

				>STYA pSCRN

				>SS
				>PUSHYA
				>PUSHWI S.OBJ.pTitle
				>PUSHEA.G SCRN.TitleBuf
				>LIBCALL LIBTUI,SetProp
				>SR

				>SS
				>PUSHW pSCRN
				>PUSHWI S.OBJ.pStatus
				>PUSHW L.SCRN.Status
				>LIBCALL LIBTUI,SetProp
				>SR

.9
CS.SCRN.Init.RTS
				rts
*--------------------------------------
CS.TBOX1.Init	>LDYAI 1024
				>LIBC Malloc
				bcs CS.SCRN.Init.RTS

				>STYA ZPPtr1

				>STYA.G TBOX1.pBuf

				lda #0
				sta (ZPPtr1)

				>SS
				>PUSHW pSCRN
				>PUSHBI 1				X1
				>PUSHBI 1				Y1
				>PUSHBI	46				W
				>PUSHBI 20				H
				>PUSHWI S.OBJ.F.bHBorder+S.OBJ.F.bVBorder+S.OBJ.F.bTitle+S.OBJ.F.bStatus+S.OBJ.F.bEdit
				>PUSHW.G TBOX1.pBuf
				>PUSHWI 1024
				>LIBCALL LIBTUI,NewTBox
				>SR
				bcs .9

				>STYA pTBOX1

				>SS
				>PUSHW pTBOX1
				>PUSHWI S.OBJ.pTitle
				>PUSHW L.TBOX1.Title
				>LIBCALL LIBTUI,SetProp
				>SR
				bcs .9
				
				>SS
				>PUSHW pTBOX1
				>PUSHWI S.OBJ.pStatus
				>PUSHW L.TBOX1.Status
				>LIBCALL LIBTUI,SetProp
				>SR

.9				rts
*--------------------------------------
CS.LBOX1.Init	>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 1				Y1
				>PUSHBI	20				W
				>PUSHBI 9				H
				>PUSHBI EV.LBOX1.CHANGED
				>PUSHBI EV.LBOX1.PRESSED	
				>PUSHWI S.OBJ.F.bHBorder+S.OBJ.F.bVBorder
				>PUSHW L.LBOX1.List
				>PUSHEA.G LBOX1.Var
				>LIBCALL LIBTUI,NewLBox
				>SR
				rts
*--------------------------------------
CS.TLINE.Init	>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 11				Y1
				>PUSHW L.LABEL.Label
				>LIBCALL LIBTUI,NewLabel
				>SR
				bcs .99

				>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 12				Y1
				>PUSHBI 25				W
				>PUSHBI 0
				>PUSHEA.G TLINE.Buf
				>PUSHWI 65				Len
				>LIBCALL LIBTUI,NewTLine
				>SR
.99				bcs .9

				>SS
				>PUSHEA.G TLINE.Buf
				>PUSHW L.TLINE.Default
*				>LIBC StrCpy
				>SR

.9				rts
*--------------------------------------
CS.RADIO.Init	>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 14				Y1
				>PUSHBI	0
				>PUSHBI 0
				>PUSHW L.RADIO1.List
				>PUSHEA.G RADIO1.Var
				>LIBCALL LIBTUI,NewRadio
				>SR
				bcs .9
				
				>STYA pRADIO
				>PUSHYA
				>LIBCALL LIBTUI,Hide
				
.9				rts
*--------------------------------------
CS.CBOX.Init	>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 18				Y1
				>PUSHBI	0
				>PUSHBI EV.CHECKBOX
				>PUSHW L.CBOX1.Label
				>PUSHEA.G CBOX1.Var
				>LIBCALL LIBTUI,NewCBox
				>SR
				rts
*--------------------------------------
CS.BUT.Init		>SS
				>PUSHW pSCRN
				>PUSHBI 50				X1
				>PUSHBI 20				Y1
				>PUSHBI	19				Ctrl-S
				>PUSHBI EV.SAVE
				>PUSHW L.BUTS.Label
				>LIBCALL LIBTUI,NewBut
				>SR
				bcs .9

				>SS
				>PUSHW pSCRN
				>PUSHBI 65				X1
				>PUSHBI 20				Y1
				>PUSHBI	17				Ctrl-Q
				>PUSHBI EV.QUIT
				>PUSHW L.BUTQ.Label
				>LIBCALL LIBTUI,NewBut
				>SR
.9
CS.BUT.Init.RTS	rts
*--------------------------------------
CS.RUN.DLGSAVE	>SS
				>PUSHW pCTX
				>PUSHBI 70				W
				>PUSHBI 11				H
				>PUSHW L.DLGS.Title
				>LIBCALL LIBTUI,NewDlg
				>SR
				bcs CS.BUT.Init.RTS

				>STYA pDLGS

				>SS
				>PUSHW pDLGS
				>PUSHBI 50				X1
				>PUSHBI 7				Y1
				>PUSHBI	C.CR
				>PUSHBI EV.OK
				>PUSHW L.BUT.OK
				>LIBCALL LIBTUI,NewBut
				>SR
				bcs .9

				>STYA ZPPtr1

				>SS
				>PUSHW pDLGS
				>PUSHBI 57				X1
				>PUSHBI 7				Y1
				>PUSHBI	3				Ctrl-C
				>PUSHBI EV.CANCEL
				>PUSHW L.BUT.Cancel
				>LIBCALL LIBTUI,NewBut
				>SR
.9				bcs CS.RUN.DLGSAVE.E

				>SS
				>PUSHW pDLGS
				>LIBCALL LIBTUI,Draw
				>SR
				bcs CS.RUN.DLGSAVE.E

				>SS
				>PUSHW ZPPtr1
				>LIBCALL LIBTUI,Activate
				>SR

				>SS
				>PUSHW pDLGS
				>LIBCALL LIBTUI,Exec
				>SR

CS.RUN.DLGSAVE.E
				php
				pha
				>SS
				>PUSHW pDLGS
				>LIBCALL LIBTUI,Free
				>SR

				>SS
				>PUSHW pSCRN
				>LIBCALL LIBTUI,Draw
				>SR

				pla
				plp
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			>LDYA.G TBOX1.pBuf

				cmp #0
				beq .1

				>LIBC Free

.1				>LDYA pCTX
				beq .2

				>SS
				>PUSHYA
				>LIBCALL LIBTUI,Close
				>SR

.2				>LDYA LIBTUI
				beq .8

				>KAPI UnloadLib

.8				clc
				rts
*--------------------------------------
CS.SCRN.Draw	>SS
				>PUSHEA.G SCRN.TitleBuf
				>PUSHW L.SCRN.Title

				ldy #S.OBJ.X1

.1				lda (pSCRN),y
				>PUSHA
				iny
				cpy #S.OBJ.InnerH+1
				bne .1

				>PUSHBI 10
				>LIBC SPrintF
				>SR
				bcs .9

				>SS
				>PUSHW pSCRN
				>LIBCALL LIBTUI,Draw
				>SR

.9				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
LIBTUI			.AZ "libtui"
*--------------------------------------
SCRN.Title		.AZ "Window: X1=%d,Y1=%d, W=%d,H=%d, X2=%d,Y2=%d, iX=%d,iY=%d, iW=%d,iH=%d"
SCRN.Status		.AZ "Status bar..."
*--------------------------------------
TBOX1.Title		.AZ "Text Box Title"
TBOX1.Status	.AZ "Text Box Status"
*--------------------------------------
LBOX1.List		.CZ "Item 1"
				.CZ "Item 2"
				.CZ "Item 3"
				.CZ "Item 4"
				.CZ "Item 5"
				.CZ "Item 6 very long line"
				.CZ "Item 7"
				.CZ "Item 8"
				.CZ "Item 9"
				.CZ "Item 10"
				.CZ "Item 11"
				.CZ "Item 12"
				.CZ "Item 13"
				.CZ "Item 14 last one"
				.HS 00
*--------------------------------------
LABEL.Label		.AZ "This is a label."
*--------------------------------------
TLINE.Default	.AZ "This is a Line."
*--------------------------------------
RADIO1.List		.CZ "\e[91mRed"
				.CZ "\e[92mGreen"
				.CZ "\e[96mBlue"
				.HS 00
*--------------------------------------
CBOX1.Label		.AZ "Check Me"
*--------------------------------------
BUTS.Label		.AZ "(^S)ave..."
BUTQ.Label		.AZ "(^Q)uit"
DLGS.Title		.AZ "Save As..."
BUT.OK			.AZ " OK "
BUT.Cancel		.AZ "Cancel"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
SCRN.TitleBuf	.BS 80
CBOX1.Var		.BS 1
TLINE.Buf		.BS 65
RADIO1.Var		.BS 1
LBOX1.Var		.BS 2
TBOX1.pBuf		.BS 2
DS.END			.ED
*--------------------------------------
MAN
SAVE root/asmtest/testtui.s
ASM
