NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
*				.TF root/asmtest/testtbuf
				.TF bin/vi
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pCmd			.BS 2
pLine			.BS 2
pTBuf			.BS 2
pFILE			.BS 2

LineNum			.BS 2
LineLen			.BS 2
ScreenX			.BS 2
ScreenY			.BS 2

PosX			.BS 2
PosY			.BS 2

CurX			.BS 1
CurY			.BS 1

ScreenH			.BS 1

Mode			.BS 1
Prompt			.BS 1

bNumber			.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
J.MODE			.DA CS.RUN.CMD
				.DA CS.RUN.INS
				.DA CS.RUN.EX
*--------------------------------------
J.CMD			.DA CS.RUN.CMD.2INS		i
				.DA CS.RUN.CMD.2INS		I
				.DA CS.RUN.CMD.2INS		a
				.DA CS.RUN.CMD.2INS		A
				.DA CS.RUN.CMD.2INS		o
				.DA CS.RUN.CMD.2INS		O
				.DA CS.RUN.CMD.2EX		:
                .DA CS.RUN.CMD.2EX		/
                .DA CS.RUN.CMD.2EX		?
                .DA CS.RUN.CMD.LEFT		h
                .DA CS.RUN.CMD.RIGHT	j
                .DA CS.RUN.CMD.UP		k
                .DA CS.RUN.CMD.DOWN		l
                .DA CS.RUN.CMD.LEFT
                .DA CS.RUN.CMD.RIGHT
                .DA CS.RUN.CMD.UP
                .DA CS.RUN.CMD.DOWN
*--------------------------------------
L.MSG.GOTOXY	.DA MSG.GOTOXY
L.MSG.LINE		.DA MSG.LINE
L.MSG.NUMLINE	.DA MSG.NUMLINE
L.MSG.LINE0		.DA MSG.LINE0
L.MSG.NUMLINE0  .DA MSG.NUMLINE0
L.MSG.CEOL		.DA MSG.CEOL
L.MSG.BLANKLINE	.DA MSG.BLANKLINE
L.MSG.CURPOS	.DA MSG.CURPOS
L.MSG.PROMPT	.DA MSG.PROMPT
L.MSG.INSERT	.DA MSG.INSERT
*--------------------------------------
L.SEQ.SCROLLUP	.DA SEQ.SCROLLUP
L.SEQ.SCROLLDN	.DA SEQ.SCROLLDN
*--------------------------------------
L.X.VT.SeqInit	.DA X.VT.SeqInit
L.X.VT.SeqReset	.DA X.VT.SeqReset
L.X.VT.SeqBS	.DA X.VT.SeqBS
L.X.VT.SeqFS    .DA X.VT.SeqFS

				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #1
				>KAPI ArgV
				bcs .9

				jsr CS.RUN.OPEN
				bcs .99



				bcc CS.RUN0

.9				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN0			jsr X.VT.Init
				bcs .99

				>LDA.G X.VT.ScreenH
				dec
				sta ScreenH

				>KAPI TBufNew
				bcs .99

				>STYA pTBuf

				>LDYAI 256
				>LIBC Malloc
				bcs .99

				>STYA pCmd

				>LDYAI 256
				>LIBC Malloc
				bcs .99

				>STYA pLine

.1				jsr CS.RUN.READ
				bcs	.3

				>SS
				>PUSHW pTBuf
				>PUSHW pLine
				>KAPI TBufAddL
				>SR
				bcc .1

.99				rts

.3				cmp #MLI.E.EOF
				bne .98

				jsr CS.RUN.CLOSE

				jsr CS.RUN.DRAWALL
				bcs .98

				jsr CS.RUN.LOOP
				bcs .98

.8				lda #0					Exit with no Error

.98				sec

				rts
*--------------------------------------
CS.RUN.LOOP		jsr CS.RUN.GetCurLine
				bcs .99

.1				jsr CS.RUN.SetCurPos
				bcs .99

.2				jsr .80
				bcs .99

				bvs .1

				bcc .2

.99				rts

.80				ldx Mode
				jmp (J.MODE,x)
*--------------------------------------
CS.RUN.CMD		jsr X.VT.GetCh
				bcs .99

				ldx #CMDs.L-1

.1				cmp CMDs,x
				beq .2

				dex
				bpl .1

				clc
				rts

.2				pha
				txa
				asl
				tax
				pla

				jmp (J.CMD,x)

.99				rts
*--------------------------------------
CS.RUN.CMD.2INS	sta Prompt

				ldx #2
				stx Mode

				>SS
				>PUSHW L.MSG.PROMPT
				lda ScreenH
				inc
				>PUSHA
				>PUSHW L.MSG.INSERT
				>PUSHBI 3
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.CMD.2EX	sta Prompt

				ldx #4
				stx Mode

				sta (pCmd)

				ldy #1
				lda #0
				sta (pCmd),y

				>SS
				>PUSHW L.MSG.PROMPT
				lda ScreenH
				inc
				>PUSHA
				>PUSHW pCmd
				>PUSHBI 3
				>LIBC PrintF
				>SR
				clv
				rts
*--------------------------------------
CS.RUN.CMD.LEFT	lda PosX
				ora PosX+1
				beq .8

				lda CurX
				beq .1

				dec
				sta CurX
				lda #C.BS
				>LIBC PutChar
				clv
				rts

.1				lda ScreenX
				ora ScreenX+1
				beq .8

				sec
				lda ScreenX
				sbc #1
				sta ScreenX
				lda ScreenX+1
				sbc #0
				sta ScreenX+1

				jsr CS.RUN.DRAWALL
				bit .99					set V
				rts

.8				clv
				clc
.99				rts
*--------------------------------------
CS.RUN.CMD.RIGHT
				lda PosX
				cmp LineLen
				lda PosX+1
				sbc LineLen+1
				bcs .8

				lda CurX
				>CMP.G X.VT.ScreenW
				bcs .1

				inc
				sta CurX

				jsr X.VT.FS
				clv
				rts

.1				>INCW ScreenX
				jsr CS.RUN.DRAWALL
				bit .99					set V
				rts

.8				clv
				clc
.99				rts
*--------------------------------------
CS.RUN.CMD.DOWN	>SS
				>PUSHW pTBuf
				>LDYA LineNum
				iny
				bne .1

				inc

.1				>PUSHYA

				>PUSHW pLine
				>KAPI TBufGetL
				>SR

				bcs .8

				>STYA LineLen

				>INCW LineNum

				lda CurY
				inc
				cmp ScreenH
				bcs .3

				sta CurY






				lda #C.LF
				>LIBC PutChar
				bit .99					set V
				rts

.3				>INCW ScreenY

				>SS
				>PUSHW L.SEQ.SCROLLUP
				>PUSHBI 1
				lda ScreenH
				>PUSHA
				>PUSHBI 2
				>LIBC Printf
				>SR
				bcs .99

				lda ScreenH
				sta R4
				jsr CS.RUN.DRAWLINE

				bit .99					set V
				rts

.8				clv
				clc
.99				rts
*--------------------------------------
CS.RUN.CMD.UP	lda LineNum
				ora LineNum+1
				beq .8

				sec
				lda LineNum
				sbc #1
				sta LineNum
				lda LineNum+1
				sbc #0
				sta LineNum+1

				>SS
				>PUSHW pTBuf
				>PUSHW LineNum
				>PUSHW pLine
				>KAPI TBufGetL
				>SR
				bcs *

				lda CurY
				beq .1

				dec CurY






				lda #C.VT
				>LIBC PutChar
				bit .99					set V
				rts

.8				clv
				clc
.99				rts

.1				lda ScreenY
				ora ScreenY+1
				beq *

				sec
				lda ScreenY
				sbc #1
				sta ScreenY
				lda ScreenY+1
				sbc #0
				sta ScreenY+1

				>SS
				>PUSHW L.SEQ.SCROLLDN
				>PUSHBI 1
				lda ScreenH
				>PUSHA
				>PUSHBI 2
				>LIBC Printf
				>SR
				bcs .99

				lda #1
				sta R4
				jsr CS.RUN.DRAWLINE

				bit .99					set V
				rts

*--------------------------------------
CS.RUN.INS		

.1				jsr X.VT.GetCh
				bcs .99

				cmp #3
				beq .8

				bra .1
				
.8				jmp CS.RUN.2CMD	

.99				rts
*--------------------------------------
CS.RUN.EX		lda #0
				sta (pCmd)
				
				sta R3					CL Ptr
				sta R3+1				CL Len

.1				jsr X.VT.GetCh
				bcs .99

				cmp #C.SPACE
				bcc .
				
				cmp #C.DEL
				beq .
				
				ldy R3+1
				iny
				beq .1
				
				sty R3+1
				
				ldy R3


				bra .1
				
.8				jmp CS.RUN.2CMD

.99				rts
*--------------------------------------
CS.RUN.2CMD		stz Mode

				lda #0
				sta (pCmd)

				>SS
				>PUSHW L.MSG.PROMPT
				lda ScreenH
				inc
				>PUSHA
				>PUSHW pCmd
				>PUSHBI 3
				>LIBC PrintF
				>SR
				bit .99
				
.99				rts
*--------------------------------------
CS.RUN.OPEN		>SS
				>PUSHYA
				>PUSHBI	O.RDONLY+O.TEXT
				>PUSHBI S.FI.T.TXT
				>PUSHWZ				Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pFILE

.9				rts
*--------------------------------------
CS.RUN.READ		>SS
				>PUSHW pLine
				>PUSHWI	256
				>PUSHW pFILE
				>LIBC FGetS
				>SR
				rts
*--------------------------------------
CS.RUN.CLOSE	>LDYA pFILE
				beq .1

				stz pFILE+1

				>LIBC FClose

.1				clc
				rts
*--------------------------------------
CS.RUN.DRAWALL	>LDYA ScreenY
				>STYA LineNum

				stz R4

				lda ScreenH
				sta R4+1

.1				>SS
				>PUSHW pTBuf
				>PUSHW LineNum
				>PUSHW pLine
				>KAPI TBufGetL
				>SR
				bcs .2

				>STYA LineLen

				inc R4

				jsr CS.RUN.DRAWLINE
				bcs .99

				>INCW LineNum
				dec R4+1
				bne .1

				jmp CS.RUN.GetCurLine

.2				inc R4

				jsr CS.RUN.DRAWLINE0

				>INCW LineNum
				dec R4+1
				bne .2

				jmp CS.RUN.GetCurLine

.99				rts
*--------------------------------------
CS.RUN.SetCurPos
				clc
				lda ScreenX
				adc CurX
				sta PosX
				lda ScreenX+1
				adc #0
				sta PosX+1

				clc
				lda ScreenY
				adc CurY
				sta PosY
				lda ScreenY+1
				adc #0
				sta PosY+1
				
				>SS
				>PUSHW L.MSG.CURPOS
				lda ScreenH
				inc
				>PUSHA
				>LDA.G X.VT.ScreenW
				sec
				sbc #16
				>PUSHA
				>PUSHW PosX
				>PUSHW PosY
				>PUSHBI 6
				>LIBC PrintF
				>SR
*--------------------------------------
CS.RUN.GOTOXY	>SS
				>PUSHW L.MSG.GOTOXY
				lda CurY
				inc
				>PUSHA
				lda CurX
				inc
				>PUSHA
				>PUSHBI 2
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.DRAWLINE	sec
				lda LineLen
				sbc ScreenX
				sta R3

				lda LineLen+1
				sbc ScreenX+1
				sta R3+1
				bcc .10

				clc
				lda pLine
				adc ScreenX
				sta R3

				lda pLine+1
				adc ScreenX+1
				sta R3+1
				bra .11

.10				>LDYA L.MSG.BLANKLINE
				>STYA R3

.11				bit bNumber
				bmi .1

				>SS
				>PUSHW L.MSG.LINE
				>PUSHB R4
				>PUSHW R3
				>PUSHBI 3
				>LIBC PrintF
				>SR
				rts

.1				>SS
				>PUSHW L.MSG.NUMLINE
				>PUSHB R4
				>PUSHW LineNum
				>PUSHW R3
				>PUSHBI 5
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.DRAWLINE0
				bit bNumber
				bmi .1

				>SS
				>PUSHW L.MSG.LINE0
				>PUSHB R4
				>PUSHBI 1
				>LIBC PrintF
				>SR
				rts

.1				>SS
				>PUSHW L.MSG.NUMLINE0
				>PUSHB R4
				>PUSHW LineNum
				>PUSHBI 3
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.GetCurLine
				lda ScreenY
				clc
				adc CurY
				sta LineNum
				lda ScreenY+1
				adc #0
				sta LineNum+1

				>SS
				>PUSHW pTBuf
				>PUSHW LineNum
				>PUSHW pLine
				>KAPI TBufGetL
				>SR
				bcs .99

				>STYA LineLen

.99				rts
*--------------------------------------
CS.QUIT			jsr CS.RUN.CLOSE

.1				>LDYA pLine
				beq .2

				>LIBC Free

.2				>LDYA pCmd
				beq .3

				>LIBC Free

.3				>LDYA pTBuf
				beq .8

				>KAPI TBufFree

.8				clc
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
				.INB usr/src/shared/x.vt.cs
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.CS "Usage : VI filename\r\n"
				.CS "   -C command line\r\n"
				.CS "   -R : Readonly mode\r\n"
MSG.CRLF		.CZ "\r\n"
*--------------------------------------
MSG.GOTOXY		.CZ "\e[%d;%dH"
MSG.LINE		.CZ "\e[%d;1H%s\e[K"		+CEOL
MSG.NUMLINE		.CZ "\e[%d;1H%5D %s\e[K"	+CEOL
MSG.LINE0		.CZ "\e[%d;1H~\e[K"			+CEOL
MSG.NUMLINE0	.CS "\e[%d;1H%5D ~"
MSG.CEOL		.CS "\e[K"					+CEOL
MSG.BLANKLINE	.DA #0
MSG.CURPOS		.CZ "\e7\e[%d;%dH%5D,%5D\e8"
MSG.PROMPT		.CZ "\e[%d;1H%s\e[K"
MSG.INSERT		.CZ "--- INSERT ---"
*--------------------------------------
SEQ.SCROLLUP	.CS "\e[%d;%dr"
				.CZ "\eD"
SEQ.SCROLLDN	.CS "\e[%d;%dr"
				.CZ "\eM"
*--------------------------------------
CMDs			.AS "iIaAoO:/?hjkl"
				.DA #C.BS,#21,#C.VT,#C.LF
CMDs.L			.EQ *-CMDs
*--------------------------------------
				.INB usr/src/shared/x.vt.id
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
				.INB usr/src/shared/x.vt.ds
DS.END			.ED
*--------------------------------------
MAN
SAVE root/asmtest/testtbuf.s
ASM
