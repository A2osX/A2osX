NEW
  AUTO 3,1
*--------------------------------------
X.TrkFmt.VolNum		.EQ X.TrkFmt.Z
X.TrkFmt.TrkNum		.EQ X.TrkFmt.Z+1
X.TrkFmt.SectNum	.EQ X.TrkFmt.Z+2
X.TrkFmt.CheckSum	.EQ X.TrkFmt.Z+3
*--------------------------------------
X.TrkFmt		lda #$ff   				(2)

				sta IO.D2.WriteMode,x	(5) goto write mode
				ora IO.D2.WShift,x		(4)

				jsr X.TrkFmt.W14		(14)

				lda #0					(2)
				sta X.TrkFmt.SectNum	(3)

				ldy #0					(2) 256 SyncBytes

				jsr X.TrkFmt.SyncBytes	(15..15)

				nop						(2)
				nop						(2)
				nop						(2)
				nop						(2)

.1				jsr X.TrkFmt.AddrField	(17..15)

				ldy #6					(2)

				jsr X.TrkFmt.SyncBytes	(15..15)

				jsr X.TrkFmt.W14		(14)

				ldy #2					(2)

.2				lda X.TrkFmt.DATA,y		(4)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)
				dey						(2)
				bmi .22					(3-)

				jsr X.TrkFmt.W12		(12)
				bra .2					(3)

.22				jsr X.TrkFmt.W14		(14)

				ldy #86					(2) 256+86...
				lda #$96				(2)

				sta IO.D2.WLoad,x		(5) ...+1
				ora IO.D2.WShift,x		(4)

				nop						(2)
				bra .3					(3)

.3				jsr X.TrkFmt.W14		(14)

				nop						(2)

				lda #$96				(2)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)

				dey						(2)
				bne .3					(3)-

				bra .5					(3)

.4				nop						(2)

.5				jsr X.TrkFmt.W14		(14)

				lda #$96				(2)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)

				dey						(2)
				bne .4					(3)-

				inc X.TrkFmt.SectNum	(5)

				jsr X.TrkFmt.EndMark	(19..15)

				ldy #16					(2)

				jsr X.TrkFmt.SyncBytes	(15..15)

				lda X.TrkFmt.SectNum	(3)
				cmp #16					(2)
				bne .1					(3-)

				lda IO.D2.ReadMode,x	(4) out of write mode
				lda IO.D2.WShift,x		(4) to read mode.

				rts
*--------------------------------------
X.TrkFmt.SyncBytes	
				nop						(2)

				lda #$ff				(2)
			
.1				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)
				dey						(2)
				beq .8					(3-)

				jsr X.TrkFmt.W12		(12)
				jsr X.TrkFmt.W12		(12)

				bra .1					(3)

.8				rts						(6)
*--------------------------------------
X.TrkFmt.AddrField
				ldy #2					(2)

.1				lda X.TrkFmt.ADDR,y		(4)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)
				dey						(2)
				bmi .2					(3-)

				lda X.TrkFmt.VolNum		(3)
				eor X.TrkFmt.TrkNum		(3)
				eor X.TrkFmt.SectNum	(3)
				sta X.TrkFmt.Checksum	(3)
				bra .1					(3)

.2				lda X.TrkFmt.VolNum		(3)
				lda X.TrkFmt.VolNum		(3)
				jsr X.TrkFmt.W4x4		(17..12)

				lda X.TrkFmt.TrkNum		(3)
				jsr X.TrkFmt.W4x4		(17..12)

				lda X.TrkFmt.SectNum	(3)
				jsr X.TrkFmt.W4x4		(17..12)

				lda X.TrkFmt.Checksum	(3)
				jsr X.TrkFmt.W4x4		(17..12)

				pha						(3)
				pla						(4)
*--------------------------------------
X.TrkFmt.EndMark
				ldy #2					(2)

				nop						(2)

.1				lda X.TrkFmt.END,y		(4)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)
				dey						(2)
				bmi .8					(3-)

				jsr X.TrkFmt.W12		(12)
				bra .1					(3)

.8				rts						(6)
*--------------------------------------
X.TrkFmt.W4x4	tay						(2)
				lsr						(2)
				ora #$AA				(2)
				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)

				jsr X.TrkFmt.W12		(12)

				pha						(3)
				pla						(4)

				tya						(2)
				ora #$AA				(2)

				sta IO.D2.WLoad,x		(5)
				ora IO.D2.WShift,x		(4)

X.TrkFmt.W14	nop						(2)

X.TrkFmt.W12	rts						(6)
*--------------------------------------
				.LIST ON
X.TrkFmt.ADDR	.HS 96AAD5
X.TrkFmt.DATA	.HS ADAAD5
X.TrkFmt.END	.HS EBAADE
X.TrkFmt.Size 	.EQ *-X.TrkFmt
				.LIST OFF
*--------------------------------------
MAN
SAVE usr/src/shared/x.trkfmt.s
LOAD usr/src/lib/libblkdev.s
ASM
