NEW
  AUTO 3,1
*--------------------------------------
X.TRKREAD.VolNum	.EQ X.TRKREAD.Z
X.TRKREAD.SectNum	.EQ X.TRKREAD.Z+1
X.TRKREAD.TrkNum	.EQ X.TRKREAD.Z+3
X.TRKREAD.CheckSum	.EQ X.TRKREAD.Z+3
X.TRKREAD.Cnt		.EQ X.TRKREAD.Z+4
X.TRKREAD.SectCnt	.EQ X.TRKREAD.Z+6
X.TRKREAD.TmpByte	.EQ X.TRKREAD.Z+7
*--------------------------------------
* ZPPtr1 = DstSectBuf (4096)
* ZPPtr2 = DstOrderBuf (16)
* X = SlotN0
*--------------------------------------
X.TRKREAD		stz X.TRKREAD.SectCnt

.1				jsr X.TRKREAD.A
				bne .9

				lda X.TRKREAD.SectNum
				ldy X.TRKREAD.SectCnt
				sta (ZPPtr2),y

				jsr X.TRKREAD.D
				bcs .9

				inc ZPPtr1+1

				inc X.TRKREAD.SectCnt
				lda X.TRKREAD.SectCnt
				cmp #16
				bcc .1

				lda X.TRKREAD.VolNum
				clc
				rts

.9				lda #MLI.E.IO
				sec
				rts
*--------------------------------------
X.TRKREAD.A		stz X.TRKREAD.Cnt

				lda #$F8				read up to 8192 nibbles
				sta X.TRKREAD.Cnt+1

.1				inc X.TRKREAD.Cnt
				bne .2

				inc X.TRKREAD.Cnt+1
				beq .9

.2				lda IO.D2.RData,x
				bpl .1

.3				cmp #$D5
				bne .1

				nop

.4	 			lda IO.D2.RData,x
				bpl .4

				cmp #$AA
				bne .3

				ldy #3

.5  			lda IO.D2.RData,x
				bpl .5

				cmp #$96
				bne .3

				lda #0

.6				sta X.TRKREAD.CheckSum

.7  			lda IO.D2.RData,x
				bpl .7

				rol
				sta X.TRKREAD.TmpByte

.8  			lda IO.D2.RData,x
				bpl .8

				and X.TRKREAD.TmpByte
				sta X.TRKREAD.VolNum,y
				eor X.TRKREAD.CheckSum
				dey
				bpl .6

				tay
				bne .9
				
				clc
				rts

.9				sec
				rts
*--------------------------------------
X.TRKREAD.D		txa
				ora #$8C
				sta rd4+1
				sta rd5+1
				sta rd6+1
				sta rd7+1
				sta rd8+1

				lda ZPPtr1
				ldy ZPPtr1+1

				sta ref3+1
				sty ref3+2
				sec
				sbc #$54
				bcs L571F

				dey

L571F  			sta ref2+1
				sty ref2+2
				sec
				sbc #$57
				bcs L572B

				dey

L572B  			sta ref1+1
				sty ref1+2

				ldy #$20

L5733  			dey
				beq L576D

L5736  			lda IO.D2.RData,x
				bpl L5736

L573B  			eor #$D5
				bne L5733

				nop

L5740  			lda IO.D2.RData,x
				bpl L5740

				cmp #$AA
				bne L573B

				nop

L574A  			lda IO.D2.RData,x
				bpl L574A

				cmp #$AD
				bne L573B
*--------------------------------------
				ldy #$AA
				lda #$00

L5757  			sta X.TRKREAD.TmpByte

rd4				ldx IO.D2.RData+$60		SELF MODIFIED
				bpl rd4

				lda X.Nib2FC-$96,x
				sta X.Buf86-$AA,y
				eor X.TRKREAD.TmpByte
				iny
				bne L5757

				ldy #$AA
				bne rd5

L576D			sec
				rts
*--------------------------------------
ref1			sta $1000,y				SELF MODIFIED

rd5				ldx IO.D2.RData+$60		SELF MODIFIED
				bpl rd5

				eor X.Nib2FC-$96,x
				ldx X.Buf86-$AA,y
				eor X.dnibl2,x
				iny
				bne ref1

				pha
				and #$FC
				ldy #$AA

rd6				ldx IO.D2.RData+$60		SELF MODIFIED
				bpl rd6

				eor X.Nib2FC-$96,x
				ldx X.Buf86-$AA,y
				eor X.dnibl3,x
ref2			sta $1000,y				SELF MODIFIED
				iny
				bne rd6

rd7				ldx IO.D2.RData+$60		SELF MODIFIED
				bpl rd7

				and #$FC
				ldy #$AC

L57A5  			eor X.Nib2FC-$96,x
				ldx X.Buf86-$AC,y
				eor X.dnibl4,x
ref3			sta $1000,y				SELF MODIFIED

rd8				ldx IO.D2.RData+$60		SELF MODIFIED
				bpl rd8

				iny
				bne L57A5

				and #$FC
				eor X.Nib2FC-$96,x

				clc
				beq L57CD

				sec

L57CD  			pla
				ldy #$55
				sta (ZPPtr1),y

				rts
*--------------------------------------
X.Nib2FC		.HS 0004
				.HS FFFF
				.HS 080C
				.HS FF
				.HS 101418
X.XX000000		.HS 008040C0
				.HS FFFF
				.HS 1C20
				.HS FFFFFF
				.HS 24282C3034
				.HS FFFF
				.HS 383C4044484C
				.HS FF
				.HS 5054585C606468
X.00XX0000		.HS 00201030
				.HS FFFFFFFF
				.HS 6C
				.HS 00
				.HS 707478
				.HS FFFFFF
				.HS 7C
				.HS FFFF
				.HS 8084
				.HS FF
				.HS 888C9094989CA0
X.0000XX00		.HS 0008040C
				.HS FF
				.HS A4A8AC
				.HS FF
				.HS B0B4B8BCC0C4C8
				.HS FFFF
				.HS CCD0D4D8DCE0
				.HS 00
				.HS E4E8ECF0F4F8FC
*--------------------------------------
X.dnibl2		.HS 00
X.dnibl3		.HS 00
X.dnibl4		.HS 00
X.FC2Nib		.HS 96
				.HS 02000097
				.HS 0100009A
				.HS 0300009B
				.HS 0002009D
				.HS 0202009E
				.HS 0102009F
				.HS 030200A6
				.HS 000100A7
				.HS 020100AB
				.HS 010100AC
				.HS 030100AD
				.HS 000300AE
				.HS 020300AF
				.HS 010300B2
				.HS 030300B3
				.HS 000002B4
				.HS 020002B5
				.HS 010002B6
				.HS 030002B7
				.HS 000202B9
				.HS 020202BA
				.HS 010202BB
				.HS 030202BC
				.HS 000102BD
				.HS 020102BE
				.HS 010102BF
				.HS 030102CB
				.HS 000302CD
				.HS 020302CE
				.HS 010302CF
				.HS 030302D3
				.HS 000001D6
				.HS 020001D7
				.HS 010001D9
				.HS 030001DA
				.HS 000201DB
				.HS 020201DC
				.HS 010201DD
				.HS 030201DE
				.HS 000101DF
				.HS 020101E5
				.HS 010101E6
				.HS 030101E7
				.HS 000301E9
				.HS 020301EA
				.HS 010301EB
				.HS 030301EC
				.HS 000003ED
				.HS 020003EE
				.HS 010003EF
				.HS 030003F2
				.HS 000203F3
				.HS 020203F4
				.HS 010203F5
				.HS 030203F6
				.HS 000103F7
				.HS 020103F9
				.HS 010103FA
				.HS 030103FB
				.HS 000303FC
				.HS 020303FD
				.HS 010303FE
				.HS 030303FF
*--------------------------------------
X.Buf86			.BS 86
*--------------------------------------
				.LIST ON
X.TRKREAD.L		.EQ *-X.TRKREAD
				.LIST OFF
*--------------------------------------
MAN
SAVE usr/src/shared/x.trkread.s
LOAD usr/src/lib/libblkdev.s
ASM
