NEW
  AUTO 3,1
*--------------------------------------
X.VT.Init		>PUSHW L.X.VT.SeqInit
				>PUSHBI 0
				>LIBC PrintF			Send Query for term W & H
				bcs .9

.1				jsr X.VT.GetCh
				bcs .9

				>LDA.G X.VT.ScreenW		Wait for Response from terminal for W & H
				beq .1

*				clc

.9				rts
*--------------------------------------
X.VT.GetCh		>LIBC GetChar
				bcs .9

				cmp #C.CR
				bne .55

				ldy #S.PS.pStdIn		Check for any extra LF
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				>LIBC FEOF
				bcs .9

				tay
				bne .8

				>LIBC GetChar
				bcs .9

.8				lda #C.CR

*				clc

.9				rts
*--------------------------------------
.55				tax

				>LDA.G X.VT.bEscMode
				bmi .1

				cpx #C.ESC
				bne .80

				lda #$ff
				>STA.G X.VT.bEscMode

				inc
				clc
				rts

.1				>LDA.G X.VT.bCSIMode
				bmi .11

				cpx #'['				\e[ ?
				bne X.VT.GetCh.Esc

				lda #$ff
				>STA.G X.VT.bCSIMode

				inc

				>STA.G X.VT.InBuf		Reset Buffer

				clc
				rts
*--------------------------------------
.11				>LDA.G X.VT.InBuf
				cmp #15
				bcc .10

.19				>STZ.G X.VT.bEscMode
				>STA.G X.VT.bCSIMode

				clc
				rts

.10				inc
				sta (pData),y
				clc
				adc #X.VT.InBuf
				tay

				txa
				sta (pData),y
				cmp #64

				bcs X.VT.GetCh.CSI

				ldx #0

.80				txa
				clc
				rts
*--------------------------------------
X.VT.GetCh.Esc	>STZ.G X.VT.bEscMode

X.VT.GetCh.Esc1	txa

				ldx #X.VT.EscIn.L-1

.1				cmp X.VT.EscIn,x
				beq .2

				dex
				bpl .1

				lda #0
				clc
				rts

.2				lda X.VT.AsciiOut,x
				clc
				rts
*--------------------------------------
* \e[A			UP
* \e[B			DOWN
* \e[C			FOWARD
* \e[D			BACK
* \e[xxx;yyyR	DSR ("\e[I6n" reply)
*--------------------------------------
* \e[1~    		Home
* \e[2~    		Insert
* \e[3~    		Delete
* \e[4~    		End
* \e[5~    		PgUp
* \e[6~    		PgDn
*--------------------------------------
X.VT.GetCh.CSI	>STZ.G X.VT.bEscMode
				>STA.G X.VT.bCSIMode

				cpx #'R'				Response to cursor position query?
				bne X.VT.GetCh.Esc1

				ldy #X.VT.InBuf

				stz R4

.4				iny

				lda (pData),y
				cmp #';'
				beq .5

				jsr .88
				bra .4

.5				phy

				lda R4
				>STA.G X.VT.ScreenH

				ply

				stz R4

.6				iny

				lda (pData),y
				cmp #'R'
				beq .7

				jsr .88
				bra .6

.7				lda R4
				>STA.G X.VT.ScreenW

.80				lda #0
				clc
				rts
*--------------------------------------
.88				and #$0f
				pha
				lda R4
				asl
				asl
				clc
				adc R4
				asl
				sta R4
				pla
				clc
				adc R4
				sta R4
				rts
*--------------------------------------
X.VT.BS			>SS
				>PUSHW L.X.VT.SeqBS
				>PUSHBI 0
				>LIBC Printf
				>SR
				rts
*--------------------------------------
X.VT.FS			>SS
				>PUSHW L.X.VT.SeqFS
				>PUSHBI 0
				>LIBC Printf
				>SR
				rts
*--------------------------------------
MAN
SAVE usr/src/shared/x.vt.cs
LOAD root/asmtest/testtbuf.s
ASM
