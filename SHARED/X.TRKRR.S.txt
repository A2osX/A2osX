NEW
  AUTO 3,1
*--------------------------------------
* In:
*  X = Slotn0
*  ZPPtr1 = pDstBuf
* Uses:
*  ZPPtr2 = Cnt
*--------------------------------------
X.TrkR.CheckSum		.EQ X.TrkFmt.Z
X.TrkR.SectNum		.EQ X.TrkFmt.Z+1
X.TrkR.TrkNum		.EQ X.TrkFmt.Z+2
X.TrkR.VolNum		.EQ X.TrkFmt.Z+3
X.TrkR.TmpByte		.EQ X.TrkFmt.Z+4
*--------------------------------------
X.TrkR
*--------------------------------------
X.TrkR.A		stz ZPPtr2

				lda #$F8				read up to 8192 nibbles
				sta ZPPtr2+1

.1				inc ZPPtr2
				bne .2

				inc ZPPtr2+1
				beq .9

.2				lda IO.D2.RData,x
				bpl .1

.3				cmp #$D5
				bne .1

				nop

.4	 			lda IO.D2.RData,x
				bpl .4

				cmp #$AA
				bne .3

				ldy #3

.5  			lda IO.D2.RData,x
				bpl .5

				cmp #$96
				bne .3

				lda #0

.6				sta X.TrkR.CheckSum

.7  			lda IO.D2.RData,x
				bpl .7

				rol
				sta X.TrkR.TmpByte

.8  			lda IO.D2.RData,x
				bpl .8

				and X.TrkR.TmpByte
				sta X.TrkR.VolNum,y
				eor X.TrkR.CheckSum
				dey
				bpl .6

				tay
				bne .9
				
				clc
				rts

.9				sec
				rts
*--------------------------------------
				.LIST ON
X.TrkR.Size 	.EQ *-X.TrkR
				.LIST OFF
*--------------------------------------
MAN
SAVE usr/src/shared/x.trkr.s
LOAD usr/src/lib/libblkdev.s
ASM
