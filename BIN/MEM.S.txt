NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/mem
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
MemStat			.BS S.MSTAT
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.SIG
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.Total		.DA MSG.Total
L.MSG.ThisBank	.DA MSG.ThisBank
L.MSG.Kernel	.DA MSG.Kernel
L.MSG.KBuffers	.DA MSG.KBuffers
L.MSG.Stat		.DA MSG.Stat
L.MSG.End		.DA MSG.End
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYAI MemStat

				>KAPI GetMemStat
				bcs .9

				jsr CS.PrintTotal
				bcs .9

				>SLEEP

				jsr CS.PrintThisBank
				bcs .9

				>SLEEP

				jsr CS.PrintKernel
				bcs .9

				>SLEEP

				jsr CS.PrintKBuffers
				bcs .9

				>LDYA L.MSG.End
				>LIBC PutS

				lda #0
				sec
.9				rts
*--------------------------------------
CS.PrintTotal	>SS
				>PUSHW L.MSG.Total
				ldy MemStat+S.MSTAT.MaxBnk
				lda #0
				iny
				bne .1

				inc

.1				>PUSHYA

				>PUSHL MemStat+S.MSTAT.UT
				lsr MemStat+S.MSTAT.UT+2
				ror MemStat+S.MSTAT.UT+1
				lsr MemStat+S.MSTAT.UT+2
				ror MemStat+S.MSTAT.UT+1
				>PUSHW MemStat+S.MSTAT.UT+1
				
				>PUSHL MemStat+S.MSTAT.UU
				lsr MemStat+S.MSTAT.UU+2
				ror MemStat+S.MSTAT.UU+1
				lsr MemStat+S.MSTAT.UU+2
				ror MemStat+S.MSTAT.UU+1
				>PUSHW MemStat+S.MSTAT.UU+1
				
				>PUSHL MemStat+S.MSTAT.UF
				lsr MemStat+S.MSTAT.UF+2
				ror MemStat+S.MSTAT.UF+1
				lsr MemStat+S.MSTAT.UF+2
				ror MemStat+S.MSTAT.UF+1
				>PUSHW MemStat+S.MSTAT.UF+1
				
				>PUSHBI 20
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.PrintThisBank
				>SS
				>PUSHW L.MSG.ThisBank
				>PUSHB A2osX.ActBnk
				>PUSHBI 1
				>LIBC PrintF
				>SR

				>SS
				>PUSHW L.MSG.Stat
				>PUSHW MEM.LoMem
				>PUSHW MEM.LoMem
				>PUSHW MEM.Free
				>PUSHW MEM.Free
				>PUSHW MEM.HiMem
				>PUSHW MEM.HiMem

				lda MEM.Free
				sec
				sbc MEM.LoMem
				tay
				lda MEM.Free+1
				sbc MEM.LoMem+1
				>PUSHYA

				>PUSHB MEM.LastSlot
				>PUSHB MEM.LastSlot+1

				>PUSHBI 16
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.PrintKernel	>LDYA L.MSG.Kernel
				>LIBC PutS

				>SS
				>PUSHW L.MSG.Stat
				>PUSHW MemStat+S.MSTAT.KL
				>PUSHW MemStat+S.MSTAT.KL
				>PUSHW MemStat+S.MSTAT.KF
				>PUSHW MemStat+S.MSTAT.KF
				>PUSHW MemStat+S.MSTAT.KH
				>PUSHW MemStat+S.MSTAT.KH

				lda MemStat+S.MSTAT.KF
				sec
				sbc MemStat+S.MSTAT.KL
				tay
				lda MemStat+S.MSTAT.KF+1
				sbc MemStat+S.MSTAT.KL+1
				>PUSHYA

				>PUSHB MemStat+S.MSTAT.XLast
				>PUSHB MemStat+S.MSTAT.XLast+1

				>PUSHBI 16
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.PrintKBuffers
				>SS
				>PUSHW L.MSG.KBuffers

				>PUSHW MemStat+S.MSTAT.BT
				asl MemStat+S.MSTAT.BT
				rol MemStat+S.MSTAT.BT+1
				>PUSHW MemStat+S.MSTAT.BT

				>PUSHW MemStat+S.MSTAT.BU
				asl MemStat+S.MSTAT.BU
				rol MemStat+S.MSTAT.BU+1
				>PUSHW MemStat+S.MSTAT.BU

				>PUSHW MemStat+S.MSTAT.BF
				asl MemStat+S.MSTAT.BF
				rol MemStat+S.MSTAT.BF+1
				>PUSHW MemStat+S.MSTAT.BF

				>PUSHBI 12
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.Total		.CS "--- User memory (%03D banks) ----\r\n"
				.CS " Total       : %8u (%5Dk)\r\n"
				.CS " Used        : %8u (%5Dk)\r\n"
				.CZ " Free        : %8u (%5Dk)\r\n"
MSG.ThisBank	.CZ "--- This bank (%03d) ------------\r\n"
MSG.Kernel		.CZ "--- Kernel memory --------------"
MSG.Stat		.CS " Low              : %5D (%H)\r\n"
				.CS " Free Ptr         : %5D (%H)\r\n"
				.CS " High             : %5D (%H)\r\n"
				.CS " Available memory : %5D Bytes\r\n"
				.CZ " Handles          :    %3d / %3d\r\n"
MSG.KBuffers	.CS "--- Kernel buffers -------------\r\n"
				.CS " Total            : %4D (%4Dk)\r\n"
				.CS " Used             : %4D (%4Dk)\r\n"
				.CZ " Free             : %4D (%4Dk)\r\n"
MSG.End			.CZ "--------------------------------"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/mem.s
ASM
