NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/mem
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
MemStat			.BS S.MSTAT
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.DOEVENT
				.DA	CS.QUIT
L.MSG.Total		.DA MSG.Total
L.MSG.ThisBank	.DA MSG.ThisBank
L.MSG.Kernel	.DA MSG.Kernel
L.MSG.Stat		.DA MSG.Stat
L.MSG.End		.DA MSG.End
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYAI MemStat

				>KAPI GetMemStat
				bcs .9

				jsr CS.PrintTotal
				bcs .9

				>SLEEP

				jsr CS.PrintThisBank
				bcs .9

				>SLEEP

				jsr CS.PrintKernel
				bcs .9

				>LDYA L.MSG.End
				>LIBC PutS

				lda #0
				sec
.9				rts
*--------------------------------------
CS.PrintTotal	>SS
				>PUSHW L.MSG.Total
				ldy MemStat+S.MSTAT.MaxBnk
				lda #0
				iny
				bne .1

				inc

.1				>PUSHYA

				>PUSHL MemStat+S.MSTAT.UT
				>PUSHL MemStat+S.MSTAT.UT
				>PUSHL MemStat+S.MSTAT.UU
				>PUSHL MemStat+S.MSTAT.UU
				>PUSHL MemStat+S.MSTAT.UF
				>PUSHL MemStat+S.MSTAT.UF
				>PUSHBI 26
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.PrintThisBank
				>SS
				>PUSHW L.MSG.ThisBank
				>PUSHB Mem.BnkID
				>PUSHBI 1
				>LIBC PrintF
				>SR

				>SS
				>PUSHW L.MSG.Stat
				>PUSHW Mem.LoMem
				>PUSHW Mem.LoMem
				>PUSHW Mem.Free
				>PUSHW Mem.Free
				>PUSHW Mem.HiMem
				>PUSHW Mem.HiMem

				lda Mem.Free
				sec
				sbc Mem.LoMem
				tay
				lda Mem.Free+1
				sbc Mem.LoMem+1
				>PUSHYA
				>PUSHBI 14
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.PrintKernel	>LDYA L.MSG.Kernel
				>LIBC PutS

				>SS
				>PUSHW L.MSG.Stat
				>PUSHW MemStat+S.MSTAT.KL
				>PUSHW MemStat+S.MSTAT.KL
				>PUSHW MemStat+S.MSTAT.KF
				>PUSHW MemStat+S.MSTAT.KF
				>PUSHW MemStat+S.MSTAT.KH
				>PUSHW MemStat+S.MSTAT.KH

				lda MemStat+S.MSTAT.KF
				sec
				sbc MemStat+S.MSTAT.KL
				tay
				lda MemStat+S.MSTAT.KF+1
				sbc MemStat+S.MSTAT.KL+1
				>PUSHYA
				>PUSHBI 14
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.DOEVENT		sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.Total		.CS "--- User memory (%03D banks) ----\r\n"
				.CS " Total     : %8u (%H%H)\r\n"
				.CS " Used      : %8u (%H%H)\r\n"
				.CZ " Free      : %8u (%H%H)\r\n"
MSG.ThisBank	.CZ "--- This bank (%03d) ------------\r\n"
MSG.Kernel		.CZ "--- Kernel memory --------------"
MSG.Stat		.CS " Low              : %5D (%H)\r\n"
				.CS " Free Ptr         : %5D (%H)\r\n"
				.CS " High             : %5D (%H)\r\n"
				.CZ " Available memory : %5D Bytes\r\n"
MSG.End			.CZ "--------------------------------"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/mem.s
ASM
