NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/netstat
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pFD				.BS 2
hFD				.BS 2
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.SIG
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
L.MSG1.T		.DA MSG1.T.RAW
				.DA MSG1.T.DGRAM
				.DA MSG1.T.SEQPKT
				.DA MSG1.T.STREAM
				.DA MSG1.T.INV
L.MSG1.P		.DA MSG1.P.ICMP
				.DA MSG1.P.TCP
				.DA MSG1.P.UDP
				.DA MSG1.P.INV
L.MSG1.S		.DA MSG1.S.0
				.DA MSG1.S.1
				.DA MSG1.S.2
				.DA MSG1.S.3
				.DA MSG1.S.4
				.DA MSG1.S.5
				.DA MSG1.S.6
				.DA MSG1.S.7
				.DA MSG1.S.8
				.DA MSG1.S.9
				.DA MSG1.S.10
				.DA MSG1.S.11
				.DA MSG1.S.INV
L.SPRINTF.IPP	.DA SPRINTF.IPP

				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.MSG0
				>LIBC PutS

				lda #0

.1				sta hFD

				>KAPI GetFDByID
				bcs .7

				>STYA pFD

				lda (pFD)
				cmp #S.FD.T.DSOCK
				beq .2

				cmp #S.FD.T.SSOCK
				bne .6

.2				ldy #S.FD.SOCK.AF
				lda (pFD),y
				cmp #AF_INET
				bne .6

				jsr CS.RUN.ADDR

				jsr CS.RUN.Print

.6				>LDYA pFD
				>LIBC Free

.7				lda hFD
				inc
				inc
				cmp #K.FD.MAX*2
				bcc .1

				lda #0					tell TSKMGR that all done ok, but
*				sec						we do not want to stay in memory
.9				rts
*--------------------------------------
CS.RUN.Print	>SS
				>PUSHW L.MSG1

				jsr CS.RUN.TP

				>PUSHEA.G LocAddr
				>PUSHEA.G RemAddr

				ldy #S.FD.SOCK.O
				>PUSHB (pFD),y

				jsr CS.RUN.S			2 bytes
				>PUSHBI 11

				>LIBC PrintF
				>SR

				rts
*--------------------------------------
CS.RUN.TP		ldy #S.FD.SOCK.T
				lda (pFD),y
				tax
				cpx #SOCK_STREAM+1
				bcc .1

				ldx #SOCK_STREAM+1

.1				>PUSHW L.MSG1.T,x

				ldy #S.FD.SOCK.P
				lda (pFD),y

				ldx #0

.3				cmp PROTO,x
				beq .4

				inx
				cpx #PROTO.Cnt
				bne .3

.4				txa
				asl
				tax

				>PUSHW L.MSG1.P,x
				rts
*--------------------------------------
CS.RUN.ADDR		>SS
				>PUSHW hFD
				>PUSHEA.G SA
				>PUSHWI S.SA.IN
				>LIBC GetSockName
				>SR

				>SS
				>PUSHEA.G LocAddr
				jsr CS.RUN.PushSA
				>PUSHBI 6
				>LIBC SPrintF
				>SR

				>SS
				>PUSHW hFD
				>PUSHEA.G SA
				>PUSHWI S.SA.IN
				>LIBC GetPeerName
				>SR

				>SS
				>PUSHEA.G RemAddr
				jsr CS.RUN.PushSA
				>PUSHBI 6
				>LIBC SPrintF
				>SR
				rts
*--------------------------------------
CS.RUN.PushSA	>PUSHW L.SPRINTF.IPP
				>PUSHB.G SA+2
				>PUSHB.G SA+3
				>PUSHB.G SA+4
				>PUSHB.G SA+5
				>PUSHW.G SA+6
				rts
*--------------------------------------
CS.RUN.S		ldy #S.FD.SOCK.S
				lda (pFD),y

				cmp #S.SKT.IN.S.TIMEWT+1
				bcc .1

				lda #S.SKT.IN.S.TIMEWT+1

.1				asl
				tax

				>PUSHW L.MSG1.S,x
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG0			.AZ "Type   Prot Local                 Remote                Options  Status"
MSG1			.CZ "%s %s %21s %21s %b %s\r\n"
MSG1.T.RAW		.AZ "RAW   "
MSG1.T.DGRAM	.AZ "DGRAM "
MSG1.T.SEQPKT	.AZ "SEQPKT"
MSG1.T.STREAM	.AZ "STREAM"
MSG1.T.INV		.AZ "???   "
PROTO			.DA #S.IP.PROTOCOL.ICMP,#S.IP.PROTOCOL.TCP,#S.IP.PROTOCOL.UDP
PROTO.Cnt		.EQ *-PROTO
MSG1.P.ICMP		.AZ "ICMP"
MSG1.P.TCP		.AZ "TCP "
MSG1.P.UDP		.AZ "UDP "
MSG1.P.INV		.AZ "??? "
MSG1.S.0		.AZ "*"
MSG1.S.1		.AZ "OPENED"
MSG1.S.2		.AZ "LISTENING"
MSG1.S.3		.AZ "SYN_SENT"
MSG1.S.4		.AZ "SYN_RECEIVED"
MSG1.S.5		.AZ "ESTABLISHED"
MSG1.S.6		.AZ "CLOSE_WAIT"
MSG1.S.7		.AZ "LAST_ACK"
MSG1.S.8		.AZ "FIN_WAIT1"
MSG1.S.9		.AZ "FIN_WAIT2"
MSG1.S.10		.AZ "CLOSING"
MSG1.S.11		.AZ "TIME_WAIT"
MSG1.S.INV		.AZ "???"
SPRINTF.IPP		.AZ "%d.%d.%d.%d:%D"
OPTIONS			.AZ "DARKDBUL"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
SA				.BS S.SA.IN
LocAddr			.BS 22
RemAddr			.BS 22
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/netstat.s
ASM
