NEW
  AUTO 3,1
                .LIST OFF
                .OP 65C02
                .OR $2000
                .TF bin/nl
*--------------------------------------
                .INB inc/macros.i
                .INB inc/a2osx.i
                .INB inc/a2osx.api.i
                .INB inc/mli.i
                .INB inc/mli.e.i
*--------------------------------------
                .DUMMY
                .OR ZPBIN
ZS.START
ArgIndex        .BS 1
ArgPattern      .BS 1
ZPPtr1          .BS 2
ZPBufPtr        .BS 2
pFile           .BS 2

char            .BS 1
LineCount       .BS 2

bPause          .BS 1

bIsTTY          .BS 1
bNewLine        .BS 1
bDummy			.BS 1

ZS.END          .ED
*--------------------------------------
* File Header (16 Bytes)
*--------------------------------------
CS.START        cld
                jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1              .DA CS.INIT
                .DA CS.RUN
                .DA CS.SIG
                .DA CS.QUIT
L.MSG.USAGE     .DA MSG.USAGE
L.MSG.CRLF      .DA MSG.CRLF
L.MSG.LINENUM   .DA MSG.LINENUM
                .DA 0
*--------------------------------------
CS.INIT         clc
                rts
*--------------------------------------
CS.RUN          inc bNewLine

				jsr CS.RUN.ISATTY
                jsr CS.RUN.CheckArgs
                bcs CS.RUN.LOOP.RTS

                lda #1
                sta LineCount
                stz LineCount+1
*--------------------------------------
CS.RUN.LOOP		>SLEEP

				jsr X.CheckCtrl
				bcs .9

				bvs CS.RUN.LOOP

                >LDYA pFile
                >LIBC GetC
                bcs .7

                jsr CS.RUN.PRINTBYTE
                bra CS.RUN.LOOP

.7              cmp #MLI.E.EOF
                bne .9

.8              lda #0                     Exit with no Error

.9              sec
CS.RUN.LOOP.RTS rts
*--------------------------------------
CS.RUN.CheckArgs
				jsr CS.RUN.NextArg
                bcs .4

                lda (ZPPtr1)
                cmp #'-'
                beq .1

.11             lda pFile+1
                bne .97

                >LDYA ZPPtr1
                jsr CS.RUN.OPEN
                bcs .9

                >STYA pFile
                bra CS.RUN.CheckArgs

.1              ldy #1
                lda (ZPPtr1),y

                ldx #OptionList.Cnt-1

.2              cmp OptionList,x
                beq .3

                dex
                bpl .2

                bra .97

.3              txa
                lsr
                beq .98

                tax
                lda #$80
                sta bDummy-1,x
                bra CS.RUN.CheckArgs
*--------------------------------------
.4              lda pFile+1
                bne .80

                jsr X.IsPipeIn
				bcs .99

				bvc .97

                >STYA pFile

.80             >LDYAI 256
                >LIBC Malloc
                bcs .9

                >STYA ZPBufPtr

*               clc

.9              rts

.97             lda #E.SYN

.98             pha
                >SS
				>PUSHW L.MSG.USAGE
                >PUSHBI 0
                >LIBC PrintF
                >SR
				pla
                sec
.99             rts
*--------------------------------------
CS.RUN.NextArg  inc ArgIndex
                lda ArgIndex
                >KAPI ArgV
                bcs .9

                >STYA ZPPtr1

.9              rts
*--------------------------------------
CS.RUN.OPEN     >SS
				>PUSHYA
                >PUSHBI O.RDONLY+O.TEXT
                >PUSHBI S.FI.T.TXT
                >PUSHWZ                         Aux type
                >LIBC FOpen
                >SR
				bcs .9

                >STYA pFile

.9              rts
*--------------------------------------
CS.RUN.ISATTY   ldy #S.PS.pStdOut
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply

				>LIBC FileNo
				bcs .9

				>SS
				>PUSHYA
				>PUSHEA.G STATBUF
				>LIBC FStat
				>SR
				bcs .9

				>LDA.G STATBUF+S.STAT.MODE+1
				and #$F0

                dec bIsTTY

.8				clc

.9              rts
*--------------------------------------
CS.QUIT         >LDYA pFile
                beq .1

                >LIBC FClose

.1              >LDYA ZPBufPtr
                beq .8

                >LIBC Free

.8              clc
                rts
*--------------------------------------
CS.RUN.PRINTBYTE
				pha
                lda bNewLine
                beq .2

                stz bNewLine

				>SS
				>PUSHW L.MSG.LINENUM
                >PUSHW LineCount
                >PUSHBI 2
                >LIBC PrintF
				>SR

.2              pla
                pha
                >LIBC PutChar
                pla
                cmp #C.CR
                bne .9

                lda bIsTTY
                beq .4

                lda #C.LF
                >LIBC PutChar

.4              inc bNewLine
                inc LineCount
                bne .9

                inc LineCount+1
.9              rts
*--------------------------------------
CS.SIG			sec
                rts
*--------------------------------------
				.INB usr/src/shared/x.ispipein.s
				.INB usr/src/shared/x.checkctrl.s
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList      .AS "Xx"
OptionList.Cnt  .EQ *-OptionList
*--------------------------------------
MSG.USAGE       .CS "Usage : NL <File> or CMD|NL"
MSG.CRLF        .CZ "\r\n"
MSG.LINENUM     .CZ "%5D "
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
                .DUMMY
                .OR 0
STATBUF			.BS S.STAT
DS.END          .ED
*--------------------------------------
MAN
SAVE usr/src/bin/nl.s
ASM
