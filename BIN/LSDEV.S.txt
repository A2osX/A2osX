NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/lsdev
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/io.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pFD				.BS 2
pName			.BS 2
hFD				.BS 2
Tmp				.BS 1
Err				.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.EVENT
				.DA	CS.QUIT
L.MSG.Header	.DA MSG.Header
L.MSG.FD		.DA MSG.FD
L.MSG.DEV		.DA MSG.DEV
L.MSG.DEV.ERR	.DA MSG.DEV.ERR
L.MSG.DEV.DIB	.DA MSG.DEV.DIB
L.MSG.DEV.NA	.DA MSG.DEV.NA
L.MSG.DEV.B		.DA MSG.DEV.B
J.FD			.DA DumpFD.REG
				.DA DumpFD.DIR
				.DA DumpFD.CDEV
				.DA DumpFD.CDEV
				.DA DumpFD.LNK
				.DA DumpFD.DSOCK
				.DA DumpFD.SSOCK
				.DA DumpFD.PIPE
L.FD.T			.DA FD.T.REG
				.DA FD.T.DIR
				.DA FD.T.CDEV
				.DA FD.T.BDEV
				.DA FD.T.LNK
				.DA FD.T.DSOCK
				.DA FD.T.SSOCK
				.DA FD.T.PIPE
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.MSG.Header
				>LIBC PutS

				lda #0

.1				sta hFD
				>KAPI GetFDByID
				bcs .7

				>STYA pFD

				stz pName
				stz pName+1

				lda hFD
				>KAPI GetNameByID
				bcs .4

				>STYA pName

.4				jsr DumpFD

				>LDYA pFD
				>LIBC Free

				>LDYA pName
				beq .7

				>LIBC Free

.7				lda hFD

				inc
				inc
				cmp #K.FD.MAX*2
				bcc .1

				lda #0

*				sec

.9				rts
*--------------------------------------
DumpFD			>SS
				>PUSHW L.MSG.FD

				>PUSHB hFD

				ldy #S.FD.DEV.BUSID
				lda (pFD),y
				asl
				asl
				asl
				asl
				sta Tmp

				iny 					#S.FD.DEV.DEVID
				lda (pFD),y

				ora Tmp
				>PUSHA

				ldy #S.FD.T
				lda (pFD),y
				tax
				>PUSHW L.FD.T,x

				ldy #S.FD.DEV.pBUS+1
				lda (pFD),y
				>PUSHA
				dey
				lda (pFD),y
				>PUSHA

				>PUSHBI 6

				>LIBC PrintF
				>SR
				bcs .9

				>SS
				>PUSHW L.MSG.DEV
				>PUSHW pName
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs .9

				lda (pFD)				#S.FD.T

				tax
				jmp (J.FD,x)

.9				rts
*--------------------------------------
DumpFD.CDEV
DumpFD.BDEV		jsr GetDevDIB
				bcc DumpDIB

				sta Err

				>SS
				>PUSHB Err
				>PUSHW pData			DATABUF
				>KAPI GetErrMsg
				>SR

				>SS
				>PUSHW L.MSG.DEV.ERR
				>PUSHB Err
				>PUSHW pData			DATABUF
				>PUSHBI 3
				>LIBC PrintF
				>SR
.9				rts
*--------------------------------------
DumpDIB			>SS
				>PUSHW L.MSG.DEV.DIB

				>LDA.G DEV.DIB+S.DIB.S

				ldy #7

.1				lsr
				pha
				lda #'-'
				bcc .2

				lda MSG.SFLAGS,y

.2				sta (pData),y			DATABUF
				pla
				dey
				bpl .1
				
				ldy #8
				lda #0
				sta (pData),y			DATABUF

				>PUSHW pData			DATABUF
				>PUSHB.G DEV.DIB+S.DIB.T
				>PUSHB.G DEV.DIB+S.DIB.ST
				>PUSHEA.G DEV.DIB+S.DIB.IDS
				>PUSHB.G DEV.DIB+S.DIB.VERSION
				>PUSHB.G DEV.DIB+S.DIB.VERSION+1

				>PUSHBI 8

				>LIBC PrintF
				>SR
				bcs .9

				>LDA.G DEV.DIB+S.DIB.S
				bpl DumpFD.NA			char device...

				>SS
				>PUSHW L.MSG.DEV.B
				>PUSHBI 0
				>PUSHB.G DEV.DIB+S.DIB.SIZE+2
				>PUSHB.G DEV.DIB+S.DIB.SIZE+1
				>PUSHB.G DEV.DIB+S.DIB.SIZE
				>PUSHBI 4
				>LIBC PrintF
				>SR

.9				rts

DumpFD.NA		>LDYA L.MSG.DEV.NA
				>LIBC PutS
				rts
*--------------------------------------
DumpFD.REG
DumpFD.DIR

				>LDYA L.MSG.DEV.NA
				>LIBC PutS

.9				rts
*--------------------------------------
DumpFD.LNK
DumpFD.DSOCK
DumpFD.SSOCK

				>LDYA L.MSG.DEV.NA
				>LIBC PutS

.9				rts
*--------------------------------------
DumpFD.PIPE

				>LDYA L.MSG.DEV.NA
				>LIBC PutS

.9				rts
*--------------------------------------
GetDevDIB		ldy #DEV.DIB+S.DIB-1
				ldx #S.DIB-1

.1				lda BLANK.DIB,x
				sta (pData),y
				dey
				dex
				bpl .1

				lda #S.IOCTL.S.GETDIB
				>STA.G DEV.IOCTL+S.IOCTL.S

				>LEA.G DEV.DIB
				>STYA.G DEV.IOCTL+S.IOCTL.BUFPTR

				>SS
				>PUSHBI 0
				>PUSHB hFD
				>PUSHBI IOCTL.STATUS
				>PUSHEA.G DEV.IOCTL
				>LIBC IOCTL
				>SR

.9				rts
*--------------------------------------
CS.EVENT		sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.Header		.CZ "FD ID Type Addr Name         Status   TP.ST ID String        Version Size (BLK)"
MSG.FD			.CZ "%2h %h %4s %H "
MSG.DEV			.CZ "%12s "
MSG.DEV.ERR		.CZ "[%h] %s\r\n"
MSG.DEV.DIB		.CZ "%s %h.%h %16S %3d.%03d "
MSG.DEV.B		.CZ "%10u\r\n"
MSG.DEV.NA		.CZ "n/a"
*--------------------------------------
MSG.SFLAGS		.AS "bwrlneio"
*--------------------------------------
FD.T.REG		.AZ "REG"
FD.T.DIR		.AZ "DIR"
FD.T.CDEV		.AZ "CDEV"
FD.T.BDEV		.AZ "BDEV"
FD.T.LNK		.AZ "LNK"
FD.T.DSOCK		.AZ "DSCK"
FD.T.SSOCK		.AZ "SSCK"
FD.T.PIPE		.AZ "PIPE"
*--------------------------------------
BLANK.DIB		.DA #0					#S.DIB.S.BLOCK+S.DIB.S.WRITE+S.DIB.S.READ
				.BS 3
				.PS "(No ID String!) "
				.DA #0
				.DA #0
				.DA 0					K.VER
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DATABUF			.BS 64
DEV.IOCTL		.BS S.IOCTL
DEV.DIB			.BS S.DIB
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/lsdev.s
ASM
