PR#3
PREFIX /A2OSX.SRC
NEW
INC 1
AUTO 6
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF /A2OSX.BOOT/BIN/LSDEV
*--------------------------------------
				.INB /A2OSX.DEV/INC/MACROS.I
				.INB /A2OSX.DEV/INC/PRODOS.I
				.INB /A2OSX.DEV/INC/A2OSX.I
*--------------------------------------
ZPPTR1			.EQ ZPBIN
ZPPTR2			.EQ ZPBIN+2
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #1					BIN Layout Version 1
				.DA 0
				.DA CS.END-CS.START		Code Length To Relocate
				.DA 0					Data Segment to Allocate
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------				
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.EVENT		
				.DA	CS.QUIT
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
L.MSG2			.DA MSG2
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.MSG0
				>SYSCALL CPrintFYA
				
				stz DEV.ID
				
				lda DEV.ID
				
.1				>SYSCALL GetDevByIDA
				bcc .10
				jmp .71
				
.10				>STYA pDev
				>STYA ZPPTR1

				ldy #S.DEV.DRV.JMP+1
				>PUSHB (pDev),y

				dey
				>PUSHB (pDev),y
				
				lda #S.DEV.NAME
				clc
				adc pDev
				tay
				
				lda /S.DEV.NAME
				adc pDev+1
				
				>PUSHYA
				
				ldy #S.DEV.F
				>PUSHB (pDev),y
				
				ldy #S.DEV.S
				>PUSHB (pDev),y
				
				>PUSHB DEV.ID
				
				>LDYA L.MSG1
				>SYSCALL CPrintFYA
				bcs .9
				
				>LDYA ZPPTR1
				>STYA pDev
				
				ldx #DEVMGR.STATUS
				jsr pDevJmp
				bcs .7
				>STYA ZPPTR2
				
				ldy #S.DEVSTAT.VERSION+1
				>PUSHB (ZPPTR2),y
				dey
				>PUSHB (ZPPTR2),y
				
				lda ZPPTR2
				clc
				adc #S.DEVSTAT.IDSTRING
				tay
				lda ZPPTR2+1
				adc #0
				
				>PUSHYA
				
				ldy #S.DEVSTAT.SUBTYPE
				>PUSHB (ZPPTR2),y
				dey
				>PUSHB (ZPPTR2),y
				
				>LDYA L.MSG2
				>SYSCALL CPrintFYA
				bcs .9
				
.7				lda #13
				>SYSCALL PutCA
				lda #10
				>SYSCALL PutCA
				
.71				inc DEV.ID
				lda DEV.ID
				cmp #K.DEV.MAX
				beq .8
				jmp .1
				
.8				lda #0					tell TSKMGR that all done ok, but 
.9				sec						we do not want to stay in memory
				rts
*--------------------------------------
CS.EVENT		sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END
MSG0			>CSTR "ID  Status   Flags    Name Addr. Typ/Sub IDString         Version\r\n"
MSG1			>CSTR "%03d %b %b %4S $%H "
MSG2			>CSTR "$%h/$%h %16S %03d.%03d"
*--------------------------------------
DEV.ID			.BS 1
MAN
SAVE BIN/LSDEV.S
ASM
