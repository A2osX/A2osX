NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/who
*--------------------------------------
				.INB INC/MACROS.I
				.INB INC/MLI.I
				.INB INC/A2OSX.I
				.INB INC/KERNEL.I
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
SessionID		.BS 1
ZPSessionPtr	.BS 2
ZS.END
				.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #1					BIN Layout Version 1
				.DA 0
				.DA CS.END-CS.START		Code Size (without Constants)
				.DA DS.END-DS.START		Data SegmentSize
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.EVENT		
				.DA	CS.QUIT
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.MSG0
				>SYSCALL PutS

				ldx #1
				stx SessionID

.1				lda S.Table.hSession-1,x
				beq .7

				>SYSCALL GetMemPtr
				>STYA ZPSessionPtr

				>PUSHW L.MSG1			format
				>PUSHB SessionID		1 byte		SID

				ldx SessionID
				lda S.Table.hSession-1,x
				>PUSHA					1 byte		hSession
				
				>PUSHB (ZPSessionPtr)	1 byte		PRIVILEGE

				ldy #S.SESSION.UID
				>PUSHB (ZPSessionPtr),y	1 byte		UID

				iny						#S.SESSION.UID
				>PUSHB (ZPSessionPtr),y	1 byte		GID

				lda ZPSessionPtr
				clc
				adc #S.SESSION.NAME
				tay
				lda ZPSessionPtr+1
				adc /S.SESSION.NAME
				>PUSHYA					2 bytes		NAME

				ldx SessionID
				lda S.Table.hFile-1,x
				tax 
				jsr CS.RUN.GETIO
				>PUSHYA					2 bytes		DEV

				>PUSHBI 9				total = 9 ... bytes

				>SYSCALL PrintF
				bcs .9
				
.7				inc SessionID
				ldx SessionID
				cpx #K.USR.MAX+1
				bne .1

.8				lda #0					tell TSKMGR that all done ok, but 
				sec						we do not want to stay in memory
.9				rts
*--------------------------------------
CS.RUN.GETIO	lda OF.Table.hPath-1,x
				bne .1

				lda OF.Table.hFD-1,x
				tax

				lda Dev.Table-1,x

				clc
				adc #S.FD.DEV
				tay
				lda Dev.Table,x

				adc /S.FD.DEV
*				clc
				rts

.1				>SYSCALL GetMemPtr
				rts
*--------------------------------------
CS.EVENT		sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END
MSG0			.AZ "SID hSD Privilege UID GID Name             Dev"
MSG1			.AZ "%3d $%0h %b  %3d %3d %16s %s\r\n"
*--------------------------------------
				.DUMMY
				.OR 0
DS.START
DS.END
				.ED
*--------------------------------------
MAN
SAVE USR/SRC/BIN/WHO.S
ASM
