NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
FILE.New		>LDYA L.MSG.NEWFILE

FILE.NewYA		>SS
				>PUSHYA
				>PUSHWZ					Allocate
				>LIBC RealPath
				>SR
				bcs .9

				>STYA.G pFileName

				jmp FILE.Reset

.9				rts
*--------------------------------------
FILE.Load		jsr FILE.NewYA
				bcs .9

				jsr FILE.Reset

				>LDYA.G pLineBuf
				>STYA LineBufPtr

				ldx #S.FI.T.ASM
				lda #O.RDONLY
				jsr FILE.OpenAX
				bcs .1

				jsr FILE.Load.ASM
				jmp FILE.Close

.1				ldx #S.FI.T.TXT
				lda #O.RDONLY
				jsr FILE.OpenAX
				bcs .9

				jsr FILE.Load.TXT
				jmp FILE.Close

.9				rts
*--------------------------------------
FILE.Load.TXT
.1				ldy #0
				jsr FILE.ReadY			read 256
				bcs .7

				sty TmpCount			Count read

				ldy #0

.2				lda (LineBufPtr),y

				cmp #C.LF
				bne .3

				phy

				lda #1
				>STA.G FileType

				ply
				bra .4

.3				jsr FILE.Load.2Buf
				bcs FILE.Load.RTS

.4				dec TmpCount
				beq .1

				iny
				bra .2

.7				cmp #MLI.E.EOF
				bne .9

.8				lda #0
				sta (BufPtr)

				clc
				rts

.9				sec

FILE.Load.RTS	rts
*--------------------------------------
FILE.Load.ASM	lda #2
				>STA.G FileType

.1				ldy #3
				jsr FILE.ReadY
				bcs .7

				lda (LineBufPtr)
				dec
				dec
				dec
				tay
				jsr FILE.ReadY
				bcs .7

				jsr FILE.Load.ASM.Decode

				bcc .1

.7				cmp #MLI.E.EOF
				bne .9

				lda #0
				sta (BufPtr)

				clc
				rts

.9				sec
.99				rts
*--------------------------------------
FILE.Load.ASM.Decode
				ldy #0

.1				lda (LineBufPtr),y
				beq .8

				iny
				tax
				bmi .2

				jsr FILE.Load.2Buf
				bcc .1

.9				rts

.2				cmp #$C0			REPEAT char?
				bne .5

				lda (LineBufPtr),y		Get Repeat Count
				iny
				tax

.4				lda (LineBufPtr),y		Get Repeat Char

				jsr FILE.Load.2Buf
				bcs .9

				dex
				bne .4

				iny					Skip Repeat char
				bra .1

.5				and #$3F			Compute blank count
				tax

.6				lda #C.SPACE

				jsr FILE.Load.2Buf
				bcs .9

				dex
				bne .6

				bra .1

.8				lda #C.CR
*--------------------------------------
FILE.Load.2Buf	phy
				phx

				sta (BufPtr)
				inc BufPtr
				bne .1

				inc BufPtr+1

.1				inc FileLen
				bne .8

				inc FileLen+1

				lda FileLen+1
				cmp BufLen+1
				bne .8

				jsr BUF.BufPtr.Realloc
				bcs .9

				>LDA.G pBuf
*				clc
				adc FileLen
				sta BufPtr
				iny
				lda (pData),y
				adc FileLen+1
				sta BufPtr+1

.8				clc

.9				plx
				ply
FILE.Save.9		rts
*--------------------------------------
FILE.Save		>LDYA.G pLineBuf
				>STYA LineBufPtr

				>LDA.G FileType
				asl
				tax
				jmp (J.SAVE,x)
*--------------------------------------
FILE.Save.CR	clc
				.HS B0					BCS
FILE.Save.CRLF	sec
				ror TmpCount

				ldx #S.FI.T.TXT
				lda #O.CREATE+O.WRONLY
				jsr FILE.OpenAX

				bcs FILE.Save.9

				>LDYA.G	pBuf
				>STYA BufPtr

.1				lda (BufPtr)
				beq .8

				ldy #0

.2				lda (BufPtr)
				beq .4

				inc BufPtr
				bne .3

				inc BufPtr+1

.3				sta (LineBufPtr),y
				iny

				cmp #C.CR
				bne .2

				bit TmpCount
				bpl .4

				lda #C.LF
				sta (LineBufPtr),y

				iny

.4				jsr FILE.WriteY
				bcs FILE.Save.99

				bra .1

.8 				>STZ.G bUnSaved
FILE.Save.99	jmp FILE.Close
*--------------------------------------
FILE.Save.ASM	stz TmpLen
				stz TmpLen+1

				inc TmpLen

				ldx #S.FI.T.ASM
				lda #O.CREATE+O.WRONLY
				jsr FILE.OpenAX
				bcs FILE.Save.99

				>LDYA.G	pBuf
				>STYA BufPtr

.1				lda (BufPtr)
				beq .8

				ldy #1
				lda TmpLen
				sta (LineBufPtr),y
				iny

				lda TmpLen+1
				sta (LineBufPtr),y

				ldy #0

.2				iny
				lda (BufPtr),y
				beq .3

				cmp #C.CR
				bne .2

.3				ldy #3				Initial LEN

.4				lda (BufPtr)
				beq .7

				ldx #0

.40				inc BufPtr
				bne .5

				inc BufPtr+1

.5				cmp #C.CR
				beq .7

				cmp (BufPtr)		same as next ?
				bne .50				no -> flush

				inx
				cpx #$3F
				bne .40

.50				cpx #0
				beq .60				only one char....

				cmp #C.SPACE
				beq .61

				cpx #3
				bcs .62

.63				sta (LineBufPtr),y
				iny
				dex
				bpl .63

				bra .4

.62				pha
				lda #$C0
				sta (LineBufPtr),y
				iny

				txa
				sta (LineBufPtr),y
				iny

				pla
				sta (LineBufPtr),y
				iny
				bra .4

.61				txa
				ora #$C0

.60				sta (LineBufPtr),y
				iny
				bra .4

.7				lda #0

				sta (LineBufPtr),y
				iny

				tya

				sta (LineBufPtr)

				jsr FILE.WriteY
				bcs FILE.Close

 				inc TmpLen
				bne .1

				inc TmpLen+1
				bra .1

.8 				>STZ.G bUnSaved
*--------------------------------------
FILE.Close		php
				pha

				>LDYA TmpIndex			pFile
				beq .2

				>LIBC FClose

.2				pla
				plp
				rts
*--------------------------------------
FILE.OpenAX		stz TmpIndex+1			pFile

				stx TmpCount
				sta TmpByte

				>SS
				>PUSHW.G pFileName
				lda TmpByte				access
				>PUSHA
				lda TmpCount			type
				>PUSHA
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA TmpIndex			pFile

.9				rts
*--------------------------------------
FILE.ReadY		>SS
				>PUSHW TmpIndex			pFile
				>PUSHW LineBufPtr

				lda #0
				cpy #0
				bne .1

				inc

.1				>PUSHYA

				>LIBC FRead
				>SR
				rts
*--------------------------------------
FILE.WriteY		>SS
				>PUSHW TmpIndex			pFile
				>PUSHW LineBufPtr

				lda #0
				>PUSHYA
				>LIBC FWrite
				>SR

FILE.WriteY.RTS	rts
*--------------------------------------
FILE.Reset		>LDA.G pBuf+1
				beq .1

				pha
				dey
				lda (pData),y
				tay
				pla

				>LIBC Free

				>STZ.G pBuf+1

.1				>STZ.G FileType			TXT/CR

				stz FileLen
				stz FileLen+1
				jsr BUF.ResetSel

				>LDYAI FILEBUF.SIZE
				>STYA BufLen
				>LIBC Malloc
				bcs FILE.WriteY.RTS

				>STYA BufPtr
				>STYA.G pBuf

				lda #0
				sta (BufPtr)

				jmp SCRN.UpdateStatusBar
*--------------------------------------
MAN
SAVE usr/src/bin/edit.s.file
LOAD usr/src/bin/edit.s
ASM
