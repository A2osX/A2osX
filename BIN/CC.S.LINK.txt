NEW
  AUTO 3,1
*--------------------------------------
CC.Link			>LDYA L.MSG.LINKING
				>LIBC PutS

				ldy #0
				lda #2

.1				>SS
				>STYA ZPPtr1			SymID
				>PUSHW.G CC.pSyms
				>PUSHW ZPPtr1			SymID
				>PUSHW pData
				>KAPI SListGetByID
				>SR
				bcs .8

				>STYA ZPPtr2			NextID

				>SS
				>PUSHW.G CC.pSyms
				>PUSHW ZPPtr1			SymID
				>PUSHW ZP.pLSymBuf
				>PUSHWI 65535			All
				>PUSHWZ					from Start
				>KAPI SListGetData
				>SR
				bcs .99

				>LDA.G bVerbose
				bpl .2

				jsr CC.Link.Dump

.2				ldy #SYM.SC
				lda (ZP.pLSymBuf),y
				bne .7					SYM.SC.STATIC

				ldy #SYM.Addr+1
				lda (ZP.pLSymBuf),y
				dey
				ora (ZP.pLSymBuf),y
				bne .7

				jsr CC.Link.Lookup
				bcs .9

				jsr CC.Link.Update
				bcs .99

.7				>LDYA ZPPtr2
				jmp .1

.8				clc
*			>DEBUG
			
				rts

.9				>LDA.G bVerbose
				bmi .90

				jsr CC.Link.Dump

.90				lda #E.FUNDEF
				sec
.99				rts
*--------------------------------------
* Input : pData = Symbol
*--------------------------------------
CC.Link.Lookup	>LDYA L.CC.LIBC
				jsr CC.Link.LookupYA
				ldx #1
				bcc .99

				>LDA.G LibCount
*				sec
				beq .99

				lda #0
				sta ArgIndex
				clc

.1				asl
				adc #pLibOs
				tay
				lda (pData),y
				pha
				iny
				lda (pData),y
				ply

				jsr CC.Link.LookupYA
				bcs .2

				ldx ArgIndex
				inx
				inx
				inx
				rts

.2				inc ArgIndex
				lda ArgIndex
				>CMP.G LibCount
				bcc .1

.99				rts
*--------------------------------------
CC.Link.LookupYA
				>STYA ZP.pLookup

				ldy #$ff

.1				iny
				lda (pData),y
				bne .1

				tya
				tax						Symbol Len

.2				lda (ZP.pLookup)
				beq .9					Ending 0, not found....

				txa						Get Back Len
				cmp (ZP.pLookup)		Same Len ?
				bne .4

				tay

.3				lda (ZP.pLookup),y

				dey
				cmp (pData),y
				bne .4

				tya
				bne .3

				lda (ZP.pLookup)
				tay
				iny
				lda (ZP.pLookup),y			get fID

				clc
				rts

.4				lda (ZP.pLookup)
				inc
				jsr CC.ZPLookupPtrAddAp1	Skip LEN\Symbol\ID
				bra .2

.9				sec
				rts
*--------------------------------------
* A=ID X=SYS|LIB
*--------------------------------------
CC.Link.Update	ldy #SYM.Addr
				sta (ZP.pLSymBuf),y

				iny
				txa
				sta (ZP.pLSymBuf),y

				>SS
				>PUSHW.G CC.pSyms
				>PUSHW ZPPtr1				SymID
				>PUSHW ZP.pLSymBuf
				>PUSHWI SYM
				>KAPI SListSetData
				>SR
				rts
*--------------------------------------
CC.Link.Dump	ldy #$ff

.1				iny
				lda (pData),y
				bne .1

				iny
				lda (pData),y
				sta ZPPtr3
				iny
				lda (pData),y
				sta ZPPtr3+1

				>SS
				>PUSHW L.MSG.LINKING2

				>PUSHW ZPPtr1			ID
				>PUSHW ZPPtr3			Scope

				>PUSHW pData			Name

				>PUSHB (ZP.pLSymBuf)	SYM.T

				ldy #1
				>PUSHB (ZP.pLSymBuf),y	SYM.Q

				iny
				>PUSHB (ZP.pLSymBuf),y	SYM.SC

				ldy #SYM.Cnt
				>PUSHB (ZP.pLSymBuf),y

				ldy #SYM.SizeOf+1
				>PUSHB (ZP.pLSymBuf),y
				dey
				>PUSHB (ZP.pLSymBuf),y

				ldy #SYM.Addr+1
				>PUSHB (ZP.pLSymBuf),y
				dey
				>PUSHB (ZP.pLSymBuf),y

				>PUSHBI 14
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CC.Link.CheckMain
				>SS
				>PUSHW.G CC.pSyms
				>PUSHW L.CC.MAIN
				>PUSHWI 0				ScopeID
				>PUSHWI	0				NextPtr
				>KAPI SListLookup
				>SR
				bcs .9

				>STYA ZPPtr1			SymID

				>SS
				>PUSHW.G CC.pSyms
				>PUSHW ZPPtr1			SymID
				>PUSHW ZP.pLSymBuf
				>PUSHWI 65535			all
				>PUSHWZ					From Start
				>KAPI SListGetData
				>SR
				bcs .99

				ldy #SYM.Addr
				lda (ZP.pLSymBuf),y
				iny
				ora (ZP.pLSymBuf),y
				beq .9

				lda (ZP.pLSymBuf),y
				sta PCC.FH+PCC.FH.MAIN+1
				dey
				lda (ZP.pLSymBuf),y
				sta PCC.FH+PCC.FH.MAIN

*				clc

				rts

.9				>LDYA L.MSG.NOMAIN
				>LIBC putS
				lda #E.CSYN
				sec
.99				rts
*--------------------------------------
MAN
SAVE usr/src/bin/cc.s.link
LOAD usr/src/bin/cc.s
ASM
