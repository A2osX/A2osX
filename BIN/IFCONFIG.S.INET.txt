NEW
  AUTO 3,1
*--------------------------------------
INET			inc Idx
				lda Idx
				>KAPI ArgV
				bcs .8

				>STYA pArg

				>LDYA L.KW.INET
				jsr CS.RUN.KW
				bcs .1

				inc Idx

				bra .2

.1				ldx #0					default to address

.2				jsr .7
				bcc INET

.9				lda #E.SYN
*				sec
				rts

.8				lda #0
				sec
				rts

.7				jmp (J.INET,x)
*--------------------------------------
INET.ADDR		lda Idx
				>KAPI ArgV
				bcs .9

				>STYA pArg

				jsr CS.RUN.GetIPCfg

				>SS
				>PUSHW pArg
				>PUSHW L.SScanF.IP

				>LEA.G DEV.IPCFG+S.IPCFG.IP
				>STYA R1

				ldx #4

.1				>PUSHW R1
				>INCW R1
				dex
				bne .1

				>PUSHBI 8				4 PTRs on stack
				>LIBC SScanF
				>SR
				bcs .9

				jmp INET.SetIPCfg

.9				lda #E.SYN
*				sec
				rts
*--------------------------------------
INET.MASK		lda Idx
				>KAPI ArgV
				bcs .9

				>STYA pArg

				jsr CS.RUN.GetIPCfg

				>SS
				>PUSHW pArg
				>PUSHW L.SScanF.IP

				>LEA.G DEV.IPCFG+S.IPCFG.MASK
				>STYA R1

				ldx #4

.1				>PUSHW R1
				>INCW R1
				dex
				bne .1

				>PUSHBI 8				4 PTRs on stack
				>LIBC SScanF
				>SR
				bcs .9

				jmp INET.SetIPCfg

.9				lda #E.SYN
*				sec
				rts
*--------------------------------------
INET.SetIPCfg	jsr CS.RUN.GetDCB

				ldy #DEV.DCB+S.DCB.NIC.F
				lda (pData),y
				ldy #DEV.IPCFG+S.IPCFG.DevF
				sta (pData),y

				lda hFD
				ldy #DEV.IPCFG+S.IPCFG.DevFD
				sta (pData),y

				ldy #DEV.DCB+S.DCB.NIC.MAC+5
				ldx #6

.1				lda (pData),y
				pha
				dey
				dex
				bne .1

				ldy #DEV.IPCFG+S.IPCFG.MAC
				ldx #6

.2				pla
				sta (pData),y
				iny
				dex
				bne .2

				jmp CS.RUN.SetIFCfg
*--------------------------------------
INET.DHCP		>LDYAI RECV.MAX
				>LIBC Malloc
				bcs .99

				>STYA pBuf

				lda #0
				jsr INET.DHCP.IFSET

				jsr INET.DHCP.INIT
				bcs .97

				lda #RETRY.MAX
				sta Retry

.1				jsr INET.DHCP.RUN
				bcc .8

				dec Retry
				bne .1

				bra .97

.8				jsr .97

				lda #S.IPCFG.S.OK
				jsr INET.DHCP.IFSET

.99				rts

.97				php
				pha

				lda hSocket
				beq .98

				>SS
				>PUSHW hSocket
				>PUSHWZ 				how
				>LIBC Shutdown
				>SR

.98				>LDYA pBuf
				>LIBC Free
				pla
				plp

				rts
*--------------------------------------
INET.DHCP.IFSET	sta IPCFG.DHCP+S.IPCFG.S

				>LDYA hFD
				>KAPI IfDown

				>SS
				>PUSHWI AF_INET
				>PUSHW hFD
				>PUSHW L.IPCFG.DHCP
				>KAPI SetIfCfg
				>SR

				>LDYA hFD
				>KAPI IfUp

				rts
*--------------------------------------
INET.DHCP.INIT	ldx #3

.1				eor A2osX.T16,x
				sta DHCP.DISC.XID,x
				sta DHCP.REQ.XID,x
				dex
				bpl .1

				jsr CS.RUN.GetDCB

				ldy #DEV.DCB+S.DCB.NIC.F
				lda (pData),y
				sta IPCFG.DHCP+S.IPCFG.DevF

				lda hFD
				sta IPCFG.DHCP+S.IPCFG.DevFD

				ldy #DEV.DCB+S.DCB.NIC.MAC+5
				ldx #5

.2				lda (pData),y
				sta IPCFG.DHCP+S.IPCFG.MAC,x
				sta DHCP.DISC.CHADDR,x
				sta DHCP.REQ.CHADDR,x
				dey
				dex
				bpl .2

				>SS
				>PUSHWI AF_INET
				>PUSHW hFD
				>PUSHW L.IPCFG.DHCP
				>KAPI SetIfCfg
				>SR
				bcs .9

				>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_DGRAM
				>PUSHWZ					no protocol
				>LIBC Socket
				>SR
.9				bcs .99

				>STYA hSocket

				>SS
				>PUSHW hSocket
				>PUSHW L.SA.LOCAL
				>PUSHWI S.SA.IN
				>LIBC Bind
				>SR
				bcs .99

				>SS
				>PUSHW hSocket
				>PUSHW L.SA.REMOTE
				>PUSHWI S.SA.IN
				>LIBC Connect
				>SR

.99				rts
*--------------------------------------
INET.DHCP.RUN	sec						send DISC

				jsr INET.DHCP.SEND
				bcs .9

.1				jsr INET.DHCP.RECV
				bcs .9

				jsr INET.DHCP.CheckOffer
				bcs .1

*				clc						send REQ

				jsr INET.DHCP.SEND
				bcs .9

.2				jsr INET.DHCP.RECV
				bcs .9

				jsr INET.DHCP.CheckAck
				bcs .2

* IP = DHCP.REQ.OPT.REQIP
* DHCPSRVR = DHCP.REQ.OPT.SVRIP

.9				rts
*--------------------------------------
INET.DHCP.SEND	>SS
				>PUSHW hSocket
				bcc .1

				>PUSHW L.DHCP.DISC
				>PUSHWI DHCP.DISC.LEN
				bra .2

.1				>PUSHW L.DHCP.REQ
				>PUSHWI DHCP.REQ.LEN

.2				>PUSHWZ					flags
				>PUSHW L.SA.DEST
				>PUSHWI S.SA.IN			len
				>LIBC SendTo
				>SR
				rts
*--------------------------------------
INET.DHCP.RECV	lda #TIMEOUT.MAX
				sta TimeOut

.1				>SLEEP

				>SS
				>PUSHW hSocket
				>PUSHW pBuf
				>PUSHWI RECV.MAX
				>PUSHWZ					flags
				>LIBC Recv
				>SR
				bcc .8

				cmp #E.NODATA
				bne .9

				lda TimeOut
				bne .1

				lda #SKT.E.NOCONN

.9				sec

				rts

.8				>STYA MsgLen

				>LDYA pBuf
				>STYA pMsg

*				clc

				rts
*--------------------------------------
INET.DHCP.CheckOffer

				jsr INET.DHCP.CheckXID
				bcs .9

				cmp #S.DHCP.OPTIONS.MT.DHCPOffer
				bne .9

*				ldy #S.DHCP.YIADDR+11	Copy YIADDR,SIADDR+GIADDR
*				ldx #11
*.1				lda (pMsg),y
*				sta DHCP.REQ.YIADDR,x
*				dey
*				dex
*				bpl .1

				ldy #S.DHCP.YIADDR+3
				ldx #3

.2				lda (pBuf),y			reletive to beginning of MSG
				sta DHCP.REQ.OPT.REQIP,x
				sta IPCFG.DHCP+S.IPCFG.IP,x
				dey
				dex
				bpl .2

				ldy #S.DHCP.SIADDR+3
				ldx #3

.4				lda (pBuf),y			reletive to beginning of MSG
				sta DHCP.REQ.OPT.SVRIP,x
				sta IPCFG.DHCP+S.IPCFG.DHCPSRVR,x
				dey
				dex
				bpl .4

.5				>INCW pMsg				skip Option 53 (DHCPOffer:530102)

				lda (pMsg)
				sec
				adc pMsg				add option len + 1
				sta pMsg
				bcc .7

				inc pMsg+1

.7				lda (pMsg)
				cmp #S.DHCP.OPTIONS.END
				beq .8

				jsr INET.DHCP.GetOption		May override SVRIP
				bra .5

.8				clc
				rts

.9				sec
				rts
*--------------------------------------
INET.DHCP.GetOption

				cmp #S.DHCP.OPTIONS.PRL.MASK
				beq INET.DHCP.GetOption.MASK

				cmp #S.DHCP.OPTIONS.PRL.GW
				beq INET.DHCP.GetOption.GW

				cmp #S.DHCP.OPTIONS.PRL.DNS
				beq INET.DHCP.GetOption.DNS

				cmp #S.DHCP.OPTIONS.SRVID
				bne .4

				ldy #5
				ldx #3

.3				lda (pMsg),y
				sta DHCP.REQ.OPT.SVRIP,x
				dey
				dex
				bpl .3

				rts

.4				cmp #S.DHCP.OPTIONS.PRL.DOMAIN
				bne .9

				ldy #1
				lda (pMsg),y				Get String len
				tay
				lda #0
				>LIBC Malloc
				bcs .9

				>STYA pDomainName

				>SS
				>PUSHW pDomainName
				lda pMsg
				clc
				adc #2
				tay
				lda pMsg+1
				adc #0
				>PUSHYA
				>LIBC StrCpy
				>SR

.9				rts
*--------------------------------------
INET.DHCP.GetOption.MASK
				ldx #3
				ldy #5

.1				lda (pMsg),y
				sta IPCFG.DHCP+S.IPCFG.MASK,x
				dey
				dex
				bpl .1

				rts
*--------------------------------------
INET.DHCP.GetOption.GW
				lda hFD
				sta ROUTE.DFLT+S.ROUTE.IF

				ldy #2

.1				lda (pMsg),y
				sta ROUTE.DFLT+S.ROUTE.GW-2,y
				iny
				cpy #6
				bcc .1

				>LDYA L.ROUTE.DFLT
				>KAPI SetRoute
				rts
*--------------------------------------
INET.DHCP.GetOption.DNS

				ldy #1
				lda (pMsg),y			Get Byte count
				lsr
				lsr
				sta Idx

				clc
				lda pMsg
				adc #2
				sta R1
				lda pMsg+1
				adc #0
				sta R1+1

.1				ldy #3

.2				lda (R1),y
				sta SA.DNS+S.SA.IN.ADDR,y
				dey
				bpl .2

				>LDYA L.SA.DNS
				>KAPI AddServInfo
				bcs .9

				lda R1
*				clc
				adc #4
				sta R1
				bcc .3

				inc R1

.3				dec Idx
				bne .1

				clc

.9				rts
*--------------------------------------
INET.DHCP.CheckAck

				jsr INET.DHCP.CheckXID
				bcs .9

				cmp #S.DHCP.OPTIONS.MT.DHCPAck
				bne .9

.8				clc
				rts

.9				sec
				rts
*--------------------------------------
INET.DHCP.CheckXID

				ldy #S.DHCP.XID+3
				ldx #3

.1				lda (pMsg),y
				cmp DHCP.DISC.XID,x	same XID ?
				bne .9

				dey
				dex
				bpl .1

				lda pMsg
				clc
				adc #S.DHCP.OPTIONS
				sta pMsg

				lda pMsg+1
				adc /S.DHCP.OPTIONS
				sta pMsg+1

				lda (pMsg)
				cmp #S.DHCP.OPTIONS.MT
				bne .9

				ldy #2
				lda (pMsg),y

				clc
				rts

.9				sec
				rts
*--------------------------------------
MAN
SAVE usr/src/bin/ifconfig.s.inet
LOAD usr/src/bin/ifconfig.s
ASM
