NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/cmp
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ArgIndex		.BS 1
ZPPtr1			.BS 2

bPause			.BS 1
bAll			.BS 1
bVerbose		.BS 1
bDiff			.BS 1

bSkip1			.BS 1
bSkip2			.BS 1

pFile1			.BS 2
ZPBufPtr1		.BS 2
pFile2			.BS 2
ZPBufPtr2		.BS 2

Offset			.BS 2
BufCnt			.BS 2
SeekOfs			.BS 4			
ZS.END			.ED	
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG		
				.DA	CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.DIFF		.DA MSG.DIFF
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			
.1				inc ArgIndex
				lda ArgIndex
				>KAPI ArgV
				bcs .7

				>STYA ZPPtr1

				lda (ZPPtr1)
				cmp #'-'
				bne .2

				jsr CS.RUN.CheckOpt
				bcc .1
				
.99				>LDYA L.MSG.USAGE
				>LIBC PutS
				lda #E.SYN
				sec
.9				rts

.2				>LDYA pFile1
				bne .3

				jsr CS.RUN.OPEN
				bcs .9

				>STYA pFile1

				bra .1

.3				>LDYA pFile2
				bne .4

				jsr CS.RUN.OPEN
				bcs .9

				>STYA pFile2

				bra .1

.4				bit bSkip1
				bmi .5

				jsr CS.RUN.SEEK
				bcs .9
				sec
				ror bSkip1
				bra .1

.5				bit bSkip2
				bmi .99

				sta bSkip2
				jsr CS.RUN.SEEK
				bcs .9

				sec
				ror bSkip2
				jmp .1

.7				lda pFile2+1
				beq .99

				lda pFile1+1
				beq .99

.8				>LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPBufPtr1

				>LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPBufPtr2
*--------------------------------------
CS.RUN.LOOP		jsr X.CheckCtrl
				bcs .99
				
				bvs CS.RUN.LOOP			Pause...

				jsr CS.RUN.READ1
				bcc .2

				cmp #MLI.E.EOF
				bne .9

				bra .99

.2				>STYA BufCnt
				jsr CS.RUN.READ2
				bcc .20

				cmp #MLI.E.EOF
				bne .9

				bra .98

.20				cpy BufCnt
				pha
				sbc BufCnt+1
				pla
				bcc .3

				>STYA BufCnt

.3				ldy #0

.4				lda (ZPBufPtr1),y
				cmp (ZPBufPtr2),y
				beq .8

				lda #$E0
				sta bDiff

				bit bVerbose
				bpl .5

				jsr CS.RUN.Print
				bcs .9

.5				bit bAll
				bpl .98

.8				iny
				cpy BufCnt
				bne .4

				inc Offset
				bne CS.RUN.LOOP

				inc Offset+1
				bra CS.RUN.LOOP

.98				lda bDiff

.9				sec
.99				rts
*--------------------------------------
CS.RUN.OPEN		>SS
				>PUSHW ZPPtr1
				>PUSHBI	O.RDONLY
				>PUSHBI 0				Type
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				rts
*--------------------------------------
CS.RUN.SEEK		>LDYA ZPPtr1
				>LIBC AToL
				bcs .9

				>PULLL SeekOfs

				lda bSkip1
				bmi .1

				>SS
				>PUSHW pFile1
				>PUSHL SeekOfs
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR
				bcs .9

.1				>SS
				>PUSHW pFile2
				>PUSHL SeekOfs
				>PUSHWI SEEK.SET
				>LIBC FSeek
				>SR

.9				rts
*--------------------------------------
CS.RUN.READ1	>SS
				>PUSHW pFile1
				>PUSHW ZPBufPtr1
				>PUSHWI 256
				>LIBC FRead
				>SR
				rts
*--------------------------------------
CS.RUN.READ2	>SS
				>PUSHW pFile2
				>PUSHW ZPBufPtr2
				>PUSHWI 256
				>LIBC FRead
				>SR
				rts
*--------------------------------------
CS.RUN.Print	phy

				>SS
				>PUSHW L.MSG.DIFF
				>PUSHW Offset
				tya
				>PUSHA
				>PUSHB (ZPBufPtr1),y
				>PUSHB (ZPBufPtr2),y
				>PUSHBI 5
				>LIBC PrintF
				>SR

				ply
				rts
*--------------------------------------
CS.QUIT			>LDYA pFile2
				beq .1

				>LIBC FClose

.1				>LDYA pFile1
				beq .2

				>LIBC FClose

.2				>LDYA ZPBufPtr2
				beq .3

				>LIBC Free

.3				>LDYA ZPBufPtr1
				beq .8

				>LIBC Free

.8				clc
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.RUN.CheckOpt	ldy #1 
				lda (ZPPtr1),y

				ldy #OptionVars-OptionList-1

.2				cmp OptionList,y
				beq .3

				dey
				bpl .2

				sec
				rts
				
.3				ldx OptionVars,y
				lda #$ff
				sta 0,x
				clc
				rts
*--------------------------------------
				.INB usr/src/shared/x.checkctrl.s
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList		.AS "AaVv"
OptionVars		.DA #bAll,#bAll,#bVerbose,#bVerbose
*--------------------------------------
MSG.USAGE		.CS "Usage : CMP File1 File2 [SKIP1 [SKIP2]]\r\n"
				.CS "   -A : Shows All differences\r\n"
				.CZ "   -V : Verbose Mode\r\n"
MSG.DIFF		.CZ "%H%h:%h %h\r\n"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/cmp.s
ASM
