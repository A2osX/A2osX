NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/lsof
*--------------------------------------
				.INB inc/macros.i
				.INB inc/mli.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pFILE			.BS 2
pFD				.BS 2
pName			.BS 2
FILE.ID			.BS 2
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.HEADER	.DA MSG.HEADER
L.MSG.BODY		.DA MSG.BODY
L.MSG.NONAME	.DA MSG.NONAME
L.FD.T			.DA FD.T.REG
				.DA FD.T.DIR
				.DA FD.T.CDEV
				.DA FD.T.BDEV
				.DA FD.T.LNK
				.DA FD.T.DSOCK
				.DA FD.T.SSOCK
				.DA FD.T.PIPE
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYA L.MSG.HEADER
				>LIBC PutS

				lda #0

.1				sta FILE.ID

				>KAPI GetFileByID
				bcs .7

				>STYA pFILE

				lda (pFILE)				S.FILE.hFD
				>KAPI GetFDByID
				bcs .6

				>STYA pFD

				jsr CS.PrintBody

				lda (pFILE)				S.FILE.hFD
				>KAPI GetNameByID
				bcs .4

				>STYA pName
				>LIBC PutS

				>LDYA pName
				>LIBC Free

				bra .5

.4				>LDYA L.MSG.NONAME
				>LIBC PutS

.5				>LDYA pFD
				>LIBC Free

.6				>LDYA pFILE
				>LIBC Free

.7				lda FILE.ID
				inc
				inc
				cmp #K.FILE.MAX*2
				bcc .1

.8				lda #0
*				sec
.9				rts
*--------------------------------------
CS.PrintBody	>SS
				>PUSHW L.MSG.BODY

				>PUSHW pFILE

				ldy #S.FILE.hFD
				>PUSHB (pFILE),y

				lda (pFD)
				tax
				>PUSHW L.FD.T,x

				ldy #S.FILE.oCnt+1
				>PUSHB (pFILE),y
				dey
				>PUSHB (pFILE),y

				>PUSHBI 7

				>LIBC PrintF
				>SR

				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.HEADER		.CZ "pFILE FD Type  oCnt  Filepath"
MSG.BODY		.CZ "%H  %h %4s %5D "
MSG.NONAME		.CZ "(no name)"
FD.T.REG		.CZ "REG"
FD.T.DIR		.CZ "DIR"
FD.T.CDEV		.CZ "CDEV"
FD.T.BDEV		.CZ "BDEV"
FD.T.LNK		.CZ "LNK"
FD.T.DSOCK		.CZ "DSCK"
FD.T.SSOCK		.CZ "SSCK"
FD.T.PIPE		.CZ "PIPE"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DS.END  		.ED
*--------------------------------------
MAN
SAVE usr/src/bin/lsof.s
ASM
