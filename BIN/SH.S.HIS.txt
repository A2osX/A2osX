NEW
  AUTO 3,1
*--------------------------------------
HIS.Init		>LDYAI HISBUF.MAX		Get a buffer for History

				>LIBC Malloc
				bcs .9

				>STYA R1
				>STYA.G HIS.pBuf

				lda #0
				sta (R1)

*				>STZ.G HIS.Count
*				>STZ.G HIS.Ptr
*				clc

.9				rts
*--------------------------------------
HIS.Add			>LDA.G HIS.pBuf+1
				beq .9

				>LDA.G HIS.Count
				cmp #HISCNT.MAX
				bcs .1

				inc
				sta (pData),y

				bra .2

.1				jsr HIS.DelOldest

.2				jmp HIS.AddNew

.9				rts
*--------------------------------------
HIS.Select		>LDA.G HIS.Count
				bne .10

				clc
.19				rts

.10				jsr CL.Reset

				stz ZPTmpW

				>LDYA.G HIS.pBuf
				>STYA R1

				lda (R1)
				beq .19

.1				>SS
				>PUSHW L.MSG.HIS
				>PUSHB ZPTmpW
				>PUSHW R1
				>PUSHBI 3
				>LIBC PrintF
				>SR
				bcs .9

				lda (R1)
				sec
				jsr HIS.AddACPtr1

				inc ZPTmpW
				lda ZPTmpW
				>CMP.G HIS.Count
				bcc .1

				>LDYA L.MSG.HISPROMPT
				jsr IO.OutYA
				bcs .9

				lda #0
				sta (ZPCLBuf)

				ldy #HIS.Ptr
				sta (ZPCLBuf),y

				>LIBC GetChar
				bcs .9

				cmp #'0'
				bcc .7

				cmp #'9'+1
				bcs .7

				and #$f
				inc
				>STA.G HIS.Ptr

.7				>LDYA L.MSG.HISROMPTCLR
				jsr IO.OutYA
				bcs .9

				jsr CL.PrintPrompt
				bcc HIS.GetToCL1
.9
HIS.Select.RTS	rts
*--------------------------------------
HIS.GetPrev		>LDA.G HIS.Count
				beq HIS.Select.RTS		empty history,nothing to do

				>LDA.G HIS.Ptr
				beq .1

				dec
				bne HIS.GetToCL

.1				>LDA.G HIS.Count

				bra HIS.GetToCL
*--------------------------------------
HIS.GetNext		>LDA.G HIS.Count
				beq HIS.Select.RTS		empty history,nothing to do

				>LDA.G HIS.Ptr
				beq .1

				>CMP.G HIS.Count
				bcc .1
				
				lda #0

.1				inc
*--------------------------------------
HIS.GetToCL		>STA.G HIS.Ptr

				jsr CL.CLR.1

HIS.GetToCL1	>LDA.G HIS.Ptr
				beq .9

				jsr HIS.GetA
				bcs .9

				tya

				>STA.G CL.Ptr
				>STA.G CL.Len

				>LDYA ZPCLBuf
				jmp IO.OutYA

.9				rts
*--------------------------------------
HIS.GetA		tax

				>LDYA.G HIS.pBuf
				>STYA R1

.1				lda (R1)
				beq .9

				dex
				beq .6

				sec
				jsr HIS.AddACPtr1
				bra .1

.6				tax

				inc R1
				bne .7
				
				inc R1+1
				
.7				ldy #0

.8				lda (R1),y
				sta (ZPCLBuf),y
				iny
				dex
				bne .8
				
				txa
				sta (ZPCLBuf),y
				
				clc						Y = StrLen
				rts

.9				lda #E.NOKEY
				sec
				rts
*--------------------------------------
HIS.AddNew		>LDYA.G HIS.pBuf
				>STYA R1

				ldy #0

.1				lda (R1),y
				beq .2

				tya
				sec
				adc (R1),y
				tay
				bra .1

.2				sty ZPTmpW

				ldy #$ff

.3				iny
				lda (ZPCLBuf),y
				bne .3

				tya
				sec
				adc ZPTmpW
				bcc .4

				jsr HIS.DelOldest

				bra HIS.AddNew

.4				lda	ZPTmpW
				clc
				jsr HIS.AddACPtr1
				
				tya
				sta (R1)
				
				inc R1
				bne .5
				
				inc R1+1

.5				ldy #$ff

.6				iny
				lda (ZPCLBuf),y
				sta (R1),y
				bne .6

				clc
				rts
*--------------------------------------
HIS.DelOldest	>LDYA.G HIS.pBuf
				>STYA R2

.1				>STYA R1

				lda (R1)
				beq .8

				sec
				jsr HIS.AddACPtr1

				ldy #$ff
				
.2				iny
				lda (R1),y
				sta (R2),y
				bne .2

.8				clc
				rts
*--------------------------------------
HIS.AddACPtr1	adc R1
				sta R1
				bcc .8

				inc R1+1

.8				rts
*--------------------------------------
MAN
SAVE usr/src/bin/sh.s.his
LOAD usr/src/bin/sh.s
ASM
