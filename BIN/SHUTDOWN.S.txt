NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/shutdown
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
*--------------------------------------
DEFAULT.TIMEOUT	.EQ 60
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPtr1			.BS 2
ZPPtr2			.BS 2
CmdPtr			.BS 2
ArgPtr			.BS 2

Index			.BS 1
Count			.BS 1

Timeout			.BS 2

bSec			.BS 1

bRestart		.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.CRLF		.DA MSG.CRLF
L.MSG.SHUTDOWN	.DA MSG.SHUTDOWN
L.MSG.PS		.DA MSG.PS
L.MSG.DEV		.DA MSG.DEV
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #DEFAULT.TIMEOUT
				sta Timeout

.1				inc Index
				lda Index
				>KAPI ArgV
				bcs .8

				>STYA ZPPtr1

				lda (ZPPtr1)
				cmp #'-'
				bne .5

				ldy #2
				lda (ZPPtr1),y
				bne .9

				dey
				lda (ZPPtr1),y

				cmp #'a'
				bcc .3

				cmp #'z'+1
				bcs .3

				eor #$20

.3				cmp #'T'
				bne .4

				inc Index
				lda Index
				>KAPI ArgV
				bcs .9

				>LIBC AToI
				bcs .9

				>STYA Timeout
				bra .1

.4				cmp #'R'
				bne .9

				sec
				ror bRestart
				bra .1
				
.5				>LDYA ZPPtr1
				ldx CmdPtr+1
				bne .6

				>STYA CmdPtr
				bra .1

.6				ldx ArgPtr+1
				bne .1	

				>STYA ArgPtr
				bra .1

.8				jsr CS.RUN.WAIT
				bcs .99

				jsr CS.RUN.PS
				jsr CS.RUN.DEV
				jsr CS.RUN.SYS

				>SS
				>PUSHWI 1					INITD
				>PUSHWI 1					SIGNAL
				>LIBC Kill
				>SR

				lda #0
				sec
.99				rts
*--------------------------------------
.9				>LDYA L.MSG.USAGE
				>LIBC PutS

				lda #E.SYN
				sec
				rts
*--------------------------------------
CS.RUN.WAIT		>SS
				>PUSHW L.MSG.SHUTDOWN
				>PUSHW Timeout
				>PUSHBI 2
				>LIBC PrintF
				>SR
				
.1				>SLEEP

				ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				>LIBC FEOF
				bcs .2

				tya
				bne .2					CC

				>LIBC GetChar
				bcs .9

				cmp #$03				Ctrl-C
				beq .9					CS

.2				lda bSec
				bpl .1

				stz bSec

				lda Timeout
				ora Timeout+1
				bne CS.RUN.WAIT

				>SS
				>PUSHW L.MSG.CRLF
				>PUSHBI 0
				>LIBC PrintF
				>SR

				clc
.9				rts
*--------------------------------------
CS.RUN.PS		stz Count

				lda #0
				sta Index

.1				>KAPI GetPSByID
				bcs .8

				>STYA ZPPtr1

.2				ldy #S.PS.S
				lda (ZPPtr1),y
				cmp #S.PS.S.QUIT
				beq .6					Dying...

*				ldy #S.PS.CPID
*				lda (ZPPtr1),y
*				iny
*				ora (ZPPtr1),y
*				bne .6

				ldy #S.PS.PID+1
				lda (ZPPtr1),y
				bne .3
				
				dey
				lda (ZPPtr1),y
				beq .7					don't kill PS0

				cmp #1
				beq .7					don't kill INITD

				jsr A2osX.GetPSy		don't kill myself
				cmp (ZPPtr1),y
				bne .3
				
				dey
				jsr A2osX.GetPSy
				cmp (ZPPtr1),y
				beq .7

.3				jsr CS.RUN.KILL

.6				inc Count

.7				>LDYA ZPPtr1
				>LIBC Free

.8				>SLEEP

				inc Index
				inc Index

				lda Index
				cmp #K.PS.MAX+2
				bcc .1

				lda Count
				bne CS.RUN.PS

*				sec

				rts
*--------------------------------------
CS.RUN.KILL		>SS
				>PUSHW L.MSG.PS
				ldy #S.PS.PID+1
				lda (ZPPtr1),y
				>PUSHA
				dey
				lda (ZPPtr1),y
				>PUSHA
				
				ldy #S.PS.pCmdLine+1
				lda (ZPPtr1),y
				>PUSHA
				dey
				lda (ZPPtr1),y
				>PUSHA

				>PUSHBI 4
				>LIBC PrintF
				>SR

				>SS
				ldy #S.PS.PID+1
				lda (ZPPtr1),y
				>PUSHA
				dey
				lda (ZPPtr1),y
				>PUSHA
				>PUSHWI 1				SIG

				>LIBC Kill
				>SR
				rts
*--------------------------------------
CS.RUN.DEV		lda #0
				
.1				sta Index
				>KAPI GetpFDByID
				beq .7

				>STYA ZPPtr1

				lda Index
				>KAPI GetNameByID
				>STYA ZPPtr2

				>SS
				>PUSHW L.MSG.DEV
				>PUSHB Index
				>PUSHW ZPPtr2
				>PUSHBI 3
				>LIBC PrintF
				>SR
				
				>LDYA ZPPtr2
				>LIBC Free
				
				>LDYA ZPPtr1
				>LIBC Close

.7				inc Index
				inc Index

				lda Index
				cmp #K.FD.MAX+2
				bcc .1

				rts
*--------------------------------------
CS.RUN.SYS		stz $280

				>LDYA CmdPtr
				beq .8
				
				>SS
				>PUSHYA
				>PUSHWI $281
				>LIBC RealPath
				>SR
				bcs .8
				
				ldx #$ff
				
.1				inx
				lda $281,x
				bne .1
				
				stx $280
				
				>LDYA ArgPtr
				beq .8
	
				>SS
				>PUSHYA
				>PUSHBI 2
				txa						Arg0 len
				ora #$80				$280
				inc						+1 byte len
				pha
				inc						skip arg1 len byte
				>PUSHA
				>LIBC RealPath
				>SR
				
				ply
				ldy $280
				ldx #$ff
				
.2				iny
				inx
				lda $281,y
				bne .2
				
				txa
				ldy $280
				iny
				sta $280,y

.8				rts
*--------------------------------------
CS.SIG			dec bSec

				clc

.1				lda Timeout
				sbc #0
				tay
				lda Timeout+1
				sbc #0
				bcc .9
				
				>STYA Timeout

.9				sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.CS "Usage : SHUTDOWN <NEXT.SYSTEM>\r\n"
				.CS "   -R : Restart System\r\n"
				.CS "   -T <seconds> : default to 60"
MSG.CRLF		.CZ "\r\n"
MSG.SHUTDOWN	.CZ "\r\e[KSHUTDOWN:System will stop in %D seconds..."
MSG.PS			.CZ "SHUTDOWN:Killing process %5D, %s\r\n"
MSG.DEV			.CZ "SHUTDOWN:Closing device  %5D, %s\r\n"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/shutdown.s
ASM
