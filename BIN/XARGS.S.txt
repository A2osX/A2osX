NEW
  AUTO 3,1
				.LIST OFF
				.OP 65C02
				.OR $2000
				.TF bin/xargs
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ArgIndex		.BS 1
ArgPattern		.BS 1
ZPPtr1			.BS 2
ZPCommandPtr	.BS 2
ZPLineBuf		.BS 2
ZPCmdBuf		.BS 2

pFile			.BS 2

DashArgs		.BS 1

ZS.END			.ED
*--------------------------------------
* File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61                6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #16                 Stack Size
				.DA #ZS.END-ZS.START    Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.CRLF		.DA MSG.CRLF
L.CMD.SPC		.DA CMD.SPC
				.DA 0
*--------------------------------------
CS.INIT         clc
                rts
*--------------------------------------
CS.RUN          jsr CS.RUN.CheckArgs
                bcs CS.RUN.LOOP.RTS

CS.RUN.LOOP     >SLEEP

				>SS
				>PUSHW ZPLineBuf
				>PUSHWI 256
				>PUSHW pFile
				>LIBC FGetS
				>SR
				bcs .9

.2				jsr CS.RUN.EXEC

				bcc CS.RUN.LOOP
				rts

.9              cmp #MLI.E.EOF
                bne .99

                lda #0                    Exit with no Error

.99             sec
CS.RUN.LOOP.RTS rts
*--------------------------------------
CS.RUN.CheckArgs
				jsr CS.RUN.NextArg
				bcs .4

				lda (ZPPtr1)
				cmp #'-'
				beq .1

				lda ArgPattern
				bne .11

				lda ArgIndex
				sta ArgPattern
				>LDYA ZPPtr1
				>STYA ZPCommandPtr
				bra CS.RUN.CheckArgs

.11				lda pFile+1
				bne .97

				>LDYA ZPPtr1
				jsr CS.RUN.OPEN
				bcs .9

				>STYA pFile
				bra CS.RUN.CheckArgs
*--------------------------------------
.1				ldy #1
				lda (ZPPtr1),y

				ldx #OptionList.Cnt-1

.2              cmp OptionList,x
                beq .3

				dex
				bpl .2

				bra .97

.3              txa
				lsr
				beq .98

				tax
				lda #$80
				sta DashArgs-1,x
				bra CS.RUN.CheckArgs
*--------------------------------------
.4              lda pFile+1
				bne .80

				jsr X.IsPipeIn
				bcs .97

				>STYA pFile

.80             >LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPLineBuf

				>LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPCmdBuf

*               clc

.9              rts

.97 			lda #E.SYN

.98             pha
				>LDYA L.MSG.USAGE
				>LIBC PutS
				pla
				sec
				rts
*--------------------------------------
CS.RUN.NextArg  inc ArgIndex
				lda ArgIndex
				>LIBC ArgV
				bcs .9

				>STYA ZPPtr1

.9              rts
*--------------------------------------
CS.RUN.OPEN     >SS
				>PUSHYA
				>PUSHBI O.RDONLY+O.TEXT
				>PUSHBI S.FI.T.TXT
				>PUSHWZ                         Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pFile

.9              rts
*--------------------------------------
CS.RUN.EXEC   	>SS
				>PUSHW ZPCmdBuf
				>PUSHW ZPCommandPtr
				>LIBC StrCpy
				>SR

				>SS
				>PUSHW ZPCmdBuf
				>PUSHW L.CMD.SPC
				>LIBC StrCat
				>SR

				>SS
				>PUSHW ZPCmdBuf
				>PUSHW ZPLineBuf
				>LIBC StrCat
				>SR

*				>LDYA ZPCmdBuf
*				>LIBC PutS

				>SS
				>PUSHW ZPCmdBuf
				>PUSHBI S.PS.F.HOLD
				>LIBC ExecL
				>SR
				bcs .9

				>SLEEP

				ldy #S.PS.ERRNO
				jsr A2osX.GetPSy
				beq .9

				sec

.9				rts
*--------------------------------------
CS.QUIT         >LDYA pFile
                beq .1

                >LIBC FClose

.1              >LDYA ZPLineBuf
                beq .2

                >LIBC Free

.2				>LDYA ZPCmdBuf
				beq .8

                >LIBC Free

.8              clc
                rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
				.INB usr/src/shared/x.ispipein.s
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList      .AS "Hh"
OptionList.Cnt  .EQ *-OptionList
*--------------------------------------
MSG.USAGE       .CS "Usage : XARGS command <file>\r\n"
                .CS "   or : CMD|XARGS command"
MSG.CRLF        .CZ "\r\n"
CMD.SPC         .CZ " "
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
STATBUF			.BS S.STAT
DS.END          .ED
*--------------------------------------
MAN
SAVE usr/src/bin/xargs.s
ASM
