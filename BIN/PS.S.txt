PR#3
PREFIX /A2OSX.SRC
NEW
INC 1
AUTO 6
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF /A2OSX.BOOT/BIN/PS
*--------------------------------------
				.INB /A2OSX.DEV/INC/MACROS.I
				.INB /A2OSX.DEV/INC/A2OSX.I
*--------------------------------------
ZPPTR1			.EQ ZPBIN
ZPPTR2			.EQ ZPBIN+2
ZPPTR3			.EQ ZPBIN+4
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #1					BIN Layout Version 1
				.DA 0
				.DA CS.END-CS.START		Code Length To Relocate
				.DA 0					Data Segment to Allocate
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------				
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.DOEVENT		
				.DA	CS.QUIT
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYAI TSKMGR.TABLE+S.PS
				>STYA ZPPTR1
				stz PS.COUNT

				>LDYA L.MSG0
				>SYSCALL CPrintFYA

.1				lda (ZPPTR1)
				bpl .4
				
				ldy #S.PS.hCMDLINE
				lda (ZPPTR1),y
				>SYSCALL GetMemPtrA
				>PUSHYA
				
				ldy #S.PS.PPID
				lda (ZPPTR1),y
				>PUSHA					Parent PID
				
				lda (ZPPTR1)			push flags
				>PUSHA

				ldy #S.PS.PID
				lda (ZPPTR1),y
				>PUSHA					PID
				>LDYA L.MSG1
				>SYSCALL CPrintFYA

.4				lda ZPPTR1
				clc
				adc #S.PS
				sta ZPPTR1
				bcc .5
				inc ZPPTR1+1
				
.5				inc PS.COUNT
				lda PS.COUNT
				cmp #K.PS.MAX
				bne .1
				
				lda #0					tell TSKMGR that all done ok, but 
				sec						we do not want to stay in memory
				rts
*--------------------------------------
CS.DOEVENT		sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			
PS.COUNT		.BS 1
MSG0			>CSTR "ID  Flags    PID Command Line\r\n"
MSG1			>CSTR "%03d %b %03d %S\r\n"
MAN
SAVE BIN/PS.S
ASM
