NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/ps
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/kernel.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPSPtr			.BS 2
ZPCmdLinePtr	.BS 2
ZPSessionPtr	.BS 2
ZPArgV			.BS 2
Index			.BS 1
ZS.END
				.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.SIG
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
L.MSG2			.DA MSG2
L.MSG3			.DA MSG3
L.STATUS		.DA MSG.INIT
				.DA MSG.RUN
				.DA MSG.SIG
				.DA MSG.QUIT
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #1
				>KAPI ArgV
				bcs CS.RUN.AllPSs

				>LIBC AToI
				bcs .90

				>LIBC GetPS
				bcs .99

				>STYA ZPPSPtr

				jsr CS.RUN.DUMP
				bcs .99

*				jsr CS.RUN.PrintArgs
*				bcs .9

				>LDYA L.MSG3
				>LIBC PutS

				lda #0
				sec
				rts

.90				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN.AllPSs	>LDYA L.MSG0
				>LIBC PutS

				lda #2					skip PS 0

.1				sta Index
				>KAPI GetPSByID
				bcs .7

				>STYA ZPPSPtr

				jsr CS.RUN.DUMP
				bcs .9

*				jsr CS.RUN.PrintArgs
*				bcs .9

				>LDYA L.MSG3
				>LIBC PutS

				>LDYA ZPPSPtr
				>LIBC Free

				>SLEEP

.7				inc Index
				inc Index

				lda Index
				cmp #K.PS.MAX*2
				bcc .1

				lda #0
				sec

.9				rts
*--------------------------------------
CS.RUN.DUMP		jsr CS.RUN.DecodeFlags

				jsr CS.RUN.GetCmdLine

				>SS
				>PUSHW L.MSG1
				ldy #S.PS.Bnk
				lda (ZPPSPtr),y
				>PUSHA

				ldy #S.PS.PPID

.1				iny
				lda (ZPPSPtr),y
				>PUSHA
				dey
				lda (ZPPSPtr),y
				>PUSHA
				iny
				iny
				cpy #S.PS.CPID+2
				bne .1

				ldy #S.PS.CPU
				lda (ZPPSPtr),y
				>PUSHA
				ldy #S.PS.S
				lda (ZPPSPtr),y
				tax
				>PUSHW L.STATUS,x

				>PUSHEA.G PS.FLAGS

*				ldy #S.PS.pSession+1
*				lda (ZPPSPtr),y
*				pha
*				dey
*				lda (ZPPSPtr),y
*				ply
*
*				>STYA ZPSessionPtr

				ldy #S.PS.UID+1
				lda (ZPPSPtr),y
				>PUSHA
				dey
				lda (ZPPSPtr),y
				>PUSHA

				>PUSHW ZPCmdLinePtr

				>PUSHBI 16
				>LIBC PrintF
				>SR
				
				>LDYA ZPCmdLinePtr
				beq .9
				
				>LIBC Free
				
.9				rts
*--------------------------------------
CS.RUN.DecodeFlags
				lda (ZPPSPtr)			Get flags
				ldy #PS.FLAGS+7
				ldx #7

.2				lsr
				pha
				lda #'-'
				bcc .3

				lda MSG.FLAGS,x

.3				sta (pData),y
				pla
				dey
				dex
				bpl .2

				rts
*--------------------------------------
CS.RUN.GetCmdLine
				stz ZPCmdLinePtr+1
				
				ldy #S.PS.PID
				lda (ZPPSPtr),y
				pha
				iny
				lda (ZPPSPtr),y
				ply

				>KAPI GetPSCmdLine
				bcs .9
				
				>STYA ZPCmdLinePtr
				
.9				rts				
*--------------------------------------
CS.RUN.PrintArgs
				ldy #S.PS.pARGV+1
				lda (ZPPSPtr),y
				beq .8					NO ARGV in PS0

				sta ZPArgV+1
				dey
				lda (ZPPSPtr),y
				sta ZPArgV

.1				lda (ZPArgV)
				beq .8

				>SS
				>PUSHW L.MSG2
				>PUSHW ZPArgV
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs .9

.2				inc ZPArgV
				bne .3

				inc ZPArgV+1

.3				lda (ZPArgV)
				bne .2

				inc ZPArgV
				bne .1

				inc ZPArgV+1
				bra .1

.8				clc

.9				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG0			.CZ "\e[?7lBnk pPID  PID   cPID   CPU% Status Flags    UID   CmdLine"
MSG1			.CZ "%3d %5D %5D %5D  %3d%% %6s %s %5D %s"
MSG2			.CS " %s"
MSG3			.DA #0
MSG.FLAGS		.AS "HS???XEN"
MSG.INIT		.AZ "Init"
MSG.RUN			.AZ "Run"
MSG.SIG			.AZ "Signal"
MSG.QUIT		.AZ "Quit"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
PS.FLAGS		.BS 9
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/ps.s
ASM
