PR#3
PREFIX /DATA/A2OSX
NEW
INC 1
AUTO 6
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF BIN/PS
*--------------------------------------
				.INB INC/MACROS.I
				.INB INC/A2OSX.I
				.INB INC/KERNEL.I
				.INB INC/LIBSTR.I
*--------------------------------------
ZPPTR1			.EQ ZPBIN
ZPPTR2			.EQ ZPBIN+2
ZPPTR3			.EQ ZPBIN+4
*--------------------------------------
* Main entry point
*--------------------------------------
* Code signature and INIT table
*--------------------------------------
* CLD 			$D8
* JMP (*,x)		$7C
* 				#JMPTABLE
*				/JMPTABLE
*--------------------------------------
CS.START		cld
				jmp (.1,x)
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.DOEVENT		
				.DA	CS.QUIT
L.LIBSTR		.DA LIBSTR
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
L.MSG2			.DA MSG2
				.DA 0
				.DA CS.END-CS.START		Code Length To Relocate
				.DA 0					Data Segment to Allocate
*--------------------------------------
CS.INIT			>LIBLOADP L.LIBSTR
				sta hLibStr

				>LDYAI TSKMGR.TABLE
				>STYA ZPPTR1
				stz PS.COUNT

				>PUSHW L.MSG0
				>LIBCALL hLibStr,LIBSTR.PRINTF

.1				lda (ZPPTR1)
				bpl .4
				ldy #S.PS.hARGS
				lda (ZPPTR1),y
				beq .2
				>SYSCALL SYS.GetMemPtrA
				>PUSHYA
				bra .3
.2				>PUSHW L.MSG2
.3				ldy #S.PS.hCMD
				lda (ZPPTR1),y
				>SYSCALL SYS.GetMemPtrA
				>PUSHYA
				
				ldy #S.PS.PID
				lda (ZPPTR1),y
				>PUSHA					Parent PID
				
				lda (ZPPTR1)			push flags
				>PUSHA

				ldy #S.PS.ID
				lda (ZPPTR1),y
				>PUSHA					DevID
				>PUSHW L.MSG1
				>LIBCALL hLibStr,LIBSTR.PRINTF

.4				lda ZPPTR1
				clc
				adc #S.PS.SIZE
				sta ZPPTR1
				bcc .5
				inc ZPPTR1+1
.5				inc PS.COUNT
				lda PS.COUNT
				cmp #K.PS.MAX
				beq .8
				jmp .1
				
.8				lda #0					tell TSKMGR that all done ok, but 
				sec						we do not want to stay in memory
				rts
*--------------------------------------
CS.RUN
CS.DOEVENT		clc
				rts
*--------------------------------------
CS.QUIT			lda hLibStr
				>SYSCALL SYS.UnloadLibA
				clc
				rts
*--------------------------------------
CS.END			
hLibStr			.BS	1
PS.COUNT		.BS 1
LIBSTR			>PSTRING "libstr.o"
MSG0			>CSTRING "hPS  Flags    PID CMD              ARGS\n"
MSG1			>CSTRING " %03d %b $%h %16S %32S\n"
MSG2			>PSTRING "(none)"
MAN
SAVE BIN/PS.S
ASM
