NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/ifconfig
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/mli.e.i
				.INB inc/nic.i
				.INB inc/eth.i
				.INB inc/ip.i
				.INB inc/net.dhcp.i
				.INB inc/net.dns.i
*--------------------------------------
RETRY.MAX		.EQ 3
TIMEOUT.MAX		.EQ 250					5 sec.
RECV.MAX		.EQ 2048
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pDevPath		.BS 2
pDevName		.BS 2
hFD				.BS 2
hSocket			.BS 2

pBuf			.BS 2
pMsg			.BS 2
MsgLen			.BS 2
pDomainName		.BS 2

pArg			.BS 2

Idx				.BS 1
AF				.BS 1
KW				.BS 1
TimeOut			.BS 1
Retry			.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T10TH
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #0					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.KW.CMD		.DA KW.CMD
L.KW.AF			.DA KW.AF
L.KW.INET		.DA KW.INET

L.MSG.USAGE		.DA MSG.USAGE

L.IPCFG.DHCP	.DA IPCFG.DHCP

L.MSG.IF		.DA MSG.IF

J.CMD			.DA CS.RUN.UP
				.DA CS.RUN.DOWN

J.AF			.DA INET
				.DA DDP

J.INET			.DA INET.ADDR
				.DA INET.MASK
				.DA INET.DHCP

J.Link			.DA CS.RUN.LINK.LO
				.DA CS.RUN.LINK.ETH

L.SScanF.IP		.DA SScanF.IP

L.MSG.Lo		.DA MSG.Lo
L.MSG.Eth		.DA MSG.Eth

L.MSG.INET		.DA MSG.INET

L.MSG.DDP		.DA MSG.DDP

L.MSG.STATS		.DA MSG.STATS
*--------------------------------------
* DHCP
*--------------------------------------
L.SA.LOCAL		.DA SA.LOCAL
L.SA.REMOTE		.DA SA.REMOTE
L.SA.DEST		.DA SA.DEST
L.DHCP.DISC		.DA DHCP.DISC
L.DHCP.REQ		.DA DHCP.REQ

L.ROUTE.DFLT	.DA ROUTE.DFLT
L.SA.DNS		.DA SA.DNS
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			inc Idx
				lda Idx
				>KAPI ArgV
				bcs CS.RUN.DumpAll

				>STYA pDevName

				>LIBC GetDevByName
				bcs .99

				sty hFD

				jsr CS.RUN.GetDIB
				bcs .99

				>LDA.G DEV.DIB+S.DIB.T
				cmp #S.DIB.T.NIC
				bne .98

				inc Idx
				lda Idx
				>KAPI ArgV
				bcs .8

				>STYA pArg

				>LDYA L.KW.CMD
				jsr CS.RUN.KW
				bcs .10

				jmp (J.CMD,x)

.10				>LDYA L.KW.AF
				jsr CS.RUN.KW
				bcc .1

				ldx #0					default to inet

.1				stx AF

				jmp (J.AF,x)

.8				jsr CS.RUN.DumpNIC

				lda #0
				sec
				rts

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

.98				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN.DumpAll	lda #0

.1				sta hFD

				>KAPI GetNameByID
				bcs .7

				>STYA pDevPath

				jsr CS.RUN.GetDIB
				bcs .6

				>LDA.G DEV.DIB+S.DIB.T
				cmp #S.DIB.T.NIC
				bne .6

				lda pDevPath
				clc
				adc #5
				sta pDevName
				lda pDevPath+1
				adc #0
				sta pDevName+1

				jsr CS.RUN.DumpNIC

.6				>LDYA pDevPath
				>LIBC Free

.7				lda hFD

				inc
				inc
				cmp #K.FD.MAX*2
				bcc .1

				lda #0

*				sec

.9				rts
*--------------------------------------
CS.RUN.DumpNIC	>SS
				>PUSHW L.MSG.IF
				>PUSHW pDevName
				>PUSHBI 2
				>LIBC PrintF
				>SR

				jsr CS.RUN.GetDCB

				>LDA.G DEV.DIB+S.DIB.ST
				tax
				jsr CS.RUN.LINK

				jsr CS.RUN.GetIPCfg
				bcs .2

				>SS
				>PUSHW L.MSG.INET
				ldx #8

				ldy #DEV.IPCFG+S.IPCFG.IP

.1				>PUSHB (pData),y
				iny
				dex
				bne .1

				>PUSHBI 8
				>LIBC PrintF
				>SR

.2				>LEA.G DEV.DCB+S.DCB.NIC.RXP
				>STYA R1

				>SS
				>PUSHW L.MSG.STATS

				ldx #12					12*DWORD

.3				ldy #3

.4				>PUSHB (R1),y
				dey
				bpl .4

				lda R1
				clc
				adc #4
				sta R1
				bcc .5

				inc R1+1

.5				dex
				bne .3

				>PUSHBI 48				12*DWORD
				>LIBC PrintF
				>SR

				rts
*--------------------------------------
CS.RUN.LINK		jmp (J.Link,x)

CS.RUN.LINK.LO	>LDYA L.MSG.Lo
				>LIBC PutS
				rts

CS.RUN.LINK.ETH	>SS
				>PUSHW L.MSG.Eth
				ldx #6

				ldy #DEV.DCB+S.DCB.NIC.MAC

.1				>PUSHB (pData),y
				iny
				dex
				bne .1

				>PUSHBI 6
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.UP		>LDYA hFD
				>KAPI IfUp
				bcs .9

				lda #0
				sec

.9				rts
*--------------------------------------
CS.RUN.DOWN		>LDYA hFD
				>KAPI IfDown
				bcs .9

				lda #0
				sec

.9				rts
*--------------------------------------
CS.RUN.GetDIB	lda #S.IOCTL.S.GETDIB
				>STA.G DEV.IOCTL+S.IOCTL.S

				>LEA.G DEV.DIB
				>STYA.G DEV.IOCTL+S.IOCTL.BUFPTR

				>SS
				>PUSHW hFD
				>PUSHBI IOCTL.STATUS
				>PUSHEA.G DEV.IOCTL
				>LIBC IOCTL
				>SR

				rts
*--------------------------------------
CS.RUN.GetDCB	lda #S.IOCTL.S.GETDCB
				>STA.G DEV.IOCTL+S.IOCTL.S

				>LEA.G DEV.DCB
				>STYA.G DEV.IOCTL+S.IOCTL.BUFPTR

				>SS
				>PUSHW hFD
				>PUSHBI IOCTL.STATUS
				>PUSHEA.G DEV.IOCTL
				>LIBC IOCTL
				>SR

				rts
*--------------------------------------
CS.RUN.GetIPCfg	>LEA.G DEV.IPCFG
				>STYA R1

				ldy #S.IPCFG-1
				lda #0

.1				sta (R1),y
				dey
				bpl .1

				>SS
				>PUSHWI AF_INET
				>PUSHW hFD
				>PUSHW R1
				>KAPI GetIfCfg
				>SR

				rts
*--------------------------------------
CS.RUN.SetIFCfg	>SS
				>PUSHWI AF_INET
				>PUSHW hFD
				>PUSHEA.G DEV.IPCFG
				>KAPI SetIfCfg
				>SR

				rts
*--------------------------------------
CS.RUN.KW		>STYA R1

				ldx #0

.1				ldy #$FF

.2				iny
				lda (R1),y
				beq .5

				cmp (pArg),y
				beq .2

.3				iny
				lda (R1),y
				bne .3

.4				inx
				inx

				tya
				sec
				adc R1
				sta R1
				bcc .1

				inc R1+1
				bra .1

.5				tya
				beq .9

				lda (pArg),y
				bne .4

				clc
				rts

.9				sec
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
				.INB usr/src/bin/ifconfig.s.ddp
				.INB usr/src/bin/ifconfig.s.inet
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
KW.CMD			.AZ "up"
				.AZ "down"
				.DA #0
*--------------------------------------
KW.AF			.AZ "inet"				aftype
				.AZ "ddp"
				.DA #0
*--------------------------------------
KW.INET			.AZ "address"			options
				.AZ "netmask"
				.AZ "auto"
				.DA #0
*--------------------------------------
SScanF.IP		.AZ "%d.%d.%d.%d"
*--------------------------------------
MSG.IF			.CZ "\r\n%8s Link encap: "
MSG.INET		.CZ "         inet addr:  %d.%d.%d.%d Mask: %d.%d.%d.%d\r\n"
MSG.DDP			.CZ "         ddp net/node: %D/%d\r\n"
MSG.STATS		.CS "         RX packets: %u bytes: %u\r\n"
				.CS "         RX errors:  %u dropped: %u overruns: %u frame: %u\r\n"
				.CS "         TX packets: %u bytes: %u\r\n"
				.CZ "         TX errors:  %u dropped: %u overruns: %u carrier: %u\r\n"
*--------------------------------------
MSG.Lo			.CZ "Local Loopback"
MSG.Eth			.CZ "Ethernet HWaddr %h:%h:%h:%h:%h:%h\r\n"
*--------------------------------------
MSG.USAGE		.CS "Usage : IFCONFIG <switches> interface [aftype] options ...\r\n"
				.CZ "      : \r\n"
*--------------------------------------
IPCFG.DHCP		.BS 1					S
				.BS 1					DevF
				.BS 2					DevFD
				.BS 6					MAC
				.DA #0,#0,#0,#0			IP
				.DA #255,#255,#255,#255	MASK
				.DA #0,#0,#0,#0			DHCPSRVR
*--------------------------------------
SA.LOCAL		.DA AF_INET				S.S.SA.IN.AF
				.HS 00.00.00.00			S.S.SA.IN.ADDR
				.DA UDP.PORT.DHCPC		S.S.SA.IN.PORT
*--------------------------------------
SA.REMOTE		.DA AF_INET				S.S.SA.IN.AF
				.HS 00.00.00.00			S.S.SA.IN.ADDR
				.DA UDP.PORT.DHCPS		S.S.SA.IN.PORT
*--------------------------------------
SA.DEST			.DA AF_INET				S.S.SA.IN.AF
				.HS FF.FF.FF.FF			S.S.SA.IN.ADDR
				.DA UDP.PORT.DHCPS		S.S.SA.IN.PORT
*--------------------------------------
DHCP.DISC		.HS 01010600			OP,HTYPE,HLEN,HOPS
DHCP.DISC.XID	.BS 4
				.HS 0000				SECS
				.DA S.DHCP.FLAGS.BRDCST
				.HS	00000000			CIADDR
DHCP.DISC.YIADDR	.HS 00000000
				.HS 00000000			SIADDR
DHCP.DISC.GIADDR	.HS 00000000
DHCP.DISC.CHADDR	.HS 00000000.00000000.00000000.00000000
				.BS 64					SNAME
				.BS 128					FILE
				.HS 63825363			COOKIE

				.DA #S.DHCP.OPTIONS.MT
				.DA #1
				.DA #S.DHCP.OPTIONS.MT.DHCPDiscover

				.DA #S.DHCP.OPTIONS.PRL
				.DA #4
				.DA #S.DHCP.OPTIONS.PRL.MASK
				.DA #S.DHCP.OPTIONS.PRL.GW
				.DA #S.DHCP.OPTIONS.PRL.DNS
				.DA #S.DHCP.OPTIONS.PRL.DOMAIN

				.DA #S.DHCP.OPTIONS.END

DHCP.DISC.LEN	.EQ *-DHCP.DISC
*--------------------------------------
DHCP.REQ		.HS 01010600			OP,HTYPE,HLEN,HOPS
DHCP.REQ.XID	.BS 4
				.HS 0000				SECS
				.DA S.DHCP.FLAGS.BRDCST
				.HS	00000000			CIADDR
DHCP.REQ.YIADDR	.HS 00000000
				.HS 00000000			SIADDR
				.HS 00000000			GIADDR
DHCP.REQ.CHADDR	.HS 00000000.00000000.00000000.00000000
				.BS 64					SNAME
				.BS 128					FILE
				.HS 63825363			COOKIE

				.DA #S.DHCP.OPTIONS.MT
				.DA #1
				.DA #S.DHCP.OPTIONS.MT.DHCPRequest

				.DA #S.DHCP.OPTIONS.REQIP
				.DA #4
DHCP.REQ.OPT.REQIP	.BS 4

				.DA #S.DHCP.OPTIONS.SRVID
				.DA #4
DHCP.REQ.OPT.SVRIP	.BS 4

				.DA #S.DHCP.OPTIONS.END

DHCP.REQ.LEN 	.EQ *-DHCP.REQ
*--------------------------------------
ROUTE.DFLT		.DA AF_INET
				.DA #0,#0,#0,#0			ADDR
				.DA #0,#0,#0,#0			MASK
ROUTE.DFLT.GW		.BS 4				GW
ROUTE.DFLT.IF		.BS 1				IF
				.DA #S.ROUTE.F.GW		F
				.DA 1					METRIC
*--------------------------------------
SA.DNS			.DA AF_INET				S.S.SA.IN.AF
			.BS 4						S.S.SA.IN.ADDR
				.DA UDP.PORT.DNS		S.S.SA.IN.PORT
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DEV.IOCTL		.BS S.IOCTL
DEV.DIB			.BS S.DIB
DEV.DCB			.BS S.DCB.NIC
DEV.IPCFG		.BS S.IPCFG
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/ifconfig.s
ASM
