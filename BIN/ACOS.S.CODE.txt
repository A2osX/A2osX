NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
CODE.PUSHYXI	lda #$A9				LDA #imm
				jsr CODE.EmitByte
				txa
				jsr CODE.EmitByte
				jsr CODE.PUSHA

				lda #$A9				LDA #imm
				jsr CODE.EmitByte
				tya
				jsr CODE.EmitByte
*--------------------------------------
CODE.PUSHA		ldx #0

.1				lda CCODE.PUSHA,x
				jsr CODE.EmitByte
				inx
				cpx #CCODE.PUSHA.LEN
				bne .1

				rts
*--------------------------------------
CODE.PUSHINT16	ldy #1

.1				lda #$A9				LDA #imm
				jsr CODE.EmitByte

				lda (pStack),y
				jsr CODE.EmitByte

				jsr CODE.PUSHA
				dey
				bpl .1

				rts
*--------------------------------------
CODE.LDYXI		lda #$A0				LDY #imm
				jsr CODE.EmitByte
				tya
				jsr CODE.EmitByte

				lda #$A2				LDX #imm
				jsr CODE.EmitByte
				txa
				jmp CODE.EmitByte
*--------------------------------------
CODE.JSRRT		lda #$20				JSR
				jsr CODE.EmitByte
				lda J.RT,x
				jsr CODE.EmitByte
				lda J.RT+1,x
				jmp CODE.EmitByte
*--------------------------------------
CODE.FPUCALL	lda #$A2				LDX #imm
				jsr CODE.EmitByte
				txa
				jsr CODE.EmitByte
				lda #$20				JSR
				jsr CODE.EmitByte
				lda #A2osX.FPUCALL
				jsr CODE.EmitByte
				lda /A2osX.FPUCALL
				bra CODE.EmitByte
*--------------------------------------
CODE.FPRINTCRLF	ldx #C.CR
				jsr CODE.PutChar
				ldx #C.LF
*--------------------------------------
CODE.PutChar	lda #$A9				LDA #imm
				jsr CODE.EmitByte
				txa
				jsr CODE.EmitByte
				ldx #SYS.PutChar
*--------------------------------------
CODE.SYSCALL	lda #$A2				LDX #imm
				jsr CODE.EmitByte
				txa
				jsr CODE.EmitByte
				lda #$20				JSR
				jsr CODE.EmitByte
				lda #A2osX.SYSCALL
				jsr CODE.EmitByte
				lda /A2osX.SYSCALL
*--------------------------------------
CODE.EmitByte	sta (ZPCodeBufPtr)
				inc ZPCodeBufPtr
				bne .8

				inc ZPCodeBufPtr+1

.8				rts
*--------------------------------------
MAN
SAVE usr/src/bin/acos.s.code
LOAD usr/src/bin/acos.s
ASM
