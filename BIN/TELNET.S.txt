NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/telnet
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/eth.i
				.INB inc/net.tcpip.i
				.INB inc/net.telnet.i
				.INB inc/net.db.i
*--------------------------------------
TIMEOUT.MAX		.EQ 20					20 sec.
BUFSIZE			.EQ 256
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
BufLen			.BS 2
pBuf			.BS 2
hSocket			.BS 2
TimeOut			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.AI.HINTS		.DA AI.HINTS
*--------------------------------------
L.MSG.IPKO		.DA MSG.IPKO
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.UNKNOWN	.DA MSG.UNKNOWN
L.MSG.HOSTOK	.DA MSG.HOSTOK
L.MSG.SKTOK		.DA MSG.SKTOK
L.MSG.SKTERR	.DA MSG.SKTERR
L.MSG.IOERR		.DA MSG.IOERR
L.MSG.USER		.DA MSG.USER
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #TIMEOUT.MAX
				sta TimeOut

.2				>SLEEP

				lda #1
				>KAPI ArgV
				bcs .9
				
				>SS
				>PUSHYA
				>PUSHWZ
				>PUSHW L.AI.HINTS
				>PUSHEA.G pAI
				>LIBC GetAddrInfo
				>SR
				bcc CS.RUN.HOSTOK

				lda TimeOut
				bne .2

				>SS
				>PUSHW L.MSG.UNKNOWN
				lda #1
				>KAPI ArgV
				>PUSHYA
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bra .98

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

.98				lda #E.SYN
				sec
.99				rts

CS.RUN.HOSTOK	>LDYA.G pAI
				>STYA R1

				ldy #S.ADDRINFO.pSOCKADDR
				lda (R1),y
				sta R2
				iny
				lda (R1),y
				sta R2+1
				
				>LEA.G SA
				>STYA R3
				
				ldy #S.SOCKADDR-1
				
.1				lda (R2),y
				sta (R3),y
				dey
				bpl .1
			
				lda #2
				>KAPI ArgV
				bcs CS.RUN.PORTOK

				>LIBC AToI
				bra .3
	
.2				>LDYAI TCP.PORT.TELNET
		
.3				>STYA.G SA+S.SOCKADDR.PORT

CS.RUN.PORTOK	>SS
				>PUSHW L.MSG.HOSTOK

				ldx #4
				ldy #S.SOCKADDR.ADDR

.1				>PUSHB (R3),y
				iny
				dex
				bne .1

				ldy #S.SOCKADDR.PORT
				>PUSHB (R3),y
				iny
				>PUSHB (R3),y

				lda #1
				>KAPI ArgV
				>PUSHYA

				>PUSHBI 8
				>LIBC PrintF
				>SR
*--------------------------------------
CS.RUN.CONNECT	>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_STREAM
				>PUSHWI IPPROTO_TCP
				>LIBC Socket
				>SR
				bcs .9

				>STYA hSocket

				lda #TIMEOUT.MAX
				sta TimeOut

.3				>SLEEP

				>SS
				>PUSHW hSocket
				>PUSHW.G SA
				>LIBC Connect
				>SR
				bcc .4

				cmp #E.SKT.NOCONN
				bne .9

				lda TimeOut
				bne .3

				lda #E.SKT.NOCONN
				bra .9

.4				>LDYA L.MSG.SKTOK
				>LIBC PutS

				>LDYAI BUFSIZE
				>LIBC Malloc
				bcc .5

.9				rts

.5				>STYA pBuf
				
*--------------------------------------
CS.RUN.LOOP		>SLEEP

				>SS
				>LDYA hSocket
				jsr CS.RUN.PushRead
				>LIBC Recv
				>SR
				bcc .1
				
				cmp #E.NODATA
				bne CS.RUN.SKTERR
				
				bra .2

.1				>STYA BufLen

				jsr CS.RUN.GetStdOut
				jsr CS.RUN.PushWrite
				>LIBC FWrite
				bcs CS.RUN.IOERR

.2				jsr CS.RUN.GetStdIn
				>LIBC FEOF
				bcs CS.RUN.IOERR

				tay
				bne CS.RUN.LOOP			EOF = true, no char from STDIN

				jsr CS.RUN.GetStdIn
				jsr CS.RUN.PushRead
				>LIBC FRead
				bcs CS.RUN.IOERR

				>STYA BufLen

				lda (pBuf)
				cmp #$14				Ctrl-T
				beq CS.RUN.USERINT

				>SS
				>LDYA hSocket
				jsr CS.RUN.PushWrite
				>LIBC Send
				>SR
				bcc CS.RUN.LOOP
*--------------------------------------
CS.RUN.SKTERR	ldx #0

				bra CS.RUN.ERR
*--------------------------------------
CS.RUN.IOERR	ldx #2

CS.RUN.ERR		pha
				tay
				>SS
				>PUSHW L.MSG.SKTERR,x
				tya
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				>SR
				pla
				sec
				rts
*--------------------------------------
CS.RUN.USERINT	>LDYA L.MSG.USER
				>LIBC PutS
				lda #0
				sec
				rts
*--------------------------------------
CS.RUN.GetStdIn	ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				rts
*--------------------------------------
CS.RUN.GetStdOut
				ldy #S.PS.pStdOut
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				rts
*--------------------------------------
CS.RUN.PushRead	>PUSHYA
				>PUSHW pBuf
				>PUSHWI BUFSIZE
				rts
*--------------------------------------
CS.RUN.PushWrite
				>PUSHYA
				>PUSHW pBuf
				>PUSHW BufLen
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				sec						do not discard TIMER event
				rts
*--------------------------------------
CS.QUIT			>LDYA hSocket
				beq .1

				>LIBC Shutdown

.1				>LDYA pBuf
				beq .8

				>LIBC Free

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.IPKO		.CZ "TCP/IP Not Loaded/Configured."
MSG.USAGE		.CZ "Usage : TELNET <ip|host> [port]"
MSG.UNKNOWN		.CZ "%s: Unknown host\r\n"
MSG.HOSTOK		.CZ "Connecting to %d.%d.%d.%d:%D (%s)..."
MSG.SKTOK		.CS "Connected\r\n"
				.CZ "(Exit key is Ctrl-T)"
MSG.SKTERR		.CZ "Socket Error : $%h\r\n"
MSG.IOERR		.CZ "I/O Error : $%h\r\n"
MSG.USER		.CZ "User interrupt."
*--------------------------------------
AI.HINTS		.DA 0					S.ADDRINFO.F			
				.DA AF_INET             S.ADDRINFO.AF			
				.DA 0                   S.ADDRINFO.SOCKTYPE		
				.DA 0                   S.ADDRINFO.PROTO		
				.DA 0                   S.ADDRINFO.ALEN			
				.DA 0                   S.ADDRINFO.pSOCKADDR	
				.DA 0                   S.ADDRINFO.pNAME		
				.DA 0                   S.ADDRINFO.pNEXT		
*--------------------------------------
SA.REMOTE		.DA AF_INET				S.SOCKADDR.AF
				.BS 4					S.SOCKADDR.ADDR
				.DA TCP.PORT.TELNET
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
				
pAI				.BS 2
SA				.BS S.SOCKADDR
*--------------------------------------
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/telnet.s
ASM
