NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/telnet
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
				.INB inc/net.telnet.i
				.INB inc/net.db.i
*--------------------------------------
TIMEOUT.MAX		.EQ 20					20 sec.
BUFSIZE			.EQ 256
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
BufLen			.BS 2
pBuf			.BS 2
hSocket			.BS 2
TimeOut			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.UNKNOWN	.DA MSG.UNKNOWN
L.MSG.CONNECT	.DA MSG.CONNECT
L.MSG.SKTOK		.DA MSG.SKTOK
L.MSG.SKTERR	.DA MSG.SKTERR
L.MSG.IOERR		.DA MSG.IOERR
L.MSG.USER		.DA MSG.USER
L.SA.LOCAL		.DA SA.LOCAL
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #TIMEOUT.MAX
				sta TimeOut

				lda #1
				>KAPI ArgV
				bcs .9

				>STYA R1

.2				>SLEEP

				>SS
				>PUSHW R1
				>PUSHWZ
				>PUSHWZ
				>PUSHEA.G pAI
				>LIBC GetAddrInfo
				>SR
				bcc CS.RUN.HOSTOK

				cmp #GAI.E.AGAIN
				bne .3

				lda TimeOut
				bne .2

.3				>SS
				>PUSHW L.MSG.UNKNOWN
				>PUSHW R1
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bra .98

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

.98				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN.HOSTOK	>LDYA.G pAI
				>STYA R1

				ldy #S.ADDRINFO.pSOCKADDR
				lda (R1),y
				sta R2
				iny
				lda (R1),y
				sta R2+1

				>LEA.G SA.REMOTE
				>STYA R3

				ldy #S.SA.IN-1

.1				lda (R2),y
				sta (R3),y
				dey
				bpl .1

				lda #2
				>KAPI ArgV
				bcs .2

				>LIBC AToI
				bra .3

.2				>LDYAI TCP.PORT.TELNET

.3				>STYA.G SA.REMOTE+S.SA.IN.PORT
*--------------------------------------
CS.RUN.PORTOK	>SS
				>PUSHW L.MSG.CONNECT

				ldx #4
				ldy #S.SA.IN.ADDR

.1				>PUSHB (R3),y
				iny
				dex
				bne .1

				ldy #S.SA.IN.PORT+1
				>PUSHB (R3),y
				dey
				>PUSHB (R3),y

				lda #1
				>KAPI ArgV
				>PUSHYA

				>PUSHBI 8
				>LIBC PrintF
				>SR
*--------------------------------------
CS.RUN.CONNECT	>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_STREAM
				>PUSHWZ
				>LIBC Socket
				>SR
				bcs .9

				>STYA hSocket

				>SS
				>PUSHW hSocket
				>PUSHW L.SA.LOCAL
				>PUSHWI S.SA.IN
				>LIBC Bind
				>SR
				bcs .9

				lda #TIMEOUT.MAX
				sta TimeOut

.3				>SLEEP

				>SS
				>PUSHW hSocket
				>PUSHEA.G SA.REMOTE
				>PUSHWI S.SA.IN
				>LIBC Connect
				>SR
				bcc .4

				cmp #SKT.E.NOCONN
				bne .9

				ldx TimeOut
				bne .3

.9				jmp CS.RUN.SKTERR

.4				>LDYA L.MSG.SKTOK
				>LIBC PutS

				>LDYAI BUFSIZE
				>LIBC Malloc
				bcc .5

				rts

.5				>STYA pBuf

*--------------------------------------
CS.RUN.LOOP		>SLEEP

				>SS
				>LDYA hSocket
				jsr CS.RUN.PushRead
				>PUSHWZ					flags
				>LIBC Recv
				>SR
				bcc .1

				cmp #E.NODATA
				bne CS.RUN.SKTERR

				bra .2

.1				>STYA BufLen

				>SS
				jsr CS.RUN.GetStdOut
				jsr CS.RUN.PushWrite
				>LIBC FWrite
				>SR
				bcs CS.RUN.IOERR

.2				jsr CS.RUN.GetStdIn
				>LIBC FEOF
				bcs CS.RUN.IOERR

				tay
.55				bne CS.RUN.LOOP			EOF = true, no char from STDIN

				>SS
				jsr CS.RUN.GetStdIn
				jsr CS.RUN.PushRead
				>LIBC FRead
				>SR
				bcs CS.RUN.IOERR

				>STYA BufLen

				lda (pBuf)
				cmp #$14				Ctrl-T
				beq CS.RUN.USERINT

				>SS
				>LDYA hSocket
				jsr CS.RUN.PushWrite
				>PUSHWZ					flags
				>LIBC Send
				>SR
				bcc .55
*--------------------------------------
CS.RUN.SKTERR	pha
				tay
				>SS
				>PUSHW L.MSG.SKTERR
				tya
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				>SR
				pla
				sec
				rts
*--------------------------------------
CS.RUN.IOERR	pha
				tay
				>SS
				>PUSHW L.MSG.IOERR
				tya
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				>SR
				pla
				sec
				rts
*--------------------------------------
CS.RUN.USERINT	>LDYA L.MSG.USER
				>LIBC PutS
				lda #0
				sec
				rts
*--------------------------------------
CS.RUN.GetStdIn	ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				rts
*--------------------------------------
CS.RUN.GetStdOut
				ldy #S.PS.pStdOut
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				rts
*--------------------------------------
CS.RUN.PushRead	>PUSHYA
				>PUSHW pBuf
				>PUSHWI BUFSIZE
				rts
*--------------------------------------
CS.RUN.PushWrite
				>PUSHYA
				>PUSHW pBuf
				>PUSHW BufLen
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				sec						do not discard TIMER event
				rts
*--------------------------------------
CS.QUIT			ldy hSocket
				beq .1

				lda hSocket+1

				>LIBC Shutdown

.1				>LDYA pBuf
				beq .8

				>LIBC Free

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.CZ "Usage : TELNET <ip|host> [port]"
MSG.UNKNOWN		.CZ "%s: Unknown host\r\n"
MSG.CONNECT		.CZ "Connecting to %d.%d.%d.%d:%D (%s)..."
MSG.SKTOK		.CS "Connected\r\n"
				.CZ "(Exit key is Ctrl-T)"
MSG.SKTERR		.CZ "[%h]:Socket Error.\r\n"
MSG.IOERR		.CZ "[%h]:I/O Error.\r\n"
MSG.USER		.CZ "User interrupt."
*--------------------------------------
SA.LOCAL		.DA AF_INET
				.DA #0,#0,#0,#0			any address
				.DA 0					Dyn port
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

pAI				.BS 2
SA.REMOTE		.BS S.SA.IN
*--------------------------------------
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/telnet.s
ASM
