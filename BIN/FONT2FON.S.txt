NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/font2fon
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/io.i
				.INB inc/gfx.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
hFDGFX			.BS 2
hFont			.BS 2

pInFilename		.BS 2
pOutFilename	.BS 2

pInFILE			.BS 2
pOutFILE		.BS 2

pInBuf			.BS 2
pOutBuf			.BS 2

bBatch			.BS 1
bVariableW		.BS 1

Cnt				.BS 1

CH				.BS 1
CV				.BS 1

pHGR			.BS 2

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.MSG.USAGE		.DA MSG.USAGE
L.DEVNAME.GFX	.DA DEVNAME.GFX
L.FONTFILE		.DA FONTFILE
L.MSG.Stat		.DA MSG.Stat
L.CB.TEXT		.DA CB.TEXT
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			jsr GetArgs
				bcs .9

				bit bBatch
				bmi .1

				jsr GFX.Open
				bcs .9

.1				jsr LoadInFile
				bcs .9

				bit bBatch
				bmi .2

				jsr DrawFont

				jsr PrintStat
				bcs .9

				jsr GFX.GetC

.2

				lda #0

				sec

.9				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			>LDYA CB.TEXT+S.CB.hSrc
				beq .1

				>KAPI FreeBuf

.1				>LDYA hFont
				beq .2

				>KAPI FreeBuf

.2				>LDYA pInBuf
				beq .3

				>LIBC Free

.3				>LDYA pOutBuf
				beq .4

				>LIBC Free

.4


.7				ldy hFDGFX
				beq .8

				lda hFDGFX+1

				>LIBC Close

.8				clc
				rts
*--------------------------------------
GetArgs			inc Cnt

				lda Cnt
				>KAPI ArgV
				bcs .7

				>STYA R1
				lda (R1)
				cmp #'-'
				bne .3

				ldx #OptionVars-OptionList-1

.1				cmp OptionList,x
				beq .2

				dex
				bpl .1

.99				>LDYA L.MSG.USAGE
				>LIBC PutS
				lda #E.SYN
				sec
				rts

.2				ldy OptionVars,x
				lda #$FF
				sta $0,y
				bra GetArgs

.3				lda pInFilename+1
				bne .4

				>LDYA R1
				>STYA pInFilename

				bra GetArgs

.4				lda pOutFilename+1
				bne .99

				>LDYA R1
				>STYA pOutFilename
				bra GetArgs

.7				lda pInFilename+1
				beq .99

				clc
				rts
*--------------------------------------
LoadInFile		>SS
				>PUSHW pInFilename
				>PUSHW pData
				>LIBC Stat
				>SR
				bcs .9

				>LDYA.G STATBUF+S.STAT.SIZE
				>LIBC Malloc
				bcs .9

				>STYA pInBuf

				>LDYA pInFilename
				jsr fOpenYAR
				bcs .9

				>SS
				>PUSHW pInFILE
				>PUSHW pInBuf
				>PUSHW.G STATBUF+S.STAT.SIZE
				>LIBC FRead
				>SR
				bcs .9

				>LDYA pInFILE
				>LIBC FClose

				stz pInFILE+1

.9				rts
*--------------------------------------
DrawFont		>LDA.G STATBUF+S.STAT.P.TYPE
				cmp #5
				beq DrawFont05

				cmp #6
				bne .9

				jmp DrawFont06

.9				sec
				rts
*--------------------------------------
DrawFont05		lda #0
				sta FON+S.FON.First
				lda #127 
				sta FON+S.FON.Last
				lda #127
				sta FON+S.FON.Default
				lda #32
				sta FON+S.FON.Break
				
				stz Cnt
				stz CV

				>LDYA pInBuf
				>STYA R1

.1				ldy #0

.2				lda CV
				clc
				adc #8
				tax

.3				dex
				lda HGR.L,x
				sta pHGR
				lda HGR.H,x
				sta pHGR+1

				lda (R1)
				sta IO.CLRWRITEAUX
				sta (pHGR),y
				sta IO.SETWRITEAUX

				>INCW R1
				txa
				and #7
				bne .3

				inc Cnt
				bmi .8

				iny
				tya
				and #$1F
				bne .2

				lda CV
				clc
				adc #8
				sta CV
				bra .1

.8				rts
*--------------------------------------
DrawFont06		lda #32
				sta FON+S.FON.First
				lda #127 
				sta FON+S.FON.Last
				lda #95
				sta FON+S.FON.Default
				lda #0
				sta FON+S.FON.Break
				
				stz Cnt
				stz CV

				>LDYA pInBuf
				>STYA R1

.1				ldy #0

.2				ldx CV

.3				lda HGR.L,x
				sta pHGR
				lda HGR.H,x
				sta pHGR+1

				lda (R1)
				sta IO.CLRWRITEAUX
				sta (pHGR),y
				sta IO.SETWRITEAUX

				>INCW R1
				inx
				txa
				and #7
				bne .3

				inc Cnt
				lda Cnt
				cmp #96
				bcs .8

				iny
				tya
				and #$1F
				bne .2

				lda CV
				clc
				adc #8
				sta CV
				bra .1

.8				rts
*--------------------------------------
PrintStat		>LDYA pInFilename
				ldx #160
				jsr GFX.PrintYAX
				bcs .9

				>SS
				>PUSHEA.G STRBUF
				>PUSHW L.MSG.Stat
				>PUSHB.G STATBUF+S.STAT.P.TYPE
				>PUSHW.G STATBUF+S.STAT.P.AUXTYPE
				>PUSHW.G STATBUF+S.STAT.SIZE
				>PUSHBI 5
				>LIBC SPrintF
				>SR
				bcs .9


				>LEA.G STRBUF
				ldx #168
				jsr GFX.PrintYAX


.9				rts
*--------------------------------------
fOpenYAR		>SS
				>PUSHYA
				>PUSHBI	O.RDONLY
				>PUSHBI 0				Type
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pInFILE

.9				rts
*--------------------------------------
fOpenYAW		>SS
				>PUSHYA
				>PUSHBI	O.RDWR
				>PUSHBI $CC				Type
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA pOutFILE

.9				rts
*--------------------------------------
GFX.Open		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC Open
				>SR
				bcs .9

				>STYA hFDGFX

				>LDYA L.FONTFILE
				>KAPI LoadBuf
				bcs .9

				>STYA hFont
				>STYA CB.TEXT+S.CB.hSrc

.9				rts
*--------------------------------------
GFX.GetC		>SLEEP

				>SS
				>PUSHW hFDGFX
				>PUSHWI 1
				>PUSHEA.G STRBUF
				>LIBC Read
				>SR
				bcs .9

				>LDA.G STRBUF

*				clc

				rts

.9				inc						#E.NODATA
				beq GFX.GetC

				dec

				rts
*--------------------------------------
GFX.PrintYAX	>STYA CB.TEXT+S.CB.pTxt

				stx CB.TEXT+S.CB.Y1

				>LDYA L.CB.TEXT
*--------------------------------------
GFX.Write.YA	>STYA R1

				>SS
				>PUSHW hFDGFX
				>PUSHW R1

				>PUSHBI 0
				lda (R1)
				and #$7F
				tax
				lda CB.CmdLen,x
				>PUSHA

				>LIBC Write
				>SR
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList		.AS "BbVw"
OptionVars		.DA #bBatch,#bBatch,#bVariableW,#bVariableW
*--------------------------------------
MSG.USAGE		.CS "Usage : FONT2FON FONT-File FON-File\r\n"
				.CS "   -B : Batch mode\r\n"
				.CZ "   -W : Convert to variable width\r\n"
*--------------------------------------
DEVNAME.GFX		.AZ "/dev/gfx"
FONTFILE		.AZ "/opt/gui/fonts/a27x8"
*--------------------------------------
MSG.Stat		.CZ "Type: %h, AuxT: %H, Size: %D"
*--------------------------------------
FON				.DA #0					S.FON.F
				.DA #7					S.FON.Ascent
				.DA #7					S.FON.PixW
				.DA #8					S.FON.PixH
			.BS 1						S.FON.First
			.BS 1						S.FON.Last
			.BS 1						S.FON.Default
			.BS 1						S.FON.Break
*--------------------------------------
CB.CmdLen		.DA #S.CB.Y1+1			SETPIXEL
				.DA #S.CB.Y1+1			GETPIXEL
				.DA #S.CB.X2+1			HLINE
				.DA #S.CB.Y2+1			VLINE
				.DA #S.CB.Y2+1			FILLRECT
				.DA #S.CB.hSrc+1		BITBLT
				.DA #S.CB.hSrc+1		GETRECTBUFSIZE
				.DA #S.CB.pTxt+1		DRAWTEXT
				.DA #S.CB.pTxt+1		GETTEXTSIZE
*--------------------------------------
CB.TEXT			.DA #S.CB.CMD.DRAWTEXT
				.DA #S.CB.OP.SET
				.DA #S.CB.M.MONO
				.DA #0					NORMAL
				.BS 2					X1
				.BS 2					Y1
				.DA 0
				.DA 0
				.DA 0					SrcX
				.DA 0					SrcY
				.DA 0
				.DA 0
				.DA 0
				.DA 0
				.BS 2					hSrc
				.BS 2					pTxt
*--------------------------------------
				.INB usr/src/shared/x.hgr.g
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

STATBUF			.BS S.STAT
STRBUF			.BS 40

DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/font2fon.s
ASM
