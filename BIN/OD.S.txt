NEW
  AUTO 3,1
				.LIST OFF
				.OP 65C02
				.OR $2000
				.TF bin/od
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ArgIndex     	.BS 1
ArgPattern   	.BS 1
ZPPtr1       	.BS 2
ZPBufPtr     	.BS 2
pFile        	.BS 2
char         	.BS 1
ByteCount    	.BS 3
bPause       	.BS 1
bIsPipe        	.BS 1
bIsTTY       	.BS 1
bTemp        	.BS 1
bDummy       	.BS 1

ZS.END       	.ED
*--------------------------------------
* File Header (16 Bytes)
*--------------------------------------
CS.START        cld
                jmp (.1,x)
                .DA #$61                  6502,Level 1 (65c02)
                .DA #2                    BIN Layout Version 2
                .DA #0                    S.PS.F.EVENT
                .DA #0
                .DA CS.END
				.DA ID.END
                .DA DS.END
                .DA #16                   Stack Size
                .DA #ZS.END-ZS.START      Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1              .DA CS.INIT
                .DA CS.RUN
                .DA CS.DOEVENT
                .DA CS.QUIT
L.MSG.USAGE     .DA MSG.USAGE
L.MSG.CRLF      .DA MSG.CRLF
L.MSG.OFFSET    .DA MSG.OFFSET
L.MSG.HEXBYTE   .DA MSG.HEXBYTE
                .DA 0
*--------------------------------------
CS.INIT         clc
				rts
*--------------------------------------
CS.RUN          jsr CS.RUN.ISATTY
				bcs CS.RUN.LOOP.RTS
                
				jsr CS.RUN.CheckArgs
                bcs CS.RUN.LOOP.RTS
*--------------------------------------
CS.RUN.LOOP     bit bIsPipe              If reading from pipe
                bpl .2                   No ^C/^S handling

.1				>SLEEP

				jsr X.CheckCtrl
				bcs .99
				
				bvs .1

.2              >LDYA pFile
                >LIBC GetC
                bcs .7

                jsr CS.RUN.PRINTBYTE
                bra CS.RUN.LOOP

.7              cmp #MLI.E.EOF
                bne .9

                jsr CS.RUN.FINISHUP

.8              lda #0                     Exit with no Error

.9              sec

.99
CS.RUN.LOOP.RTS rts
*--------------------------------------
CS.RUN.CheckArgs
				jsr CS.RUN.NextArg
                bcs .4

                lda (ZPPtr1)
                cmp #'-'
                beq .1

.11             lda pFile+1
                bne .97

                >LDYA ZPPtr1
                jsr CS.RUN.OPEN
                bcs .9

                >STYA pFile
                bra CS.RUN.CheckArgs
*--------------------------------------
.1              ldy #1
                lda (ZPPtr1),y

                ldx #OptionList.Cnt-1

.2              cmp OptionList,x
                beq .3

                dex
                bpl .2

                bra .97

.3              txa
                lsr
                beq .98

                tax
                lda #$80
                sta bDummy-1,x
                bra CS.RUN.CheckArgs
*--------------------------------------
.4              lda pFile+1
                beq .98

.80             >LDYAI 256
                >LIBC Malloc
                bcs .9

                >STYA ZPBufPtr

*               clc

.9              rts

.97             lda #E.SYN

.98             pha
                >LDYA L.MSG.USAGE
                >LIBC PutS
				pla
                sec
                rts
*--------------------------------------
CS.RUN.NextArg  inc ArgIndex
                lda ArgIndex
                >KAPI ArgV
                bcs .9

                >STYA ZPPtr1

.9              rts
*--------------------------------------
CS.RUN.OPEN     >SS
				>PUSHYA
                >PUSHBI O.RDONLY+O.TEXT
                >PUSHBI S.FI.T.TXT
                >PUSHWZ                         Aux type
                >LIBC FOpen
                >SR
				bcs .9
 
				>STYA pFile

.9              rts
*--------------------------------------
CS.RUN.ISATTY   ldy #S.PS.pStdOut
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply

				>LIBC FileNo
				bcs .9

				>SS
				>PUSHYA
				>PUSHEA.G STATBUF
				>LIBC FStat
				>SR
				bcs .9
				
				>LDA.G STATBUF+S.STAT.MODE+1
				and #$F0

                dec bIsTTY

.8				clc

.9              rts
*--------------------------------------
CS.QUIT         >LDYA pFile
                beq .1

                >LIBC FClose

.1              >LDYA ZPBufPtr
                beq .8

                >LIBC Free

.8              clc
                rts
*--------------------------------------
CS.RUN.PRINTBYTE
                sta char                      Char is in A

                lda ByteCount                 Divisible by 16
                and #$0f
                sta bTemp
                bne .2

                >SS
				>PUSHW L.MSG.OFFSET           Print offset
                lda ByteCount+2
                >PUSHA
                lda ByteCount+1
                >PUSHA
                lda ByteCount
                >PUSHA
                >PUSHBI 3
                >LIBC PrintF
				>SR

.2              ldy bTemp                     Offset mod 16
                lda char
                sta (ZPBufPtr),y

                >SS
				>PUSHW L.MSG.HEXBYTE
                >PUSHB char
                >PUSHBI 1
                >LIBC PrintF
				>SR

.6              inc ByteCount
                bne .7
 
				inc ByteCount+1
                bne .7
                
				inc ByteCount+2

.7              lda bTemp
                cmp #$0f
                bne .9

                jsr CS.RUN.PRINTASCII

.9              rts
*--------------------------------------
CS.RUN.PRINTASCII
                lda #'>'
                >LIBC PutChar

                ldy #$00
.7              lda (ZPBufPtr),y
                phy
                cmp #$7f
                bcs .75                  Character >= $7f
                cmp #C.SPACE
                bcc .75                  Character < Space
                bra .8
.75             lda #'.'

.8              >LIBC PutChar
                ply
                cpy bTemp
                beq .85
                iny
                bra .7

.85             lda #'<'
                >LIBC PutChar

                lda #C.CR
                >LIBC PutChar
                lda bIsTTY
                beq .9
                lda #C.LF
                >LIBC PutChar
.9              rts
*--------------------------------------
CS.RUN.FINISHUP
                lda ByteCount
                and #$0f
                bne .1
                rts
.1              pha
                lda #' '
                >LIBC PutChar
                lda #' '
                >LIBC PutChar
                lda #' '
                >LIBC PutChar
                pla
                inc
                cmp #$10
                bne .1
.2              jsr CS.RUN.PRINTASCII
                rts
*--------------------------------------
CS.DOEVENT      sec
                rts
*--------------------------------------
				.INB usr/src/shared/x.ispipein.s
				.INB usr/src/shared/x.checkctrl.s
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList      .AS "x"
OptionList.Cnt  .EQ *-OptionList
*--------------------------------------
MSG.USAGE       .CS "Usage : OD <File> or CMD|OD"
MSG.CRLF        .CZ "\r\n"
MSG.OFFSET      .CZ "%h%h%h "
MSG.HEXBYTE     .CZ "%h "
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
                .DUMMY
                .OR 0

STATBUF			.BS S.STAT
DS.END          .ED
*--------------------------------------
MAN
SAVE usr/src/bin/od.s
ASM
