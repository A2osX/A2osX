NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/cut
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPtr1			.BS 2
ZPBuf			.BS 2
ZPBufPtr		.BS 2
pFILE			.BS 2

ArgIndex		.BS 1

OptionIdx		.BS 1

FieldNum		.BS 1
StartChar		.BS 1
EndChar			.BS 1
Separator		.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.CRLF		.DA MSG.CRLF
L.MSG.SPACE		.DA MSG.SPACE
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			>LDYAI 256
				>LIBC Malloc
				bcs .99

				>STYA ZPBuf

				lda #0
				sta (ZPBuf)

				lda #C.SPACE
				sta Separator

.1				jsr CS.RUN.NextArg
				bcs .8

				lda (ZPPtr1)
				cmp #'-'
				bne .2

				jsr CS.RUN.CheckOp
				bcc .1					scan for any other args

.98				>LDYA L.MSG.USAGE
				>LIBC PutS
				lda #E.SYN
				sec
.99				rts

.2				lda (ZPBuf)
				beq .3

				>SS
				>PUSHW ZPBuf
				>PUSHW L.MSG.SPACE
				>LIBC StrCat
				>SR

.3				>SS
				>PUSHW ZPBuf
				>PUSHW ZPPtr1
				>LIBC StrCat
				>SR
				bcc .1

				rts
*--------------------------------------
.8				lda (ZPBuf)
				beq .80

				jsr CS.RUN.OUT
				bcs .99

				lda #0					Exit with no Error
				sec
				rts
*--------------------------------------
.80				jsr X.IsPipeIn
				bcs .99
				
				bvc .98

				>STYA pFILE
*--------------------------------------
CS.RUN.LOOP		>SLEEP

				>SS
				>PUSHW ZPBuf
				>PUSHWI	256
				>PUSHW pFILE
				>LIBC FGetS
				>SR
				bcs	.8

				lda (ZPBufPtr)
				beq CS.RUN.LOOP

				jsr CS.RUN.OUT
				bcc CS.RUN.LOOP

.8				cmp #MLI.E.EOF
				bne .9

				lda #0

.9				sec
				rts
*--------------------------------------
CS.RUN.OUT		>LDYA ZPBuf
				>STYA ZPBufPtr

				ldx FieldNum
				beq CS.RUN.OUT.MN

				ldy #$ff

.1				iny						skip spaces
				lda (ZPBufPtr),y
				beq CS.RUN.PRINTCR

				cmp Separator
				beq .1

				dex
				beq .4

.2				iny						skip token
				lda (ZPBufPtr),y
				beq CS.RUN.PRINTCR

				eor Separator
				bne .2

				bra .1

.4				jsr CS.RUN.AddYToBufPtr

				ldy #$ff

.6				iny
				lda (ZPBufPtr),y
				beq .8

				eor Separator
				bne .6

				sta (ZPBufPtr),y

.8				>LDYA ZPBufPtr
				>LIBC PutS

				rts
*--------------------------------------
CS.RUN.PRINTCR	>SS
				>PUSHW L.MSG.CRLF
				>PUSHBI 0
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.RUN.OUT.MN	ldy #0

				ldx StartChar
				beq .2

.1				dex
				beq .2

				lda (ZPBufPtr),y
				beq CS.RUN.PRINTCR

				iny
				bra .1

.2				lda EndChar
				beq .8

.3				lda (ZPBufPtr),y
				beq .8

				cpy EndChar
				beq .4

				iny
				bra .3

.4				lda #0
				sta (ZPBufPtr),y

.8				ldy StartChar
				beq .80

				dey
				beq .80

				jsr CS.RUN.AddYToBufPtr

.80				>LDYA ZPBufPtr
				>LIBC PutS

				rts
*--------------------------------------
CS.RUN.CheckOp	ldy #1
				lda (ZPPtr1),y

				ldx #OptionList.Cnt-1

.2				cmp OptionList,x
				beq .3

				dex
				bpl .2

.98				lda #E.SYN
				sec
.99				rts

.3				txa
				lsr
				beq .98					-H

				cmp #4					-S
				bne .4

				jsr CS.RUN.NextArg
				bcs .98

				lda (ZPPtr1)
				sta Separator
				clc
				rts

.4				sta OptionIdx
				jsr CS.RUN.NextArg
				bcs .98

				>LIBC AToI
				bcs .99

				tax
				bne .98

				tya
				ldx OptionIdx
				sta FieldNum-1,x

				clc
				rts
*--------------------------------------
CS.RUN.NextArg	inc ArgIndex
				lda ArgIndex
				>LIBC ArgV
				bcs .9

				>STYA ZPPtr1

.9				rts
*--------------------------------------
CS.RUN.AddYToBufPtr
				tya
				clc
				adc	ZPBufPtr
				sta ZPBufPtr
				bcc .8

				inc ZPBufPtr+1
.8				rts
*--------------------------------------
CS.QUIT			>LDYA pFILE
				beq .1

				>LIBC FClose			Close the pipe

.1				>LDYA ZPBuf
				beq .8

				>LIBC Free

.8				clc
				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
				.INB usr/src/shared/x.ispipein.s
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList		.AS "HhFfMmNnSs"
OptionList.Cnt	.EQ *-OptionList
*--------------------------------------
MSG.USAGE		.CS 'Usage : CUT "line of text"\r\n'
				.CS "   -H : This help screen\r\n"
				.CS "   -F nn : Output field nn\r\n"
				.CS "   -M nn : Output starting at nn\r\n"
				.CS "   -N nn : Output Ending at nn\r\n"
				.CS "   -S ch : Change default SPACE separator to 'ch'"
MSG.CRLF		.CZ "\r\n"
MSG.SPACE		.CZ " "
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
STATBUF			.BS S.STAT
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/cut.s
ASM
