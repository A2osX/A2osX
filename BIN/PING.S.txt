NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/ping
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
				.INB inc/net.db.i
*--------------------------------------
TIMEOUT.MAX		.EQ 240					4 sec.
BUFSIZE			.EQ 1024
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
Identifier		.BS 2
Sequence		.BS 2

TimeOut.ms		.BS 2
pHostName		.BS 2
hSocket			.BS 2

pBuf			.BS 2
ReplyLen		.BS 2

PingCount		.BS 1
TimeOut			.BS 1
ArgIndex		.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
*--------------------------------------
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.UNKNOWN	.DA MSG.UNKNOWN
L.MSG.HOSTOK	.DA MSG.HOSTOK
L.MSG.SKTKO		.DA MSG.SKTKO
L.MSG.REPLY		.DA MSG.REPLY
L.MSG.UNREACH	.DA MSG.UNREACH
				.DA 0
*--------------------------------------
CS.INIT			clc

CS.INIT.RTS		rts
*--------------------------------------
CS.RUN			jsr CS.RUN.Args
				bcs .99

				jsr CS.RUN.Resolv
				bcs .99

				>LDYAI BUFSIZE
				>LIBC Malloc
				bcs .99

				>STYA pBuf

				jsr CS.RUN.Socket
				bcs .99

				ldy #ICMP.RequestLen-1

.1				lda ICMP.Request,y
				sta (pData),y			RequestBuf
				dey
				bpl .1

.2				jsr CS.RUN.Send
				bcs .98

				jsr CS.RUN.Recv
				bcs .98

				lda PingCount
				beq .2

				dec	PingCount
				bne .2

				lda #0

.98				pha

				>LDYA hSocket
				>LIBC Shutdown

				pla
				sec

.99				rts
*--------------------------------------
CS.RUN.Args		inc ArgIndex
				lda ArgIndex
				>KAPI ArgV
				bcs .8

				>STYA R1
				lda (R1)
				cmp #'-'
				bne .1

				ldy #1
				lda (R1),y
				beq .98

				cmp #'0'
				bcc .98

				cmp #'9'+1
				bcs .98

				and #$0F
				sta PingCount
				bra CS.RUN.Args

.1				lda pHostName+1
				bne .98

				>LDYA R1
				>STYA pHostName
				bra CS.RUN.Args

.8				lda pHostName+1
				beq .98

				clc
				rts

.98				>LDYA L.MSG.USAGE
				>LIBC PutS

				lda #E.SYN
				sec
.99				rts
*--------------------------------------
CS.RUN.Resolv	lda #TIMEOUT.MAX
				sta TimeOut

.1				>SLEEP

				>SS
				>PUSHW pHostName
				>PUSHWZ
				>PUSHWZ
				>PUSHEA.G pAI
				>LIBC GetAddrInfo
				>SR
				bcc .2

				cmp #GAI.E.AGAIN
				bne .9

				lda TimeOut
				bne .1

				>SS
				>PUSHW L.MSG.UNKNOWN
				>PUSHW pHostName
				>PUSHBI 2
				>LIBC PrintF
				>SR

				lda #GAI.E.FAIL

.9				sec
				rts

.2				>LDYA.G pAI
				>STYA R1

				ldy #S.ADDRINFO.pSOCKADDR
				lda (R1),y
				sta R2
				iny
				lda (R1),y
				sta R2+1

				>LEA.G SA.REMOTE
				>STYA R3

				ldy #S.SA.IN-1

.3				lda (R2),y
				sta (R3),y
				dey
				bpl .3

				>LDYA R1
				>LIBC Free

				>SS
				>PUSHW L.MSG.HOSTOK

				ldx #4
				ldy #SA.REMOTE+S.SA.IN.ADDR

.4				lda (pData),y
				>PUSHA
				iny
				dex
				bne .4

				>PUSHW pHostName

				>PUSHBI 6
				>LIBC PrintF
				>SR

				rts
*--------------------------------------
CS.RUN.Socket	lda #AF_INET
				>STA.G SA.LOCAL

				lda A2osX.R16
				eor A2osX.T16+1
				>STA.G SA.LOCAL+S.SA.IN.PORT
				>STA.G SA.REMOTE+S.SA.IN.PORT
				sta Identifier

				lda A2osX.R16+1
				eor A2osX.T16
				>STA.G SA.LOCAL+S.SA.IN.PORT+1
				>STA.G SA.REMOTE+S.SA.IN.PORT+1
				sta Identifier+1

				>SS
				>PUSHWI AF_INET
				>PUSHWI SOCK_RAW
				>PUSHWI IPPROTO_ICMP
				>LIBC Socket
				>SR

				bcs .9

				>STYA hSocket

				>SS
				>PUSHW hSocket
				>PUSHEA.G SA.LOCAL
				>PUSHWI S.SA.IN
				>LIBC Bind
				>SR
				bcs .9

				>SS
				>PUSHW hSocket
				>PUSHEA.G SA.REMOTE
				>PUSHWI S.SA.IN
				>LIBC Connect
				>SR
				bcs .9

				rts

.9				pha
				>LDYA L.MSG.SKTKO
				>LIBC PutS
				pla
				sec
				rts
*--------------------------------------
CS.RUN.Send		lda Identifier
				>STA.G RequestBuf+S.ICMP.IDENTIFIER-S.IP+1

				lda Identifier+1
				dey
				sta (pData),y

				inc Sequence
				bne .1

				inc Sequence+1

.1				lda Sequence
				>STA.G RequestBuf+S.ICMP.SEQUENCE-S.IP+1

				lda Sequence+1
				dey
				sta (pData),y

				>SS
				>PUSHW hSocket
				>PUSHW pData			RequestBuf
				>PUSHWI ICMP.RequestLen+S.SA.IN+S.SA.IN
				>PUSHWZ					flags
				>LIBC Send
				>SR
				bcc .99

				cmp #SKT.E.PENDING
				sec
				bne .99

				clc

.99				rts
*--------------------------------------
CS.RUN.Recv		lda #TIMEOUT.MAX
				sta TimeOut

.10				>SLEEP

				ldy #S.PS.pStdIn
				jsr A2osX.GetPSy
				pha
				iny
				jsr A2osX.GetPSy
				ply
				>LIBC FEOF
				bcs .9

				tya
				bne	.1

				>LIBC GetChar
				bcs .9

				cmp #3					Ctrl-C
				beq .9					CS

.1				>SS
				>PUSHW hSocket
				>PUSHW pBuf
				>PUSHWI BUFSIZE
				>PUSHWZ					flags
				>LIBC Recv
				>SR
				bcc .2

				lda TimeOut
				bne .10

				jmp CS.Print.UNREACH

.2				>STYA ReplyLen

				ldy #S.ICMP.SEQUENCE-S.IP
				lda (pBuf),y
				cmp Sequence+1
				bne .10

				iny
				lda (pBuf),y
				cmp Sequence
				bne .10

				jmp CS.Print.REPLY

.9				rts
*--------------------------------------
CS.Print.UNREACH
				>SS
				>PUSHW L.MSG.UNREACH

				ldx #4
				ldy #SA.REMOTE+S.SA.IN.ADDR

.1				lda (pData),y
				>PUSHA
				iny
				dex
				bne .1

				>PUSHBI 4
				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.Print.REPLY	lda #TIMEOUT.MAX
				sec
				sbc TimeOut

				stz TimeOut.ms
				stz TimeOut.ms+1
				tax
				beq .3

.1				lda TimeOut.ms
				clc
				adc #100
				sta TimeOut.ms
				bcc .2

				inc TimeOut.ms+1

.2				dex
				bne .1

.3				>SS
				>PUSHW L.MSG.REPLY

				>PUSHW ReplyLen

*				ldx #4
*				ldy #S.IP.SRC
*
*4				>PUSHB (pBuf),y
*				iny
*				dex
*				bne .4

				ldx #4
				ldy #SA.REMOTE+S.SA.IN.ADDR

.4				lda (pData),y
				>PUSHA
				iny
				dex
				bne .4

				ldy #S.ICMP.SEQUENCE-S.IP
				>PUSHB (pBuf),y
				iny
				>PUSHB (pBuf),y

				ldy #S.IP.TTL-S.IP
				>PUSHB (pBuf),y

				>PUSHW TimeOut.ms
				>PUSHBI 11

				>LIBC PrintF
				>SR
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				rts
*--------------------------------------
CS.QUIT			>LDYA pBuf
				beq .1

				>LIBC Free

.1				>LDYA hSocket
				beq .8

				>LIBC Shutdown

.8				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.CS "Usage : PING <ip|host>\r\n"
				.CZ " -1-9 : Ping Count\r\n"
MSG.UNKNOWN		.CZ "%s: Unknown host\r\n"
MSG.HOSTOK		.CZ "PING %d.%d.%d.%d (%s)\r\n"
MSG.SKTKO		.CZ "Failed to Open Socket."
MSG.UNREACH		.CZ "%d.%d.%d.%d: Timeout/Host unreachable\r\n"
MSG.REPLY		.CZ "%D bytes from %d.%d.%d.%d, icmp_seq=%D, ttl=%d, time=%D ms\r\n"
*--------------------------------------
ICMP.Request	.DA #S.ICMP.TYPE.ECHOREQ
				.DA #0					CODE
				.DA 0					CHECKSUM
				.BS 2					ID
				.BS 2					DQ
				.AS "ABCDEFGHIJKLMNOP"
				.AS "QRSTUVWXYZ012345"
ICMP.RequestLen	.EQ *-ICMP.Request
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
RequestBuf		.BS ICMP.RequestLen
SA.LOCAL		.BS S.SA.IN
SA.REMOTE		.BS S.SA.IN
pAI				.BS 2
*--------------------------------------
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/ping.s
ASM
