NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/man
*--------------------------------------
				.INB inc/macros.i
				.INB inc/mli.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/libtui.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pCTX			.BS 2
pSCR			.BS 2
pTBOX			.BS 2

pLineBuf		.BS 2
pFileBuf		.BS 2
BufSize			.BS 2

ZPPtr1			.BS 2
ZPPtr2			.BS 2
ZPPtr3			.BS 2

TimeOut			.BS 1
ErrCode			.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.LIBTUI		.DA LIBTUI
*--------------------------------------
L.ENV.MANPATH	.DA ENV.MANPATH
L.FILE.INDEX	.DA FILE.INDEX
L.MSG.ScrTitle	.DA MSG.ScrTitle

L.MSG.USAGE		.DA MSG.USAGE
L.MSG.CRLF		.DA MSG.CRLF
L.MSG.NULL		.DA MSG.NULL
L.MSG.GOTO		.DA MSG.GOTO	
L.MSG.OK		.DA MSG.OK		
L.MSG.ERROR		.DA MSG.ERROR	
				.DA 0
*--------------------------------------
CS.INIT			>LDYA L.LIBTUI
                >KAPI LoadLib
                bcs .9

                >STYA LIBTUI

.9
CS.INIT.RTS		rts
*--------------------------------------
CS.RUN.USAGE	>LDYA L.MSG.USAGE
				>LIBC PutS
				lda #E.SYN
				sec
				rts
*--------------------------------------
CS.RUN			>LDYAI 256
				>LIBC Malloc
				bcs CS.INIT.RTS

				>STYA pLineBuf
				
				lda #1
				>KAPI ArgV
				bcc .1
				
				>LDYA L.FILE.INDEX

.1				jsr CS.GetLink
				bcs CS.RUN.USAGE

				jsr CS.GetFile
				bcs CS.INIT.RTS

				jsr CS.Scr.Init
				bcs CS.INIT.RTS
*--------------------------------------
CS.RUN.LOOP0	jsr CS.Scr.SetBuf
				jsr CS.Scr.SetStatusOkErr
*--------------------------------------
CS.RUN.LOOP		>SS
				>PUSHW pSCR
                >LIBCALL LIBTUI,Exec
                >SR
				bcs .9

				cmp #EV.HL.SELECTED
				bne .1

				>LEA.G LinkBuf
				jsr CS.GetLink
				
				jsr CS.Scr.SetStatusGoto
				bcc CS.RUN.LOOP

				rts

.1				cmp #EV.HL.PRESSED
				bne .2
				
				jsr CS.GetFile
				jsr CS.Scr.SetStatusOkErr
				bcc CS.RUN.LOOP0

				rts
				
.2				>DEBUG
				bcc CS.RUN.LOOP0

.9				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				sec
				rts
*--------------------------------------
CS.QUIT			>LDYA pLineBuf
				jsr .7
				
				>LDYA pFileBuf
				jsr .7
				
				>LDYA pCTX
				beq .1
				
				>LIBCALL LIBTUI,Close

.1				>LDYA LIBTUI
                beq .2

                >KAPI UnloadLib

.2				clc
				rts

.7				beq .8

				>LIBC Free
				
.8				rts
*--------------------------------------
CS.Scr.Init		>LIBCALL LIBTUI,Init
                bcs .98

                >STYA pCTX

				>SS
				>PUSHYA					pCTX
                >PUSHWI S.OBJ.F.bTitle+S.OBJ.F.bStatus
                >LIBCALL LIBTUI,NewScrn
                >SR
.98				bcs .99

                >STYA pSCR

				jsr CS.Scr.SetTitle
				jsr CS.Scr.SetStatus

				>SS
				>PUSHW pSCR
                >PUSHBI 0    			X1
                >PUSHBI 0    			Y1
                >PUSHBI 80    			W
                >PUSHBI 22    			H
                >PUSHWI 0				F
                >PUSHW pFileBuf
                >PUSHW BufSize
                >LIBCALL LIBTUI,NewTBox
				>SR
.99             bcs .9

				>STYA pTBOX
				>PUSHYA
                >LIBCALL LIBTUI,Activate
				bcs .9

				>SS
				>PUSHW pTBOX
                >PUSHWI S.OBJ.pVar
                >PUSHEA.G LinkBuf
                >LIBCALL LIBTUI,SetProp
				>SR

.9              rts
*--------------------------------------
CS.Scr.SetTitle	>SS
				>PUSHW pSCR
                >PUSHWI S.OBJ.pTITLE
                >PUSHW L.MSG.ScrTitle
                >LIBCALL LIBTUI,SetProp
				>SR
				rts
*--------------------------------------
CS.Scr.SetBuf	>SS
				>PUSHW pTBOX
				>PUSHWI S.OBJ.pBuf
				>PUSHW pFileBuf
                >LIBCALL LIBTUI,SetProp
				>SR
				bcs .9
				
				>SS
				>PUSHW pTBOX
				>PUSHWI S.OBJ.BufSize
				>PUSHW BufSize
                >LIBCALL LIBTUI,SetProp
				>SR
				
.9				rts
*--------------------------------------
CS.Scr.SetStatusGoto
				>SS
				>PUSHEA.G SCR.StatusBuf	skip type
				>PUSHW L.MSG.GOTO
				>PUSHEA.G PathBuf
				>PUSHEA.G HtagBuf
				>PUSHBI 4
				>LIBC SPrintF
				>SR
				bra CS.Scr.SetStatus
*--------------------------------------
CS.Scr.SetStatusOkErr
				bcc .1

				sta ErrCode
				
				>SS
				>PUSHEA.G SCR.StatusBuf
				>PUSHW L.MSG.ERROR
				>PUSHB ErrCode
				>PUSHBI 1
				>LIBC SPrintF
				>SR
				
				jsr CS.Scr.SetStatus
				
				lda ErrCode
				sec
				rts

.1				>SS
				>PUSHEA.G SCR.StatusBuf
				>PUSHW L.MSG.OK
				>PUSHBI 0
				>LIBC SPrintF
				>SR
*--------------------------------------
CS.Scr.SetStatus
				>SS
				>PUSHW pSCR
                >PUSHWI S.OBJ.pSTATUS
                >PUSHEA.G SCR.StatusBuf
                >LIBCALL LIBTUI,SetProp
				>SR
				rts
*--------------------------------------
* [[text]host:port/path#htag]
* [#htag]text]
*--------------------------------------
CS.GetLink		>STYA ZPPtr1

				>LEA.G PathBuf
				>STYA ZPPtr2
				
				>LDYA L.ENV.MANPATH
				>LIBC GetEnv
				
				>STYA ZPPtr3
				
				ldy #$ff
				
.1				iny
				lda (ZPPtr3),y
				sta (ZPPtr2),y
				bne .1
				
.2				jsr CS.GetCharPtr1
				beq .4
				
				cmp #'#'
				beq .3
				
				sta (ZPPtr2),y
				iny
				bra .2
				
.3				lda #0

.4				sta (ZPPtr2),y

				>LEA.G HTagBuf
				>STYA ZPPtr3

				ldy #$ff

.5				iny
				jsr CS.GetCharPtr1
				sta (ZPPtr3),y
				bne .5

				clc

.9				rts
*--------------------------------------
CS.GetFile		>LEA.G PathBuf
				>LIBC LoadTxtFile
				bcs .9
		
				>STYA pFileBuf
				>STYA ZPPtr1
				
				ldy #1
				ldx #0
				
.1				jsr CS.GetCharPtr1
				beq .8
				
				iny
				bne .1
				
				inx
				bra .1
				
.8				sty BufSize
				stx BufSize+1
				
*				clc				

.9				rts
*--------------------------------------
CS.GetCharPtr1	lda (ZPPtr1)
				beq .8
				
				inc ZPPtr1
				bne .8
				
				inc ZPPtr1+1
				
.8				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
LIBTUI          .AZ "libtui"
ENV.MANPATH		.AZ "MANPATH"
FILE.INDEX		.AZ "index"
*--------------------------------------
MSG.ScrTitle	.CZ "A2osX-MAN - <Ctrl-Q>uit"
*--------------------------------------
MSG.USAGE		.CS "Usage : MAN <manfile>[#htag]"
MSG.CRLF		.CS "\r\n"
MSG.NULL		.HS 00
MSG.GOTO		.CZ "Goto: %s#%s"
MSG.OK			.CZ "Ok"
MSG.ERROR		.CZ "Error: $%h"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
LinkBuf			.BS 32
PathBuf			.BS MLI.MAXPATH+1
HTagBuf			.BS 32
SCR.StatusBuf	.BS 80
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/man.s
ASM
