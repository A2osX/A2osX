NEW
  AUTO 3,1
*--------------------------------------
CODE.Init		>STZ.G CC.bInitCode

				>STA.G CC.ScopeID
				iny
				sta (pData),y

				ldy #SCOPE-1
				lda #0

.1				sta (ScopeStk),y		reset initial Scope ID, LocalPtr, FrameSize
				dey
				bpl .1

				>LDYAI PCC.FH.CS
				>STYA ZPCCCode

				stz ZPCCConst
				stz ZPCCConst+1

				jsr CODE.IDSelect
				bcs .99

				stz ArgIndex

.2				lda ArgIndex
				>CMP.G LibCount
				beq .8

				clc
				adc #hLibNs
				tay
				lda (pData),y
				>LIBC ArgV
				>STYA ZPPtr1

				ldy #0

.3				lda (ZPPtr1),y
				beq .4

				jsr CODE.EmitDATA
				bcs .99

				iny
				bne .3

.4				jsr CODE.EmitDATA		\0
				bcs .99

				inc ArgIndex
				bra .2

.8				lda #0
				jsr CODE.EmitDATA		\0
				bcs .99

				jsr CODE.CSSelect
				bcs .99

				>LDYAI 20				char *ARGV[10]
				>STYA ZPCCData

				clc
.99				rts
*--------------------------------------
CODE.Quit		>LDYA.G CC.pOutFile
				cmp #0
				beq .8

				>LIBC FClose

.8				clc
				rts
*--------------------------------------
CODE.AddLocal	cmp #0
*				beq .8

				eor #$FF
				inc

				jsr CODE.LDAI
				bcs .9

				>LDYA L.PCC.ADDLOCAL
				jmp CODE.EmitPCC

.8				clc
.9				rts
*--------------------------------------
CODE.RemLocal	cmp #0
*				beq .8
				
				jsr CODE.LDAI
				bcs .9

				>LDYA L.PCC.REMLOCAL
				jmp CODE.EmitPCC

.8				clc
.9				rts
*--------------------------------------
CODE.SetRetValue
				jsr CODE.LDXI
				bcs .9

				>LDYA L.PCC.SetRetValue
				jmp CODE.EmitPCC

.9				rts
*--------------------------------------
CODE.PUSHAXI	pha
				txa
				jsr CODE.PUSHI
				bcs CODE.LDAXI.RTS

				pla
*--------------------------------------
CODE.PUSHI		jsr CODE.LDAI
				bcs CODE.LDAXI.RTS

				bra CODE.PUSHA
*--------------------------------------
CODE.LDYAI		pha

				lda #$A0				LDY #imm
				jsr CODE.EmitByte
				bcs CODE.LDAXI.RTS

				tya
				jsr CODE.EmitByte
				bcs CODE.LDAXI.RTS

				lda #$A9				LDA #imm
				jsr CODE.EmitByte
				bcs CODE.LDAXI.RTS

				pla

				jmp CODE.EmitByte
*--------------------------------------
CODE.LDAXI		jsr CODE.LDAI
				bcc CODE.LDXI

CODE.LDAXI.RTS	rts
*--------------------------------------
CODE.LDYI		lda #$A0				LDY #imm
				jsr CODE.EmitByte
				bcs .9

				tya
				jmp CODE.EmitByte

.9				rts
*--------------------------------------
CODE.LDXI		lda #$A2				LDX #imm
				jsr CODE.EmitByte
				bcs .9

				txa
				jmp CODE.EmitByte

.9				rts
*--------------------------------------
CODE.LDAI		pha
				lda #$A9				LDA #imm
				jsr CODE.EmitByte
				bcs .9

				pla
				jmp CODE.EmitByte

.9				plx
				rts
*--------------------------------------
CODE.PUSHYA		jsr CODE.PUSHA
				lda #$98				TYA
				jsr CODE.EmitByte
*--------------------------------------
CODE.PUSHA		lda #$C6				DEC zp
				jsr CODE.EmitByte
				lda #pStack
				jsr CODE.EmitByte

				lda #$92				STA (zp)
				jsr CODE.EmitByte
				lda #pStack
				jmp CODE.EmitByte
*--------------------------------------
CODE.PULLYA		jsr CODE.PULLA
				lda #$48				PHA
				jsr CODE.EmitByte
				jsr CODE.PULLA
				lda #$7A				PLY
				bra CODE.EmitByte
*--------------------------------------
CODE.PULLA		lda #$B2				LDA (zp)
				jsr CODE.EmitByte
				lda #pStack
				jsr CODE.EmitByte
*--------------------------------------
CODE.INCPSTACK	lda #$E6				INC zp
				jsr CODE.EmitByte
				lda #pStack
				bra CODE.EmitByte
*--------------------------------------
CODE.LIBC		jsr CODE.LDXI
				bcs .9

				lda #$20				JSR
				ldx #A2osX.LIBC
				ldy /A2osX.LIBC
				bra CODE.TOABSYX

.9				rts
*--------------------------------------
CODE.FPUCALL	jsr CODE.LDXI
				bcs .9

				lda #$20				JSR
				ldx #A2osX.FPU
				ldy /A2osX.FPU
				bra CODE.TOABSYX

.9				rts
*--------------------------------------
CODE.LIBCALL	jsr CODE.LDXI
				bcs .9

				tya
				dec
				dec
				dec
				asl
				asl
				clc
				adc #PCC.PH.JMPLIBs
				tax
				lda #0
				adc /PCC.PH.JMPLIBs
				tay
				lda #$20				JSR
				bra CODE.TOABSYX

.9				rts
*--------------------------------------
CODE.TOABSYX	jsr CODE.EmitByte		A=opcode : JMP, JSR .....
				bcs .9

				txa
				jsr CODE.EmitByte
				bcs .9

				tya
				bra CODE.EmitByte

.9				rts
*--------------------------------------
CODE.DEBUG		>LDYA L.PCC.DEBUG
*--------------------------------------
CODE.EmitPCC	>STYA ZP.pLookup

				ldy #0

.1				iny
				lda (ZP.pLookup),y
				jsr CODE.EmitByte
				bcs .9

				tya
				eor (ZP.pLookup)
				bne .1

*				clc

.9				rts
*--------------------------------------
CODE.EmitByte	clc						if Pass 1...
				bit bPass2
				bpl .1

				phx
				phy

				.DO _DBG_OUT=1

				tay

				lda ZPCCCode
				and #$F
				bne .11

				phy
				>SS
				>PUSHW L.MSG.DEBUG.CSOUT
				>PUSHW ZPCCCode
				>PUSHBI 2
				>LIBC PrintF
				>SR
				ply

.11				>SS
				phy
				>PUSHW L.MSG.DEBUG.OUT
				pla
				pha
				>PUSHA
				>PUSHBI 1
				>LIBC PrintF
				pla
				>SR

				.FIN

				>SS

				>PUSHA
				>PUSHW.G CC.pOutFile
				>LIBC fputc
				>SR
				
				ply
				plx

.1				inc ZPCCCode
				bne .8

				inc ZPCCCode+1

.8				rts						CC/CS from fputc
*--------------------------------------
CODE.EmitDATA	clc
				bit bPass2
				bpl .1

				phx
				phy

				>SS
				>PUSHA
				>PUSHW.G CC.pOutFile
				>LIBC fputc
				>SR
				ply
				plx

.1				inc ZPCCConst
				bne .8

				inc ZPCCConst+1

.8				rts						CC/CS from fputc
*--------------------------------------
CODE.CSSelect	clc
				bit bPass2
				bpl .8

				>SS
				>PUSHW.G CC.pOutFile
				>PUSHWZ
				lda ZPCCCode+1
				sec
				sbc #$20
				>PUSHA
				lda ZPCCCode
				>PUSHA
				>PUSHWI SEEK.SET
				>LIBC fseek
				>SR
.8				rts
*--------------------------------------
CODE.IDSelect	clc
				bit bPass2
				bpl .8

				>SS
				>PUSHW.G CC.pOutFile
				>PUSHWZ

				lda PCC.FH+PCC.FH.CONST
				clc
				adc ZPCCConst
				tay
				
				lda PCC.FH+PCC.FH.CONST+1
				adc ZPCCConst+1

				sec
				sbc #$20

				>PUSHYA
				>PUSHWI SEEK.SET
				>LIBC fseek
				>SR

.8				rts
*--------------------------------------
MAN
SAVE usr/src/bin/cc.s.code
LOAD usr/src/bin/cc.s
ASM
