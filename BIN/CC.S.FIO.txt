NEW
  AUTO 3,1
*--------------------------------------
FIO.LoadLIB.o	>LDA.G LibCount
				cmp #LIBS.MAX
				bne .1

				lda #E.OOH
				sec
.9				rts

.1				clc
				adc #hLibNs
				tay

				lda ArgIndex
				sta (pData),y

				jsr FIO.MakeLibPath
				bcs .99

				>SS
				>PUSHW L.MSG.READING
				>PUSHW pData
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs .99
				
				>SS
				>PUSHW pData
				>PUSHBI	O.RDONLY
				>PUSHBI S.FI.T.BIN
				>PUSHWZ					Aux type
				>LIBC LoadFile
				>SR
				bcs .99

				pha
				phy
				
				>LDA.G LibCount
				asl
				clc
				adc #pLibOs
				tay
				pla
				sta (pData),y
				iny
				pla
				sta (pData),y
				
				>INC.G LibCount
				clc

.99				rts
*--------------------------------------
FIO.MakeLibPath	>SS
				>LDYA L.ENV.LIB
				>LIBC GetEnv
				>SR
				bcs .9

				>SS
				pha
				>PUSHW pData			>PUSHEA.G PathBuf
				pla
				>PUSHA
				tya
				>PUSHA
				>LIBC StrCpy
				>SR

				>SS
				>PUSHW pData
				lda ArgIndex
				>KAPI ArgV
				>PUSHYA
				>LIBC StrCat
				>SR

				>SS
				>PUSHW pData
				>PUSHW L.LIB.O
				>LIBC StrCat
				>SR

.9				rts
*--------------------------------------
FIO.FOpen1st	>LDYA.G CC.pInFileName

FIO.FOpen		>STYA ZPPtr1

				>SS
				>PUSHW L.MSG.READING
				>PUSHW ZPPtr1
				>PUSHBI 2
				>LIBC PrintF
				>SR

				>SS
				>PUSHW ZPPtr1
				>PUSHBI	O.RDONLY
				>PUSHBI S.FI.T.TXT
				>PUSHWZ					Aux type
				>LIBC FOpen
				>SR
				bcs .9

				pha
				phy

				>LDA.G CC.pInFiles
				inc
				inc
				sta (pData),y
				tay
				pla
				sta (pData),y
				iny
				pla
				sta (pData),y

				>LDA.G CC.LineCnts
				inc
				inc
				sta (pData),y
				tay
				lda #0
				sta (pData),y
				iny
				sta (pData),y

.9				rts
*--------------------------------------
FIO.FGetS		>LDYA.G CC.pLineBuf
				>STYA ZP.pLineBuf

				lda #0
				sta (ZP.pLineBuf)		set to Empty line

				>LDA.G CC.LineCnts
				tay
				lda (pData),y
				inc
				sta (pData),y
				bne .1

				iny
				lda (pData),y
				inc
				sta (pData),y

.1				>SS
				>PUSHW ZP.pLineBuf
				>PUSHWI 255
				>LDA.G CC.pInFiles
				tay
				
				iny
				lda (pData),y
				>PUSHA
				dey
				lda (pData),y
				>PUSHA
				
				>LIBC FGetS
				>SR
				bcs .9

				>LDA.G bVerbose
				bpl .8

				jsr PrintVerboseMsg

.8

.9				rts
*--------------------------------------
FIO.FClose		>LDA.G CC.pInFiles
				cmp #CC.pInFiles
				beq .8					Z

				tay
				lda (pData),y
				pha
				iny
				lda (pData),y
				ply

				>LIBC FClose

				>LDA.G CC.pInFiles
				dec
				dec
				sta (pData),y
				cmp #CC.pInFiles
				beq .8					Z

				>LDA.G CC.LineCnts
				dec
				dec
				sta (pData),y			NZ

.8				rts
*--------------------------------------
FIO.FCreate		lda ZPCCCode
				sta PCC.FH+PCC.FH.CS.END
				sta PCC.FH+PCC.FH.CONST
				
				clc
				adc ZPCCConst
				sta PCC.FH+PCC.FH.ID.END
				
				lda ZPCCCode+1
				sta PCC.FH+PCC.FH.CS.END+1
				sta PCC.FH+PCC.FH.CONST+1
				
				adc ZPCCConst+1
				sta PCC.FH+PCC.FH.ID.END+1
				
				lda ZPCCData
				sta PCC.FH+PCC.FH.DS.SIZE

				lda ZPCCData+1
				sta PCC.FH+PCC.FH.DS.SIZE+1

				>SS
				>PUSHW L.MSG.GENERATING
				>PUSHW.G CC.pOutFileName
				>PUSHBI 2
				>LIBC PrintF
				>SR
				bcs .9
				
				>SS
				>PUSHW.G CC.pOutFileName
				>PUSHBI	O.WRONLY+O.CREATE
				>PUSHBI S.FI.T.BIN
				>PUSHWI	$2000			Aux type
				>LIBC FOpen
				>SR
				bcs .9

				>STYA.G CC.pOutFile

				>SS
				>PUSHW.G CC.pOutFile
				>PUSHW L.PCC.FH
				>PUSHWI PCC.FH.L
				>LIBC FWrite
				>SR

.9				rts
*--------------------------------------
MAN
SAVE usr/src/bin/cc.s.fio
LOAD usr/src/bin/cc.s
ASM
