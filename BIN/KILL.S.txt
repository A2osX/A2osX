NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/kill
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPTmpPtr		.BS 2
Signal			.BS 2
PID				.BS 2
ArgIndex		.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size	
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.SSCANF.PID	.DA SSCANF.PID
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			ldy #S.PS.ARGC
				jsr A2osX.GetPSy
				beq .9

				lda #1
				sta Signal

.1				inc ArgIndex
				lda ArgIndex
				>KAPI ArgV
				bcs .9

				>STYA ZPTmpPtr
				lda (ZPTmpPtr)
				cmp #'-'
				bne .2

				ldy #1
				lda (ZPTmpPtr),y
				beq .9

				cmp #'0'
				bcc .9

				cmp #'9'+1
				bcs .9

				and #$0F
				sta Signal
				bra .1

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

				lda #E.SYN
				sec
				rts

.2				>SS
				>PUSHW ZPTmpPtr
				>PUSHW L.SSCANF.PID
				>PUSHWI PID
				>PUSHBI 2				1 Ptr
				>LIBC SScanF
				>SR
				bcs .9

				>SS
				>PUSHW PID
				>PUSHW Signal
				>LIBC Kill
				>SR
				bcs .99

				lda #0
				sec
.99				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.CS "Usage : KILL <signal> PID\r\n"
				.CS "   -0 : No Signal\r\n"
				.CS "   -1 : SIGQUIT\r\n"
				.CZ "   -2 : SIGKILL\r\n"
SSCANF.PID		.CZ "%D"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/kill.s
ASM
