NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/arp
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
*--------------------------------------
TIMEOUT.MAX		.EQ 40					40*100ms = 4 sec.
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPTR1			.BS 2
EntryCount		.BS 1
TimeOut			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG		
				.DA	CS.QUIT
*--------------------------------------
L.SSCANF.IP		.DA SSCANF.IP
L.SSCANF.MAC	.DA SSCANF.MAC
L.MSG.Usage		.DA MSG.Usage
L.MSG0			.DA MSG0
L.MSG1			.DA MSG1
L.MSG2			.DA MSG2
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #1
				>KAPI ArgV
				bcc .1
				
				jmp CS.RUN.DUMP
				
.1				>SS
				>PUSHYA
				>PUSHW L.SSCANF.IP
				>PUSHEA.G DST.IP
				>PUSHEA.G DST.IP+1
				>PUSHEA.G DST.IP+2
				>PUSHEA.G DST.IP+3
				>PUSHBI 8				4 Ptrs
				>LIBC SScanF
				>SR
				bcs CS.RUN.ERR

				lda #2
				>KAPI ArgV
				bcc .2
				
				jmp CS.RUN.QUERY

.2				jmp CS.RUN.ADD
				
CS.RUN.ERR		>LDYA L.MSG.Usage
				>LIBC PutS
				lda #E.SYN
				sec
				rts
*--------------------------------------
CS.RUN.DUMP		>LDYAI AF_INET
				>KAPI ArpGet
				bcs .9
				
				>STYA ZPPTR1
				
				>LDYA L.MSG0
				>LIBC PutS

				lda #K.ARPCACHE.SIZE
				sta EntryCount

.1				lda (ZPPTR1)
				beq .4

				>SS
				>PUSHW L.MSG1
				
				>PUSHB (ZPPTR1)
				
				ldy #S.ARPCACHE.TTL+1
				>PUSHB (ZPPTR1),y
				dey
				>PUSHB (ZPPTR1),y

				ldy #S.ARPCACHE.MAC

.3				>PUSHB (ZPPTR1),y
				iny
				cpy #S.ARPCACHE.MAC+6
				bne .3

				ldy #S.ARPCACHE.IP

.2				>PUSHB (ZPPTR1),y
				iny
				cpy #S.ARPCACHE.IP+4
				bne .2
				
				>PUSHBI 13
				>LIBC PrintF
				>SR

.4				lda ZPPTR1
				clc
				adc #S.ARPCACHE
				sta ZPPTR1
				bcc .5

				inc ZPPTR1+1

.5				dec EntryCount
				bne .1

.8				lda #0					tell TSKMGR that all done ok, but 
				sec						we do not want to stay in memory
.9				rts
*--------------------------------------
CS.RUN.QUERY	lda #TIMEOUT.MAX
				sta TimeOut

.1				>SS
				>PUSHWI AF_INET
				>PUSHEA.G DST.MAC
				>PUSHEA.G DST.IP
				>KAPI ArpQuery
				>SR
				bcc .2					success, print & exit

				dec TimeOut
				beq .8

				>SLEEP

				>LIBC GetChar
				bcs .1

				cmp #3
				beq .9

				bra .1
				
.2				>SS
				>PUSHW L.MSG2

				ldx #4
				ldy #DST.IP

.4				>PUSHB (pData),y
				iny
				dex
				bne .4

				ldx #6
				ldy #DST.MAC

.3				>PUSHB (pData),y
				iny
				dex
				bne .3

				>PUSHBI 10
				>LIBC PrintF
				>SR

.8				lda #0

.9				sec
				rts
*--------------------------------------
CS.RUN.ADD		lda #2
				>KAPI ArgV
				
				>SS
				>PUSHYA
				>PUSHW L.SSCANF.MAC
				>PUSHEA.G DST.MAC
				>PUSHEA.G DST.MAC+1
				>PUSHEA.G DST.MAC+2
				>PUSHEA.G DST.MAC+3
				>PUSHEA.G DST.MAC+4
				>PUSHEA.G DST.MAC+5
				>PUSHBI 12				6 Ptrs
				>LIBC SScanF
				>SR
				bcs .9

				>SS
				>PUSHWI AF_INET
				>PUSHEA.G DST.MAC
				>PUSHEA.G DST.IP
				>KAPI ArpAdd
				>SR

.9				jmp CS.RUN.ERR
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
SSCANF.IP		.AZ "%d.%d.%d.%d"
SSCANF.MAC		.AZ "%h:%h:%h:%h:%h:%h"
MSG.Usage		.CS "Usage : ARP display ARP cache\r\n"
				.CS "        ARP ip.ip.ip.ip query cache or network\r\n"
				.CZ "        ARP ip.ip.ip.ip HH:HH:HH:HH:HH:HH add a static ARP record in cache\r\n"
MSG0			.CZ "STS TTL   MAC Address       IP Address"
MSG1			.CZ "$%h %5D %h:%h:%h:%h:%h:%h %d.%d.%d.%d\r\n"
MSG2			.CZ "%d.%d.%d.%d is at %h:%h:%h:%h:%h:%h\r\n"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0

DST.IP			.BS 4
DST.MAC			.BS 6
*--------------------------------------
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/arp.s
ASM
