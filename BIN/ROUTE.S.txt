NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/route
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
				.INB inc/eth.i
				.INB inc/ip.i
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
pArg			.BS 2

pRoutes			.BS 2
pRoute			.BS 2
pDevName		.BS 2

Idx				.BS 1
FlagByte		.BS 1

bContinue		.BS 1
bRecurse		.BS 1

ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #0
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #64					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT

L.MSG.USAGE		.DA MSG.USAGE
L.MSG.ROUTE0	.DA MSG.ROUTE0
L.MSG.ROUTE		.DA MSG.ROUTE

L.KW			.DA KW
J.KW			.DA CS.RUN.ADD
				.DA	CS.RUN.DEL

L.SScanF.IP		.DA SScanF.IP

				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			inc Idx
				lda Idx
				>KAPI ArgV
				bcc .1

				jmp CS.RUN.PRINT

.1				>STYA pArg
				lda (pArg)
				cmp #'-'
				bne .2

				jsr CS.RUN.CheckOpt
				bcc CS.RUN

.9				>LDYA L.MSG.USAGE
				>LIBC PutS
				lda #E.SYN
				sec
				rts

.2				>LDYA L.KW
				jsr CS.RUN.KW
				bcs .9

				jmp (J.KW,x)

.99				rts
*--------------------------------------
CS.RUN.PRINT	>LDYAI AF_INET
				>KAPI GetRoute
				bcs .9

				>STYA pRoutes
				>STYA pRoute

				>LDYA L.MSG.ROUTE0
				>LIBC PutS

				lda #K.ROUTE.MAX
				sta Idx

.1				ldy #S.ROUTE.IF
				lda (pRoute),y
				beq .7

				jsr CS.RUN.FRMTIP

				jsr CS.RUN.FRMTF

				jsr CS.RUN.PRINTRT

.7				lda pRoute
				clc
				adc #S.ROUTE
				sta pRoute
				bcc .8

				inc pRoute+1

.8				dec Idx
				bne .1

				>LDYA pRoutes
				>LIBC Free

				lda #0
				sec
.9				rts
*--------------------------------------
CS.RUN.FRMTIP	>SS
				>PUSHEA.G ADDR
				>PUSHW L.SScanF.IP

				ldy #S.ROUTE.ADDR

.1				>PUSHB (pRoute),y
				iny
				cpy #S.ROUTE.ADDR+4
				bcc .1

				>PUSHBI 4
				>LIBC SPrintF
				>SR

				>SS
				>PUSHEA.G MASK
				>PUSHW L.SScanF.IP

				ldy #S.ROUTE.MASK

.2				>PUSHB (pRoute),y
				iny
				cpy #S.ROUTE.MASK+4
				bcc .2

				>PUSHBI 4
				>LIBC SPrintF
				>SR

				>SS
				>PUSHEA.G GW
				>PUSHW L.SScanF.IP

				ldy #S.ROUTE.GW

.3				>PUSHB (pRoute),y
				iny
				cpy #S.ROUTE.GW+4
				bcc .3

				>PUSHBI 4
				>LIBC SPrintF
				>SR

				rts
*--------------------------------------
CS.RUN.FRMTF	ldy #S.ROUTE.F
				lda (pRoute),y
				sta FlagByte

				ldx #0
				ldy #FLAGS

.1				lda #'-
				asl FlagByte
				bcc .2

				lda MSG.FLAGS,x

.2				sta (pData),y

.7				iny
				inx
				cpx #8
				bcc .1

CS.RUN.FRMTF.RTS
				rts
*--------------------------------------
CS.RUN.PRINTRT	ldy #S.ROUTE.IF
				lda (pRoute),y

				>KAPI GetNameByID
				bcs CS.RUN.FRMTF.RTS

				>STYA pDevName
				pha
				tya
*				clc
				adc #5
				sta R1
				pla
				adc #0
				sta R1+1			skip /dev/

				>SS
				>PUSHW L.MSG.ROUTE
				>PUSHEA.G ADDR
				>PUSHEA.G MASK
				>PUSHEA.G GW

				ldy #S.ROUTE.METRIC+1
				lda (pRoute),y
				>PUSHA
				dey
				lda (pRoute),y
				>PUSHA

				>PUSHEA.G FLAGS

				>PUSHW R1
				>PUSHBI 12
				>LIBC PrintF
				>SR

				>LDYA pDevName
				>LIBC Free

				rts
*--------------------------------------
CS.RUN.ADD		>LEA.G ROUTE+S.ROUTE.ADDR
				jsr CS.RUN.ScanIP
				bcs .9

				>LEA.G ROUTE+S.ROUTE.MASK
				jsr CS.RUN.ScanIP
				bcs .9

				>LEA.G ROUTE+S.ROUTE.GW
				jsr CS.RUN.ScanIP
				bcs .9

				inc Idx
				lda Idx
				>KAPI ArgV
				bcs .9

				>LIBC GetDevByName
				bcs .9

				>STYA.G ROUTE+S.ROUTE.IF

				inc Idx
				lda Idx
				>KAPI ArgV
				bcs .7

				>LIBC AToI
				>STYA.G ROUTE+S.ROUTE.METRIC
				bra .8

.7				lda #1
				>STA.G ROUTE+S.ROUTE.METRIC

.8				lda #AF_INET
				>STA.G ROUTE+S.ROUTE.AF

				lda #S.ROUTE.F.GW
				>STA.G ROUTE+S.ROUTE.F

				>LEA.G ROUTE
				>KAPI SetRoute

.9				rts
*--------------------------------------
CS.RUN.DEL		>LEA.G ROUTE+S.ROUTE.ADDR
				jsr CS.RUN.ScanIP
				bcs .9

				>LEA.G ROUTE+S.ROUTE.MASK
				jsr CS.RUN.ScanIP
				bcs .9

				lda #AF_INET
				>STA.G ROUTE+S.ROUTE.AF

				>LEA.G ROUTE
				>KAPI SetRoute

.9				rts
*--------------------------------------
CS.RUN.ScanIP	>STYA R1

				inc Idx
				lda Idx
				>KAPI ArgV
				bcs .9

				>STYA pArg

				>SS
				>PUSHW pArg
				>PUSHW L.SScanF.IP

				ldx #4

.1				>PUSHW R1
				>INCW R1
				dex
				bne .1

				>PUSHBI 8				4 PTRs on stack
				>LIBC SScanF
				>SR

.9				rts
*--------------------------------------
CS.SIG			sec
				rts
*--------------------------------------
CS.QUIT			clc
				rts
*--------------------------------------
CS.RUN.CheckOpt	ldy #1
				lda (pArg),y

				ldx #OptionVars-OptionList-1

.2				cmp OptionList,x
				beq .3

				dex
				bpl .2

				sec
				rts

.3				ldy OptionVars,x
				lda #$ff
				sta 0,y
				clc
				rts
*--------------------------------------
CS.RUN.KW		>STYA R1

				ldx #0

.1				ldy #$FF

.2				iny
				lda (R1),y
				beq .5

				cmp (pArg),y
				beq .2

.3				iny
				lda (R1),y
				bne .3

.4				inx
				inx

				tya
				sec
				adc R1
				sta R1
				bcc .1

				inc R1+1
				bra .1

.5				tya
				beq .9

				lda (pArg),y
				bne .4

				clc
				rts

.9				sec
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
OptionList		.AS "CRcr"
OptionVars		.DA #bContinue,#bRecurse
				.DA #bContinue,#bRecurse
*--------------------------------------
KW				.AZ "add"
				.AZ "del"
				.DA #0
*--------------------------------------
SScanF.IP		.AZ "%d.%d.%d.%d"
*--------------------------------------
MSG.USAGE		.CS "Usage : ROUTE add net mask gw if metric\r\n"
				.CZ "      : ROUTE del net mask\r\n"
MSG.ROUTE0		.CZ "Destination     Mask            Gateway        Metric Flags    Interface"
MSG.ROUTE		.CZ "%15s %15s %15s %5D %s %s\r\n"
MSG.FLAGS		.CZ "G???????"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
ROUTE			.BS S.ROUTE
ADDR			.BS 16
MASK			.BS 16
GW				.BS 16
FLAGS			.BS 9
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/route.s
ASM
