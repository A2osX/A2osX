NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/term
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/a2osx.api.i
*--------------------------------------
TIMEOUT.MAX		.EQ 200					20 sec.
BUFSIZE			.EQ 256
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPBufPtr		.BS 2
ZPDataLen		.BS 2
pFILE			.BS 2
pSTDIN			.BS 2
pSTDOUT			.BS 2
TimeOut			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA #S.PS.SIG.T1SEC
				.DA #0
				.DA CS.END
				.DA ID.END
				.DA DS.END
				.DA #32					Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.SIG
				.DA	CS.QUIT
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.DEVOK		.DA MSG.DEVOK
				.DA 0
*--------------------------------------
CS.INIT			clc
				rts
*--------------------------------------
CS.RUN			lda #1
				>KAPI ArgV
				bcs .9

				>SS
				>PUSHYA
				>PUSHBI O.RDWR
				>PUSHBI 0				type
				>PUSHWZ					auxtype
				>LIBC FOpen
				>SR
				bcs .99

				>STYA pFILE
				
				>LDYA L.MSG.DEVOK
				>LIBC PutS

				ldy #S.PS.pStdIn
				ldx #0

.1				jsr A2osX.GetPSy
				sta pSTDIN,x
				iny
				inx
				cpx #4
				bcc .1

				>LDYAI BUFSIZE
				>LIBC Malloc
				bcs .99

				>STYA ZPBufPtr

				bra CS.RUN.LOOP

.9				>LDYA L.MSG.USAGE
				>LIBC PutS

				lda #E.SYN
				sec
.99
CS.RUN.RTS		rts
*--------------------------------------
CS.RUN.LOOP		>SLEEP

				>LDYA pFILE
				>LIBC FEOF
				bcs .9

				tya
				bne .1					EOF, no char

				ldy #pFILE
				jsr CS.Read
				bcs CS.RUN.RTS

				>STYA ZPDataLen
				
				ldy #pSTDOUT
				jsr CS.Write
				bcs .9

.1				>LDYA pSTDIN
				>LIBC FEOF
				bcs .9

				tya
				bne CS.RUN.LOOP			EOF = true, no char from STDIN

				ldy #pSTDIN
				jsr CS.Read
				bcs .9

				>STYA ZPDataLen
				
				lda (ZPBufPtr)
				eor #$14				Ctrl-T
				sec
				beq .9					exit with A=0,CS

				ldy #pFILE
				jsr CS.Write
				bcc CS.RUN.LOOP
				
.9				rts
*--------------------------------------
CS.Read			>SS
				>PUSHW 0,y
				>PUSHW ZPBufPtr
				>PUSHWI BUFSIZE
				>LIBC FRead
				>SR
				rts
*--------------------------------------
CS.Write		>SS
				>PUSHW 0,y
				>PUSHW ZPBufPtr
				>PUSHW ZPDataLen
				>LIBC FWrite
				>SR
				rts
*--------------------------------------
CS.SIG			lda TimeOut
				beq .9

				dec TimeOut

.9				sec						do not discard TIMER event
				rts
*--------------------------------------
CS.QUIT			>LDYA pFILE
				beq .1

				>LIBC FClose

.1				>LDYA ZPBufPtr
				beq .2

				>LIBC Free

.2				clc
				rts
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
MSG.USAGE		.AZ "Usage : TERM device"
MSG.DEVOK		.AZ "Connected (Exit key is Ctrl-T)"
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
				.DUMMY
				.OR 0
DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/term.s
ASM
