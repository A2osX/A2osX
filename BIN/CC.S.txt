NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF bin/cc
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/mli.i
				.INB inc/mli.e.i
*--------------------------------------
FILES.MAX		.EQ 4
LIBS.MAX		.EQ 4
SCOPE.MAX		.EQ 16
LOOKUP.MAX		.EQ 16
*--------------------------------------
SYM.T			.EQ 0
SYM.T.VOID			.EQ 1
SYM.T.UCHAR			.EQ 2
SYM.T.SCHAR			.EQ 3
SYM.T.UINT			.EQ 4
SYM.T.SINT			.EQ 5
SYM.T.ULONG			.EQ 6
SYM.T.SLONG			.EQ 7
SYM.T.FLOAT			.EQ 8
SYM.T.STRUCT		.EQ 9
SYM.T.UNION			.EQ 10
SYM.T.ENUM			.EQ 11
SYM.T.VARIADIC		.EQ $FF
SYM.Q			.EQ 1
SYM.Q.POINTER		.EQ %01000000
SYM.Q.PPOINTER		.EQ %10000000
SYM.Q.PPPOINTER		.EQ %11000000
SYM.Q.ARRAY			.EQ %00010000
SYM.Q.AARRAY 		.EQ %00100000
SYM.Q.AAARRAY		.EQ %00110000
SYM.Q.CONST			.EQ %00001000
SYM.Q.VOLATILE		.EQ %00000100
SYM.Q.FUNC			.EQ %00000010
SYM.Q.FASTCALL		.EQ %00000001
SYM.SC			.EQ 2
SYM.SC.STATIC		.EQ 0
SYM.SC.AUTO			.EQ 2
SYM.SC.REGISTER		.EQ 4
SYM.SC.EXTERN		.EQ 6
SYM.SC.TYPEDEF		.EQ 8
SYM.SC.DEFINE		.EQ 10
SYM.SC.TAG			.EQ 12
SYM.DefSize		.EQ 3
SYM.SizeOf		.EQ 4
SYM.Addr		.EQ 6
SYM.Def			.EQ 8					Funtions : fTQ,a1TQ,a2TQ....
*
SYM				.EQ 10
*--------------------------------------
STRUCTF.T		.EQ 0
STRUCTF.Q		.EQ 1
STRUCTF.BitOfs	.EQ 2
STRUCTF.BitWidth	.EQ 3
STRUCTF.Name	.EQ 4
*--------------------------------------
* Zero Page Segment, up to 32 bytes
*--------------------------------------
				.DUMMY
				.OR ZPBIN
ZS.START
ZPPtr1			.BS 2
ZPPtr2			.BS 2
ZPPtr3			.BS 2
ArgIndex		.BS 1
bPass2			.BS 1

ZPCCCode		.BS 2
ZPCCConst		.BS 2
ZPCCData		.BS 2
ZPCCStack		.BS 2

ZPLineBufPtr	.BS 2
ZPLookupPtr		.BS 2
CStackPtr		.BS 1
LocalPtr		.BS 1
ScopeIdx		.BS 1
bLocalScope		.BS 1

ZPSymBufPtr		.BS 2
ZPLookupSymPtr	.BS 2

bInitCode		.BS 1
StmtPreOp		.BS 1
ExpState		.BS 1
*ExpState.VinYA		.EQ $10
ExpState.AonStack	.EQ $20
ExpState.AinPTR		.EQ $40
ExpState.VonStack	.EQ $80
ZPState			.BS 1
ZS.END			.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #1					BIN Layout Version 1
				.DA #0					S.PS.F.EVENT
				.DA #0
				.DA CS.END-CS.START		Code Size (without Constants)
				.DA DS.END-DS.START		Data Size
				.DA #256				Stack Size
				.DA #ZS.END-ZS.START	Zero Page Size
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA CS.INIT
				.DA CS.RUN
				.DA CS.DOEVENT
				.DA	CS.QUIT
*--------------------------------------
L.MSG.GREETINGS	.DA MSG.GREETINGS
L.MSG.USAGE		.DA MSG.USAGE
L.MSG.READING	.DA MSG.READING
L.MSG.LINKING	.DA MSG.LINKING
L.MSG.LINKING2	.DA MSG.LINKING2
L.MSG.GENERATING .DA MSG.GENERATING
L.MSG.DEBUG		.DA MSG.DEBUG
L.MSG.TRACE		.DA MSG.TRACE
L.MSG.RTSTK		.DA MSG.RTSTK
L.MSG.ERROR		.DA MSG.ERROR
L.MSG.SUMMARY	.DA MSG.SUMMARY
L.MSG.NOMAIN	.DA MSG.NOMAIN
L.ENV.INCLUDE	.DA ENV.INCLUDE
L.ENV.LIB		.DA ENV.LIB
L.LIB.O			.DA LIB.O
*--------------------------------------
L.CC.AOPS		.DA CC.AOPS
L.CC.PREOPS		.DA CC.PREOPS
J.CC.PREOPS		.DA CC.EXP.Ref
				.DA CC.EXP.Deref
				.DA CC.EXP.Abs
				.DA CC.EXP.negate
				.DA CC.EXP.lnot
				.DA CC.EXP.bnot
				.DA CC.EXP.preinc
				.DA CC.EXP.predec
L.CC.POSTOPS	.DA CC.POSTOPS
J.CC.POSTOPS	.DA CC.EXP.postinc
				.DA CC.EXP.postdec
				.DA CC.EXP.idx
				.DA CC.EXP.Struct
				.DA CC.EXP.pStruct
L.CC.BOPS		.DA CC.BOPS
L.CC.DIRS		.DA CC.DIRS
L.CC.KW			.DA CC.KW
L.CC.KW2.IF		.DA CC.KW2.IF
L.CC.KW2.DO		.DA CC.KW2.DO
L.CC.SCSPEC		.DA CC.SCSPEC
L.CC.TYPEQUAL	.DA CC.TYPEQUAL
L.CC.TYPESPEC	.DA CC.TYPESPEC
L.CC.TYPES		.DA CC.TYPES
L.CC.FTYPES		.DA CC.FTYPES
J.CC.DIRS		.DA CC.DIR.DEFINE
				.DA CC.DIR.INCLUDE
*--------------------------------------
J.CC.KW			.DA CC.KW.IF
				.DA CC.KW.WHILE
				.DA CC.KW.ELSE
				.DA CC.KW.DO
				.DA CC.KW.FOR
				.DA CC.KW.SWITCH
				.DA CC.KW.CASE
				.DA CC.KW.DEFAULT
				.DA CC.KW.BREAK
				.DA CC.KW.CONTINUE
				.DA CC.KW.RETURN
*--------------------------------------
				.DA CC.KW.SIZEOF
				.DA CC.KW.STRUCT
				.DA CC.KW.UNION
				.DA CC.KW.ENUM
*--------------------------------------
J.CC.KW.END		.DA CC.KW.IF.END
				.DA CC.KW.WHILE.END
				.DA CC.KW.ELSE.END
				.DA CC.KW.DO.END
				.DA CC.KW.FOR.END
				.DA CC.KW.SWITCH.END
*--------------------------------------
J.CC.SCSPEC		.DA CC.SCSPEC.AUTO
				.DA CC.SCSPEC.REGISTER
				.DA CC.SCSPEC.STATIC
				.DA CC.SCSPEC.EXTERN
				.DA CC.SCSPEC.TYPEDEF
J.CC.TYPEQUAL	.DA CC.TYPE.CONST
				.DA CC.TYPE.VOLATILE
J.CC.TYPESPEC	.DA CC.TYPE.SIGNED
				.DA CC.TYPE.UNSIGNED
				.DA CC.TYPE.SHORT
J.CC.TYPES		.DA CC.TYPE.VOID
				.DA CC.TYPE.CHAR
				.DA CC.TYPE.INT
				.DA CC.TYPE.LONG
				.DA CC.TYPE.FLOAT
J.CC.UTYPES		.DA CC.TYPE.UCHAR
				.DA CC.TYPE.UINT
				.DA CC.TYPE.ULONG
J.CC.STYPES		.DA CC.TYPE.SCHAR
				.DA CC.TYPE.SINT
				.DA CC.TYPE.SLONG
*--------------------------------------
L.CC.MAIN		.DA CC.MAIN
L.CC.SYSCALL	.DA CC.SYSCALL
L.CC.FPUCALL	.DA CC.FPUCALL
L.PCC.FH		.DA PCC.FH
*--------------------------------------
L.PCC.DEBUG		.DA PCC.DEBUG
L.PCC.SLEEP		.DA PCC.SLEEP
L.PCC.ENTER		.DA PCC.ENTER
L.PCC.LEAVE		.DA PCC.LEAVE
L.PCC.ADDLOCAL	.DA PCC.ADDLOCAL
L.PCC.SetRetValue	.DA PCC.SetRetValue
L.PCC.TestZero1	.DA PCC.TestZero1
L.PCC.TestZero2	.DA PCC.TestZero2
L.PCC.TestZeroX	.DA PCC.TestZeroX
L.PCC.TestnZero1	.DA PCC.TestnZero1
L.PCC.TestnZero2	.DA PCC.TestnZero2
L.PCC.TestnZeroX	.DA PCC.TestnZeroX
L.PCC.PushLAddrH	.DA PCC.PushLAddrH
L.PCC.PushLAddrL	.DA PCC.PushLAddrL
L.PCC.PushIAddr	.DA PCC.PushIAddr
L.PCC.PushUAddr	.DA PCC.PushUAddr
L.PCC.GetLAddr1	.DA PCC.GetLAddr1
L.PCC.GetIAddr1	.DA PCC.GetIAddr1
L.PCC.GetUAddr1	.DA PCC.GetUAddr1
L.PCC.PopAddr1	.DA PCC.PopAddr1
L.PCC.PopValue1	.DA PCC.PopValue1
L.PCC.PopValue2	.DA PCC.PopValue2
L.PCC.PopValueX	.DA PCC.PopValueX
L.PCC.GetLAddr2	.DA PCC.GetLAddr2
L.PCC.GetIAddr2	.DA PCC.GetIAddr2
L.PCC.GetUAddr2	.DA PCC.GetUAddr2
L.PCC.PopAddr2	.DA PCC.PopAddr2
L.PCC.Deref2	.DA PCC.Deref2
L.PCC.PushDeref2	.DA PCC.PushDeref2
L.PCC.PushValue1	.DA PCC.PushValue1
L.PCC.PushValue2	.DA PCC.PushValue2
L.PCC.PushValueY	.DA PCC.PushValueY
L.PCC.Inc1		.DA PCC.Inc1
L.PCC.Inc2		.DA PCC.Inc2
L.PCC.Inc4		.DA PCC.Inc4
L.PCC.Dec1		.DA PCC.Dec1
L.PCC.Dec2		.DA PCC.Dec2
L.PCC.Dec4		.DA PCC.Dec4
*--------------------------------------
L.PCC.bBOPS		.DA PCC.BOPS.bMUL
				.DA PCC.BOPS.bDIV
				.DA PCC.BOPS.bMOD
				.DA PCC.BOPS.ADD
				.DA PCC.BOPS.SUB
				.DA PCC.BOPS.SHL
				.DA PCC.BOPS.SHR
				.DA PCC.BOPS.L
				.DA PCC.BOPS.G
				.DA PCC.BOPS.LE
				.DA PCC.BOPS.GE
				.DA PCC.BOPS.EQ
				.DA PCC.BOPS.NE
				.DA PCC.BOPS.LAND
				.DA PCC.BOPS.AND
				.DA PCC.BOPS.LOR
				.DA PCC.BOPS.OR
				.DA PCC.BOPS.EOR
*--------------------------------------
L.PCC.cBOPS		.DA PCC.BOPS.cMUL
				.DA PCC.BOPS.cDIV
				.DA PCC.BOPS.cMOD
				.DA PCC.BOPS.ADD
				.DA PCC.BOPS.SUB
				.DA PCC.BOPS.SHL
				.DA PCC.BOPS.SHR
				.DA PCC.BOPS.L
				.DA PCC.BOPS.G
				.DA PCC.BOPS.LE
				.DA PCC.BOPS.GE
				.DA PCC.BOPS.EQ
				.DA PCC.BOPS.NE
				.DA PCC.BOPS.LAND
				.DA PCC.BOPS.AND
				.DA PCC.BOPS.LOR
				.DA PCC.BOPS.OR
				.DA PCC.BOPS.EOR
*--------------------------------------
				.DA 0
*--------------------------------------
* Called once at process creation
* Put code for loading LIB here
*--------------------------------------
CS.INIT			clc
CS.INIT.RTS		rts
*--------------------------------------
* Called until exit with CS
* if RUN exits with CC, RN entered again
*--------------------------------------
CS.RUN			>PUSHW L.MSG.GREETINGS
				>PUSHW A2osX.KVER
				>PUSHBI 2
				>SYSCALL PrintF
				bcs CS.INIT.RTS

				lda #hFilePtr
				>STA.G hFilePtr

				lda #LineCntPtr
				>STA.G LineCntPtr

				>LDYAI 256
				>SYSCALL GetMem
				bcs CS.INIT.RTS

				>STYA.G LineBufPtr
				txa
				>STA.G hLineBuf

				jsr CS.RUN.ARGS
				bcs CS.INIT.RTS

				jsr CORE.Init
				bcs CS.INIT.RTS

				jsr CODE.Init
				bcs CS.INIT.RTS

				jsr CC.FIO.FOpen1st
				bcs CS.INIT.RTS
*--------------------------------------
CS.RUN.CLOOP	jsr CC.CompileFile

				cmp #MLI.E.EOF
				bne .9

				jsr CC.FIO.FClose
				bne CS.RUN.CLOOP
*--------------------------------------
				jsr PrintSummary
				bcs .99

				jsr CC.Link
				bcs .99
*			>DEBUG
				jsr CC.Link.CheckMain
				bcs .99

				dec bPass2

				jsr CC.FIO.FCreate
				bcs .99

				jsr CODE.Init
				bcs .99

				jsr CC.FIO.FOpen1st
				bcs .99
*--------------------------------------
.2				jsr CC.CompileFile

				cmp #MLI.E.EOF
				bne .9
				
				jsr CODE.EmitTail
				bcs .9

				jsr CC.FIO.FClose
				bne .2
*--------------------------------------
.8				lda #0					Exit Code = Success
				sec
				rts

.9				pha
				jsr PrintErrorMsg
				pla
				sec
.99				rts
*--------------------------------------
CS.RUN.ARGS		inc ArgIndex

				lda ArgIndex
				>SYSCALL ArgV
				bcs .8

				>STYA ZPPtr1
				lda (ZPPtr1)
				cmp #'-'
				bne .4

				ldy #1
				lda (ZPPtr1),y
				beq .90

				ldx #OptionVars-OptionList-1

.1				cmp OptionList,x
				beq .2

				dex
				bpl .1

				bra .90

.2				cpx #4
				bcc .3
				
				inc ArgIndex

				lda ArgIndex
				>SYSCALL ArgV
				bcs .90
				
				jsr CC.FIO.LoadLIB.o
				bcc CS.RUN.ARGS

				rts

.3				ldy OptionVars,x
				lda #$ff
				sta (pData),y
				bra CS.RUN.ARGS

.4				>LDA.G CC.InFileArg
				bne .5

				lda ArgIndex
				>STA.G CC.InFileArg
				bra CS.RUN.ARGS

.5				>LDA.G CC.OutFileArg
				bne .90

				lda ArgIndex
				>STA.G CC.OutFileArg
				bra CS.RUN.ARGS

.8				>LDA.G CC.InFileArg
				beq .90

				>LDA.G CC.OutFileArg
				beq .90

				clc
.9				rts

.90				>PUSHW L.MSG.USAGE
				>PUSHBI 0
				>SYSCALL PrintF

				lda #E.SYN
				sec
				rts
*--------------------------------------
CS.DOEVENT		sec
				rts
*--------------------------------------
CS.QUIT			jsr CODE.Quit
				jsr CORE.Quit
				
.1				>LDA.G LibCount
				beq .6
				
				dec 
				sta (pData),y

				clc
				adc #hLibOs
				tay
				lda (pData),y
				>SYSCALL FreeMem
				bra .1

.6				>LDA.G hLineBuf
				beq .8

				>SYSCALL FreeMem

.8				clc
				rts
*--------------------------------------
PrintTraceMsg
				ldy #S.PS.hStdOut
*				ldy #S.PS.hStdErr
				lda (pPS),y
				>PUSHA

				>PUSHW L.MSG.TRACE

				>LDA.G LineCntPtr
				inc
				tay
				lda (pData),y
				>PUSHA
				dey
				lda (pData),y
				>PUSHA
				>PUSHW.G LineBufPtr
				>PUSHBI 4
				>SYSCALL FPrintF

				rts
*--------------------------------------
PrintDebugMsg	>PUSHW L.MSG.DEBUG

				>PUSHW ZPCCCode
				>PUSHW ZPCCConst
				>PUSHW ZPCCData

				lda pStack
				clc
				adc #8
				pha
				lda pStack+1
				adc #0
				>PUSHA
				pla
				>PUSHA

				>PUSHB LocalPtr

				>PUSHW ZPCCStack

				>PUSHB CStackPtr
				tay
				beq .1

				lda (ZPCCStack),y
				pha
				iny
				lda (ZPCCStack),y
				ply

.1				>PUSHYA

				lda ScopeIdx
				sec
				sbc #CC.hScopes
				>PUSHA

				>PUSHBI 15
				>SYSCALL PrintF
			>DEBUG
				rts
*--------------------------------------
PrintErrorMsg	>LDA.G CC.hDefineBuf
				beq .10

				>LDYA.G CC.SaveDefine
				>STYA ZPLineBufPtr

.10				>LDA.G bTrace
				bmi .1

				jsr PrintTraceMsg

.1				lda ZPLineBufPtr

				sec
				>SBC.G LineBufPtr
				tax

				>LDYA.G LineBufPtr
				>STYA ZPLineBufPtr

				txa
				beq .3

				ldy #0
				lda #'-'

.2				sta (ZPLineBufPtr),y
				iny
				dex
				bne .2

				txa

.3				sta (ZPLineBufPtr),y

.8				ldy #S.PS.hStdErr
				lda (pPS),y
				>PUSHA
				>PUSHW L.MSG.ERROR
				>PUSHW ZPLineBufPtr
				>PUSHBI 2
				>SYSCALL FPrintF
				rts
*--------------------------------------
PrintSummary	>PUSHW L.MSG.SUMMARY
				lda ZPCCCode+1
				sec
				sbc #$20
				>PUSHA
				>PUSHB ZPCCCode
				>PUSHW ZPCCConst
				>PUSHW ZPCCData
				>PUSHBI 6
				>SYSCALL PrintF
*			>DEBUG
				rts
*--------------------------------------
				.INB usr/src/bin/cc.s.code
				.INB usr/src/bin/cc.s.core
				.INB usr/src/bin/cc.s.dir
				.INB usr/src/bin/cc.s.decl
				.INB usr/src/bin/cc.s.stmt
				.INB usr/src/bin/cc.s.kw
				.INB usr/src/bin/cc.s.type
				.INB usr/src/bin/cc.s.f
				.INB usr/src/bin/cc.s.exp
				.INB usr/src/bin/cc.s.sym
				.INB usr/src/bin/cc.s.link
				.INB usr/src/bin/cc.s.fio
*--------------------------------------
CS.END
*--------------------------------------
* Initialized DATA
*--------------------------------------
MSG.GREETINGS	.AZ "\r\nA2osX C Compiler %d.%d\r\n\r\n"
MSG.USAGE		.AS "Usage : CC <option> srcfile.c dstfile\r\n"
				.AS "   -D : Debug Mode\r\n"
				.AS "   -T : Trace On\r\n"
				.AZ "   -L Libname1 -O Libname2 ...\r\n"
MSG.READING		.AZ "*** Reading file: %s\r\n"
MSG.LINKING		.AZ "*** Linking..."
MSG.LINKING2	.AZ "*** %H:%18s T/Q=%h/%h, SC=%h, Def=%h, SizeOf=%H, @=%H\r\n"
MSG.GENERATING	.AZ "*** Generating: %s\r\n"
MSG.DEBUG		.AS "*** pCode=%H, pConst=%H, pData=%H, pStack=%H\r\n"
				.AZ "    LSP=%h, CSP=%H:%h>%h:%h, ScopeIdx=%h\r\n"
MSG.TRACE		.AZ "%5D> %s\r\n"
MSG.RTSTK		.AZ "<pBase=%H, pVar=%H, (pVar)=%H, pStack=%H>\r\n"
MSG.ERROR		.AZ "       %s^\r\n"
MSG.SUMMARY		.AZ "*** Compilation OK : Code size=%5D, Const size=%5D, uData size=%5D\r\n"
MSG.NOMAIN		.AZ "*** No 'main()' function defined."
*--------------------------------------
OptionList		.AS "DdTtLl"
OptionVars		.DA #bDebug,#bDebug,#bTrace,#bTrace
*--------------------------------------
ENV.INCLUDE		.AZ "INCLUDE"
ENV.LIB			.AZ "LIB"
LIB.O			.AZ ".o"
*--------------------------------------
CC.EscChars		.AS "abefnrtv"
				.HS 5C27223F
CC.EscChars.Cnt	.EQ *-CC.EscChars
CC.EscCodes		.HS 07081B0C0A0D090B
				.HS 5C27223F
*--------------------------------------
CC.OPChars		.AS "!~+-*/%=&|^<>["
CC.OPChars.Cnt	.EQ *-CC.OPChars
*--------------------------------------
* Assignment Operators: = += -= *= /= %= <<= >>= &= ^= |=
*--------------------------------------
CC.AOPS			>PSTR "="
				>PSTR "+="
				>PSTR "-="
				>PSTR "*="
				>PSTR "/="
				>PSTR "%="
				>PSTR "<<="
				>PSTR ">>="
				>PSTR "&="
				>PSTR "^="
				>PSTR "!="
				.HS 00
*--------------------------------------
* Pre Operators:  & * + - ! ~ ++ --
*--------------------------------------
CC.PREOPS		>PSTR "&"				ref
				>PSTR "*"				deref
				>PSTR "+"				abs
				>PSTR "-"				negate
				>PSTR "!"				logical not
				>PSTR "~"				bitwise not
				>PSTR "++"				pre increase
				>PSTR "--"				pre decrease
				.HS 00
*--------------------------------------
* Post Operators: ++ --
*--------------------------------------
CC.POSTOPS		>PSTR "++"
				>PSTR "--"
				>PSTR "["
				>PSTR "."
				>PSTR "->"
				.HS 00
*-- Binary ---- H to L precedence -----
* Arithmetic Operators: * / %
* Arithmetic Operators: + -
* Shift Operators: << >>
* Relational Operators: < > >= < >= == !=
* Bitwise Operators: & | ^ ~
* Logical Operators: && ||
*--------------------------------------
CC.BOPS			>PSTR "*"
				>PSTR "/"
				>PSTR "%"
				>PSTR "+"
				>PSTR "-"
				>PSTR "<<"
				>PSTR ">>"
				>PSTR "<"
				>PSTR "<="
				>PSTR ">"
				>PSTR ">="
				>PSTR "=="
				>PSTR "!="
				>PSTR "&"
				>PSTR "|"
				>PSTR "^"
				>PSTR "&&"
				>PSTR "||"
				.HS 00
*--------------------------------------
* Reserved Directives:
*--------------------------------------
CC.DIRS			>PSTR "define"
				>PSTR "include"
				.HS 00
*--------------------------------------
* Reserved Keywords:
*--------------------------------------
CC.KW			>PSTR "if"
				>PSTR "while"
				>PSTR "else"
				>PSTR "do"
				>PSTR "for"
				>PSTR "switch"
				>PSTR "case"
				>PSTR "default"
				>PSTR "break"
				>PSTR "continue"
				>PSTR "return"
				>PSTR "sizeof"
				>PSTR "struct"
				>PSTR "union"
				>PSTR "enum"
				.HS 00
CC.KW2.IF		>PSTR "else"
				.HS 00
CC.KW2.DO		>PSTR "while"
				.HS 00
*--------------------------------------
CC.SCSPEC		>PSTR "auto"
				>PSTR "register"
				>PSTR "static"
				>PSTR "extern"
				>PSTR "typedef"
				.HS 00
*--------------------------------------
CC.TYPEQUAL		>PSTR "const"			ALL
				>PSTR "volatile"
CC.TYPESPEC		>PSTR "signed"			char,int,long
				>PSTR "unsigned"		char,int,long
				>PSTR "short"			int
CC.TYPES		>PSTR "void"
				>PSTR "char"
				>PSTR "int"
				>PSTR "long"
				>PSTR "float"
				.HS 00
*--------------------------------------
CC.Type2FPU	.DA #FPU.wMUL,#FPU.iMUL,#FPU.uMUL,#FPU.lMUL,#FPU.fMUL
*--------------------------------------
CC.FTYPES		>PSTR "fastcall"
*				.HS 00
*--------------------------------------
CC.TYPESIZE	.HS 0001010202040405
CC.TYPESIGN	.HS 0000FF00FF00FFFF
*--------------------------------------
CC.MAIN			.AZ "main"
CC.MAIN.DEF		.DA #SYM.T.SINT,#0,#SYM.T.SINT,#0,#SYM.T.UCHAR,#SYM.Q.PPOINTER,0
CC.MAIN.DEF.LEN	.EQ *-CC.MAIN.DEF
*--------------------------------------
CC.SYSCALL		>PSTR "chtyp"
				.DA #SYS.ChTyp
				>PSTR "chmod"
				.DA #SYS.ChMod
				>PSTR "fstat"
				.DA #SYS.FStat
				>PSTR "stat"
				.DA #SYS.Stat
				>PSTR "mkdir"
				.DA #SYS.MKDir
				>PSTR "mknod"
				.DA #SYS.MKNod
				>PSTR "mkfifo"
				.DA #SYS.MKFIFO
				>PSTR "pipe"
				.DA #SYS.Pipe
				>PSTR "opendir"
				.DA #SYS.OpenDir
				>PSTR "readdir"
				.DA #SYS.ReadDir
				>PSTR "closedir"
				.DA #SYS.CloseDir
				>PSTR "open"
				.DA #SYS.Open
				>PSTR "close"
				.DA #SYS.Close
				>PSTR "read"
				.DA #SYS.Read
				>PSTR "write"
				.DA #SYS.Write
				>PSTR "chown"
				.DA #SYS.ChOwn
				>PSTR "fputc"
				.DA #SYS.FPutC
				>PSTR "putchar"
				.DA #SYS.PutChar
				>PSTR "getc"
				.DA #SYS.GetC
				>PSTR "getchar"
				.DA #SYS.GetChar
				>PSTR "fputs"
				.DA #SYS.FPutS
				>PSTR "puts"
				.DA #SYS.PutS
				>PSTR "fgets"
				.DA #SYS.FGetS
				>PSTR "ungetc"
				.DA #SYS.UngetC
				>PSTR "fopen"
				.DA #SYS.FOpen
				>PSTR "fclose"
				.DA #SYS.FClose
				>PSTR "fread"
				.DA #SYS.FRead
				>PSTR "fwrite"
				.DA #SYS.FWrite
				>PSTR "fflush"
				.DA #SYS.FFlush
				>PSTR "fseek"
				.DA #SYS.FSeek
				>PSTR "ftell"
				.DA #SYS.FTell
				>PSTR "feof"
				.DA #SYS.FEOF
				>PSTR "remove"
				.DA #SYS.Remove
				>PSTR "rename"
				.DA #SYS.Rename
				>PSTR "printf"
				.DA #SYS.PrintF
				>PSTR "fprintf"
				.DA #SYS.FPrintF
				>PSTR "sprintf"
				.DA #SYS.SPrintF
				>PSTR "scanf"
				.DA #SYS.ScanF
				>PSTR "fscanf"
				.DA #SYS.FScanF
				>PSTR "sscanf"
				.DA #SYS.SScanF
*				>PSTR "$50"
*				.DA #0
*				>PSTR "$52"
*				.DA #0
*				>PSTR "$54"
*				.DA #0
*				>PSTR "$56"
*				.DA #0
*				>PSTR "$58"
*				.DA #0
*				>PSTR "$5A"
*				.DA #0
*				>PSTR "$5C"
*				.DA #0
				>PSTR "setattr"
				.DA #SYS.SetAttr
*				>PSTR "mount"
*				.DA #0
*				>PSTR "umount"
*				.DA #0
*				>PSTR "online"
*				.DA #0
*				>PSTR "getstkobj"
*				.DA #0
*				>PSTR "shift"
*				.DA #0
*				>PSTR "argv"
*				.DA #0
*				>PSTR "argvDup"
*				.DA #0
*				>PSTR "geterrmsg"
*				.DA #0
				>PSTR "atof"
				.DA #SYS.AToF
				>PSTR "atoi"
				.DA #SYS.AToI
				>PSTR "atol"
				.DA #SYS.AToL
				>PSTR "strtof"
				.DA #SYS.StrToF
				>PSTR "strtol"
				.DA #SYS.StrToL
				>PSTR "strtoul"
				.DA #SYS.StrToUL
				>PSTR "realpath"
				.DA #SYS.RealPath
				>PSTR "expand"
				.DA #SYS.Expand
				>PSTR "strlen"
				.DA #SYS.StrLen
				>PSTR "strcpy"
				.DA #SYS.StrCpy
				>PSTR "strcat"
				.DA #SYS.StrCat
				>PSTR "strdup"
				.DA #SYS.StrDup
				>PSTR "strupr"
				.DA #SYS.StrUpr
				>PSTR "strlwr"
				.DA #SYS.StrLwr
				>PSTR "strcmp"
				.DA #SYS.StrCmp
				>PSTR "strcasecmp"
				.DA #SYS.StrCaseCmp
*				>PSTR "getdevbyname"
*				.DA #0
*				>PSTR "getdevstatus"
*				.DA #0
*				>PSTR "mkdev"
*				.DA #0
*				>PSTR "ioctl"
*				.DA #0
*				>PSTR "loadlib"
*				.DA #0
*				>PSTR "unloadlib"
*				.DA #0
*				>PSTR "loaddrv"
*				.DA #0
*				>PSTR "insdrv"
*				.DA #0
				>PSTR "execl"
				.DA #SYS.ExecL
*				>PSTR "execv"
*				.DA #0
				>PSTR "fork"
				.DA #SYS.Fork
				>PSTR "kill"
				.DA #SYS.Kill
*				>PSTR "loadfile"
*				.DA #0
*				>PSTR "loadtxtfile"
*				.DA #0
*				>PSTR "filesearch"
*				.DA #0
*				>PSTR "getmemstat"
*				.DA #0
*				>PSTR "strvnew"
*				.DA #0
*				>PSTR "strvset"
*				.DA #0
*				>PSTR "strvget"
*				.DA #0
*				>PSTR "strvfree"
*				.DA #0
				>PSTR "putenv"
				.DA #SYS.PutEnv
				>PSTR "setenv"
				.DA #SYS.SetEnv
				>PSTR "getenv"
				.DA #SYS.GetEnv
				>PSTR "unsetenv"
				.DA #SYS.UnsetEnv
				>PSTR "time"
				.DA #SYS.Time
				>PSTR "strftime"
				.DA #SYS.StrFTime
*				>PSTR "ptime2time"
*				.DA #0
				>PSTR "ctime2time"
				.DA #SYS.CTime2Time
				>PSTR "md5"
				.DA #SYS.MD5
				>PSTR "md5init"
				.DA #SYS.MD5Init
				>PSTR "md5update"
				.DA #SYS.MD5Update
				>PSTR "md5finalize"
				.DA #SYS.MD5Finalize
				>PSTR "getpwuid"
				.DA #SYS.GetPWUID
				>PSTR "getpwname"
				.DA #SYS.GetPWName
				>PSTR "putpw"
				.DA #SYS.PutPW
				>PSTR "getgrpgid"
				.DA #SYS.GetGRGID
				>PSTR "getgrpname"
				.DA #SYS.GetGRName
				>PSTR "putgr"
				.DA #SYS.PutGR
*				>PSTR "opensession"
*				.DA #0
*				>PSTR "closesession"
*				.DA #0
*				>PSTR "slistgetdata"
*				.DA #0
*				>PSTR "slistadddata"
*				.DA #0
*				>PSTR "slistsetdata"
*				.DA #0
*				>PSTR "slistgetbyid"
*				.DA #0
*				>PSTR "slistnewkey"
*				.DA #0
*				>PSTR "slistlookup"
*				.DA #0
*				>PSTR "slistfree"
*				.DA #0
*				>PSTR "slistnew"
*				.DA #0
				>PSTR "malloc"
				.DA #SYS.GetMem
				>PSTR "realloc"
				.DA #SYS.Realloc
*				>PSTR "getmemptr"
*				.DA #0
				>PSTR "free"
				.DA #SYS.FreeMem
*				>PSTR "newstkobj"
*				.DA #0
*				>PSTR "loadstkobj"
*				.DA #0
*				>PSTR "getstkobjptr"
*				.DA #0
*				>PSTR "freestkobj"
*				.DA #0
				.DA #0
*--------------------------------------
CC.FPUCALL		>PSTR "pwr"
				.DA #FPU.PWR
				>PSTR "log"
				.DA #FPU.LOG
				>PSTR "sqr"
				.DA #FPU.SQR
				>PSTR "exp"
				.DA #FPU.EXP
				>PSTR "cos"
				.DA #FPU.COS
				>PSTR "sin"
				.DA #FPU.SIN
				>PSTR "tan"
				.DA #FPU.TAN
				>PSTR "atan"
				.DA #FPU.ATAN
				>PSTR "lrintf"
				.DA #FPU.QINT
				.DA #0
*--------------------------------------
				.INB usr/src/bin/cc.s.pcc
*--------------------------------------
				.DUMMY
				.OR 0
DS.START
PathBuf			.BS MLI.MAXPATH+1

bDebug			.BS 1
bTrace			.BS 1

LibCount		.BS 1
hLibNs			.BS LIBS.MAX
hLibOs			.BS LIBS.MAX

CC.InFileArg	.BS 1
CC.OutFileArg	.BS 1
CC.hOutFile		.BS 1

hFilePtr		.BS 1
hFiles			.BS FILES.MAX

LineCntPtr		.BS 1
				.BS 1					for inc inc
LineCnts		.BS FILES.MAX*2

hLineBuf		.BS 1
LineBufPtr		.BS 2

CC.CmdSave		.BS 1

CC.hDefineBuf	.BS 1

CC.hDefines		.BS 1
CC.SaveDefine	.BS 2

CC.hTags		.BS 1
CC.hStack		.BS 1

CC.hScopes		.BS 1+SCOPE.MAX			1 Global+ MAX locals

CC.CPSID		.BS 2					AA
CC.CPSPFX		.BS 5					X_id\0
*--------------------------------------
CC.hSymBuf		.BS 1
CC.SymID		.BS 2
CC.SymIDSave	.BS 2
CC.ArgBuf		.BS SYM
*--------------------------------------
CC.LookupSymSize	.BS 2
CC.LookupSymPtr		.BS 2
CC.LookupIdx		.BS 1
CC.hLookupBufs		.BS LOOKUP.MAX

DS.END			.ED
*--------------------------------------
MAN
SAVE usr/src/bin/cc.s
ASM
