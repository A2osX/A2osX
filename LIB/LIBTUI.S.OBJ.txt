NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
OBJ.Create		pha						Type
				tax

				ldy OBJ.SIZES,x
				lda #0
				>LIBC Malloc
				bcs .9

				>STYA ZPpObj

				pla
				sta (ZPpObj)
				tax

				ldy OBJ.SIZES,x

				lda #0

				dey

.1				sta (ZPpObj),y
				dey
				bne .1

*				clc

				rts

.9				plx
				rts
*--------------------------------------
OBJ.rDestroy	ldx ZPpObj
				phx
				ldx ZPpObj+1
				phx

				jsr OBJ.Destroy

				plx
				stx ZPpObj+1
				plx
				stx ZPpObj
				
				rts
*--------------------------------------
OBJ.Destroy		>STYA ZPpObj

				ldy #S.OBJ.S
				lda (ZPpObj),y
				and #S.OBJ.S.bHasChilds
				beq .3

				ldy #S.OBJ.Childs

.1				phy
				
				lda (ZPpObj),y
				pha
				iny
				lda (ZPpObj),y
				ply
				
				cmp #0
				beq .2
				
				jsr OBJ.rDestroy
				
.2				ply
				iny
				iny
				cpy #S.OBJ
				bcc .1

.3				lda (ZPpObj)			S.OBJ.T
				tax
				jmp (J.ObjFree,x)
*--------------------------------------
OBJ.Free		>LDYA ZPpObj
				>LIBC Free

				rts
*--------------------------------------
OBJ.Activate	>STYA ZPpObj

				ldy #S.OBJ.S
				lda (ZPpObj),y
				ora #S.OBJ.S.bActive
				sta (ZPpObj),y

				jsr .1

				jmp OBJ.CurOn

.1				lda (ZPpObj)
				tax
				jmp (J.ObjActivate,x)
*--------------------------------------
OBJ.Deactivate	>STYA ZPpObj

				jsr OBJ.CurOff

				ldy #S.OBJ.S
				lda (ZPpObj),y
				and #S.OBJ.S.bActive^$ff
				sta (ZPpObj),y

				lda (ZPpObj)
				tax
				jmp (J.ObjDeactivate,x)
*--------------------------------------
OBJ.SetParent	>STAX ZPpParent
				ldy #S.OBJ.pParent
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y
				rts
*--------------------------------------
OBJ.SetX1Y1		ldy #S.OBJ.X1+1
				sta (ZPpObj),y
				txa
				dey
				sta (ZPpObj),y
				rts
*--------------------------------------
OBJ.SetWH		ldy #S.OBJ.W+1
				sta (ZPpObj),y
				txa
				dey
				sta (ZPpObj),y
				rts
*--------------------------------------
OBJ.SetpTitle	phy
				ldy #S.OBJ.pTitle+1
				bra OBJ.SetWord
*--------------------------------------
OBJ.SetpBuf		phy
				ldy #S.OBJ.pBuf+1
				bra OBJ.SetWord
*--------------------------------------
OBJ.SetBufSize	phy
				ldy #S.OBJ.BufSize+1
				bra OBJ.SetWord

OBJ.SetWord		sta (ZPpObj),y
				dey
				pla
				sta (ZPpObj),y

				rts
*--------------------------------------
OBJ.SetX2Y2		ldy #S.OBJ.X1
				jsr .7

				ldy #S.OBJ.Y1
				jsr .7

				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bHBorder
				beq .1

				ldy #S.OBJ.InnerX
				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

				ldy #S.OBJ.InnerW

				lda (ZPpObj),y
				dec
				dec
				sta (ZPpObj),y

.1				ldx #0

				ldy #S.OBJ.F
				lda (ZPpObj),y
				bit #S.OBJ.F.bVBorder+S.OBJ.F.bTitle
				beq .2

				dex

.2				bit #S.OBJ.F.bVBorder+S.OBJ.F.bStatus
				beq .3

				dex

.3				txa
				beq .4

				ldy #S.OBJ.InnerY
				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

 				txa

				ldy #S.OBJ.InnerH

				clc
				adc (ZPpObj),y
				sta (ZPpObj),y

.4				ldy #S.OBJ.F
				lda (ZPpObj),y
				bit #S.OBJ.F.bMenu
				beq .8

				ldy #S.OBJ.InnerY
				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

				ldy #S.OBJ.InnerH
				lda (ZPpObj),y
				dec
				sta (ZPpObj),y

.8				clc
				rts
*--------------------------------------
.7				lda (ZPpObj),y			S.OBJ.X1/Y1
				tax

				iny
				iny						S.OBJ.W/H

				phy

				clc
				adc (ZPpObj),y
				dec
				iny
				iny						S.OBJ.X2/Y2
				sta (ZPpObj),y
				iny
				iny

				txa
				sta (ZPpObj),y			S.OBJ.InnerX/Y

				ply						S.OBJ.W/H

				lda (ZPpObj),y
				iny
				iny
				iny
				iny
				iny
				iny
				sta (ZPpObj),y		S.OBJ.InnerW/H

				rts
*--------------------------------------
* (pStack) = Event
*--------------------------------------
OBJ.Run			ldy #S.OBJ.S
				lda (ZPpObj),y

				and #S.OBJ.S.bDisabled+S.OBJ.S.bHidden
				bne .9
				
				jsr .80

				tax
				bne .99					error / event

				bcc .99

				ldy #S.OBJ.Childs

.1				phy
				
				iny
				lda (ZPpObj),y
				beq .5

				ldx ZPpObj
				phx
				ldx ZPpObj+1
				phx
				
				tax
				dey
				lda (ZPpObj),y
				sta ZPpObj
				stx ZPpObj+1
			
				jsr OBJ.Run

				plx
				stx ZPpObj+1
				plx
				stx ZPpObj

				bcc .8

				tax
				bne .8

.5				ply
				
				iny
				iny
				cpy #S.OBJ
				bcc .1

.9				sec
				lda #0
				rts

.8				ply

.99				rts

.80				lda (ZPpObj)			S.OBJ.T
				tax
				
				jmp (J.ObjRun,x)
*--------------------------------------
OBJ.Draw		ldy #S.OBJ.S
				lda (ZPpObj),y
				and #S.OBJ.S.bHidden
				beq .5
				
				ldy #S.OBJ.Y1
				lda (ZPpObj),y
				>PUSHA
				
.1				jsr LB.Init

				jsr LB.SetColor

				ldy #S.OBJ.X1
				lda (ZPpObj),y
				tax
				lda (pStack)
				tay
				jsr LB.GotoXY
				
				ldy #S.OBJ.W
				lda (ZPpObj),y
				
				tax
				
				lda #C.SPACE
				
.2				jsr LB.COut
				iny
				dex
				bne .2
				
				jsr IO.LBWrite
				bcs .98
				
				lda (pStack)
				ldy #S.OBJ.Y2
				cmp (ZPpObj),y
				inc
				sta (pStack)
				bcc .1
				
				inc pStack
				
				clc
				rts
*--------------------------------------
.5				jsr OBJ.Draw.Top
				bcs .99

				jsr OBJ.Draw.Body
				bcs .99

				jsr OBJ.Draw.Bottom
				bcs .99

				ldy #S.OBJ.S
				lda (ZPpObj),y
				and #S.OBJ.S.bHasChilds
*				clc
				beq .99

				lda #S.OBJ.Childs
				>PUSHA

.6				tay
				lda (ZPpObj),y
				tax
				iny
				lda (ZPpObj),y
				beq .7

				ldy ZPpObj
				phy
				ldy ZPpObj+1
				phy

				stx ZPpObj
				sta ZPpObj+1

				jsr OBJ.Draw
				
				plx
				stx ZPpObj+1
				plx
				stx ZPpObj
				
				bcs .98

.7				lda (pStack)
				inc
				inc
				sta (pStack)
				cmp #S.OBJ
				bcc .6

				clc

.98				inc pStack

.99				rts
*--------------------------------------
OBJ.Draw.Top	ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bTitle+S.OBJ.F.bHBorder
				bne OBJ.Draw.Top.I

				clc
				rts

OBJ.Draw.Top.I	jsr LB.Init

				ldy #S.OBJ.X1
				lda (ZPpObj),y
				tax
				ldy #S.OBJ.Y1
				lda (ZPpObj),y
				tay
				jsr LB.GotoXY

				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bVBorder
				beq .1

				jsr LB.SetColor

				ldx #SEQ.BORDERTL-SEQS
				jsr LB.SeqOut

.1				ldy #S.OBJ.F
				lda (ZPpObj),y
				bpl .4					S.OBJ.F.bTitle

				ldx #SEQ.BAR-SEQS
				jsr LB.SeqOut

				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				ldy #S.OBJ.pTitle
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1
				beq .3

				lda #C.SI
				jsr LB.COut

				ldy #0

.2				lda (ZPPtr1),y
				beq .3

				jsr LB.COut
				iny
				dex
				bne .2

.3				txa
				beq .31

				lda #C.SPACE

.30				jsr LB.COut
				dex
				bne .30

.31				ldx #SEQ.BAREND-SEQS
				jsr LB.SeqOut
				bra .6

.4				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				lda #'q'

.5				jsr LB.COut
				dex
				bne .5

.6				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bVBorder
				beq .7

				jsr LB.SetColor

				ldx #SEQ.BORDERTR-SEQS
				jsr LB.SeqOut

.7				jmp IO.LBWrite
*--------------------------------------
OBJ.Draw.Body	ldy #S.OBJ.InnerY
				lda (ZPpObj),y
				>PUSHA					Start

				ldy #S.OBJ.InnerH
				lda (ZPpObj),y
				>PUSHA					Count

				>PUSHBI 0				Relative Y

OBJ.Draw.Lines	jsr OBJ.CurOff

.1				jsr LB.Init

				ldy #S.OBJ.X1
				lda (ZPpObj),y
				tax

				ldy #2					InnerY
				lda (pStack),y
				tay
				jsr LB.GotoXY

				jsr LB.SetColor

				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bVBorder
				php
				beq .2

				ldx #SEQ.BORDERV-SEQS
				jsr LB.SeqOut

.2				jsr OBJ.Draw.Body.JMP

				ldx #SEQ.NORMAL-SEQS
				jsr LB.SeqOut

				plp
				beq .4

				ldy #S.OBJ.X2
				lda (ZPpObj),y
				tax

				ldy #2					InnerY
				lda (pStack),y
				tay
				jsr LB.GotoXY

				jsr LB.SetColor

				ldx #SEQ.BORDERV-SEQS
				jsr LB.SeqOut

.4				jsr IO.LBWrite
				bcs .9

				lda (pStack)			Relative Y
				inc
				sta (pStack)

				ldy #2					InnerY
				lda (pStack),y
				inc
				sta (pStack),y

				dey

				lda (pStack),y			Count
				dec
				sta (pStack),y
				bne .1

				jsr OBJ.CurOn

.9				inc pStack
				inc pStack
				inc pStack

OBJ.Draw.Body.RTS
				rts
*--------------------------------------
OBJ.Draw.Body.JMP
				lda (ZPpObj)
				tax
				jmp (J.ObjDraw,x)
*--------------------------------------
OBJ.Draw.Bottom	ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bStatus+S.OBJ.F.bHBorder
*				clc
				beq OBJ.Draw.Body.RTS

OBJ.Draw.Bottom.I
				jsr LB.Init

				ldy #S.OBJ.X1
				lda (ZPpObj),y
				tax
				ldy #S.OBJ.Y2
				lda (ZPpObj),y
				tay
	
				jsr LB.GotoXY

				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bVBorder
				beq .1

				jsr LB.SetColor

				ldx #SEQ.BORDERBL-SEQS
				jsr LB.SeqOut

.1				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bStatus
				beq .4

				ldx #SEQ.BAR-SEQS
				jsr LB.SeqOut

				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				ldy #S.OBJ.pStatus
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1
				beq .3

				lda #C.SI
				jsr LB.COut

				ldy #0

.2				lda (ZPPtr1),y
				beq .3

				jsr LB.COut
				iny
				dex
				bne .2

.3				txa
				beq .31

				lda #C.SPACE

.30				jsr LB.COut
				dex
				bne .30

.31				ldx #SEQ.BAREND-SEQS
				jsr LB.SeqOut
				bra .6

.4				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				lda #'q'

.5				jsr LB.COut
				dex
				bne .5

.6				ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bVBorder
				beq .7

				jsr LB.SetColor

				ldx #SEQ.BORDERBR-SEQS
				jsr LB.SeqOut

.7				jmp IO.LBWrite
*--------------------------------------
OBJ.SetProp		cpy #S.OBJ.pTitle
				bne .1

				jsr OBJ.Draw.Top.I
				ldy #0
				rts

.1				cpy #S.OBJ.pStatus
				bne .8

				jsr OBJ.Draw.Bottom.I
				ldy #0
				rts

.8				clc
				rts
*--------------------------------------
OBJ.FindActive	ldy #S.OBJ.Childs

.1				phy
				lda (ZPpObj),y
				sta ZPPtr1

				iny

				lda (ZPpObj),y
				beq .7

				sta ZPPtr1+1

				ldy #S.OBJ.S
				lda (ZPPtr1),y			S.OBJ.S.bActive
				bmi .8

.7				ply
				iny
				iny
				cpy #S.OBJ
				bcc .1

*				sec

				rts

.8				ply

				clc

				rts
*--------------------------------------
OBJ.CurUpdPos	ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bEdit
*				beq .8

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .8

				jsr LB.Init

				ldy #S.OBJ.InnerX
				lda (ZPpObj),y
				clc
				ldy #S.OBJ.CurX
				adc (ZPpObj),y
				tax

				ldy #S.OBJ.InnerY
				lda (ZPpObj),y
				clc
				ldy #S.OBJ.CurY
				adc (ZPpObj),y
				tay
				jsr LB.GotoXY

				jsr IO.LBWrite

.8				clc
				rts
*--------------------------------------
OBJ.CurOn		ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bEdit
				beq .8

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .8

				bit #S.OBJ.S.bCurOn
				bne .8

				ora #S.OBJ.S.bCurOn
				sta (ZPpObj),y

				jsr LB.Init

				ldy #S.OBJ.InnerX
				lda (ZPpObj),y
				clc
				ldy #S.OBJ.CurX
				adc (ZPpObj),y
				tax

				ldy #S.OBJ.InnerY
				lda (ZPpObj),y
				clc
				ldy #S.OBJ.CurY
				adc (ZPpObj),y
				tay
				jsr LB.GotoXY

				ldx #SEQ.CURON-SEQS
				jsr LB.SeqOut

				jsr IO.LBWrite

.8				lda #0
				clc
				rts
*--------------------------------------
OBJ.CurOff		ldy #S.OBJ.F
				lda (ZPpObj),y
				and #S.OBJ.F.bEdit
				beq .8

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .8

				bit #S.OBJ.S.bCurOn
				beq .8

				eor #S.OBJ.S.bCurOn
				sta (ZPpObj),y

				jsr LB.Init

				ldx #SEQ.CUROFF-SEQS
				jsr LB.SeqOut

				jmp IO.LBWrite

.8				lda #0
				clc
				rts
*--------------------------------------
OBJ.ZeroWordAtY	lda #0
				sta (ZPpObj),y
				iny
				sta (ZPpObj),y
				rts
*--------------------------------------
OBJ.IncWordAtY	lda (ZPpObj),y
				inc
				sta (ZPpObj),y
				bne .8

				iny

				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

.8				rts
*--------------------------------------
OBJ.DecWordAtY	clc
				lda (ZPpObj),y
				sbc #0
				sta (ZPpObj),y

				iny

				lda (ZPpObj),y
				sbc #0
				sta (ZPpObj),y
				rts
*--------------------------------------
OBJ.GetLineXAtY	jsr OBJ.GetpBufPtr1

				stz TempI

.1				lda (ZPPtr1)
				beq .9

				cpx TempI
				beq .8

.2				inc TempI

				jsr OBJ.NextLinePtr1
				bra .1

.8				clc
				rts

.9				sec
				rts
*--------------------------------------
OBJ.GetpBufPtr1	ldy #S.OBJ.pBuf
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1
				rts
*--------------------------------------
OBJ.NextLinePtr1
				ldy #$ff

.1				iny
				lda (ZPPtr1),y
				bne .1

				tya
				beq .9					Empty Line

				sec
				adc ZPPtr1
				sta ZPPtr1
				bcc .8

				inc ZPPtr1+1

				clc

.8				rts
				
.9				sec
				rts	
*--------------------------------------
OBJ.GetpVarPtr2	ldy #S.OBJ.pVar
				lda (ZPpObj),y
				sta ZPPtr2
				iny
				lda (ZPpObj),y
				sta ZPPtr2+1
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.obj
LOAD usr/src/lib/libtui.s
ASM
