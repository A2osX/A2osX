NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* BUT.New
* (pStack)+6 pParent
* (pStack)+4 X1,Y1
* (pStack)+2 HK,EV
* (pStack)+0 pLabel
*--------------------------------------
BUT.New			lda #S.OBJ.T.BUT
				jsr OBJ.Create
				bcs .99

				>GETSTKW 6
				jsr OBJ.SetParent

				>GETSTKW 4
				jsr OBJ.SetX1Y1
				
				>GETSTKW 2
				ldy #S.BUT.EV
				sta (ZPpObj),y
				txa
				ldy #S.BUT.HK
				sta (ZPpObj),y

				>GETSTKW
				ldy #S.BUT.pLabel
				sta (ZPpObj),y
				sta ZPPtr1
				txa
				iny
				sta (ZPpObj),y
				sta ZPPtr1+1

				ldy #$ff

.1				iny
				lda (ZPPtr1),y
				bne .1

				tya
				inc
				inc
				ldy #S.OBJ.W
				sta (ZPpObj),y

				ldy #S.OBJ.H
				lda #1
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj
*				clc

.99				rts
*--------------------------------------
* (pStack) = Event
*--------------------------------------
BUT.Run			lda (pStack)			Event

				ldy #S.BUT.HK
				cmp (ZPpObj),y
				beq .8

				ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .9					#S.OBJ.S.bActive
				
				lda (pStack)			Event
				cmp #C.SPACE
				bne .9

.8				ldy #S.BUT.EV
				lda (ZPpObj),y

				clc
				rts

.9				lda #0
				sec
				rts
*--------------------------------------
BUT.Draw		ldy #S.BUT.pLabel
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

				ldx #SEQ.INVERSE-SEQS
				jsr LB.SeqOut

				ldx #C.SPACE

				ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .1

				ldx #'['

.1				txa
				jsr LB.COut

				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				dex
				dex

				ldy #0

.2				lda (ZPPtr1),y
				beq .3

				jsr LB.COut
				iny
				dex
				bne .2

.3				txa
				beq .5

				lda #C.SPACE

.4				jsr LB.COut
				dex
				bne .4

.5				ldx #C.SPACE

				ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .20

				ldx #']'

.20				txa
				jsr LB.COut

.8				clc
				rts
*--------------------------------------
BUT.Activate
BUT.Deactivate	jmp OBJ.Draw.Body
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.but
LOAD usr/src/lib/libtui.s
ASM
