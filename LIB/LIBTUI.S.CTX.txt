NEW
  AUTO 3,1
				.LIST OFF
*/--------------------------------------
* # tuiInit
* Initialize screen & create a new context
* ## C
* `CTX *pCTX tuiInit();`
* ## ASM
* `>SS`
* `>PUSHWI retries`
* `>LIBCALL pLIBTUI,LIBTUI.Init`
* `>SR`
* ## RETURN VALUE
* CC = success, Y,A = pCTX
*\--------------------------------------
CTX.Init		>LDYAI S.CTX
				>LIBC Malloc
				bcs .99

				>STYA ZPpCtx

				lda #0

				ldy #S.CTX-1

.1				sta (ZPpCtx),y
				dey
				bpl .1
	
				>LDYA L.SEQ.INIT
				jsr IO.fputs
				bcs .98

				lda #0
				>PUSHA					retry count

.2				jsr IO.GetC
				bcs .98
				
				ldy #S.CTX.W
				lda (ZPpCtx),y
				bne .8
				
				lda (pStack)
				dec
				sta (pStack)
				bne .2
			
				lda #80
				ldy #S.CTX.W
				sta (ZPpCtx),y

				lda #24
				ldy	#S.CTX.H
				sta (ZPpCtx),y
				
.8				inc pStack

				>LDYA ZPpCtx

				clc
				rts

.98				inc pStack

				pha
				>LDYA ZPpCtx
				>LIBC Free
				pla

				sec

.99				rts
*/--------------------------------------
* # tuiClose
* destroy context
* ## C
* `void tuiClose(CTX *pCTX);`
* ## ASM
* `>PUSHW pCTX`
* `>LIBCALL hLIBTUI,LIBTUI.Close`
* ## RETURN VALUE
* CC = success
*\--------------------------------------
CTX.Close		>LDYA L.SEQ.CLOSE
				jsr IO.fputs
				bcs .9
				
				>GETSTKW
				>STAX ZPpCtx

				ldy #S.CTX.pSCRN+1
				lda (ZPpCtx),y
				beq .1
				
				pha
				
				lda (ZPpCtx)			S.CTX.pSCRN
				tay

				pla

				jsr OBJ.Destroy

.1				>LDYA ZPpCtx
				>LIBC Free

.9				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.ctx
LOAD usr/src/lib/libtui.s
ASM
