NEW
  AUTO 3,1
*--------------------------------------
CB.GetY2X2Y1X1	>GETSTKW
				>STAX CB.Cache+S.CB.Y2

				>GETSTKW 2
				>STAX CB.Cache+S.CB.X2

				>GETSTKW 4
				>STAX CB.Cache+S.CB.Y1

				>GETSTKW 6
				>STAX CB.Cache+S.CB.X1

				rts
*--------------------------------------
CB.GetY1X1		>GETSTKW
				>STAX CB.Cache+S.CB.Y1

				>GETSTKW 2
				>STAX CB.Cache+S.CB.X1

				rts
**--------------------------------------
*CB.SwapX1X2		ldx CB.Cache+S.CB.X1
*				ldy CB.Cache+S.CB.X1+1
*
*				lda CB.Cache+S.CB.X2
*				sta CB.Cache+S.CB.X1
*				lda CB.Cache+S.CB.X2+1
*				sta CB.Cache+S.CB.X1+1
*
*				stx CB.Cache+S.CB.X2
*				sty CB.Cache+S.CB.X2+1
*
*				rts
**--------------------------------------
*CB.SwapY1Y2		ldx CB.Cache+S.CB.Y1
*				ldy CB.Cache+S.CB.Y1+1
*
*				lda CB.Cache+S.CB.Y2
*				sta CB.Cache+S.CB.Y1
*				lda CB.Cache+S.CB.Y2+1
*				sta CB.Cache+S.CB.Y1+1
*
*				stx CB.Cache+S.CB.Y2
*				sty CB.Cache+S.CB.Y2+1
*
*				rts
*--------------------------------------
CB.BorderLineH	lda #S.CB.CMD.HLINE
				sta CB.Cache+S.CB.CMD
				lda #S.CB.OP.SET
				sta CB.Cache+S.CB.OP
				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M
				lda PREFS.BorderColor
				sta CB.Cache+S.CB.COLOR
				rts
*--------------------------------------
CB.FillRectMonoA
				sta CB.Cache+S.CB.COLOR

				stz CB.Cache+S.CB.pPat+1

				lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M
				rts
*--------------------------------------
CB.AddA2X1		clc
				adc CB.Cache+S.CB.X1
				sta CB.Cache+S.CB.X1
				bcc .1

				inc CB.Cache+S.CB.X1+1

.1				rts
*--------------------------------------
CB.AddAX2X1		clc
				adc CB.Cache+S.CB.X1
				sta CB.Cache+S.CB.X1

				txa
				adc CB.Cache+S.CB.X1+1
				sta CB.Cache+S.CB.X1+1

				rts
*--------------------------------------
CB.GetObjX1Y1	ldy #S.OBJ.X1+3
				ldx #3
				bra CB.GetObjXY
*--------------------------------------
CB.GetObjX1Y1X2Y2
				ldy #S.OBJ.X1+7
				ldx #7

CB.GetObjXY		lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X1,x
				dey
				dex
				bpl CB.GetObjXY

				rts
*--------------------------------------
CB.GetObjSrcWH	ldy #S.OBJ.W+3			W,H
				ldx #3

.1				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.SrcW,x
				dey
				dex
				bpl .1

				rts
**--------------------------------------
*CB.GetObjInnerWH
*				ldy #S.WND.InnerW+3
*				ldx #3
*
*.1				lda (ZPpOBJ),y
*				sta CB.Cache+S.CB.SrcW,x
*				dey
*				dex
*				bpl .1
*
*				rts
*--------------------------------------
CB.ZeroX1Y1		ldx #3

.1				stz CB.Cache+S.CB.X1,x
				dex
				bpl .1

				rts
*--------------------------------------
CB.SetX2Y2		ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				ldy #S.OBJ.H+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.Y2
				rts
*--------------------------------------
CB.SetInnerX2Y2	ldy #S.WND.InnerW+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				ldy #S.WND.InnerH+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.Y2
				rts
*--------------------------------------
CB.ZeroSrcXSrcY	ldx #3

.1				stz CB.Cache+S.CB.SrcX,x
				dex
				bpl .1

				rts
*--------------------------------------
CB.SrcWCenterH	ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sec
				sbc CB.Cache+S.CB.SrcW
				pha
				iny
				lda (ZPpOBJ),y
				sbc CB.Cache+S.CB.SrcW+1
				lsr
				sta CB.Cache+S.CB.X1+1
				pla
				ror
				sta CB.Cache+S.CB.X1
				rts
**--------------------------------------
*CB.GetBuf		clc
*
*				lda pCBBuf+1
*				bne .9
*
*				>LDYAI 1024
*				>LIBC Malloc
*				bcs .9
*
*				>STYA pCBBuf
*				>STYA ZPCBBufPtr
*
*.9				rts
**--------------------------------------
*CB.Cache2CBBuf	ldx CB.Cache
*				ldy CB.CmdLen-1,x
*
*.1				lda CB.Cache,y
*				sta (ZPCBBufPtr),y
*				dey
*				bpl .1
**--------------------------------------
*CB.BufNext		lda (ZPCBBufPtr)		Get Cmd
*				tax
*				lda CB.CmdLen-1,x
*				sec						+1
*				adc ZPCBBufPtr
*				sta ZPCBBufPtr
*				bcc .1
*
*				inc ZPCBBufPtr+1
*
*.1				rts
**--------------------------------------
** Y,A = Target Object
**--------------------------------------
*CB.FlushBuf		lda pCBBuf+1
*				beq .8
*
*				jsr PTR.Disable
*
*				lda #0
*				sta (ZPCBBufPtr)
*
*				>LDYA pCBBuf
*				>STYA ZPCBBufPtr
*
*.1				lda (ZPCBBufPtr)		Get Cmd
*				beq .7
*
*				tax
*				ldy CB.CmdLen-1,x
*
*.2				lda (ZPCBBufPtr),y
*				sta CB.Cache,y
*				dey
*				bpl .2
*
*				jsr CB.Write0
*
*.5				jsr CB.BufNext
*				bra .1
*
*.7				>LDYA pCBBuf
*				stz pCBBuf+1
*				>LIBC Free
*
*				jmp PTR.Enable
*
*.8				clc
*.9				rts
*--------------------------------------
CB.WriteP		jsr CB.Copy2Clip

				jsr CB.SetObjPos

				ldy #S.OBJ.pPARENT
				lda (ZPpOBJ),y
				sta ZPpClipWND
				iny
				lda (ZPpOBJ),y
				sta ZPpClipWND+1

				bra CB.WriteW1
*--------------------------------------
CB.WriteInnerW	jsr CB.Copy2Clip

				>LDYA ZPpOBJ
				>STYA ZPpClipWND

CB.WriteInnerW1	lda (ZPpClipWND)
				bne CB.WriteW1

				ldy #S.WND.InnerW
				jsr CB.GetClipRectY		clip into 0,0,iW,iH

				jsr CLIP
				bcs CB.Write.8

*				jsr CB.SetObjInnerPos	set object POS relative to ZPpClipWND

				jsr CB.UpdClipCache1	update CLIP.Cache X1,Y1,X2,Y2

				bra CB.WriteW1

CB.Write.8		clc
				rts
*--------------------------------------
CB.WriteW		jsr CB.Copy2Clip

				>LDYA ZPpOBJ
				>STYA ZPpClipWND

*				jsr CB.GetClipWND
*				beq CB.WriteS

CB.WriteW1		ldy #S.OBJ.W
				jsr CB.GetClipRectY		clip into 0,0,W,H

				jsr CLIP
				bcs CB.Write.8

				ldy #S.OBJ.pPARENT+1
				lda (ZPpClipWND),y
				beq CB.WriteS

				jsr CB.UpdClipCache2	update CLIP.Cache X1,Y1,X2,Y2

*				jsr CB.SetObjPos

				ldy #S.OBJ.pPARENT+1
				lda (ZPpClipWND),y
				tax
				dey
				lda (ZPpClipWND),y
				>STAX ZPpClipWND

				bra CB.WriteInnerW1

CB.WriteS		jsr PTR.Disable
				jsr CB.Write
				bcs .9

				jmp PTR.Enable

.9				rts
*--------------------------------------
CB.Copy2Clip	ldx #S.CB-1

.1				lda CB.Cache,x
				sta CLIP.Cache,x
				dex
				bpl .1

				rts
*--------------------------------------
CB.GetClipWND	ldy #S.OBJ.pPARENT
				lda (ZPpOBJ),y
				sta ZPpClipWND
				iny
				lda (ZPpOBJ),y
				sta ZPpClipWND+1
				rts
*--------------------------------------
CB.GetClipRectY	ldx #3					reset CLIP.Rect X1,Y1

.1				stz	CLIP.Rect+S.RECT.X1,x
				dex
				bpl .1

				lda (ZPpClipWND),y
				pha
				iny
				lda (ZPpClipWND),y
				plx
				bne .2

				dec

.2				dex

				stx CLIP.Rect+S.RECT.X2
				sta CLIP.Rect+S.RECT.X2+1

				iny

				lda (ZPpClipWND),y
				pha
				iny
				lda (ZPpClipWND),y
				plx
				bne .3

				dec

.3				dex

				stx CLIP.Rect+S.RECT.Y2
				sta CLIP.Rect+S.RECT.Y2+1

				rts
*--------------------------------------
CB.UpdClipCache1
				ldy #S.WND.InnerX1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X1
				sta CLIP.Cache+S.CB.X1
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X1+1
				sta CLIP.Cache+S.CB.X1+1

				iny 					#S.WND.InnerY1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y1
				sta CLIP.Cache+S.CB.Y1
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y1+1
				sta CLIP.Cache+S.CB.Y1+1

				ldy #S.WND.InnerX1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X2
				sta CLIP.Cache+S.CB.X2
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X2+1
				sta CLIP.Cache+S.CB.X2+1

				iny 					#S.WND.InnerY1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y2
				sta CLIP.Cache+S.CB.Y2
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y2+1
				sta CLIP.Cache+S.CB.Y2+1

				rts
*--------------------------------------
CB.UpdClipCache2
				ldy #S.OBJ.X1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X1
				sta CLIP.Cache+S.CB.X1
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X1+1
				sta CLIP.Cache+S.CB.X1+1

				iny

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y1
				sta CLIP.Cache+S.CB.Y1
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y1+1
				sta CLIP.Cache+S.CB.Y1+1

				ldy #S.OBJ.X1

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X2
				sta CLIP.Cache+S.CB.X2
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.X2+1
				sta CLIP.Cache+S.CB.X2+1

				iny

				clc
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y2
				sta CLIP.Cache+S.CB.Y2
				iny
				lda (ZPpClipWND),y
				adc CLIP.Cache+S.CB.Y2+1
				sta CLIP.Cache+S.CB.Y2+1

				rts
*--------------------------------------
CB.Write2		sec

				ror bPTR

				ldx #S.CB-1

.1				lda CB.Cache,x
				sta CLIP.Cache,x
				dex
				bpl .1

				jsr OBJ.GetpWND

				jsr CB.SetObjPos		set object POS relative to parent

				ldx #3					reset CLIP.Rect X1,Y1

.2				stz	CLIP.Rect+S.RECT.X1,x
				dex
				bpl .2

				lda ZPpWND+1
				bne .4

				ldx #3

.3				lda CLIP.Screen+S.RECT.X2,x
				sta CLIP.Rect+S.RECT.X2,x
				dex
				bpl .3

				bra .5
*--------------------------------------
.4				lda (ZPpWND)
				bne .5

				ldy #S.WND.InnerW+1		...and set X2,Y2 to this W inner space
				jsr WND.GetAXAtYm1
				>STAX CLIP.Rect+S.RECT.X2

				ldy #S.WND.InnerH+1
				jsr WND.GetAXAtYm1
				>STAX CLIP.Rect+S.RECT.Y2	clip into 0,0,iW,iH

.5				jsr CB.ClipRecurse
				bcs .8

				bit bPTR
				bpl CB.Write

				jsr PTR.Disable
				jsr CB.Write
				bcs .9

				jmp PTR.Enable

.8				clc

.9				rts
*--------------------------------------
CB.Write0		>LDYA L.CB.Cache
				>STYA IOCTL+S.IOCTL.BUFPTR

				ldx CB.Cache+S.CB.CMD
				lda CB.CmdLen-1,x
				inc
				sta IOCTL+S.IOCTL.BYTECNT
				stz IOCTL+S.IOCTL.BYTECNT+1

				>SS
				>PUSHW hDevGFX
				>PUSHBI IOCTL.WRITE
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR

				rts
*--------------------------------------
CB.Write		>LDYA L.CLIP.Cache
				>STYA IOCTL+S.IOCTL.BUFPTR

				ldx CLIP.Cache+S.CB.CMD
				lda CB.CmdLen-1,x
				inc
				sta IOCTL+S.IOCTL.BYTECNT
				stz IOCTL+S.IOCTL.BYTECNT+1

				>SS
				>PUSHW hDevGFX
				>PUSHBI IOCTL.WRITE
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR

				rts
*--------------------------------------
CB.ClipRecurse	jsr CLIP
				bcs .9

				lda ZPpWND+1
				beq .8					screen

* TODO: write to offline Bitmap if window is in offline mode

				ldy #S.WND.InnerX1
				ldx #S.CB.X1
				jsr CB.UpdClipCache		update CLIP.Cache X1,Y1

				ldy #S.WND.InnerX1
				ldx #S.CB.X2
				jsr CB.UpdClipCache		update CLIP.Cache X2,Y2

* now CLIP.Cache X1,Y1,X2,Y2 relative to outer window

				ldy #S.WND.InnerX1
				jsr CB.UpdClipRect

* now CLIP.Rect X1,Y1,X2,Y2 relative to outer window

				ldy #S.OBJ.pPARENT+1
				lda (ZPpWND),y
				beq .8

				ldy #S.OBJ.X1
				ldx #S.CB.X1
				jsr CB.UpdClipCache		update CLIP.Cache X1,Y1

				ldy #S.OBJ.X1
				ldx #S.CB.X2
				jsr CB.UpdClipCache		update CLIP.Cache X2,Y2

				ldy #S.OBJ.X1
				jsr CB.UpdClipRect

				ldy #S.OBJ.pPARENT+1
				lda (ZPpWND),y
				tax
				dey
				lda (ZPpWND),y
				>STAX ZPpWND

				bra CB.ClipRecurse

.8				clc

.9				rts
*--------------------------------------
CB.SetObjInnerPos
				ldx #S.CB.X1
				jsr .1

				ldx #S.CB.X2

.1				ldy #S.WND.InnerX1
				clc
				lda (ZPpOBJ),y
				adc CLIP.Cache,x
				sta CLIP.Cache,x
				iny
				lda (ZPpOBJ),y
				adc CLIP.Cache+1,x
				sta CLIP.Cache+1,x

				iny						#S.OBJ.InnerY1

				clc
				lda (ZPpOBJ),y
				adc CLIP.Cache+2,x
				sta CLIP.Cache+2,x
				iny
				lda (ZPpOBJ),y
				adc CLIP.Cache+3,x
				sta CLIP.Cache+3,x

				rts
*--------------------------------------
CB.SetObjPos	ldx #S.CB.X1
				jsr .1

				ldx #S.CB.X2

.1				ldy #S.OBJ.X1
				clc
				lda (ZPpOBJ),y
				adc CLIP.Cache,x
				sta CLIP.Cache,x
				iny
				lda (ZPpOBJ),y
				adc CLIP.Cache+1,x
				sta CLIP.Cache+1,x

				iny						#S.OBJ.Y1

				clc
				lda (ZPpOBJ),y
				adc CLIP.Cache+2,x
				sta CLIP.Cache+2,x
				iny
				lda (ZPpOBJ),y
				adc CLIP.Cache+3,x
				sta CLIP.Cache+3,x

				rts
*--------------------------------------
CB.UpdClipCache	clc
				lda (ZPpWND),y
				adc CLIP.Cache,x
				sta CLIP.Cache,x
				iny
				lda (ZPpWND),y
				adc CLIP.Cache+1,x
				sta CLIP.Cache+1,x		X

				iny

				clc
				lda (ZPpWND),y
				adc CLIP.Cache+2,x
				sta CLIP.Cache+2,x
				iny
				lda (ZPpWND),y
				adc CLIP.Cache+3,x
				sta CLIP.Cache+3,x		Y

				rts
*--------------------------------------
CB.UpdClipRect	ldx #0

				clc

.1				lda (ZPpWND),y
				adc CLIP.Rect,x
				sta CLIP.Rect,x
				iny
				inx
				lda (ZPpWND),y
				adc CLIP.Rect,x
				sta CLIP.Rect,x
				iny
				inx
				cpx #8					X1,Y1,X2,Y2
				bcc .1

				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.cb
LOAD usr/src/lib/libgui.s
ASM
