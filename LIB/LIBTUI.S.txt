NEW
  AUTO 3,1
				.LIST OFF
				.OP	65C02
				.OR	$2000
				.TF lib/libtui
*--------------------------------------
				.INB inc/macros.i
				.INB inc/a2osx.i
				.INB inc/io.i
				.INB inc/net.telnet.i
				.INB inc/libtui.i
*--------------------------------------
				.DUMMY
				.OR ZPLIB

ZPpCtx			.BS 2
ZPpParent		.BS 2
ZPpObj			.BS 2

ZPLineBufPtr	.BS 2
ZPLineBufCnt	.BS 2

ZPPtr1			.BS 2
ZPPtr2			.BS 2
ZPPtr3			.BS 2

bTemp1			.BS 1
bTemp2			.BS 1
TempW			.BS 2
TempC			.BS 2
TempI			.BS 2

bRepaintAll		.BS 1

				.ED
*--------------------------------------
*			File Header (16 Bytes)
*--------------------------------------
CS.START		cld
				jmp (.1,x)
				.DA #$61				6502,Level 1 (65c02)
				.DA #2					BIN Layout Version 2
				.DA 0
				.DA CS.END
				.DA ID.END
				.DA 0
				.DA 0
*--------------------------------------
* Relocation Table
*--------------------------------------
.1				.DA LIB.LOAD
				.DA LIB.UNLOAD
				.DA CTX.Init
				.DA CTX.Close
*--------------------------------------
				.DA SCRN.New
				.DA DLG.New
				.DA LABEL.New
				.DA TLINE.New
				.DA TBOX.New
				.DA LBOX.New
				.DA CBOX.New
				.DA BUT.New
				.DA RADIO.New
				.DA LIB.Free
				.DA LIB.Exec
				.DA LIB.Draw
				.DA LIB.GetProp
				.DA LIB.SetProp
				.DA LIB.Activate
				.DA LIB.Deactivate
				.DA LIB.FindActive
				.DA LIB.Show
				.DA LIB.Hide
*--------------------------------------
J.ObjFree		.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
				.DA OBJ.Free
*--------------------------------------
J.ObjDraw		.DA CONT.Draw			SCRN
				.DA CONT.Draw			DLG
				.DA LABEL.Draw
				.DA TLINE.Draw
				.DA TBOX.Draw
				.DA LBOX.Draw
				.DA CBOX.Draw
				.DA BUT.Draw
				.DA RADIO.Draw
*--------------------------------------
J.ObjActivate	.DA OBJ.Activate
				.DA OBJ.Activate
				.DA LABEL.Activate
				.DA TLINE.Activate
				.DA TBOX.Activate
				.DA LBOX.Activate
				.DA CBOX.Activate
				.DA BUT.Activate
				.DA RADIO.Activate
*--------------------------------------
J.ObjDeactivate	.DA OBJ.Deactivate
				.DA OBJ.Deactivate
				.DA LABEL.Deactivate
				.DA TLINE.Deactivate
				.DA TBOX.Deactivate
				.DA LBOX.Deactivate
				.DA CBOX.Deactivate
				.DA BUT.Deactivate
				.DA RADIO.Deactivate
*--------------------------------------
J.ObjSetProp	.DA OBJ.SetProp
				.DA OBJ.SetProp
				.DA OBJ.SetProp
				.DA OBJ.SetProp
				.DA TBOX.SetProp
				.DA OBJ.SetProp
				.DA OBJ.SetProp
				.DA OBJ.SetProp
				.DA OBJ.SetProp
*--------------------------------------
J.ObjRun		.DA CONT.Run			SCRN
				.DA CONT.Run			DLG
				.DA LABEL.Run
				.DA TLINE.Run
				.DA TBOX.Run
				.DA LBOX.Run
				.DA CBOX.Run
				.DA BUT.Run
				.DA RADIO.Run
*--------------------------------------
J.TBOX.RUN.RO	.DA TBOX.RUN.Ignore
				.DA TBOX.RUN.Ignore		Ctrl-A
				.DA TBOX.RUN.Ignore		Ctrl-B
				.DA TBOX.RUN.Ignore		Ctrl-C
				.DA TBOX.RUN.Ignore		Ctrl-D
				.DA TBOX.RUN.Ignore		Ctrl-E
				.DA TBOX.RUN.Ignore		Ctrl-F
				.DA TBOX.RUN.Ignore		Ctrl-G
				.DA TBOX.RUN.PrevLink	BS
				.DA TBOX.RUN.Ignore		TAB
				.DA TBOX.RUN.ScrollUp	LF
				.DA TBOX.RUN.ScrollDown	VT
				.DA TBOX.RUN.EOL		Ctrl-L
				.DA TBOX.RUN.GetLink	CR
				.DA TBOX.RUN.Ignore		Ctrl-N
				.DA TBOX.RUN.PGUP		Ctrl-O
				.DA TBOX.RUN.PGDN		Ctrl-P
				.DA TBOX.RUN.Ignore		Ctrl-Q
				.DA TBOX.RUN.Ignore		Ctrl-R
				.DA TBOX.RUN.Ignore		Ctrl-S
				.DA TBOX.RUN.Home		Ctrl-T
				.DA TBOX.RUN.NextLink	FS
				.DA TBOX.RUN.Ignore		Ctrl-V
				.DA TBOX.RUN.Ignore		Ctrl-W
				.DA TBOX.RUN.Ignore		Ctrl-X
				.DA TBOX.RUN.EOF		Ctrl-Y
				.DA TBOX.RUN.Ignore		Ctrl-Z
*--------------------------------------
J.TBOX.RUN.RW	.DA TBOX.RUN.Ignore
				.DA TBOX.RUN.Ignore
				.DA TBOX.RUN.SelBegin	Ctrl-B
				.DA TBOX.RUN.Ignore		** Copy
				.DA TBOX.RUN.Ignore		** Delete Selection
				.DA TBOX.RUN.SelEnd		Ctrl-E
				.DA TBOX.RUN.Ignore		** Find...
				.DA TBOX.RUN.SOL		Ctrl-G
				.DA TBOX.RUN.BS
				.DA TBOX.RUN.Ignore		TAB
				.DA TBOX.RUN.LF
				.DA TBOX.RUN.VT
				.DA TBOX.RUN.EOL		Ctrl-L
				.DA TBOX.RUN.CR
				.DA TBOX.RUN.Ignore		** Next...
				.DA TBOX.RUN.PGUP		Ctrl-O
				.DA TBOX.RUN.PGDN		Ctrl-P
				.DA TBOX.RUN.Ignore		Ctrl-Q
				.DA TBOX.RUN.Ignore		** Replace
				.DA TBOX.RUN.Ignore		** save
				.DA TBOX.RUN.Home		Ctrl-T
				.DA TBOX.RUN.FS
				.DA TBOX.RUN.Ignore		** Paste
				.DA TBOX.RUN.EraseCL	Ctrl-W
				.DA TBOX.RUN.Ignore		** Cut
				.DA TBOX.RUN.EOF		Ctrl-Y
				.DA TBOX.RUN.Ignore		** delete under cusror
*--------------------------------------
L.SEQ.INIT		.DA SEQ.INIT
L.SEQ.GOTOXY	.DA SEQ.GOTOXY
L.SEQ.COLOR		.DA SEQ.COLOR
L.SEQ.CLOSE		.DA SEQ.CLOSE
L.FMT.ESCCSI.R	.DA FMT.ESCCSI.R
				.DA 0
*--------------------------------------
LIB.LOAD		lda LibCnt
				bne .8

				>LDYAI 512
				>LIBC Malloc
				bcs .9

				>STYA pLineBuf

.8				inc LibCnt

				clc

.9				rts
*--------------------------------------
LIB.UNLOAD		dec LibCnt
				bne .8

				>LDYA pLineBuf
				>LIBC Free

.8				clc

				rts
*--------------------------------------
* pStack+0 : pObj
*--------------------------------------
LIB.Free		>GETSTKW
				
				tay
				txa
				>LIBC Free

				rts
*--------------------------------------
* pStack+0 : pObj
*--------------------------------------
LIB.Exec		>GETSTKW
				>STAX ZPpObj
				
				jsr LIB.GetCTX

.1				jsr IO.GetC
				bcs .9

				cmp #EV.RESIZE
				clc
				beq .9

				tax
				beq .1

				>PUSHA

				jsr OBJ.Run
				inc pStack

				tay
				beq .1					no event, no error

.9				rts
*--------------------------------------
LIB.Draw		>GETSTKW
				>STAX ZPpObj

				jmp OBJ.Draw
*--------------------------------------
LIB.GetProp		>GETSTKW 2
				>STAX ZPpObj

				>GETSTKW 0				prop

				tay
				lda (ZPpObj),y
				pha
				iny
				lda (ZPpObj),y
				ply

				clc
				rts
*--------------------------------------
LIB.SetProp		>GETSTKW 4
				>STAX ZPpObj
				
				>GETSTKW 0				Value
				>STAX TempW

				>GETSTKW 2				prop

				tay
				lda TempW
				sta (ZPpObj),y
				iny
				lda TempW+1
				sta (ZPpObj),y
				
				dey
							
				lda (ZPpObj)			S.OBJ.T
				tax
				jmp (J.ObjSetProp,x)
*--------------------------------------
LIB.Activate	>GETSTKW
				>STAX ZPpObj

				jsr OBJ.FindActive
				bcs .1

				>LDYA ZPPtr1
				jsr OBJ.Deactivate

.1				>LDYA ZPpObj
				jsr OBJ.Activate
				
				clc
				rts
*--------------------------------------
LIB.Deactivate	>GETSTKW
				>STAX ZPpObj

				lda (ZPpObj)
				tax
				jmp (J.ObjDeactivate,x)
*--------------------------------------
LIB.FindActive	>GETSTKW
				>STAX ZPpObj
				
				ldx ZPpObj
				phx
				ldx ZPpObj+1
				phx

				jsr OBJ.FindActive
				bcs .9
				
				>LDYA ZPPtr1
				
				rts
				
.9				lda #E.BADPTR			

				rts
*--------------------------------------
LIB.Show		>GETSTKW
				>STAX ZPpObj

				jsr OBJ.CurOff

				ldy #S.OBJ.S
				lda (ZPpObj),y
				and #S.OBJ.S.bHidden^$ff
				sta (ZPpObj),y

				jmp OBJ.Draw
*--------------------------------------
LIB.Hide		>GETSTKW
				>STAX ZPpObj

				jsr OBJ.CurOff

				ldy #S.OBJ.S
				lda (ZPpObj),y
				ora #S.OBJ.S.bHidden
				sta (ZPpObj),y
				
				jmp OBJ.Draw
*--------------------------------------
LIB.GetCTX		>STAX ZPpCtx

.1				lda (ZPpCtx)
				cmp #S.OBJ.T.DLG+1
				bcc .8

				ldy #S.OBJ.pParent
				lda (ZPpCtx),y
				tax
				iny 
				lda (ZPpCtx),y
				sta ZPpCtx+1
				stx ZPpCtx
				bra .1

.8				ldy #S.OBJ.pParent
				lda (ZPpObj),y
				sta ZPpCtx
				iny
				lda (ZPpObj),y
				sta ZPpCtx+1
			beq *

				rts
*--------------------------------------
				.INB usr/src/lib/libtui.s.io
				.INB usr/src/lib/libtui.s.lb
				.INB usr/src/lib/libtui.s.ctx
				.INB usr/src/lib/libtui.s.cont
				.INB usr/src/lib/libtui.s.obj
				.INB usr/src/lib/libtui.s.scrn
				.INB usr/src/lib/libtui.s.dlg
				.INB usr/src/lib/libtui.s.label
				.INB usr/src/lib/libtui.s.tline
				.INB usr/src/lib/libtui.s.tbox
				.INB usr/src/lib/libtui.s.lbox
				.INB usr/src/lib/libtui.s.cbox
				.INB usr/src/lib/libtui.s.but
				.INB usr/src/lib/libtui.s.radio
*--------------------------------------
CS.END			.EQ *
*--------------------------------------
LibCnt			.BS 1
pLineBuf		.BS 2
*--------------------------------------
ESC.In			.AS "DABC"
ESC.Out			.DA #C.BS,#C.VT,#C.LF,#21
*--------------------------------------
FMT.ESCCSI.R	.AZ "%d;%d"
*--------------------------------------
OBJ.SIZES		.DA S.SCRN,S.DLG,S.LABEL,S.TLINE,S.TBOX,S.LBOX,S.CBOX,S.BUT,S.RADIO
*--------------------------------------
OBJ.COLORS		.DA #93,#44				SCRN
				.DA #93,#46				DLG
				.DA #93,#0				LABEL
				.DA #93,#104			TLINE
				.DA #93,#104			TBOX
				.DA #93,#104			LBOX
				.DA #93,#0				CBOX
				.DA #92,#100			BUT
				.DA #93,#0				RADIO
*--------------------------------------
SEQ.INIT		.CS "\ec"				reset
				.CS "Querying termcap..."
				.CS "\e[999;999H"		Set Cursor Pos to 999,999
				.CS "\e[6n"				then query Cursor Pos
				.CS "\e(B"				font select
				.CS "\e)0"				font select
				.CS "\e[?7l"			Disable Line Wrap
				.CZ "\e[?25l"			Hide Cursor
*--------------------------------------
SEQ.GOTOXY		.CZ "\e[%d;%dH"
*--------------------------------------
SEQ.COLOR		.CZ "\e[%d;%dm"
*--------------------------------------
SEQS			.EQ *
*--------------------------------------
SEQ.CURON		.CZ "\e[?25h"			Show Cursor
SEQ.CUROFF		.CZ "\e[?25l"			Hide Cursor
*--------------------------------------
SEQ.BAR			.CS "\e[1m"				BOLD
				.CS "\e[7m"				INVERSE
				.CZ "\e[37;40m"
SEQ.BAREND		.CZ "\e[0m"
*--------------------------------------
SEQ.BORDERTL	.DA #C.SO,#'l'
				.DA #0
SEQ.BORDERTR	.DA #C.SO,#'k',#C.SI
				.DA #0
SEQ.BORDERBL	.DA #C.SO,#'m'
				.DA #0
SEQ.BORDERBR	.DA #C.SO,#'j',#C.SI
				.DA #0
*SEQ.BORDERH		.DA #C.SO,#'q',#C.SI
*					.DA #0
SEQ.BORDERV		.DA #C.SO,#'x',#C.SI
				.DA #0
SEQ.INVERSE		.CZ "\e[7m"
SEQ.NORMAL		.CZ "\e[0m"
*--------------------------------------
*SEQ.SCROLLCURUP	.CS "\e[?7l\e[%d;%dr"
*SEQ.SCROLLUP	.CZ "\eD"
*SEQ.SCROLLCURDN	.CS "\e[?7l\e[%d;%dr"
*SEQ.SCROLLDN	.CZ "\eM"
*--------------------------------------
SEQ.CLOSE		.CZ "\ec\e[?7h"			Enable Line Wrap
*--------------------------------------
ID.END			.EQ *
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s
ASM
