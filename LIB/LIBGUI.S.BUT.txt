NEW
  AUTO 3,1
*--------------------------------------
* NewBut(HWND, wFlags, wW, pICON, pTEXT, bKEYMOD, bKEY, wID, iX, iY)
*--------------------------------------
BUT.New			>GETSTKW 16
				>STAX ZPpWND

				lda #S.OBJ.T.BUT
				jsr OBJ.New
				bcs .9

*				>STYA ZPpOBJ

				>LDAX ZPpWND
				jsr OBJ.SetParentAX

				jsr BUT.GetParams

				jsr BUT.SetSize

				jsr OBJ.SetX2Y2

				jsr WND.AddNext.I

				>LDYA ZPpOBJ

*				clc

.9				rts
*--------------------------------------
BUT.GetParams	>GETSTKW
				ldy #S.OBJ.Y1
				jsr OBJ.SetAXAtY

				>GETSTKW 2
				ldy #S.OBJ.X1
				jsr OBJ.SetAXAtY

				>GETSTKW 4
				ldy #S.BUT.ID
				jsr OBJ.SetAXAtY

				>GETSTKB 6
				ldy #S.BUT.KEY
				sta (ZPpOBJ),y

				>GETSTKB 7
				ldy #S.BUT.KEYMOD
				sta (ZPpOBJ),y

				>GETSTKW 8
				ldy #S.BUT.pTEXT
				jsr OBJ.SetAXAtY

				>GETSTKW 10
				ldy #S.BUT.pICON
				jsr OBJ.SetAXAtY

				>GETSTKW 12
				ldy #S.OBJ.W
				jsr OBJ.SetAXAtY

				>GETSTKW 14
				ldy #S.OBJ.F
				jsr OBJ.SetAXAtY



				rts
*--------------------------------------
BUT.SetSize		ldy #S.OBJ.W
				jsr OBJ.GetPropAtY
				bcc .5					W in !NULL, static size

				stz DX
				stz DX+1

				ldy #S.BUT.pICON
				jsr OBJ.GetPropAtY
				bcs .1					no icon

				lda #16					IconW
				sta DX

.1				ldy #S.BUT.pTEXT
				jsr OBJ.GetPropAtY
				bcs .3					no text

				ldx #FON.ID.SYS
				jsr FON.GetTextWYAX

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				and #S.BUT.F.NOSPACE
				bne .2

				lda DX
				beq .2

				clc
				adc PREFS.XSeparator
				sta DX

.2				clc
				lda CB.Cache+S.CB.SrcW
				adc DX
				sta DX

				lda CB.Cache+S.CB.SrcW+1
				adc DX+1
				sta DX+1

.3				lda PREFS.XSeparator
				asl
*				clc
				adc DX
				ldy #S.OBJ.W
				sta (ZPpOBJ),y
				lda #0
				adc DX+1
				iny
				sta (ZPpOBJ),y

.5				lda SYSFON.H
				clc
				adc #4
				ldy #S.OBJ.H
				sta (ZPpOBJ),y
				lda #0
				iny
				sta (ZPpOBJ),y

				rts
*--------------------------------------
BUT.fEnter		ldy #S.OBJ.S

				lda (ZPpOBJ),y
				bit #S.OBJ.S.SELECTED
				bne .1

				eor #S.OBJ.S.SELECTED
				sta (ZPpOBJ),y
				jsr OBJ.PaintpOBJ
				bcs .9

.1				lda	MSG+S.MSG.S
				bit #S.XY.S.CLK
				beq .8

				lda #S.MSG.T.ID
				sta MSG+S.MSG.T

.8				clc

.9				rts
*--------------------------------------
BUT.fLeave		ldy #S.OBJ.S
				lda (ZPpOBJ),y
				bit #S.OBJ.S.SELECTED
				beq .8

				eor #S.OBJ.S.SELECTED
				sta (ZPpOBJ),y
				jmp OBJ.PaintpOBJ

.8				clc
				rts
*--------------------------------------
BUT.fPaint		jsr DRAW.BordersIW

				ldx #C.WHITE

				ldy #S.OBJ.S
				lda (ZPpOBJ),y

				and #S.OBJ.S.SELECTED
				beq .1

				ldx #C.BLACK

.1				txa

				jsr DRAW.InnerA

				ldy #S.BUT.pTEXT
				jsr OBJ.GetPropAtY

				ldx #FON.ID.SYS
				jsr FON.GetTextWYAX

				jsr CB.SrcWCenterH

				>INCW CB.Cache+S.CB.Y1

				ldy #S.BUT.pTEXT
				jsr OBJ.GetPropAtY
				ldx #FON.ID.SYS
				jmp DRAW.TextYAXIW

.9				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.but
LOAD usr/src/lib/libgui.s
ASM
