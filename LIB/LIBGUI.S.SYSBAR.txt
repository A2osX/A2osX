NEW
  AUTO 3,1
*--------------------------------------
SYSBUT.Enter	bit WND.SYSBAR.bSysBut
				bmi .8

				sec
				ror WND.SYSBAR.bSysBut

				jsr MENU.DestroyAll

				jsr CB.GetBuf
				bcs .9

				jsr SYSBAR.ShowSysBut

				>LDYA L.WND.SysBar
				jsr CB.FlushBufToYA

				>PUSHW L.MENU.Sys
				>PUSHW WND.SysBar+S.OBJ.X1
				>PUSHW WND.SysBar+S.OBJ.Y1

				jsr MENU.New
				bcs .9

				jmp MENU.Draw

.8				clc
.9				rts
*--------------------------------------
SYSBUT.Leave	bit WND.SYSBAR.bSysBut
				bpl .8

				stz WND.SYSBAR.bSysBut

				jsr CB.GetBuf
				bcs .9

				jsr SYSBAR.ShowSysBut

				>LDYA L.WND.SysBar
				jmp CB.FlushBufToYA

.8				clc
.9				rts
*--------------------------------------
SYSBAR.Show		jsr CB.GetBuf
				bcs .9

				jsr SYSBAR.ShowSysBut

				jsr SYSBAR.ShowSysWNDs

				jsr SYSBAR.ShowSysClk

				>LDYA L.WND.SysBar
				jmp CB.FlushBufToYA

.9				rts
*--------------------------------------
SYSBAR.ShowSysBut
				lda PREFS.SysBarColor
				bit WND.SysBar.bSysBut
				bpl .1

				eor #$ff

.1				jsr CB.FillRectMonoA

				ldx #S.RECT-1

.2				lda RECT.SysBut,x
				sta CB.Cache+S.CB.X1,x
				dex
				bpl .2

				jsr CB.Cache2CBBuf

				lda #4
				jsr CB.AddA2X1

				jsr CB.IncY1

				ldx #BM.ID.APPLE
				jsr DRAW.xBM

				lda #16+1
				jsr CB.AddA2X1

				jsr CB.IncY1

				>LDYA L.WND.SysBar.2osX
				ldx hSYSFON
				jmp DRAW.YAText
*--------------------------------------
SYSBAR.ShowSysWNDs
				lda PREFS.SysBarColor
				jsr CB.FillRectMonoA

				ldx #S.RECT-1

.1				lda RECT.SysWNDs,x
				sta CB.Cache+S.CB.X1,x
				dex
				bpl .1

				jmp CB.Cache2CBBuf
*--------------------------------------
SYSBAR.ShowSysClk
				lda PREFS.SysBarColor
				jsr CB.FillRectMonoA

				ldx #S.RECT

.1				lda RECT.SysClk-1,x
				sta CB.Cache+S.CB.X1-1,x
				dex
				bne .1

				jsr CB.Cache2CBBuf
				
				lda #1
				jsr CB.AddA2X1

				jsr CB.IncY1
				
				>LDYA L.WND.SysBar.Clk
				ldx hSYSFON
				jmp DRAW.YAText
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.sysbar
LOAD usr/src/lib/libgui.s
ASM
