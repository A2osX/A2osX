NEW
  AUTO 3,1
*--------------------------------------
* MsgBox(pWND, pTITLE, pBM, pTEXT, idButtons)
*--------------------------------------
MBOX.MsgBox		>GETSTKW 8
				>STAX ZPpWND

				>LDYAI 360
				>STYA IE

				>LDYAI 70
				>STYA INE

				ldy #S.OBJ.W
				lda (ZPpWND),y
				sec
				sbc IE
				sta DX

				iny
				lda (ZPpWND),y
				sbc IE+1
				lsr
				sta DX+1
				ror DX

			lda PREFS.nBestViewedX
			trb DX

				iny
				lda (ZPpWND),y
				sec
				sbc INE
				sta DY
				iny

				lda (ZPpWND),y
				sbc INE+1
				lsr
				sta DY+1
				ror DY

				>SS
				>PUSHW ZPpWND
				>PUSHWI S.OBJ.F.BORDER+S.WND.F.MOVE+S.WND.F.CLOSE+S.WND.F.MODAL
				>PUSHW DX
				>PUSHW DY
				>PUSHW IE
				>PUSHW INE
				jsr WND.New
				>SR
				bcs .9

				>STYA ZPpOBJ
				>STYA ZPpWND

				ldy #S.OBJ.BGCOLOR
				lda #C.WHITE
				sta (ZPpOBJ),y

				>GETSTKW 6
				ldy #S.WND.pTITLE
				jsr OBJ.SetAXAtY

				lda #16
				sta DX
				stz DX+1
				lda #20
				sta DY
				stz DY+1
				>GETSTKW 4				pBM
				jsr BITMAP.NewAX
				bcs .9

*				>STYA ZPpOBJ

				lda #60
				sta DX
				stz DX+1
				lda #10
				sta DY
				stz DY+1
				>GETSTKW 2				pTEXT
				jsr LABEL.NewAX
				bcs .9

*				>STYA ZPpOBJ

				jsr MBOX.AddButtons
				bcs .9

*				>STYA ZPpOBJ

				ldy #S.WND.hPTR
				lda #PTR.T.CROSS
				sta (ZPpWND),y

				>LDYA ZPpWND

.9				rts
*--------------------------------------
MBOX.AddButtons	lda #360-10
				sta D
				lda /360-10
				sta D+1

				stz IY

.1				lda (pStack)			idButtons
				beq .8

				lsr
				sta (pStack)
				bcc .7

				lda D
				sec
				sbc #55
				sta D
				bcs .2

				dec D+1

.2				jsr MBOX.AddButton
				bcs .9

.7				inc IY
				inc IY
				bra .1

.8				clc

.9				rts
*--------------------------------------
MBOX.AddButton	>SS
				>PUSHW ZPpWND
				>PUSHWI 0				flags
				>PUSHWI 50				w
				>PUSHWZ					pIcon
				ldx IY
				>PUSHW L.BUT.Labels,x	pText
				>PUSHWZ					keymod/key
				>PUSHWZ					id
				>PUSHW D				X
				>PUSHWI 45				Y
				jsr BUT.New
				>SR
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.mbox
LOAD usr/src/lib/libgui.s
ASM
