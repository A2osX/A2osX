NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* TLINE.New
* (pStack)+8 pParent
* (pStack)+6 Y1X1
* (pStack)+4 W
* (pStack)+2 pBuf
* (pStack)+0 MaxLen
*--------------------------------------
TLINE.New		lda #S.OBJ.T.TLINE
				jsr OBJ.Create
				bcs .99

				>GETSTKW 8
				jsr OBJ.SetParent
				
				>GETSTKW 6
				jsr OBJ.SetX1Y1

				>GETSTKW 4
				jsr OBJ.SetWH

				>GETSTKW 2
				ldy #S.OBJ.pBuf
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				>GETSTKW
				ldy #S.OBJ.BufSize
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				ldy #S.OBJ.F
				lda #S.OBJ.F.bHScroll+S.OBJ.F.bEdit
				sta (ZPpObj),y

				ldy #S.OBJ.H
				lda #1
				sta (ZPpObj),y

				ldy #S.OBJ.CurX
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj

*				clc

.99				rts
*--------------------------------------
TLINE.Run		ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .9					S.OBJ.S.bActive

				jsr OBJ.GetpBufPtr1

				lda (pStack)			Event
				cmp #C.DEL
				beq TLINE.RUN.DEL

				cmp #C.SPACE
				bcs .2

				cmp #C.BS
				beq TLINE.RUN.BS

				cmp #21					Forward Space
				beq TLINE.RUN.FS

.9				lda #0
				sec
				rts
*--------------------------------------
.2				ldy #S.OBJ.BufSize
				lda (ZPpObj),y

				ldy #S.TLINE.Len
				cmp (ZPpObj),y
				beq	.8

				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

				pha

				ldy #S.TLINE.Ptr
				lda (ZPpObj),y
				sta TempW
				inc
				sta (ZPpObj),y

				ply

.1				dey
				lda (ZPPtr1),y
				iny
				sta (ZPPtr1),y

				dey
				cpy TempW
				bne .1

				lda (pStack)
				ldy TempW
				sta (ZPPtr1),y


				jmp TLINE.SetViewPort

.8				lda #0
				clc
				rts
*--------------------------------------
TLINE.RUN.BS	ldy #S.TLINE.Ptr
				lda (ZPpObj),y
				beq .8

				dec
				sta (ZPpObj),y

				jmp TLINE.SetViewPort

.8				clc
				rts
*--------------------------------------
TLINE.RUN.FS	ldy #S.TLINE.Len
				lda (ZPpObj),y

				ldy #S.TLINE.Ptr
				eor (ZPpObj),y
				beq .8

				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

				jmp TLINE.SetViewPort

.8				clc
				rts
*--------------------------------------
TLINE.RUN.DEL	ldy #S.TLINE.Ptr
				lda (ZPpObj),y
				beq .8

				tay

.1				lda (ZPPtr1),y
				dey
				sta (ZPPtr1),y
				tax
				beq .2

				iny
				iny
				bra .1

.2				ldy #S.TLINE.Len
				lda (ZPpObj),y
				dec
				sta (ZPpObj),y

				jmp TLINE.RUN.BS

.8				clc
				rts
*--------------------------------------
TLINE.Draw		jsr OBJ.GetpBufPtr1

				ldx #C.SPACE

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .1

				ldx #'['

.1				txa
				jsr LB.COut

				ldy #S.TLINE.HScroll
				lda (ZPpObj),y
				beq .12

				tax

.11				jsr TLINE.GetLineChar
				beq .12

				dex
				bne .11

.12				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				dex
				dex

.2				jsr TLINE.GetLineChar
				beq .3

				jsr LB.COut
				dex
				bne .2

.3				txa
				beq .5

				lda #C.SPACE

.4				jsr LB.COut
				dex
				bne .4

.5				ldx #C.SPACE

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .6

				ldx #']'

.6				txa
				jsr LB.COut

.8				clc
				rts
*--------------------------------------
TLINE.Activate	
TLINE.Deactivate
				jmp OBJ.Draw.Body
*--------------------------------------
TLINE.GetLineChar
				lda (ZPPtr1)
				beq .8

				inc ZPPtr1
				bne .8

				inc ZPPtr1+1

.8				rts
*--------------------------------------
TLINE.SetViewPort
				ldy #S.TLINE.Ptr
				lda (ZPpObj),y

				ldy #S.TLINE.HScroll
				sec
				sbc (ZPpObj),y

				ldy #S.OBJ.CurX			set CurX = Ptr - HScroll
				inc						+1
				sta (ZPpObj),y
				bcs .10					yes..

				ldy #S.TLINE.Ptr		no, set HScroll = Ptr
				lda (ZPpObj),y

				ldy #S.TLINE.HScroll
				sta (ZPpObj),y

				lda #0					set CurX = 0+1
				bra .20
*--------------------------------------
.10				ldy #S.TLINE.HScroll
				lda (ZPpObj),y

				ldy #S.OBJ.InnerW
				clc
				adc (ZPpObj),y
				dec
				dec

				ldy #S.TLINE.Ptr		(HScroll + InnerW-2) > (Ptr + 1) ?
				clc						Ptr+1
				sbc (ZPpObj),y
				bcs .8					no...

				ldy #S.OBJ.InnerW		yes, set HScroll = Ptr - (InnerW - 1)
				lda (ZPpObj),y
				dec
				dec
				dec
				sta TempW				(InnerW - 1)

				ldy #S.TLINE.Ptr
				lda (ZPpObj),y
				sec
				sbc TempW
				bcc .12					Ptr - (InnerW - 1) < 0

				ldy #S.TLINE.HScroll
				sta (ZPpObj),y

				lda TempW				set CurX = (InnerW - 1)
				bra .20

.12				lda #0
				ldy #S.TLINE.HScroll
				sta (ZPpObj),y

.20				inc
				ldy #S.OBJ.CurX
				sta (ZPpObj),y

				sec
				ror bRepaintAll

.8				jsr OBJ.Draw.Body
				bcs .9

				lda #0
*				clc
.9				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.tline
LOAD usr/src/lib/libtui.s
ASM
