NEW
  AUTO 3,1
*--------------------------------------
UpdateCursor	lda CUR.T
				cmp #CUR.T.WAIT
				bcc SetCursor.RTS
				inc
				inc
				cmp #CUR.T.WAIT+12
				bne SetCursor
				lda #CUR.T.WAIT
*--------------------------------------
* Set X2,Y2,SrcPtr
*--------------------------------------
SetCursor		sta CUR.T

				lda CUR.bVisible
				bpl .1

				jsr HideCursor.I

.1				ldx CUR.T
				lda L.Cursors,x
				sta CUR.CB+S.CB.SrcPtr
				sta ZPPtr1
				lda L.Cursors+1,x
				sta CUR.CB+S.CB.SrcPtr+1
				sta ZPPtr1+1

				ldy #S.BM.W
				lda (ZPPtr1),y
				sta CUR.CB+S.CB.SrcW

				clc
				adc CUR.CB+S.CB.X1
				pha

				iny
				lda (ZPPtr1),y
				sta CUR.CB+S.CB.SrcW+1
				adc CUR.CB+S.CB.X1+1
				tax
				pla
				bne .2

				dex
				
.2				dec				
				sta CUR.CB+S.CB.X2
				stx CUR.CB+S.CB.X2+1
				
				iny 					S.BM.H
				lda (ZPPtr1),y
				sta CUR.CB+S.CB.SrcH
				
				clc
				adc CUR.CB+S.CB.Y1
				pha

				iny
				lda (ZPPtr1),y
				sta CUR.CB+S.CB.SrcH+1
				adc CUR.CB+S.CB.Y1+1
				tax
				pla
				bne .3

				dex
				
.3				dec				
				sta CUR.CB+S.CB.Y2
				stx CUR.CB+S.CB.Y2+1

				lda CUR.bVisible
				bmi ShowCursor.I
	
SetCursor.RTS	rts
*--------------------------------------
* Set DestX,DestY
*--------------------------------------
SetCursorPos	>PULLW ZPPtr1
				lda CUR.bVisible

				bpl .1
				jsr HideCursor.I

.1				ldy #S.POINT-1

.2				lda (ZPPtr1),y
				sta CUR.Pos,y
				dey
				bpl .2

				lda CUR.bVisible
				bmi ShowCursor.I
	
				rts
*--------------------------------------
HideCursor		lda CUR.bVisible
				bpl .9
				jsr HideCursor.I
				stz CUR.bVisible
.9				rts
*--------------------------------------
ShowCursor		lda CUR.bVisible
				bmi .9

				jsr ShowCursor.I
				dec CUR.bVisible
.9				rts
*--------------------------------------
HideCursor.I	lda #S.CB.OP.RESTORE
				bra DrawCursor.I
*--------------------------------------
ShowCursor.I	sta CUR.CB+S.CB.OP
				ldx CUR.T
				lda CUR.Pos+S.POINT.X
				sec
				sbc CUR.HotPoints,x
				sta CUR.CB+S.CB.X1
				lda CUR.Pos+S.POINT.X+1
				sbc #0
				sta CUR.CB+S.CB.X1+1
				lda CUR.Pos+S.POINT.Y
				sec
				sbc CUR.HotPoints+1,x
				sta CUR.CB+S.CB.Y1
				lda CUR.Pos+S.POINT.Y+1
				sbc #0
				sta CUR.CB+S.CB.Y1+1

				lda #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.SAVE
				
DrawCursor.I	sta CUR.CB+S.CB.OP
				
				>LDYA L.SCR
				jsr OBJ.Get

				>LDYA L.CUR.CB
				jsr CLIP.YA
				bcs .9
			
				jmp GFXWrite.CB

.9				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.cur
LOAD usr/src/lib/libgui.s
ASM
