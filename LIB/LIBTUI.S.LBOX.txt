NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* LBOX.New
* (pStack)+12 pParent
* (pStack)+10 X1,Y1
* (pStack)+8 W,H
* (pStack)+6 S.LBOX.EV.CHANGED,S.LBOX.EV.PRESSED
* (pStack)+4 F
* (pStack)+2 pList
* (pStack)+0 pVar
*--------------------------------------
LBOX.New		lda #S.OBJ.T.LBOX
				jsr OBJ.Create
				bcs .99

				>GETSTKW 12
				jsr OBJ.SetParent

				>GETSTKW 10
				jsr OBJ.SetX1Y1

				>GETSTKW 8
				jsr OBJ.SetWH

				>GETSTKW 6
				ldy #S.LBOX.EV.PRESSED
				sta (ZPpObj),y
				txa
				ldy #S.LBOX.EV.CHANGED
				sta (ZPpObj),y

				>GETSTKW 4
				ldy #S.OBJ.F
				sta (ZPpObj),y

				>GETSTKW 2
				ldy #S.OBJ.pBuf
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				>GETSTKW
				ldy #S.OBJ.pVar
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj

*				clc

.99				rts
*--------------------------------------
* (pStack) = Event
*--------------------------------------
LBOX.Run		ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .9

				lda (pStack)
				cmp #C.LF
				bne .2

				ldy #S.LBOX.Idx
				lda (ZPpObj),y
				inc
				tax

				jsr OBJ.GetLineXAtY
				bcs .8

				ldy #S.LBOX.Idx
				lda (ZPpObj),y
				inc
				sta (ZPpObj),y

				clc
				ldy #S.LBOX.VScroll
				adc (ZPpObj),y

				ldy #S.OBJ.InnerH
				cmp (ZPpObj),y
				bcc .7

				ldy #S.LBOX.VScroll
				lda (ZPpObj),y
				inc
				sta (ZPpObj),y
				bra .7

.2				cmp #C.VT
				bne .9

				ldy #S.LBOX.Idx
				lda (ZPpObj),y
				beq .8

				dec
				sta (ZPpObj),y

				ldy #S.LBOX.VScroll
				cmp (ZPpObj),y
				bcs .7

				ldy #S.LBOX.VScroll
				lda (ZPpObj),y
				dec
				sta (ZPpObj),y

.7				jsr OBJ.Draw.Body
				bcs .99

				jsr LBOX.SetpVar

				ldy #S.LBOX.EV.CHANGED
				lda (ZPpObj),y

.8				clc
				rts

.9				lda #0
				sec
.99				rts
*--------------------------------------
LBOX.Draw		ldy #S.LBOX.VScroll
				lda (ZPpObj),y
				clc
				adc (pStack)			Relative Y
				tax

				jsr OBJ.GetLineXAtY
				bcs .5

				txa
				ldy #S.LBOX.Idx
				cmp (ZPpObj),y
				bne .1

				ldy #S.OBJ.S
				lda (ZPpObj),y		S.OBJ.S.bActive
				bpl .1

				ldx #SEQ.INVERSE-SEQS
				jsr LB.SeqOut

.1				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				jsr LB.Ptr1OutEscMaxX
				txa
				beq .8

				bne .6

.5				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

.6				lda #C.SPACE

.7				jsr LB.COut
				dex
				bne .7

.8				clc
				rts
*--------------------------------------
LBOX.SetpVar	ldy #S.LBOX.Idx
				lda (ZPpObj),y
				tax

				jsr OBJ.GetLineXAtY
				bcs .8

				jsr OBJ.GetpVarPtr2

				lda ZPPtr1
				sta (ZPPtr2)
				lda ZPPtr1+1
				ldy #1
				sta (ZPPtr2),y

.8				clc

				rts
*--------------------------------------
LBOX.Activate
LBOX.Deactivate
				jmp OBJ.Draw.Body
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.lbox
LOAD usr/src/lib/libtui.s
ASM
