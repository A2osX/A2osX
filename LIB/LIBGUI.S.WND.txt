NEW
  AUTO 3,1
*--------------------------------------
WND.Init		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC open
				>SR
				bcs .9

				>STYA hDevGFX

				>LDYA L.DCB.GFX
				>STYA IOCTL+S.IOCTL.BUFPTR
				lda #S.IOCTL.S.GETDCB
				sta IOCTL+S.IOCTL.S

				>SS
				>PUSHW hDevGFX
				>PUSHBI IOCTL.STATUS
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR
.9				bcs .99

				lda #S.OBJ.T.WND
				jsr OBJ.New
				bcs .99

*				>STYA ZPpOBJ

				>STYA WND.Screen

				>LDAX DCB.GFX+S.DCB.GFX.W
				>STAX CLIP.Screen+S.RECT.X2
				ldy #S.OBJ.W
				jsr OBJ.SetAXAtY

				>LDAX DCB.GFX+S.DCB.GFX.H
				>STAX CLIP.Screen+S.RECT.Y2
				ldy #S.OBJ.H
				jsr OBJ.SetAXAtY

				>DECW CLIP.Screen+S.RECT.X2
				>DECW CLIP.Screen+S.RECT.Y2

				jsr OBJ.SetX2Y2
				jsr WND.ResetInnerW

				clc

.99				rts
*--------------------------------------
WND.Quit		>LDYA hDevGFX
				cpy #0
				beq .8

				>LIBC Close

.8				clc
WND.Quit.RTS	rts
*--------------------------------------
* guiGetWnd(bWID)
*--------------------------------------
WND.Get			>GETSTKB
				tax
				>LDYA WND.Screen,x

				clc
				rts
*--------------------------------------
* guiSetWnd(bWID,hWND)
*--------------------------------------
WND.Set			>GETSTKB 2
				pha

				>GETSTKW

				ply
				sta WND.Screen,y
				txa
				sta WND.Screen+1,y

				clc
				rts
*--------------------------------------
* pWND *guiNewWnd(pWND, wFlags, iX, iY, wW, wH)
*--------------------------------------
WND.New			lda #S.OBJ.T.WND
				jsr OBJ.New
				bcs .99

*				>STYA ZPpOBJ

				>GETSTKW
				ldy #S.OBJ.H
				jsr OBJ.SetAXAtY

				>GETSTKW 2
				ldy #S.OBJ.W
				jsr OBJ.SetAXAtY

				>GETSTKW 4
				ldy #S.OBJ.Y1
				jsr OBJ.SetAXAtY

				>GETSTKW 6
				ldy #S.OBJ.X1
				jsr OBJ.SetAXAtY

				>GETSTKW 8
				ldy #S.OBJ.F
				jsr OBJ.SetAXAtY

				>GETSTKW 10
				>STAX ZPpWND
				ldy #S.OBJ.pParent
				jsr OBJ.SetAXAtY

				jsr OBJ.SetX2Y2			for adding childs before guiShowWND
				jsr WND.ResetInnerW

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.RESIZE
				beq .1

				iny
				lda (ZPpOBJ),y
				ora /S.OBJ.F.BORDER
				sta (ZPpOBJ),y

.1				jsr WND.AddChild.I

				>LDYA ZPpOBJ

				clc

.99				rts
*--------------------------------------
* int guiSetMenuBar (pWND, pMBARDEF)
*--------------------------------------
WND.SetMBar		>GETSTKW 2
				>STAX ZPpWND

				>GETSTKW
				jsr MBAR.New
				bcs .9

				phy
				ldy #S.WND.pMBAR+1
				sta (ZPpWND),y
				dey
				pla
				sta (ZPpWND),y

*				clc

.9				rts
*--------------------------------------
WND.Destroy		>GETSTKW

				>STAX ZPpOBJ

				jsr OBJ.GetpWND

				jsr WND.RemoveFromChilds.I

				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .1

				pha
				dey
				lda (ZPpOBJ),y
				tay
				pla
				>LIBC Free

.1
				>LDYA ZPpOBJ
				>LIBC Free

				>LDAX ZPpWND

				jmp WND.ShowAllAX

.9				rts
*--------------------------------------
WND.Activate	>GETSTKW
				>STAX ZPpOBJ


				clc
WND.Activate.RTS
				rts
*--------------------------------------
* guiBring2Front(pWnd, pObj)
*--------------------------------------
WND.Bring2Front	jsr WND.RemoveFromChilds.I
				bcs .9

				jmp WND.AddChild.I

.9				rts
*--------------------------------------
* guiRemoveFromChilds(pWnd, pObj)
*--------------------------------------
WND.RemoveFromChilds
				>GETSTKW 2
				>STAX ZPpWND

				>GETSTKW
				>STAX ZPpObj

WND.RemoveFromChilds.I
				lda ZPpWND
				sta R1
				lda ZPpWND+1
				sta R1+1

.1				ldy #S.WND.pChilds+1
				lda (R1),y
				beq .9

				cmp ZPpObj+1
				bne .2

				dey
				lda (R1),y
				cmp ZPpObj
				beq .3

.2				ldy #S.WND.pChilds
				lda (R1),y
				tax
				iny
				lda (R1),y

				sta R1+1
				dey
				txa
				sta R1
				bra .1
*--------------------------------------
.3				ldy #S.WND.pChilds
				lda (ZPpObj),y
				tax
				iny
				lda (ZPpObj),y
				sta (R1),y

				dey
				txa
				sta (R1),y
				clc
				rts

.9				sec
				rts
*--------------------------------------
* void guiAddChild(pWnd, pObj)
*--------------------------------------
WND.AddChild	>GETSTKW 2
				>STAX ZPpWND
				>GETSTKW
				>STAX ZPpOBJ

WND.AddChild.I	ldy #S.WND.pChilds
				lda (ZPpWND),y
				sta R1
				lda ZPpObj
				sta (ZPpWND),y

				iny
				lda (ZPpWND),y
				sta R1+1
				lda ZPpObj+1
				sta (ZPpWND),y

				lda R1+1
				beq .8

				sta (ZPpObj),y
				dey
				lda R1
				sta (ZPpObj),y

.8				clc
				rts
*--------------------------------------
* guiAddNext(pWnd, pObj)
*--------------------------------------
WND.AddNext.I	>LDAX ZPpWND

.1				>STAX R1

				ldy #S.OBJ.pNext+1
				lda (R1),y
				beq .2

				tax
				dey
				lda (R1),y
				bra .1

.2				lda ZPpOBJ+1
				sta (R1),y
				dey
				lda ZPpOBJ
				sta (R1),y
				rts
*--------------------------------------
* int guiShowWnd( pWND )
*--------------------------------------
WND.Show		>GETSTKW

				bra WND.ShowAX

WND.ShowAll		>GETSTKW

WND.ShowAllAX	phx
				pha

				jsr WND.ShowAX
				bcs .9

				pla
				plx

				>STAX ZPpOBJ

				ldy #S.WND.pChilds+1
				lda (ZPpOBJ),y
				beq .8

				tax
				dey
				lda (ZPpOBJ),y
				bra WND.ShowAllAX

.8				clc
				rts

.9				plx
				plx
				rts
*--------------------------------------
WND.ShowAX		jsr WND.PaintAX
				bcs .9

.1				jsr OBJ.GetpNext
				bcs .8

				jsr OBJ.PaintAX
				bcc .1

.9				rts

.8				clc
				rts
*--------------------------------------
WND.PaintAX		>STAX ZPpOBJ

				jsr OBJ.SetX2Y2
				jsr WND.ResetInnerW

				jsr WND.DrawBorders
*--------------------------------------
				stz Counter
				stz Counter+1

				jsr CB.ZeroX1Y1

				jsr WND.bHasTBar
				beq .3

				lda SYSFON.Hp1
				jsr WND.Add2Counter

				ldy #S.WND.TBarY2
				jsr WND.SetCounterAtY

				>INCW Counter

				jsr WND.DrawTitleBar
				bcs .9
*--------------------------------------
.3				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .4

				ldy #S.WND.MBarY1
				jsr WND.SetCounterAtY

				lda SYSFON.Hp1
				jsr WND.Add2Counter

				ldy #S.WND.MBarY2
				jsr WND.SetCounterAtY

				>INCW Counter

				jsr WND.DrawMenuBar
				bcc .4

.9				rts
*--------------------------------------
.4				ldy #S.WND.InnerY1
				jsr WND.SetCounterAtY

				ldy #S.WND.pSTATUS+1
				lda (ZPpOBJ),y
				beq .8

				lda SYSFON.Hp2
				jsr WND.Add2Counter

				ldy #S.WND.InnerY2
				lda (ZPpOBJ),y
				sec
				sbc SYSFON.Hp2
				sta (ZPpOBJ),y
				tax
				iny
				lda (ZPpOBJ),y
				sbc #0
				sta (ZPpOBJ),y

				inx
				bne .5

				inc

.5				ldy	#S.WND.SBarY1+1
				sta (ZPpOBJ),y
				dey
				txa
				sta (ZPpOBJ),y

				jsr WND.DrawStatusBar
*--------------------------------------
.8				ldy #S.WND.InnerH
				lda (ZPpOBJ),y
				sec
				sbc Counter
				sta (ZPpOBJ),y

				iny
				lda (ZPpOBJ),y
				sbc Counter+1
				sta (ZPpOBJ),y

				>LDAX ZPpOBJ
				jmp OBJ.PaintAX			go paint inner window
*--------------------------------------
WND.bHasTBar	ldy #S.WND.pTITLE+1
				lda (ZPpOBJ),y
				bne .8

				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				and #S.WND.F.MOVE+S.WND.F.CLOSE+S.WND.F.MIN+S.WND.F.MAX

.8				rts
*--------------------------------------
* reset Inner 0,0,W-1,H-1,W,H (6 words)
*--------------------------------------
WND.ResetInnerW	lda #0

				ldy #S.WND.InnerX1
				ldx #4

.1				sta (ZPpOBJ),y
				iny
				dex
				bne .1
*--------------------------------------
				ldy #S.OBJ.W+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.WND.InnerW
				sta (ZPpOBJ),y
				pha
				iny
				txa
				sta (ZPpOBJ),y
				pla
				bne .2

				dex

.2				dec

				ldy #S.WND.InnerX2
				sta (ZPpOBJ),y
				iny
				txa
				sta (ZPpOBJ),y
*--------------------------------------
				ldy #S.OBJ.H+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.WND.InnerH
				sta (ZPpOBJ),y
				pha
				iny
				txa
				sta (ZPpOBJ),y
				pla
				bne .3

				dex

.3				dec

				ldy #S.WND.InnerY2
				sta (ZPpOBJ),y
				iny
				txa
				sta (ZPpOBJ),y

				rts
*--------------------------------------
WND.Add2Counter	clc
				adc Counter
				sta Counter
				bcc .8

				inc Counter+1

.8				rts
*--------------------------------------
WND.SetCounterAtY
				lda Counter
				sta (ZPpObj),y
				iny
				lda Counter+1
				sta (ZPpObj),y

WND.SetAXAtY.9	rts
*--------------------------------------
WND.DrawBorders	ldy #S.OBJ.F+1

				lda (ZPpOBJ),y
*				and /S.OBJ.F.BORDER
*				beq .8

				bmi .1

				clc
				rts

.1				jsr CB.BorderLineH

				>LDYAI -1
				>STYA CB.Cache+S.CB.X1
				>STYA CB.Cache+S.CB.Y1
				>STYA CB.Cache+S.CB.Y2	top  -1,-1,W,-1

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X2
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X2+1

				jsr CB.WriteP
				bcs .9

				ldy #S.OBJ.H
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1
				sta CB.Cache+S.CB.Y2
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1+1
				sta CB.Cache+S.CB.Y2+1	bottom  -1,H,W,H

				jsr CB.WriteP
				bcs .9

				lda #S.CB.CMD.VLINE
				sta CB.Cache+S.CB.CMD

				>LDYAI -1
				>STYA CB.Cache+S.CB.X1
				>STYA CB.Cache+S.CB.X2

				stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1
				>DECW CB.Cache+S.CB.Y2	left  -1,0,-1,H-1

				jsr CB.WriteP
				bcs .9

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X1
				sta CB.Cache+S.CB.X2
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X1+1
				sta CB.Cache+S.CB.X2+1	right  W,0,W,H-1
				jmp CB.WriteP

.9				rts
*--------------------------------------
WND.DrawTitleBar
				lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET
				sta CB.Cache+S.CB.OP
				lda #S.CB.M.C16
				sta CB.Cache+S.CB.M

				ldx PREFS.TBarColorI

				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				and #S.OBJ.S.ACTIVE
				beq .10

				ldx PREFS.TBarColorA

.10				stx CB.Cache+S.CB.COLOR

				stz CB.Cache+S.CB.pPat+1

				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				lda SYSFON.Hp1
				sta CB.Cache+S.CB.Y2
				stz CB.Cache+S.CB.Y2+1

				jsr CB.WriteW

				>INCW CB.Cache+S.CB.Y1

* TOP LEFT ICON
				ldy #S.WND.pICON+1
				lda (ZPpOBJ),y
				tax
				dey
				ora (ZPpOBJ),y
				bne .1

				>LDYA L.BM.Apple
				bra .2

.1				lda (ZPpOBJ),y
				tay
				txa

.2				jsr BM.YA2CBCache
				jsr CB.WriteW

				jsr WND.DrawTitleBarText
				bcs .9

* TOP RIGHT ICONS

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				clc						+1
				sbc #48
				sta CB.Cache+S.CB.X1

				iny
				lda (ZPpOBJ),y
				sbc #0
				sta CB.Cache+S.CB.X1+1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.MIN
				beq .3

				ldx #BM.ID.MIN
				jsr BM.X2CBCache
				jsr CB.WriteW

.3				lda #16
				jsr CB.AddA2X1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.MAX
				beq .4

				ldx #BM.ID.MAX
				jsr BM.X2CBCache
				jsr CB.WriteW

.4				lda #16
				jsr CB.AddA2X1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.CLOSE
				beq .8

				ldx #BM.ID.CLOSE
				jsr BM.X2CBCache
				jmp CB.WriteW

.8				clc

.9				rts
*--------------------------------------
WND.DrawTitleBarText
				ldy #S.WND.pTITLE
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYSB
				jsr FON.GetTextWYAX

				lda PREFS.XMargin
				asl
*				clc
				adc CB.Cache+S.CB.SrcW
				sta CB.Cache+S.CB.SrcW
				bcc .1

				inc CB.Cache+S.CB.SrcW+1

.1				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sec
				sbc CB.Cache+S.CB.SrcW
				sta DX
				iny
				lda (ZPpOBJ),y
				sbc CB.Cache+S.CB.SrcW+1

				lsr
				ror DX
				sta DX+1

				lda DX

			and PREFS.BestViewedX

				sta CB.Cache+S.CB.X1
				lda DX+1
				sta CB.Cache+S.CB.X1+1

				lda DX
				clc
				adc CB.Cache+S.CB.SrcW

			ora PREFS.nBestViewedX

				sta CB.Cache+S.CB.X2

				lda DX+1
				adc CB.Cache+S.CB.SrcW+1
				sta CB.Cache+S.CB.X2+1

				>DECW CB.Cache+S.CB.Y2

				lda #C.WHITE
				jsr CB.FillRectMonoA
				jsr CB.WriteW

				lda PREFS.XMargin
				jsr CB.AddA2X1

				ldy #S.WND.pTITLE
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYSB
				jmp DRAW.TextYAXW
*--------------------------------------
WND.DrawMenuBar	ldy #S.WND.pMBAR
				lda (ZPpOBJ),y
				sta R1
				iny
				lda (ZPpOBJ),y
				sta R1+1

				ldy #S.WND.MBarY1+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.Y1
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldy #S.WND.MBarY2+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.Y2
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sta (R1),y
				iny
				lda (ZPpOBJ),y
				sta (R1),y

				ldx ZPpOBJ+1
				phx
				ldx ZPpOBJ
				phx

				>LDAX R1
				jsr OBJ.PaintAX

				plx
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				rts
*--------------------------------------
WND.DrawStatusBar
				jsr CB.BorderLineH

				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				ldy	#S.WND.SBarY1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1+1

				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				jsr CB.WriteW
				bcs .9

				lda PREFS.SBarColor
				jsr CB.FillRectMonoA

				>INCW CB.Cache+S.CB.Y1

				lda CB.Cache+S.CB.Y1
				clc
				adc SYSFON.H
				sta CB.Cache+S.CB.Y2
				lda CB.Cache+S.CB.Y1+1
				adc #0
				sta CB.Cache+S.CB.Y2+1

				jsr CB.WriteW
				bcs .9

				>INCW CB.Cache+S.CB.Y1

				lda #4
				jsr CB.AddA2X1

				ldy #S.WND.pSTATUS
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYS
				jmp DRAW.TextYAXW

.9				rts
*--------------------------------------
WND.CheckOver	>LDAX WND.Screen

.1				jsr OBJ.ToGlobalAX		AX -> ZPpOBJ, CLIP.Rect = OBJ abs RECT
				jsr MSG.InClipRect
				bcs .19

				lda (ZPpOBJ)
				beq .2					WND

				ldy #S.WND.hPTR
				lda (ZPpOBJ),y
				jsr PTR.SetA

				jsr MSG.ToLocal

				>LDAX ZPpOBJ
				jsr OBJ.EnterAX
				bcs .19

.10				jsr OBJ.GetpNext
				bcs .18

				jsr OBJ.LeaveAX
				bcc .10

				rts

.18				clc
.19				rts
*--------------------------------------
.2				ldy #S.OBJ.F+1
				lda (ZPpOBJ),y
				and /S.OBJ.F.BORDER
				beq .21

				jsr WND.CheckOverB
				bcs .21

				jmp MOU.ResizeWND
*--------------------------------------
.21				jsr WND.CheckOverW		TitleBar, Menurbar... ?
				bcc .99					yes, all done.

				ldy #S.WND.pChilds+1	in inner space...
				lda (ZPpOBJ),y
				beq .80

				tax
				dey
				lda (ZPpOBJ),y

				ldy ZPpOBJ+1
				phy
				ldy ZPpOBJ
				phy
*--------------------------------------
.3				phx						Save AX = pChild
				pha

				jsr .1					recurse child window
				bcc .6

				plx						set child as base...
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				ldy #S.WND.pChilds+1	..to get next xhild
				lda (ZPpOBJ),y
				beq .4

				tax
				dey
				lda (ZPpOBJ),y
				bra .3
*--------------------------------------
.4				plx						not in any child,restore base OBJ
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1
				bra .80					go check pNext
*--------------------------------------
.6				plx						set child as base...
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				plx						discard base OBJ
				plx
*--------------------------------------
.80				jsr OBJ.GetpNext
				bcs .86

				ldy ZPpOBJ+1
				phy
				ldy ZPpOBJ
				phy

.81				jsr .1
				bcc .82

				jsr OBJ.GetpNext
				bcc .81
*--------------------------------------
.82				plx
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1
				bcc .99

.86				ldy #S.WND.hPTR
				lda (ZPpOBJ),y
				jsr PTR.SetA

				jsr MSG.ToLocal

				lda (ZPpOBJ)
				bne .87

				jsr MOU.ToLocalInnerW

.87				>LDAX ZPpOBJ
				jmp OBJ.EnterAX

.99				rts
*--------------------------------------
WND.CheckOverB	stz IY

				ldy #S.OBJ.X1
				lda (ZPpOBJ),y
				sec
				sbc #1
				eor MSG+S.MSG.X1
				bne .1

				iny
				lda (ZPpOBJ),y
				sbc #0
				eor MSG+S.MSG.X1+1
				bne .1

				lda #PTR.T.RESIZEX
				sta IY

.1				ldy #S.OBJ.Y1
				lda (ZPpOBJ),y
				sec
				sbc #1
				eor MSG+S.MSG.Y1
				bne .3

				iny
				lda (ZPpOBJ),y
				sbc #0
				eor MSG+S.MSG.Y1+1
				bne .3

				lda IY
				beq .2

				lda #PTR.T.RESIZEXY1
				jmp PTR.SetA

.2				lda #PTR.T.RESIZEY
				sta IY

.3				ldy #S.OBJ.X2
				lda (ZPpOBJ),y
				clc
				adc #1
				eor MSG+S.MSG.X1
				bne .5

				iny
				lda (ZPpOBJ),y
				adc #0
				eor MSG+S.MSG.X1+1
				bne .5

				lda IY
				beq .4

				lda #PTR.T.RESIZEXY2
				jmp PTR.SetA

.4				lda #PTR.T.RESIZEX
				sta IY

.5				ldy #S.OBJ.Y2
				lda (ZPpOBJ),y
				clc
				adc #1
				eor MSG+S.MSG.Y1
				bne .7

				iny
				lda (ZPpOBJ),y
				adc #0
				eor MSG+S.MSG.Y1+1
				bne .7

				lda #PTR.T.RESIZEXY1
.6				jmp PTR.SetA

.7				lda IY
				bne .6

.9				sec
				rts
*--------------------------------------
WND.CheckOverW	lda	MSG+S.MSG.S
				bit #S.XY.S.CLK
				beq .2

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				and #S.WND.F.STACKED
				beq .2

				jsr WND.Bring2Front

.2				jsr WND.bHasTBar
				beq .3

				ldy #S.WND.TBarY2
				jsr MOU.WNDCmpY
				bcc .3					not in TitleBAR

				jmp MOU.MoveWND
*--------------------------------------
.3				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .4

				ldy	#S.WND.MBarY2
				jsr MOU.WNDCmpY
				bcc .4					not in MenuBAR

				lda #PTR.T.ARROW
				jsr PTR.SetA

				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y
				jmp OBJ.EnterAX
*--------------------------------------
.4				ldy #S.WND.pSTATUS+1
				lda (ZPpOBJ),y
				sec
				beq .9

				ldy #S.WND.InnerY2
				jsr MOU.WNDCmpY
*				bcs .9					not in StatusBAR

*				clc

.9				rts
*--------------------------------------
WND.fEnter
WND.fLeave
				clc
				rts
*--------------------------------------
* default inner window fPaint in Y,A
* ZPpOBJ = pWND
*--------------------------------------
WND.fPaint		lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET+S.CB.OP.COLOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				ldy #S.OBJ.BGCOLOR+1
				lda (ZPpOBJ),y
				bne .10

				stz CB.Cache+S.CB.pPat+1

				dey
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.COLOR
				beq .1

				inc
				beq .1

				lda #S.CB.M.C16
				sta CB.Cache+S.CB.M
				bra .1

.10				sta CB.Cache+S.CB.pPat+1
				dey
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.pPat

.1				jsr CB.ZeroX1Y1
				jsr CB.SetInnerX2Y2

				jsr CB.WriteInnerW

				bcs .9
*--------------------------------------
				ldy #S.WND.BGBM
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.hSrc
				iny
				ora (ZPpOBJ),y
				beq .8

				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.hSrc+1

				jsr CB.ZeroSrcXSrcY

				>LDYA CB.Cache+S.CB.hSrc

				jsr BM.GetProps
				bcs .9

				ldx #3

.2				lda TEXTBUF+S.BM.W,x
				sta CB.Cache+S.CB.SrcW,x
				dex
				bpl .2

				lda TEXTBUF+S.BM.F

.3				lsr
				bcs .4

				asl CB.Cache+S.CB.SrcW
				rol CB.Cache+S.CB.SrcW+1
				bra .3

.4				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD

				jmp CB.WriteInnerW

.8

*				clc

.9				rts
*--------------------------------------
WND.GetAXAtYm1	lda (ZPpWND),y
				tax
				dey
				lda (ZPpWND),y
				bne .1

				dex

.1				dec

				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.wnd
LOAD usr/src/lib/libgui.s
ASM
