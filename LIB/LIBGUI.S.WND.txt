NEW
  AUTO 3,1
*--------------------------------------
WND.Init		>SS
				>PUSHW L.DEVNAME.GFX
				>PUSHBI 0
				>LIBC open
				>SR
				bcs .9

				>STYA hDevGFX

				>LDYA L.DCB.GFX
				>STYA IOCTL+S.IOCTL.BUFPTR
				lda #S.IOCTL.S.GETDCB
				sta IOCTL+S.IOCTL.S

				>SS
				>PUSHW hDevGFX
				>PUSHBI IOCTL.STATUS
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR
.9				bcs .99

				lda #S.OBJ.T.WND
				jsr OBJ.New
				bcs .99

*				>STYA ZPpOBJ

				>STYA WND.Screen

				>LDAX DCB.GFX+S.DCB.GFX.W
				>STAX CLIP.Screen+S.RECT.X2
				ldy #S.OBJ.W
				jsr OBJ.SetAXAtY

				>LDAX DCB.GFX+S.DCB.GFX.H
				>STAX CLIP.Screen+S.RECT.Y2
				ldy #S.OBJ.H
				jsr OBJ.SetAXAtY

				>DECW CLIP.Screen+S.RECT.X2
				>DECW CLIP.Screen+S.RECT.Y2

				jsr OBJ.SetX2Y2
				jsr WND.ResetInnerW

				clc

.99				rts
*--------------------------------------
WND.Quit		>LDYA hDevGFX
				cpy #0
				beq .8

				>LIBC Close

.8				clc
WND.Quit.RTS	rts
*--------------------------------------
* guiGetWnd(bWID)
*--------------------------------------
WND.Get			>GETSTKB
				tax
				>LDYA WND.Screen,x

				clc
				rts
*--------------------------------------
* guiSetWnd(bWID,hWND)
*--------------------------------------
WND.Set			>GETSTKB 2
				pha

				>GETSTKW

				ply
				sta WND.Screen,y
				txa
				sta WND.Screen+1,y

				clc
				rts
*--------------------------------------
* pWND *guiNewWnd(pWND, wFlags, iX, iY, wW, wH)
*--------------------------------------
WND.New			lda #S.OBJ.T.WND
				jsr OBJ.New
				bcs .99

*				>STYA ZPpOBJ

				>GETSTKW
				ldy #S.OBJ.H
				jsr OBJ.SetAXAtY

				>GETSTKW 2
				ldy #S.OBJ.W
				jsr OBJ.SetAXAtY

				>GETSTKW 4
				ldy #S.OBJ.Y1
				jsr OBJ.SetAXAtY

				>GETSTKW 6
				ldy #S.OBJ.X1
				jsr OBJ.SetAXAtY

				>GETSTKW 8
				ldy #S.OBJ.F
				jsr OBJ.SetAXAtY

				>GETSTKW 10
				>STAX ZPpWND
				ldy #S.OBJ.pParent
				jsr OBJ.SetAXAtY

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.RESIZE
				beq .1

				iny
				lda (ZPpOBJ),y
				ora /S.OBJ.F.BORDER
				sta (ZPpOBJ),y

				ldy #S.OBJ.X1
				lda (ZPpOBJ),y
				ora #3
				sta (ZPpOBJ),y

.1				jsr OBJ.SetX2Y2
				jsr WND.ResetInnerW		for adding childs before guiShowWND

				jsr WND.AddChild.I

				>LDYA ZPpOBJ

				clc

.99				rts
*--------------------------------------
* int guiSetMenuBar (pWND, pMBARDEF)
*--------------------------------------
WND.SetMBar		>GETSTKW 2
				>STAX ZPpWND

				>GETSTKW
				jsr MBAR.New
				bcs .9

				phy
				ldy #S.WND.pMBAR+1
				sta (ZPpWND),y
				dey
				pla
				sta (ZPpWND),y

*				clc

.9				rts
*--------------------------------------
WND.Destroy		>GETSTKW

				>STAX ZPpOBJ

				jsr OBJ.GetpWND

				jsr WND.RemoveFromChilds.I

				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .1

				pha
				dey
				lda (ZPpOBJ),y
				tay
				pla
				>LIBC Free

.1
				>LDYA ZPpOBJ
				>LIBC Free

				>LDAX ZPpWND

				jmp WND.ShowAllAX

.9				rts
*--------------------------------------
* guiBring2Top(pObj)
*--------------------------------------
WND.Bring2Top	>GETSTKW
				>STAX ZPpObj

WND.Bring2Top.I	jsr WND.RemoveFromChilds.I
				bcs .9

				jsr OBJ.GetpWND
				jsr WND.AddChild.I

.9				rts
*--------------------------------------
* guiRemoveFromChilds(pObj)
*--------------------------------------
WND.RemoveFromChilds
				>GETSTKW
				>STAX ZPpObj

WND.RemoveFromChilds.I
				ldy #S.OBJ.pPARENT
				lda (ZPpOBJ),y
				sta R1
				iny
				lda (ZPpOBJ),y
				sta R1+1				R1 = pWND

.1				ldy #S.WND.pChilds+1
				lda (R1),y
				beq .9

				cmp ZPpObj+1
				bne .2

				dey

				lda (R1),y
				cmp ZPpObj
				beq .8

.2				ldy #S.WND.pChilds
				lda (R1),y
				tax
				iny
				lda (R1),y

				sta R1+1
				stx R1
				bra .1
*--------------------------------------
.8				lda (ZPpObj),y			y = #S.WND.pChilds
				tax

				iny

				lda (ZPpObj),y
				sta (R1),y

				dey

				txa
				sta (R1),y

				lda #0
				sta (ZPpObj),y
				iny
				sta (ZPpObj),y

				clc
				rts

.9				sec
				rts
*--------------------------------------
* void guiAddChild(pWnd, pObj)
*--------------------------------------
WND.AddChild	>GETSTKW 2
				>STAX ZPpWND
				>GETSTKW
				>STAX ZPpOBJ

WND.AddChild.I	ldy #S.OBJ.F
				lda (ZPpObj),y
				ora #S.WND.F.STACKED
				sta (ZPpObj),y

				ldy #S.WND.pChilds
				lda (ZPpWND),y
				sta R1
				lda ZPpObj
				sta (ZPpWND),y

				iny

				lda (ZPpWND),y
				sta R1+1
				lda ZPpObj+1
				sta (ZPpWND),y

				lda R1+1				R1 = previous active Child
				beq .8

				sta (ZPpObj),y
				dey
				lda R1
				sta (ZPpObj),y

				>LDAX R1
				jsr WND.DeactivateAX
				bcs .9

.8				>LDAX ZPpObj
				jsr WND.ActivateAX

.9				rts
*--------------------------------------
WND.Desctivate	>GETSTKW

WND.DeactivateAX
				ldy ZPpOBJ+1
				phy
				ldy ZPpOBJ
				phy

				>STAX ZPpOBJ

				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				bit #S.OBJ.S.ACTIVE
				clc
				beq .8

				and #S.OBJ.S.ACTIVE^$ff
				sta (ZPpOBJ),y
				bpl .8					!S.OBJ.S.VISIBLE

				>LDAX ZPpOBJ
				jsr WND.ShowAX

.8				plx
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				rts
*--------------------------------------
WND.Activate	>GETSTKW

WND.ActivateAX	ldy ZPpOBJ+1
				phy
				ldy ZPpOBJ
				phy

				>STAX ZPpOBJ

				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				bit #S.OBJ.S.ACTIVE
				clc
				bne .8

				ora #S.OBJ.S.ACTIVE
				sta (ZPpOBJ),y
				bpl .8					!S.OBJ.S.VISIBLE

				>LDAX ZPpOBJ
				jsr WND.ShowAX

.8				plx
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				rts
*--------------------------------------
* guiAddNext(pWnd, pObj)
*--------------------------------------
WND.AddNext.I	>LDAX ZPpWND

.1				>STAX R1

				ldy #S.OBJ.pNext+1
				lda (R1),y
				beq .2

				tax
				dey
				lda (R1),y
				bra .1

.2				lda ZPpOBJ+1
				sta (R1),y
				dey
				lda ZPpOBJ
				sta (R1),y
				rts
*--------------------------------------
* int guiShowWnd( pWND )
*--------------------------------------
WND.Show		>GETSTKW

				bra WND.ShowAX

WND.ShowAll		>GETSTKW

WND.ShowAllAX	phx
				pha

				jsr WND.ShowAX
				bcs .9

				pla
				plx

				>STAX ZPpOBJ

				ldy #S.WND.pChilds+1
				lda (ZPpOBJ),y
				beq .8

				tax
				dey
				lda (ZPpOBJ),y
				bra WND.ShowAllAX

.8				clc
				rts

.9				plx
				plx
				rts
*--------------------------------------
WND.ShowAX		>STAX ZPpOBJ
				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				ora #S.OBJ.S.VISIBLE
				sta (ZPpOBJ),y

				jsr WND.Paint
				bcs .9

.1				jsr OBJ.GetpNext
				bcs .8

				jsr OBJ.PaintAX
				bcc .1

.9				rts

.8				clc
				rts
*--------------------------------------
WND.Paint		jsr OBJ.SetX2Y2
				jsr WND.ResetInnerW

				stz Counter				X pixel to rembr from innerX
				stz Counter+1			Y pixel to rembr from innerY

				ldy #S.OBJ.F+1
				lda (ZPpOBJ),y
*				and /S.OBJ.F.BORDER
*				beq .1

				bpl .1

				jsr DRAW.Borders
				bcs .9

				inc Counter
*--------------------------------------
.1				jsr WND.bHasTBar
				beq .3

				ldy #S.WND.TBarY1
				jsr WND.SetCounterAtY

				lda SYSFON.Hp1
				jsr WND.Add2Counter

				ldy #S.WND.TBarY2
				jsr WND.SetCounterAtY

				inc Counter

				jsr WND.DrawTitleBar
				bcs .9
*--------------------------------------
.3				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .4

				ldy #S.WND.MBarY1
				jsr WND.SetCounterAtY

				lda SYSFON.Hp1
				jsr WND.Add2Counter

				ldy #S.WND.MBarY2
				jsr WND.SetCounterAtY

				inc Counter

				jsr WND.DrawMenuBar
				bcc .4

.9				rts
*--------------------------------------
.4				ldy #S.WND.InnerY1
				jsr WND.SetCounterAtY

				ldy #S.WND.pSTATUS+1
				lda (ZPpOBJ),y
				beq .8

				lda SYSFON.Hp1
				jsr WND.Add2Counter

				ldy #S.WND.InnerY2
				lda (ZPpOBJ),y
				ldy #S.WND.SBarY2
				sta (ZPpOBJ),y
				sec
				sbc SYSFON.Hp1
				ldy #S.WND.InnerY2
				sta (ZPpOBJ),y
				tax
				ldy #S.WND.InnerY2+1
				lda (ZPpOBJ),y
				ldy #S.WND.SBarY2+1
				sta (ZPpOBJ),y
				sbc #0
				ldy #S.WND.InnerY2+1
				sta (ZPpOBJ),y

				inx
				bne .5

				inc

.5				ldy	#S.WND.SBarY1+1
				sta (ZPpOBJ),y
				dey
				txa
				sta (ZPpOBJ),y

				jsr WND.DrawStatusBar
*--------------------------------------
.8				ldy #S.WND.InnerH
				lda (ZPpOBJ),y
				sec
				sbc Counter
				sta (ZPpOBJ),y

				iny
				lda (ZPpOBJ),y
				sbc #0
				sta (ZPpOBJ),y

				>LDAX ZPpOBJ
				jmp OBJ.PaintAX			go paint inner window
*--------------------------------------
* reset InnerX1,InnerY1,InnerX2,InnerY2,InnerW,InnerH (6 words)
*--------------------------------------
WND.ResetInnerW	stz IY

				ldy #S.OBJ.F+1
				lda (ZPpOBJ),y
				and /S.OBJ.F.BORDER
				beq .1

				inc IY

.1				lda IY
				ldy #S.WND.InnerX1
				sta (ZPpOBJ),y
				ldy #S.WND.InnerY1
				sta (ZPpOBJ),y

				lda #0
				ldy #S.WND.InnerX1+1
				sta (ZPpOBJ),y
				ldy #S.WND.InnerY1+1
				sta (ZPpOBJ),y

				asl IY
*--------------------------------------
				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sec
				sbc IY
				ldy #S.WND.InnerW
				sta (ZPpOBJ),y
				ldy #S.WND.BarW
				sta (ZPpOBJ),y

				pha

				ldy #S.OBJ.W+1
				lda (ZPpOBJ),y
				sbc #0
				ldy #S.WND.InnerW+1
				sta (ZPpOBJ),y
				ldy #S.WND.BarW+1
				sta (ZPpOBJ),y

				tax
				pla
				bne .2

				dex

.2				dec

				ldy #S.WND.InnerX2
				sta (ZPpOBJ),y
				iny
				txa
				sta (ZPpOBJ),y
*--------------------------------------
				ldy #S.OBJ.H
				lda (ZPpOBJ),y
				sec
				sbc IY
				ldy #S.WND.InnerH
				sta (ZPpOBJ),y

				pha

				ldy #S.OBJ.H+1
				lda (ZPpOBJ),y
				sbc #0
				ldy #S.WND.InnerH+1
				sta (ZPpOBJ),y

				tax
				pla
				bne .3

				dex

.3				dec

				ldy #S.WND.InnerY2
				sta (ZPpOBJ),y
				iny
				txa
				sta (ZPpOBJ),y

				rts
*--------------------------------------
WND.Add2Counter	clc
				adc Counter
				sta Counter
				bcc .8

				inc Counter+1

.8				rts
*--------------------------------------
WND.SetCounterAtY
				lda Counter
				sta (ZPpObj),y
				iny
				lda Counter+1
				sta (ZPpObj),y

				rts
*--------------------------------------
WND.DrawTitleBar
				lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET
				sta CB.Cache+S.CB.OP
				lda #S.CB.M.C16
				sta CB.Cache+S.CB.M

				ldx PREFS.TBarColorI

				ldy #S.OBJ.S
				lda (ZPpOBJ),y

				and #S.OBJ.S.ACTIVE
				beq .10

				ldx PREFS.TBarColorA

.10				stx CB.Cache+S.CB.COLOR

				stz CB.Cache+S.CB.pPat+1

				ldy #S.WND.InnerX1			0 or 1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				ldy #S.WND.TBarY1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1

				ldy #S.WND.BarW+1
				jsr OBJ.GetAXAtYm1
				clc
				adc CB.Cache+S.CB.X1
				sta CB.Cache+S.CB.X2
				txa
				adc CB.Cache+S.CB.X1+1
				sta CB.Cache+S.CB.X2+1

				lda SYSFON.Hp1
				clc
				adc CB.Cache+S.CB.Y1
				sta CB.Cache+S.CB.Y2
				stz CB.Cache+S.CB.Y2+1

				jsr CB.WriteW

				>INCW CB.Cache+S.CB.Y1

* TOP LEFT ICON
				ldy #S.WND.pICON+1
				lda (ZPpOBJ),y
				tax
				dey
				ora (ZPpOBJ),y
				bne .1

				>LDYA L.BM.Apple
				bra .2

.1				lda (ZPpOBJ),y
				tay
				txa

.2				jsr BM.YA2CBCache
				jsr CB.WriteW

				jsr WND.DrawTitleBarText
				bcs .9

* TOP RIGHT ICONS

				ldy #S.WND.BarW
				lda (ZPpOBJ),y
				clc						+1
				sbc #48
				sta CB.Cache+S.CB.X1

				iny
				lda (ZPpOBJ),y
				sbc #0
				sta CB.Cache+S.CB.X1+1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.MIN
				beq .3

				ldx #BM.ID.MIN
				jsr BM.X2CBCache
				jsr CB.WriteW

.3				lda #16
				jsr CB.AddA2X1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.MAX
				beq .4

				ldx #BM.ID.MAX
				jsr BM.X2CBCache
				jsr CB.WriteW

.4				lda #16
				jsr CB.AddA2X1

				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				bit #S.WND.F.CLOSE
				beq .8

				ldx #BM.ID.CLOSE
				jsr BM.X2CBCache
				jmp CB.WriteW

.8				clc

.9				rts
*--------------------------------------
WND.DrawTitleBarText
				ldy #S.WND.pTITLE
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYSB
				jsr FON.GetTextWYAX

				lda PREFS.XMargin
				asl
*				clc
				adc CB.Cache+S.CB.SrcW
				sta CB.Cache+S.CB.SrcW
				bcc .1

				inc CB.Cache+S.CB.SrcW+1

.1				ldy #S.WND.BarW
				lda (ZPpOBJ),y
				sec
				sbc CB.Cache+S.CB.SrcW
				sta DX
				iny
				lda (ZPpOBJ),y
				sbc CB.Cache+S.CB.SrcW+1

				lsr
				ror DX
				sta DX+1

				lda DX

			and PREFS.BestViewedX

				sta CB.Cache+S.CB.X1
				lda DX+1
				sta CB.Cache+S.CB.X1+1

				lda DX
				clc
				adc CB.Cache+S.CB.SrcW

			ora PREFS.nBestViewedX

				sta CB.Cache+S.CB.X2

				lda DX+1
				adc CB.Cache+S.CB.SrcW+1
				sta CB.Cache+S.CB.X2+1

				>DECW CB.Cache+S.CB.Y2

				lda #C.WHITE
				jsr CB.FillRectMonoA
				jsr CB.WriteW

				lda PREFS.XMargin
				jsr CB.AddA2X1

				ldy #S.WND.pTITLE
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYSB
				jmp DRAW.TextYAXW
*--------------------------------------
WND.DrawMenuBar	ldy #S.WND.pMBAR
				lda (ZPpOBJ),y
				sta R1
				iny
				lda (ZPpOBJ),y
				sta R1+1

				ldy #S.WND.InnerX1+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.X1
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldy #S.WND.MBarY1+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.Y1
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldy #S.WND.MBarY2+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.Y2
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldy #S.WND.BarW+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				ldy #S.OBJ.W
				sta (R1),y
				iny
				txa
				sta (R1),y

				ldx ZPpOBJ+1
				phx
				ldx ZPpOBJ
				phx

				>LDAX R1
				jsr OBJ.PaintAX

				plx
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				rts
*--------------------------------------
WND.DrawStatusBar
				jsr CB.BorderLineH

				ldy #S.WND.InnerX1			0 or 1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				ldy	#S.WND.SBarY1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1+1

				ldy	#S.WND.SBarY2
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y2
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y2+1

				ldy #S.WND.BarW+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				jsr CB.WriteW
				bcs .9

				lda PREFS.SBarColor
				jsr CB.FillRectMonoA

				>INCW CB.Cache+S.CB.Y1

				jsr CB.WriteW
				bcs .9

				>INCW CB.Cache+S.CB.Y1

				lda #4
				jsr CB.AddA2X1

				ldy #S.WND.pSTATUS
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				ldx #FON.ID.SYS
				jmp DRAW.TextYAXW

.9				rts
*--------------------------------------
WND.CheckOver	>LDAX WND.Screen

WND.CheckOverAX	>STAX ZPpOBJ
				jsr MSG.InOBJ
				bcs .99

				ldy #S.OBJ.X1
				jsr MSG.ToLocalY

				lda (ZPpOBJ)
				beq .10					WND

				>LDAX ZPpOBJ
				jsr OBJ.EnterAX
				bcs .99					Error

				jsr WND.CheckOverN
				bcc .99

				ldy #S.WND.hPTR
				lda (ZPpOBJ),y
				jsr PTR.SetA

				clc

				rts
*--------------------------------------
.10				ldy #S.OBJ.F
				lda (ZPpOBJ),y
				and #S.WND.F.STACKED
				beq .20

				ldy	#S.OBJ.S
				lda (ZPpOBJ),y
				and #S.OBJ.S.ACTIVE
				bne .20

				lda	MSG+S.MSG.S
				bit #S.XY.S.CLK
				sec
				beq .99

				jsr WND.Bring2Top.I		will become active...
*--------------------------------------
.20				ldy #S.OBJ.F+1
				lda (ZPpOBJ),y
				bpl .30					#S.OBJ.F.BORDER

				jsr WND.CheckOverB
				bcs .30

				jmp MOU.ResizeWND
*--------------------------------------
.30				jsr WND.CheckOverW		TitleBar, Menubar... ?
				bcc .99					yes, all done.

				ldy #S.WND.InnerX1		in innerW...
				jsr MSG.ToLocalY

				jsr WND.CheckOverC
				bcc .99

.40				jsr WND.CheckOverN		go check pNext
				bcc .99

				ldy #S.WND.hPTR
				lda (ZPpOBJ),y
				jsr PTR.SetA

				clc

.99				rts
*--------------------------------------
WND.CheckOverB	stz IY

				lda MSG+S.MSG.X1
				eor MSG+S.MSG.X1+1
				bne .1

				lda #PTR.T.RESIZEX
				sta IY

.1				lda MSG+S.MSG.Y1
				eor MSG+S.MSG.Y1+1
				bne .3

				lda IY
				beq .2

				lda #PTR.T.RESIZEXY1
				jmp PTR.SetA

.2				lda #PTR.T.RESIZEY
				sta IY

.3				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sec
				sbc #1
				eor MSG+S.MSG.X1
				bne .5

				iny
				lda (ZPpOBJ),y
				sbc #0
				eor MSG+S.MSG.X1+1
				bne .5

				lda IY
				beq .4

				lda #PTR.T.RESIZEXY2
				jmp PTR.SetA

.4				lda #PTR.T.RESIZEX
				sta IY

.5				ldy #S.OBJ.H
				lda (ZPpOBJ),y
				sec
				sbc #1
				eor MSG+S.MSG.Y1
				bne .7

				iny
				lda (ZPpOBJ),y
				sbc #0
				eor MSG+S.MSG.Y1+1
				bne .7

				lda #PTR.T.RESIZEXY1
.6				jmp PTR.SetA

.7				lda IY
				bne .6

.9				sec
				rts
*--------------------------------------
WND.CheckOverW	jsr WND.bHasTBar
				beq .3

				ldy #S.WND.TBarY2
				jsr WND.CmpY
				bcc .3					not in TitleBAR

				jmp MOU.MoveWND

				rts
*--------------------------------------
.3				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				beq .4

				ldy	#S.WND.MBarY2
				jsr WND.CmpY
				bcc .4					not in MenuBAR

				lda #PTR.T.ARROW
				jsr PTR.SetA

				ldy #S.WND.pMBAR+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y
				jmp OBJ.EnterAX
*--------------------------------------
.4				ldy #S.WND.pSTATUS+1
				lda (ZPpOBJ),y
				beq .9

				ldy #S.WND.InnerY2
				jsr WND.CmpY
*			bcc .8					not in InnerW, status bar

*			sec
				rts

.9				sec
.99				rts
*--------------------------------------
WND.CheckOverC	jsr WND.GetpChild
				bcs .99

				ldy ZPpOBJ+1			Save Base WND
				phy
				ldy ZPpOBJ
				phy

.1				jsr WND.CheckOverAX
				bcc .2

				jsr WND.GetpChild
				bcc .1

				plx						Back To Base Object
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

*				sec

				rts

.2				plx						Discard Base Object
				plx

.99				rts
*--------------------------------------
WND.CheckOverN	jsr OBJ.GetpNext
				bcs .99

				ldy ZPpOBJ+1
				phy
				ldy ZPpOBJ
				phy

.1				>STAX ZPpOBJ
				jsr MSG.InOBJ
				bcc .2

				>LDAX ZPpOBJ
				jsr OBJ.LeaveAX
				bcs .98					Error

				jsr OBJ.GetpNext
				bcc .1

				plx						Back To Base Object
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

*				sec

				rts

.2				plx						Discard Base Object
				plx

				ldy #S.OBJ.X1
				jsr MSG.ToLocalY

				ldy #S.WND.hPTR
				lda (ZPpOBJ),y
				jsr PTR.SetA

				>LDAX ZPpOBJ
				jsr OBJ.EnterAX
				bcs .99					Error

				ldy ZPpOBJ+1			Save this object
				phy
				ldy ZPpOBJ
				phy

.3				jsr OBJ.GetpNext
				bcs .4

				>STAX ZPpOBJ
				jsr OBJ.LeaveAX
				bcc .3

				bcs .98

.4				plx						Back To Saved Object
				stx ZPpOBJ
				plx
				stx ZPpOBJ+1

				clc

				rts

.98				plx
				plx

.99				rts
*--------------------------------------
WND.bHasTBar	ldy #S.WND.pTITLE+1
				lda (ZPpOBJ),y
				bne .8

				ldy #S.OBJ.S
				lda (ZPpOBJ),y
				and #S.WND.F.MOVE+S.WND.F.CLOSE+S.WND.F.MIN+S.WND.F.MAX

.8				rts
*--------------------------------------
WND.CmpY		lda (ZPpOBJ),y
				cmp MSG+S.MSG.Y1
				iny
				lda (ZPpOBJ),y
				sbc MSG+S.MSG.Y1+1
				rts
*--------------------------------------
WND.GetpChild	ldy #S.WND.pChilds+1
				lda (ZPpOBJ),y
				beq .9

				tax
				dey
				lda (ZPpOBJ),y

				clc
				rts

.9				sec
				rts
*--------------------------------------
WND.fEnter
WND.fLeave
				clc
				rts
*--------------------------------------
* default inner window fPaint in Y,A
* ZPpOBJ = pWND
*--------------------------------------
WND.fPaint		lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET+S.CB.OP.COLOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				ldy #S.OBJ.BGCOLOR+1
				lda (ZPpOBJ),y
				bne .10

				stz CB.Cache+S.CB.pPat+1

				dey
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.COLOR
				beq .1

				inc
				beq .1

				lda #S.CB.M.C16
				sta CB.Cache+S.CB.M
				bra .1

.10				sta CB.Cache+S.CB.pPat+1
				dey
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.pPat

.1				jsr CB.ZeroX1Y1
				jsr CB.SetInnerX2Y2

				jsr CB.WriteInnerW

				bcs .9
*--------------------------------------
				ldy #S.WND.BGBM
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.hSrc
				iny
				ora (ZPpOBJ),y
				beq .8

				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.hSrc+1

				jsr CB.ZeroSrcXSrcY

				>LDYA CB.Cache+S.CB.hSrc

				jsr BM.GetProps
				bcs .9

				ldx #3

.2				lda TEXTBUF+S.BM.W,x
				sta CB.Cache+S.CB.SrcW,x
				dex
				bpl .2

				lda TEXTBUF+S.BM.F

.3				lsr
				bcs .4

				asl CB.Cache+S.CB.SrcW
				rol CB.Cache+S.CB.SrcW+1
				bra .3

.4				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD

				jmp CB.WriteInnerW

.8

*				clc

.9				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.wnd
LOAD usr/src/lib/libgui.s
ASM
