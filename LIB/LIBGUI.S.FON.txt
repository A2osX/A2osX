NEW
  AUTO 3,1
*--------------------------------------
FON.Init		>LDYA L.fSYS
				>KAPI LoadBuf
				bcs .9

				>STYA hSYSFON

				>LDYA L.fSYSB
				>KAPI LoadBuf
				bcs .9

				>STYA hSYSFONB

				>LDYA L.fFIXED
				>KAPI LoadBuf
				bcs .9

				>STYA hFIXFON

				lda #7
				sta SYSFON.H
				inc
				sta SYSFON.Hp1
				inc
				sta SYSFON.Hp2

.9				rts
*--------------------------------------
* guiGetFontProperties(pFON, &S.FONBuf)
*--------------------------------------
FON.GetProps	>GETSTKW 2
				cpx #0
				bne .10

				tay
				>LDAX hSYSFON-2,y

.10				>STAX R1				pFON

				>GETSTKW
				>STAX R2				S.FONbuf

				>SS
				>PUSHW R1				hBuf
				>PUSHWZ					start
				>PUSHWI S.FON			len
				>PUSHW R2				dst
				>KAPI GetBufData
				>SR
				rts
*--------------------------------------
* guiGetTextWidth(pFON, pTXT)
*--------------------------------------
FON.GetTextW	>GETSTKW 2				pFON

				cpx #0
				bne .1

				tay
				>LDAX hSYSFON-2,y

.1				>STAX CB.Cache+S.CB.hFont

				>GETSTKW
				>STAX CB.Cache+S.CB.pTxt
				bra FON.GetTextW.1

FON.GetTextWYAX	>STYA CB.Cache+S.CB.pTxt

				>LDYA hSYSFON-2,x
				>STYA CB.Cache+S.CB.hFont

FON.GetTextW.1	lda #S.CB.CMD.GETTEXTW
				sta CB.Cache+S.CB.CMD

				jsr CB.Write0
				bcs .9

				>STYA CB.Cache+S.CB.SrcW

.9				rts
*--------------------------------------
* guiGetTextSize(pFON, pTXT, &RECT)
*--------------------------------------
FON.GetTextSize	>GETSTKW 4				pFON

				cpx #0
				bne .1

				tay
				>LDAX hSYSFON-2,y

.1				>STAX CB.Cache+S.CB.hSrc

				>GETSTKW 2
				jsr FON.GetTextSizeAX
				bcs .9




.9				rts
*--------------------------------------
FON.GetTextSizeAX
				>STAX ZPPtr1

				lda #S.CB.CMD.GETTEXTW
				sta CB.Cache+S.CB.CMD

				jsr FON.GetBuf
				bcs .9

				stz DX
				stz DX+1
				stz DY
				stz DY+1

.1				jsr FON.GetLine
				beq .8

				jsr CB.Write0
				bcs .9

				ldx CB.Cache+S.CB.SrcW
				cpx DX
				lda CB.Cache+S.CB.SrcW+1
				tay
				sbc DX+1
				bcc .2

				stx DX
				sty DX+1

.2				lda CB.Cache+S.CB.SrcH
				clc
				adc DY
				sta DY

				lda CB.Cache+S.CB.SrcH+1
				adc DY+1
				sta DY+1

				lda (ZPPtr1)
				bne .1

.8				>LDYA ZPPtr2
				>LIBC Free

.9				rts
*--------------------------------------
FON.DrawTextML	>STYA ZPPtr1

				lda #S.CB.CMD.DRAWTEXT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.XOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				jsr CB.ZeroSrcXSrcY

				jsr FON.GetBuf
				bcs .9

.1				jsr FON.GetLine
				beq .8

				>LDYA ZPPtr1
				>STYA CB.Cache+S.CB.pSrc

				jsr CB.WriteInnerW
				bcs .9

				lda CB.Cache+S.CB.SrcH
				clc
				adc CB.Cache+S.CB.Y1
				sta CB.Cache+S.CB.Y1
				bcc .1

				inc CB.Cache+S.CB.Y1+1

				bra .1

.8				>LDYA ZPPtr2
				>LIBC Free

.9				rts
*--------------------------------------
FON.GetBuf		>LDYAI 256
				>LIBC Malloc
				bcs .9

				>STYA ZPPtr2
				>STYA CB.Cache+S.CB.pTxt

.9				rts
*--------------------------------------
FON.GetLine		ldy #0

.1				lda (ZPPtr1)
				beq .8

.2				cmp #C.SPACE
				bcs .3

				eor #C.CR
				beq .7

				bne .4

.3				sta (ZPPtr2),y
				iny

.4				inc ZPPtr1
				bne .1

				inc ZPPtr1+1
				bra .1

.7				sta (ZPPtr2),y

				>INCW ZPPtr1

				lda (ZPPtr1)
				eor #C.LF
				bne .8

				>INCW ZPPtr1			skip LF if CR/LF

.8				tya						Y = line length, set Z flag if 0

				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.fon
LOAD usr/src/lib/libgui.s
ASM
