NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* CBOX.New
* (pStack)+8 pParent
* (pStack)+6 X1,Y1
* (pStack)+4 HK,EV
* (pStack)+2 pLabel
* (pStack)+0 pVar
*--------------------------------------
CBOX.New		lda #S.OBJ.T.CBOX
				jsr OBJ.Create
				bcs .99

				>GETSTKW 8
				jsr OBJ.SetParent

				>GETSTKW 6
				jsr OBJ.SetX1Y1

				>GETSTKW 4
				ldy #S.CBOX.EV
				sta (ZPpObj),y
				txa
				ldy #S.CBOX.HK
				sta (ZPpObj),y

				>GETSTKW 2
				ldy #S.CBOX.pLabel
				sta (ZPpObj),y
				sta ZPPtr1
				txa
				iny
				sta (ZPpObj),y
				sta ZPPtr1+1

				>GETSTKW
				ldy #S.OBJ.pVar
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				ldy #$ff

.1				iny
				lda (ZPPtr1),y
				bne .1

				tya
				inc
				inc
				inc
				inc
				inc
				ldy #S.OBJ.W
				sta (ZPpObj),y

				ldy #S.OBJ.H
				lda #1
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj

*				clc

.99				rts
*--------------------------------------
* (pStack) = Event
*--------------------------------------
CBOX.Run		ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .9

				lda (pStack)			Event
				ldy #S.CBOX.HK
				cmp (ZPpObj),y
				beq .8

				cmp #C.SPACE
				bne .9

.8				ldy #S.OBJ.pVar
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

				lda (ZPPtr1)
				eor #$ff
				sta (ZPPtr1)

				jsr OBJ.Draw.Body
				bcs .99
*			>DEBUG
				ldy #S.CBOX.EV
				lda (ZPpObj),y
*				clc
				rts

.9				lda #0
				sec
.99				rts
*--------------------------------------
CBOX.Draw		ldx #'<'

				ldy #S.OBJ.S
				lda (ZPpObj),y
				php
				bpl .10

				ldx #'['

.10				txa
				jsr LB.COut

				ldy #S.OBJ.pVar
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

				ldx #C.SPACE

				lda (ZPPtr1)
				bpl .11

				ldx #'X'

.11				txa
				jsr LB.COut

				ldx #'>'

				plp
				bpl .12

				ldx #']'

.12				txa
				jsr LB.COut

				lda #C.SPACE
				jsr LB.COut

				ldy #S.BUT.pLabel
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

.1				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				dex
				dex
				dex
				dex

				ldy #0

.2				lda (ZPPtr1),y
				beq .3

				jsr LB.COut
				iny
				dex
				bne .2

.3				txa
				beq .8

				bra .6

.5				ldy #S.OBJ.InnerW
				lda (ZPpObj),y

				tax

.6				lda #C.SPACE

.7				jsr LB.COut
				dex
				bne .7

.8				clc
				rts
*--------------------------------------
CBOX.Activate
*--------------------------------------
CBOX.Deactivate	jmp OBJ.Draw.Body
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.cbox
LOAD usr/src/lib/libtui.s
ASM
