NEW
  AUTO 3,1
*--------------------------------------
DRAW.Borders	jsr CB.BorderLineH
* top 0,0,X2,0
				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1
				stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1

				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				stz CB.Cache+S.CB.Y2
				stz CB.Cache+S.CB.Y2+1
				jsr CB.WriteW
				bcs .9

* bottom 0,Y2,X2,Y2
				ldy #S.OBJ.H+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.Y1
				>STAX CB.Cache+S.CB.Y2
				jsr CB.WriteW
				bcs .9

* left 0,0,0,Y2
				lda #S.CB.CMD.VLINE
				sta CB.Cache+S.CB.CMD

				stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1
				stz CB.Cache+S.CB.X2
				stz CB.Cache+S.CB.X2+1
				jsr CB.WriteW
				bcs .9

* right X2,0,X2,Y2
				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X1
				>STAX CB.Cache+S.CB.X2

				jmp CB.WriteW

.9				rts
*--------------------------------------
* drawline(int pWND, bOP, bCOLOR, iX1, iY1, iX2, iX2)
*--------------------------------------
DRAW.Line		jsr CB.GetY2X2Y1X1

				>GETSTKB 8
				sta CB.Cache+S.CB.COLOR
				>GETSTKB 9
				sta CB.Cache+S.CB.OP

				>GETSTKW 10
				>STAX ZPpWND

*				jsr WND.GetInnerInClip

*				jsr CB.ToGlobal

				lda CB.Cache+S.CB.X2
				sec
				sbc CB.Cache+S.CB.X1
				sta DX
				lda CB.Cache+S.CB.X2+1
				sbc CB.Cache+S.CB.X1+1
				sta DX+1				DX = X2 - X1

				bmi .1					DX < 0....

				ora DX
				bne .2					DX > 0...

				ldx CB.Cache+S.CB.Y2
				cpx CB.Cache+S.CB.Y1
				lda CB.Cache+S.CB.Y2+1
				sbc CB.Cache+S.CB.Y1+1
				bcs .17

				>SWAP16 CB.Cache+S.CB.Y1,CB.Cache+S.CB.Y2

.17				lda #S.CB.CMD.VLINE		DX=0 : VLINE Y1,Y2 At X1
				bra .20
*--------------------------------------
.1				>SWAP16 CB.Cache+S.CB.X1,CB.Cache+S.CB.X2
				>SWAP16 CB.Cache+S.CB.Y1,CB.Cache+S.CB.Y2

				>NOT16 DX				swap X1/X2, Y1/Y2, set DX=-DX (DX positive)

.2				lda #1
				sta	IY

				lda CB.Cache+S.CB.Y2
				sec
				sbc CB.Cache+S.CB.Y1
				sta DY
				lda CB.Cache+S.CB.Y2+1
				sbc CB.Cache+S.CB.Y1+1
				sta DY+1				DY = Y2 - Y1

				bmi .3					DY < 0 ...

				ora DY
				bne .4

				lda #S.CB.CMD.HLINE		DY=0 : HLINE X1,X2 At Y1

.20				sta CB.Cache+S.CB.CMD

				jmp CB.WriteInnerW

.8				clc
				rts
*--------------------------------------
.3				>NOT16 DY				set DY=-DY
				lda #$ff
				sta IY					set IY=-1

.4
*				jsr CLIP.Line
*				bcs .8

				jsr PTR.Disable

				lda #S.CB.CMD.SETPIXEL
				sta CB.Cache+S.CB.CMD
				jsr CB.WriteInnerW

				lda DX
				sec
				sbc DY
				tax
				lda DX+1
				sbc DY+1
				bmi DRAW.Line.IncY		DY is greater than DX, we will increase on Y axis

				txa
				beq DRAW.Line.IncXY		DX=DY, go inc X & Y

				jmp DRAW.Line.IncX		DY is lower than DX, we will increase on X axis
*--------------------------------------
DRAW.Line.IncXY	lda DX					DX=DY
				eor #$ff
				sta Counter
				lda DX+1
				eor #$ff
				sta Counter+1

.1				inc Counter
				bne .2

				inc Counter+1
				beq .8

.2				inc CB.Cache+S.CB.X1
				bne .3

				inc CB.Cache+S.CB.X1+1

.3				lda CB.Cache+S.CB.Y1	Y1=Y1+IY
				clc
				adc IY
				sta CB.Cache+S.CB.Y1
				jsr CB.WriteInnerW
				bra .1

.8				jmp PTR.Enable
*--------------------------------------
DRAW.Line.IncY	lda DY
				eor #$ff
				sta Counter
				lda DY+1
				eor #$ff
				sta Counter+1

				lda DX					IE=2*DX
				asl
				sta IE
				pha
				lda DX+1
				rol
				sta IE+1
				tax

				pla						D=IE-DY
				sec
				sbc DY
				sta D
				txa
				sbc DY+1
				sta D+1
				lda DX					INE=2*(DX-DY)
				sec
				sbc DY
				pha
				lda DX+1
				sbc DY+1
				tax
				pla
				asl
				sta INE
				txa
				rol
				sta INE+1

.1				inc Counter
				bne .2

				inc Counter+1
				beq .8

.2				lda D+1					IF D < 0 ...
				bmi .4

				lda D					D > 0 : D=D+INE
				clc
				adc INE
				sta D
				lda D+1
				adc INE+1
				sta D+1
				inc CB.Cache+S.CB.X1	X1=X1+1
				bne .5

				inc CB.Cache+S.CB.X1+1
				bra .5

.4				lda D					D > 0 : D=D+IE
				clc
				adc IE
				sta D
				lda D+1
				adc IE+1
				sta D+1

.5				lda CB.Cache+S.CB.Y1	Y1=Y1+IY
				clc
				adc IY
				sta CB.Cache+S.CB.Y1
				jsr CB.WriteInnerW
				bra .1

.8				jmp PTR.Enable
*--------------------------------------
DRAW.Line.IncX	lda DX
				eor #$ff
				sta Counter
				lda DX+1
				eor #$ff
				sta Counter+1

				lda DY					IE=2*DY
				asl
				sta IE
				pha
*				lda DY+1
				lda #0
				rol
				sta IE+1
				tax
				pla						D=IE-DX
				sec
				sbc DX
				sta D
				txa
				sbc DX+1
				sta D+1
				lda DY					INE=2*(DY-DX)
				sec
				sbc DX
				pha
*				lda DY+1
				lda #0
				sbc DX+1
				tax
				pla
				asl
				sta INE
				txa
				rol
				sta INE+1

.1				inc Counter
				bne .2

				inc Counter+1
				beq .8

.2				lda D+1					ID D < 0 ....
				bmi .4

				lda D					D > 0 : D=D+INE
				clc
				adc INE
				sta D
				lda D+1
				adc INE+1
				sta D+1
				lda CB.Cache+S.CB.Y1	Y1=Y1+IY
				clc
				adc IY
				sta CB.Cache+S.CB.Y1
				bra .5

.4				lda D					D < 0 : D=D+IE
				clc
				adc IE
				sta D
				lda D+1
				adc IE+1
				sta D+1

.5				inc CB.Cache+S.CB.X1	X1=X1+1
				bne .6

				inc CB.Cache+S.CB.X1+1

.6				jsr CB.WriteInnerW
				bra .1

.8				jmp PTR.Enable
*--------------------------------------
* fill (pWND, bOP, bCOLOR, iX1, iY1, iX2, iY2)
*--------------------------------------
DRAW.Fill		jsr CB.GetY2X2Y1X1

				>GETSTKB 8
				sta CB.Cache+S.CB.COLOR
				>GETSTKB 9
				sta CB.Cache+S.CB.OP

				>GETSTKW 10
				>STAX ZPpWND

				lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				stz CB.Cache+S.CB.pPat+1

				jmp CB.WriteInnerW
*--------------------------------------
* drawBitmap (pWND, bOP, bM, pBM, iX, iY)
*--------------------------------------
DRAW.BitMap		jsr CB.GetY1X1

				>GETSTKW 4
				cpx #0
				bne .1

				tay
				>LDAX L.BMs,y

.1				>STAX CB.Cache+S.CB.pSrc
				>STAX ZPpBM

				>GETSTKB 6
				sta CB.Cache+S.CB.M
				>GETSTKB 7
				sta CB.Cache+S.CB.OP

				>GETSTKW 8
				>STAX ZPpWND

				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD

				stz CB.Cache+S.CB.hSrc+1

				jsr CB.ZeroSrcXSrcY

				ldy #S.BM.W+3
				ldx #3

.2				lda (ZPpBM),y
				sta CB.Cache+S.CB.SrcW,x
				dey
				dex
				bpl .2

				jmp CB.WriteInnerW
*--------------------------------------
* drawtext (pWND, bOP, pFONT, iX1, iY1, pTEXT)
*--------------------------------------
DRAW.Text		>GETSTKW
				>STAX CB.Cache+S.CB.pTxt

				>GETSTKW 2
				>STAX CB.Cache+S.CB.Y1

				>GETSTKW 4
				>STAX CB.Cache+S.CB.X1

				>GETSTKW 6
				cpx #0
				bne .1

				tay
				>LDAX hSYSFON-2,y

.1				>STAX CB.Cache+S.CB.hSrc

				>GETSTKB 8
				sta CB.Cache+S.CB.OP

				>GETSTKW 9
				>STAX ZPpWND

				lda #S.CB.CMD.DRAWTEXT
				sta CB.Cache+S.CB.CMD

*				lda #S.CB.OP.XOR
*				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				lda #C.BLACK
				sta CB.Cache+S.CB.COLOR

				jmp CB.WriteInnerW
*--------------------------------------
DRAW.TextYAXW	>STYA CB.Cache+S.CB.pTxt
				>LDYA hSYSFON-2,x
				>STYA CB.Cache+S.CB.hFont

				lda #S.CB.CMD.DRAWTEXT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.XOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				lda #C.BLACK
				sta CB.Cache+S.CB.COLOR

				jsr CB.ZeroSrcXSrcY

				jmp CB.WriteW
*--------------------------------------
DRAW.TextYAXIW	>STYA CB.Cache+S.CB.pTxt
				>LDYA hSYSFON-2,x
				>STYA CB.Cache+S.CB.hFont

				lda #S.CB.CMD.DRAWTEXT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.XOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				lda #C.BLACK
				sta CB.Cache+S.CB.COLOR

				jsr CB.ZeroSrcXSrcY

				jmp CB.WriteInnerW
*--------------------------------------
DRAW.BordersIW	jsr CB.BorderLineH
* top 0,0,X2,0
				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1
				stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1

				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X2

				stz CB.Cache+S.CB.Y2
				stz CB.Cache+S.CB.Y2+1
				jsr CB.WriteInnerW
				bcs .9

* bottom 0,Y2,X2,Y2
				ldy #S.OBJ.H+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.Y1
				>STAX CB.Cache+S.CB.Y2
				jsr CB.WriteInnerW
				bcs .9

* left 0,0,0,Y2
				lda #S.CB.CMD.VLINE
				sta CB.Cache+S.CB.CMD

				stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1
				stz CB.Cache+S.CB.X2
				stz CB.Cache+S.CB.X2+1
				jsr CB.WriteInnerW
				bcs .9

* right X2,0,X2,Y2
				ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				>STAX CB.Cache+S.CB.X1
				>STAX CB.Cache+S.CB.X2

				jmp CB.WriteInnerW

.9				rts
*--------------------------------------
DRAW.InnerA		sta CB.Cache+S.CB.COLOR

				stz CB.Cache+S.CB.pPat+1

				lda #S.CB.CMD.FILLRECT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SET
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				lda #1
				sta CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1
				sta CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1

				ldy #S.OBJ.W
				sec
				lda (ZPpOBJ),y
				sbc #2
				sta CB.Cache+S.CB.X2
				iny
				lda (ZPpOBJ),y
				sbc /2
				sta CB.Cache+S.CB.X2+1

				ldy #S.OBJ.H
				sec
				lda (ZPpOBJ),y
				sbc #2
				sta CB.Cache+S.CB.Y2
				iny
				lda (ZPpOBJ),y
				sbc /2
				sta CB.Cache+S.CB.Y2+1

				jmp CB.WriteInnerW
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.draw
LOAD usr/src/lib/libgui.s
ASM
