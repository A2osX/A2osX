NEW
  AUTO 3,1
*--------------------------------------
* Y,A = MBAR definition
*--------------------------------------
MBAR.New		>STAX ZPPtr1			MBAR definition
				>STAX ZPPtr2			for pass #2

				jsr MBAR.GetSize
				>LIBC Malloc
				bcs .9

				>STYA ZPpOBJ

				lda #S.OBJ.T.MBAR
				sta (ZPpOBJ)			S.OBJ.T

				lda #0

				ldy #S.MBAR.Items-1

.1				sta (ZPpOBJ),y
				dey
				bne .1

				>LDAX ZPpWND
				jsr OBJ.SetParentAX

				jsr MBAR.GetData

				>LDYA ZPpOBJ

				clc
.9				rts
*--------------------------------------
* ZPPtr1 = defintion
*--------------------------------------
MBAR.GetSize	lda #S.MBAR.Items+1		Ending\0
				sta Counter
				stz Counter+1

.1				lda (ZPPtr1)
				beq .8

				ldy #$ff

.2				iny
				lda (ZPPtr1),y
				bne .2

				tya

				sec						text Ending\0

				inc
				inc						skip pMENU
				jsr SkipAPtr1

				lda #S.MBITEM
				clc
				adc Counter
				sta Counter
				bcc .1

				inc Counter+1
				bra .1

.8				>LDYA Counter
				rts
*--------------------------------------
* ZPPtr2 = defintion
* ZPpOBJ = DstMBar
* ZPPtr1 = DstMBItem
*--------------------------------------
MBAR.GetData	jsr MBAR.GetMBItems

.1				lda (ZPPtr2)
				beq .8

				lda #S.MBITEM.T.SUBMENU
				jsr MBAR.AddByte

				lda #0					S.MBITEM.S
				jsr MBAR.AddByte

				>LDYA ZPPtr2			pTEXT
				ldx #FON.ID.SYSB
				jsr FON.GetTextWYAX

				lda PREFS.XMargin
				asl						x2
				clc
				adc CB.Cache+S.CB.SrcW
				tay

				lda #0
				adc CB.Cache+S.CB.SrcW+1
				jsr MBAR.AddWord		S.MBITEM.W

				>LDYA ZPPtr2			pTEXT
				jsr MBAR.AddWord

				jsr SkipStrZPtr2		skip "TEXT\0"

				lda (ZPPtr2)
				jsr MBAR.AddByte		pMENU

				ldy #1
				lda (ZPPtr2),y
				jsr MBAR.AddByte		pMENU+1

				lda #2
				jsr SkipAPtr2			stkip pMENU
				bra .1

.8				sta (ZPPtr1)
				rts
*--------------------------------------
MBAR.AddWord	pha

				tya
				jsr MBAR.AddByte
				pla

MBAR.AddByte	sta (ZPPtr1)
				inc ZPPtr1
				bne .8

				inc ZPPtr1+1

.8				rts
*--------------------------------------
* ZPpWND parent WND
* MSG.X1, MSG.Y1
*--------------------------------------
MBAR.fEnter		stz ZPPtr2
				stz ZPPtr2+1

				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				jsr MBAR.GetMBItems

.1				lda (ZPPtr1)
				beq .3

				jsr MBAR.SetX2			set X2 = X1 + W - 1

				lda MSG+S.MSG.X1
				cmp CB.Cache+S.CB.X1
				lda MSG+S.MSG.X1+1
				sbc CB.Cache+S.CB.X1+1
				bcc .2

				lda CB.Cache+S.CB.X2
				cmp MSG+S.MSG.X1
				lda CB.Cache+S.CB.X2+1
				sbc MSG+S.MSG.X1+1
				bcc .2

				ldy #S.MBITEM.S			in RECT....
				lda (ZPPtr1),y
				bmi .8					Already Selected, nothing to do

				>LDYA ZPPtr1
				>STYA ZPPtr2			save MBITEM to activate

.2				jsr MBAR.NextMBItem
				bra .1

.3				lda ZPPtr2
				ora ZPPtr2+1
				bne MBAR.Set

.8				clc
.9				rts
*--------------------------------------
MBAR.fLeave		clc
				rts
*--------------------------------------
* ZPpOBJ = MBAR
* ZPPtr2 = Item to activate
*--------------------------------------
MBAR.Set		jsr MENU.DestroyAll

				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				jsr MBAR.GetMBItems

.4				lda (ZPPtr1)
				beq .7

				jsr MBAR.SetX2			set X2 = X1 + W - 1

				ldy #S.MBITEM.S
				lda (ZPPtr1),y
				bmi .5

				ldx ZPPtr1
				cpx ZPPtr2
				bne .6

				ldx ZPPtr1+1
				cpx ZPPtr2+1
				bne .6

				ldx CB.Cache+S.CB.X1
				stx Counter
				ldx CB.Cache+S.CB.X1+1
				stx Counter+1

.5				eor #S.MBITEM.S.SELECTED
				sta (ZPPtr1),y

				jsr MBAR.DrawMBItem
				bcs .9

.6				jsr MBAR.NextMBItem		set X1 = X2+1
				bra .4
*--------------------------------------
.7				>LDYA Counter
				>STYA DX

				stz DY
				stz DY+1

				jsr OBJ.GetpWND

				ldy #S.MBITEM.pMENU+1
				lda (ZPPtr2),y
				tax
				dey
				lda (ZPPtr2),y

				jsr MENU.NewAX
				bcs .9

				jmp MENU.fPaint


.9				rts
*--------------------------------------
* ZPpWND = parent WND
*--------------------------------------
MBAR.fPaint		>STYA ZPpOBJ			pMBAR

				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				jsr MBAR.GetMBItems

.1				lda (ZPPtr1)
				beq .8

				jsr MBAR.SetX2			set X2 = X1 + W - 1

				jsr MBAR.DrawMBItem
				bcs .9

				jsr MBAR.NextMBItem
				bra .1
*--------------------------------------
.8				jsr MBAR.X2p12X1

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X2
				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.X2+1

				jsr MBAR.SetY1Y2

				lda PREFS.MBarColor
				jsr CB.FillRectMonoA

				jsr CB.WriteP
				bcs .9

				jsr CB.BorderLineH

				stz CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				lda SYSFON.Hp1
				sta CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1
				jmp CB.WriteP

.9				rts
*--------------------------------------
MBAR.DrawMBItem	ldy #S.MBITEM.S
				lda (ZPPtr1),y
				asl

				lda PREFS.MBarColor
				bcc .1

				eor #$ff

.1				jsr CB.FillRectMonoA

				jsr MBAR.SetY1Y2

				jsr CB.WriteP
				bcs .9

				lda PREFS.XMargin
				jsr CB.AddA2X1

				>INCW CB.Cache+S.CB.Y1

				ldy #S.MBITEM.pTEXT
				lda (ZPPtr1),y
				pha
				iny
				lda (ZPPtr1),y
				ply
				ldx #FON.ID.SYSB

				>STYA CB.Cache+S.CB.pTxt
				>LDYA hSYSFON-2,x
				>STYA CB.Cache+S.CB.hFont

				lda #S.CB.CMD.DRAWTEXT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.XOR
				sta CB.Cache+S.CB.OP

				lda #S.CB.M.MONO
				sta CB.Cache+S.CB.M

				lda #C.BLACK
				sta CB.Cache+S.CB.COLOR

				jsr CB.ZeroSrcXSrcY

				jmp CB.WriteP

.9				rts
*--------------------------------------
MBAR.GetMBItems	lda ZPpOBJ
				clc
				adc #S.MBAR.Items
				sta ZPPtr1

				lda ZPpOBJ+1
				adc /S.MBAR.Items
				sta ZPPtr1+1
				rts
*--------------------------------------
MBAR.NextMBItem	jsr MBAR.X2p12X1

				lda #S.MBITEM
				jmp SkipAPtr1
*--------------------------------------
MBAR.SetX2		ldy #S.MBITEM.W
				lda (ZPPtr1),y
				clc
				adc CB.Cache+S.CB.X1
				pha

				iny
				lda (ZPPtr1),y
				adc CB.Cache+S.CB.X1+1

				ply
				bne .1

				dec

.1				dey

				sty CB.Cache+S.CB.X2
				sta CB.Cache+S.CB.X2+1

				rts
*--------------------------------------
MBAR.X2p12X1	ldy CB.Cache+S.CB.X2
				lda CB.Cache+S.CB.X2+1
				iny
				bne .1

				inc

.1				sty CB.Cache+S.CB.X1
				sta CB.Cache+S.CB.X1+1
				rts
*--------------------------------------
MBAR.SetY1Y2	stz CB.Cache+S.CB.Y1
				stz CB.Cache+S.CB.Y1+1

				lda SYSFON.H
				sta CB.Cache+S.CB.Y2
				stz CB.Cache+S.CB.Y2+1
				rts


				clc
				ldy #S.OBJ.Y1
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1
				adc SYSFON.H
				sta CB.Cache+S.CB.Y2

				iny
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.Y1+1
				adc #0
				sta CB.Cache+S.CB.Y2+1
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.mbar
LOAD usr/src/lib/libgui.s
ASM
