NEW
  AUTO 3,1
*--------------------------------------
PTR.Reset		ldx #PTR.T.ARROW
				bra PTR.SetX
*--------------------------------------
PTR.Update		ldy hDevMouse
				beq .8

				lda PTR.T
				cmp #PTR.T.WAIT
				bcc .9

				inc
				inc
				cmp #PTR.T.WAIT+12
				bne PTR.SetA

				lda #PTR.T.WAIT
				bra PTR.SetA

.8				clc
.9				rts
*--------------------------------------
* ShowPtr(bT.PTR)
*--------------------------------------
PTR.Set			>GETSTKB

PTR.SetA		cmp PTR.T
				beq PTR.Disable.8

				pha

				jsr PTR.Disable

				plx

PTR.SetX		stx PTR.T

				lda L.PTRs,x
				sta CB.Ptr+S.CB.pSrc
				sta R1
				lda L.PTRs+1,x
				sta CB.Ptr+S.CB.pSrc+1
				sta R1+1

				ldy #S.BM.W+3
				ldx #3

.1				lda (R1),y
				sta PTR.WH,x
				dey
				dex
				bpl .1

				bra PTR.Enable
*--------------------------------------
PTR.SetRectYA	>STYA R1

				jsr PTR.Disable
				sec
				ror PTR.T

				ldy #7

.1				lda (R1),y
				sta PTR.Rect,y
				dey
				bpl .1

				bra PTR.Enable
*--------------------------------------
* SetPos DestX,DestY
*--------------------------------------
PTR.SetPos		jsr PTR.Disable

				>GETSTKW 2
				>STAX PTR.Pos+S.POINT.X

				>GETSTKW
				>STAX PTR.Pos+S.POINT.Y
*--------------------------------------
PTR.Enable		bit PTR.bVisible
				bmi PTR.Show.I

				clc
				rts
*--------------------------------------
PTR.Disable		bit PTR.bVisible
				bmi PTR.Hide.I

PTR.Disable.8	clc
				rts
*--------------------------------------
PTR.Hide		lda PTR.bVisible
				bpl PTR.Disable.8

				stz PTR.bVisible
*--------------------------------------
PTR.Hide.I		ldx PTR.T
				bpl .1

				jmp PTR.ShowRect.I

.1				lda #S.CB.OP.RESTORE
				jmp PTR.WriteA
*--------------------------------------
PTR.Show		lda PTR.bVisible
				bmi PTR.Disable.8

				dec PTR.bVisible
*--------------------------------------
PTR.Show.I		ldx PTR.T
				bpl .1

				jmp PTR.ShowRect.I

.1				lda #S.CB.OP.ORA+S.CB.OP.MASK+S.CB.OP.SAVE
*--------------------------------------
PTR.WriteA		sta CB.Ptr+S.CB.OP

				lda PTR.Pos+S.POINT.X
				sec
				sbc PTR.HotPoints,x
				sta CB.Ptr+S.CB.X1

				lda PTR.Pos+S.POINT.X+1
				sbc #0
				sta CB.Ptr+S.CB.X1+1

				lda PTR.Pos+S.POINT.Y
				sec
				sbc PTR.HotPoints+1,x
				sta CB.Ptr+S.CB.Y1

				lda PTR.Pos+S.POINT.Y+1
				sbc #0
				sta CB.Ptr+S.CB.Y1+1

				ldx PTR.WH+S.POINT.X+1
				stx CB.Ptr+S.CB.SrcW+1
				lda PTR.WH+S.POINT.X
				sta CB.Ptr+S.CB.SrcW
				bne .1

				dex

.1				dec

				clc
				adc CB.Ptr+S.CB.X1
				sta CB.Ptr+S.CB.X2
				txa
				adc CB.Ptr+S.CB.X1+1
				sta CB.Ptr+S.CB.X2+1

				ldx PTR.WH+S.POINT.Y+1
				stx CB.Ptr+S.CB.SrcH+1
				lda PTR.WH+S.POINT.Y
				sta CB.Ptr+S.CB.SrcH
				bne .2

				dex

.2				dec

				clc
				adc CB.Ptr+S.CB.Y1
				sta CB.Ptr+S.CB.Y2
				txa
				adc CB.Ptr+S.CB.Y1+1
				sta CB.Ptr+S.CB.Y2+1

PTR.Write		jsr PTR.Clip
				bcs .8

				>LDYA L.CB.Ptr
				>STYA IOCTL+S.IOCTL.BUFPTR

				lda #S.CB.pSrc+2
				sta IOCTL+S.IOCTL.BYTECNT
				stz IOCTL+S.IOCTL.BYTECNT+1

				>SS
				>PUSHW hDevGFX
				>PUSHBI IOCTL.WRITE
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR

.8				clc

				rts
*--------------------------------------
PTR.ShowRect.I	lda #S.CB.CMD.HLINE
				sta CB.Ptr+S.CB.CMD
				lda #S.CB.OP.XOR
				sta CB.Ptr+S.CB.OP
				lda #S.CB.M.MONO
				sta CB.Ptr+S.CB.M
				lda #C.WHITE
				sta CB.Ptr+S.CB.COLOR

				ldx #5					X1,Y1,X2

.1				lda PTR.Rect+S.RECT.X1,x
				sta CB.Ptr+S.CB.X1,x
				dex
				bpl .1

				jsr PTR.Write

				>LDYA PTR.Rect+S.RECT.Y2
				>STYA CB.Ptr+S.CB.Y1

				jsr PTR.Write

				lda #S.CB.CMD.VLINE
				sta CB.Ptr+S.CB.CMD

				>LDYA PTR.Rect+S.RECT.Y1
				>STYA CB.Ptr+S.CB.Y1
				>LDYA PTR.Rect+S.RECT.Y2
				>STYA CB.Ptr+S.CB.Y2

				>LDYA PTR.Rect+S.RECT.X1
				>STYA CB.Ptr+S.CB.X1

				jsr PTR.Write

				>LDYA PTR.Rect+S.RECT.X2
				>STYA CB.Ptr+S.CB.X1

				jmp PTR.Write
*--------------------------------------
PTR.Clip		lda CLIP.Screen+S.RECT.X1
				sec
				sbc CB.Ptr+S.CB.X1
				sta CLIP.TmpW

				lda CLIP.Screen+S.RECT.X1+1
				sbc CB.Ptr+S.CB.X1+1
				sta CLIP.TmpW+1
				bvc .1

				eor #$80

.1				bmi .2

				ldy #S.CB.X1
				jsr PTR.AddTmpW2CBPtrY

				ldy #S.CB.SrcX
				jsr PTR.AddTmpW2CBPtrY

				ldy #S.CB.SrcW
				jsr PTR.SubTmpW2CBPtrY
				bcc .99
*--------------------------------------
.2				lda CB.Ptr+S.CB.X2
				sec
				sbc CLIP.Screen+S.RECT.X2
				sta CLIP.TmpW

				lda CB.Ptr+S.CB.X2+1
				sbc CLIP.Screen+S.RECT.X2+1
				sta CLIP.TmpW+1
				bvc .3

				eor #$80

.3				bmi .4

				ldy #S.CB.SrcW
				jsr PTR.SubTmpW2CBPtrY
.99				bcc .9
*--------------------------------------
.4				lda CLIP.Screen+S.RECT.Y1
				sec
				sbc CB.Ptr+S.CB.Y1
				sta CLIP.TmpW

				lda CLIP.Screen+S.RECT.Y1+1
				sbc CB.Ptr+S.CB.Y1+1
				sta CLIP.TmpW+1
				bvc .5

				eor #$80

.5				bmi .6

				ldy #S.CB.Y1
				jsr PTR.AddTmpW2CBPtrY

				ldy #S.CB.SrcY
				jsr PTR.AddTmpW2CBPtrY

				ldy #S.CB.SrcH
				jsr PTR.SubTmpW2CBPtrY
				bcc .9
*--------------------------------------
.6				lda CB.Ptr+S.CB.Y2
				sec
				sbc CLIP.Screen+S.RECT.Y2
				sta CLIP.TmpW

				lda CB.Ptr+S.CB.Y2+1
				sbc CLIP.Screen+S.RECT.Y2+1
				sta CLIP.TmpW+1
				bvc .7

				eor #$80

.7				bmi .8

				ldy #S.CB.SrcH
				jsr PTR.SubTmpW2CBPtrY
				bcc .9

.8				clc
				rts

.9				sec
				rts
*--------------------------------------
PTR.AddTmpW2CBPtrY
				clc

				lda CB.Ptr,y
				adc CLIP.TmpW
				sta CB.Ptr,y

				lda CB.Ptr+1,y
				adc CLIP.TmpW+1
				sta CB.Ptr+1,y
				rts
*--------------------------------------
PTR.SubTmpW2CBPtrY
				sec

				lda CB.Ptr,y
				sbc CLIP.TmpW
				sta CB.Ptr,y

				lda CB.Ptr+1,y
				sbc CLIP.TmpW+1
				sta CB.Ptr+1,y
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.ptr
LOAD usr/src/lib/libgui.s
ASM
