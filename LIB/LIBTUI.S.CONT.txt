NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* CONT.New
* (pStack)+2 pParent
* (pStack)+0 F
*--------------------------------------
CONT.New		jsr OBJ.Create			A = Class
				bcs .9

				>GETSTKW 2
				jsr OBJ.SetParent

				>GETSTKW
				ldy #S.OBJ.F
				sta (ZPpObj),y

				ldy #S.CTX.W
				lda (ZPpParent),y
				ldy #S.OBJ.W
				sta (ZPpObj),y

				ldy #S.CTX.H
				lda (ZPpParent),y
				ldy #S.OBJ.H
				sta (ZPpObj),y

				jmp OBJ.SetX2Y2

.9				rts
*--------------------------------------
CONT.Insert		ldy #S.OBJ.Childs

.1				lda (ZPpParent),y
				beq .2

				iny
				iny
				cpy #S.OBJ
				bcc .1

				lda #E.OOH
*				sec
				rts

.2				lda ZPpObj
				sta (ZPpParent),y
				iny
				lda ZPpObj+1
				sta (ZPpParent),y

				ldy #S.OBJ.S
				lda (ZPpParent),y
				ora #S.OBJ.S.bHasChilds
				sta (ZPpParent),y

				ldy #S.OBJ.InnerX
				lda (ZPpParent),y

				ldy #S.OBJ.X1
				clc
				adc (ZPpObj),y
				sta (ZPpObj),y

				ldy #S.OBJ.InnerY
				lda (ZPpParent),y

				ldy #S.OBJ.Y1
				clc
				adc (ZPpObj),y
				sta (ZPpObj),y

				jmp OBJ.SetX2Y2
*--------------------------------------
* (pStack) = Event
*--------------------------------------
CONT.Run		lda (pStack)			Event

				cmp #C.TAB
				bne .9

				jsr OBJ.FindActive
				bcs .9

.1				iny						Y=index, ZPPtr1=active child
				iny

				cpy #S.OBJ
				bcc .2

				ldy #S.OBJ.Childs

.2				iny
				lda (ZPpObj),y
				sta ZPPtr3+1

				dey
				lda (ZPpObj),y
				sta ZPPtr3

				lda ZPPtr3+1
				beq .1

				cmp ZPPtr1+1			same as hActive ?
				bne .3

				lda ZPPtr3
				cmp ZPPtr1
				beq .8

.3				phy
				ldy #S.OBJ.S
				lda (ZPPtr3),y
				ply
				and #S.OBJ.S.bDisabled+S.OBJ.S.bHidden
				bne .1

				ldx ZPpObj
				phx
				ldx ZPpObj+1
				phx

				>LDYA ZPPtr1
				jsr OBJ.Deactivate

				>LDYA ZPPtr3
				jsr OBJ.Activate

				plx
				stx ZPpObj+1
				plx
				stx ZPpObj

.8				clc
				rts

.9				lda #0
				sec
				rts
*--------------------------------------
CONT.Draw		ldy #S.OBJ.InnerW
				lda (ZPpObj),y

				tax
				lda #C.SPACE

.1				jsr LB.COut
				dex
				bne .1

				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.cont
LOAD usr/src/lib/libtui.s
ASM
