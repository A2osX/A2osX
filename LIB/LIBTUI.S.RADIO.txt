NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* RADIO.New
* (pStack)+8 pParent
* (pStack)+6 X1,Y1
* (pStack)+4 HK,EV
* (pStack)+2 pList
* (pStack)+0 pVar
*--------------------------------------
RADIO.New		lda #S.OBJ.T.RADIO
				jsr OBJ.Create
				bcs .99

				>GETSTKW 8
				jsr OBJ.SetParent

				>GETSTKW 6
				jsr OBJ.SetX1Y1

				>GETSTKW 4
				ldy #S.RADIO.EV
				sta (ZPpObj),y
				txa
				ldy #S.RADIO.HK
				sta (ZPpObj),y

				>GETSTKW 2
				ldy #S.OBJ.pBuf
				sta (ZPpObj),y
				sta ZPPtr1
				txa
				iny
				sta (ZPpObj),y
				sta ZPPtr1+1

				jsr RADIO.SetWH

				>GETSTKW
				ldy #S.OBJ.pVar
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj
*				clc

.99				rts
*--------------------------------------
RADIO.SetWH		ldx #0

.1				jsr OBJ.NextLinePtr1
				bcs .5					empty line

.3				tya
				clc
				adc #5					room for " ( ) "
				ldy #S.OBJ.W
				cmp (ZPpObj),y
				bcc .4

				sta (ZPpObj),y

.4				inx

				lda (ZPPtr1)
				bne .1

.5				ldy #S.OBJ.H
				txa
				sta (ZPpObj),y

				rts
*--------------------------------------
RADIO.Run		lda (pStack)			Event
				ldy #S.RADIO.HK
				cmp (ZPpObj),y
				beq .8

				ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .9

				lda (pStack)			Event
				cmp #C.SPACE
				bne .2

				ldy #S.OBJ.pVar
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

				ldy #S.RADIO.Idx
				lda (ZPpObj),y
				sta (ZPPtr1)

				bra .7

.2				cmp #C.LF
				bne .5

				ldy #S.RADIO.Idx
				lda (ZPpObj),y
				inc
				ldy #S.OBJ.InnerH
				cmp (ZPpObj),y
				bne .3

				lda #0

.3				ldy #S.RADIO.Idx
				sta (ZPpObj),y

				bra .7

.5				cmp #C.VT
				bne .9

				ldy #S.RADIO.Idx
				lda (ZPpObj),y
				bne .6

				ldy #S.OBJ.InnerH
				lda (ZPpObj),y

.6				dec

				ldy #S.RADIO.Idx
				sta (ZPpObj),y

.7				jmp OBJ.Draw.Body

.8				clc
				rts

.9				lda #0
				sec
				rts
*--------------------------------------
RADIO.Draw		lda (pStack)			Relative Y
				tax

				jsr OBJ.GetLineXAtY
				bcs .8

				ldy #S.OBJ.pVar
				lda (ZPpObj),y
				sta ZPPtr2
				iny
				lda (ZPpObj),y
				sta ZPPtr2+1

				clc
				ldx #'('

				ldy #S.OBJ.S
				lda (ZPpObj),y
				bpl .10

				ldy #S.RADIO.Idx
				lda (ZPpObj),y
				eor (pStack)			Relative Y
				bne .10

				ldx #'['
				sec

.10				php
				txa
				jsr LB.COut

				lda (pStack)
				cmp (ZPPtr2)
				bne .11

				lda #C.SO
				jsr LB.COut

				lda #'`'
				jsr LB.COut

				lda #C.SI
				bra .13

.11				lda #C.SPACE
.13				jsr LB.COut

				ldx #')'

				plp
				bcc .12

				ldx #']'

.12				txa
				jsr LB.COut

				lda #C.SPACE
				jsr LB.COut

.1				ldy #S.OBJ.InnerW
				lda (ZPpObj),y
				tax

				dex
				dex
				dex
				dex

				jsr LB.Ptr1OutEscMaxX

.3				txa
				beq .8

				lda #C.SPACE

.7				jsr LB.COut
				dex
				bne .7

.8				clc
				rts
*--------------------------------------
RADIO.Activate
*--------------------------------------
RADIO.Deactivate
				jmp OBJ.Draw.Body
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.radio
LOAD usr/src/lib/libtui.s
ASM
