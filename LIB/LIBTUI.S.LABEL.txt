NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* LABEL.New hParent,X1,Y1,pLabel		5
*--------------------------------------
LABEL.New		jsr OBJ.Create			A = Class
				bcs .9

				ldy #S.OBJ.S
				lda #S.OBJ.S.bDisabled
				sta (ZPObjPtr),y

				ldy #S.LABEL.pLabel
				jsr OBJ.PullWordY

				ldy #S.OBJ.H
				lda #1
				sta (ZPObjPtr),y

				jsr OBJ.PullY1X1hParent

				jmp OBJ.Insert

.9				>POP 5
				rts
*--------------------------------------
LABEL.Draw		ldy #S.LABEL.pLabel
				lda (ZPObjPtr),y
				sta ZPPtr1
				iny
				lda (ZPObjPtr),y
				sta ZPPtr1+1

				ldy #0

.1				lda (ZPPtr1),y
				beq .8

				jsr LB.COut
				iny
				bne .1

.8				
*--------------------------------------
LABEL.Destroy
*--------------------------------------
LABEL.Run
*--------------------------------------
LABEL.Activate
*--------------------------------------
LABEL.Deactivate
				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.label
LOAD usr/src/lib/libtui.s
ASM
