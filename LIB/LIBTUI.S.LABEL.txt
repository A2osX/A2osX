NEW
  AUTO 3,1
				.LIST OFF
*--------------------------------------
* LABEL.New
* (pStack)+4 pParent
* (pStack)+2 Y1X1
* (pStack)+0 pLabel
*--------------------------------------
LABEL.New		lda #S.OBJ.T.LABEL
				jsr OBJ.Create
				bcs .99

				>GETSTKW 4
				jsr OBJ.SetParent

				>GETSTKW 2
				jsr OBJ.SetX1Y1

				>GETSTKW
				ldy #S.LABEL.pLabel
				sta (ZPpObj),y
				txa
				iny
				sta (ZPpObj),y

				ldy #S.OBJ.S
				lda #S.OBJ.S.bDisabled
				sta (ZPpObj),y

				ldy #S.OBJ.H
				lda #1
				sta (ZPpObj),y

				jsr CONT.Insert
				bcs .99

				>LDYA ZPpObj

*				clc

.99				rts
*--------------------------------------
LABEL.Draw		ldy #S.LABEL.pLabel
				lda (ZPpObj),y
				sta ZPPtr1
				iny
				lda (ZPpObj),y
				sta ZPPtr1+1

				ldy #0

.1				lda (ZPPtr1),y
				beq .8

				jsr LB.COut
				iny
				bne .1

.8
*--------------------------------------
LABEL.Run
*--------------------------------------
LABEL.Activate
*--------------------------------------
LABEL.Deactivate
				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libtui.s.label
LOAD usr/src/lib/libtui.s
ASM
