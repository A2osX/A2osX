NEW
  AUTO 3,1
*--------------------------------------
* pMENU *guiNewMenu(pWND, pMENUDEF, iX1, iY1)
*--------------------------------------
MENU.New		>GETSTKW 2
				>STAX DX

				>GETSTKW
				>STAX DY

				>GETSTKW 6
				>STAX ZPpWND

				>GETSTKW 4

MENU.NewAX		>STAX ZPPtr1			MENU definition
				>STAX ZPPtr2			for MENU.GetData

				ldx MENU.Stack.Cnt
				cpx #MENU.MAX*2
				beq .90

				jsr MENU.GetSize

				>LIBC Malloc
				bcs .99

				>STYA ZPpOBJ

				ldx MENU.Stack.Cnt
				sta MENU.Stack+1,x
				tya
				sta MENU.Stack,x

				lda #S.OBJ.T.MENU
				sta (ZPpOBJ)			S.OBJ.T

				lda #0

				ldy #S.MENU.Items-1

.1				sta (ZPpOBJ),y
				dey
				bne .1

				>LDAX ZPpWND
				jsr OBJ.SetParentAX

				jsr OBJ.SetDXDYX1Y1

				jsr MENU.GetData

				jsr MENU.SetMenuWH

				jsr MENU.SetMenuPos

				ldx MENU.Stack.Cnt
				>LDYA MENU.Stack,x

				inc	MENU.Stack.Cnt
				inc	MENU.Stack.Cnt

				clc
				rts

.90				lda #E.OOH
				sec
.99
MENU.New.RTS	rts
*--------------------------------------
* ZPPtr1 = defintion
*--------------------------------------
MENU.GetSize	lda #S.MENU.Items+1		Ending\0
				sta Counter
				stz Counter+1

.1				lda (ZPPtr1)
				beq .8

				lda #S.MITEM
				clc
				adc Counter
				sta Counter
				bcc .2

				inc Counter+1

.2				lda (ZPPtr1)
				cmp #S.MITEM.T.SEP
				bne .3

				inc ZPPtr1
				bne .1

				inc ZPPtr1+1
				bra .1
*--------------------------------------
.3				cmp #S.MITEM.T.ITEM
				bne .4

				lda #6					T+ID+KEMOD+KEY+pICON+"TEXT"
				bra .5

.4				lda #5					T+pMENU+pICON+"TEXT"

.5				jsr SkipAPtr1
				jsr SkipStrZPtr1		skip "TEXT\0"
				bra .1

.8				>LDYA Counter
				rts
*--------------------------------------
* ZPPtr2 = src defintion
* ZPpOBJ = DstMenu
* ZPPtr1 = DstMItem
*--------------------------------------
MENU.GetData	jsr MENU.GetMItems		in ZPPtr1

.1				lda (ZPPtr2)
				sta (ZPPtr1)
				beq .8

				cmp #S.MITEM.T.SEP
				bne .2

				inc ZPPtr2				skip 1 byte
				bne .5

				inc ZPPtr2+1
				bra .5
*--------------------------------------
.2				cmp #S.MITEM.T.ITEM
				bne .3

				ldy #1					 T+ID+KEMOD+KEY+pICON+"text"
				lda (ZPPtr2),y
				ldy #S.MITEM.ID
				sta (ZPPtr1),y

				ldy #2
				lda (ZPPtr2),y
				tax
				iny
				lda (ZPPtr2),y

				ldy #S.MITEM.KEYMOD+1
				sta (ZPPtr1),y
				dey						KEY
				txa
				sta (ZPPtr1),y

				lda #4
				bra .4					skip 4 bytes
*--------------------------------------
.3				ldy #1					skip T+pMENU+pICON+"text"
				lda (ZPPtr2),y
				tax
				iny
				lda (ZPPtr2),y

				ldy #S.MITEM.pMENU+1
				sta (ZPPtr1),y
				dey
				txa
				sta (ZPPtr1),y

				lda #3					skip 3 bytes

.4				jsr SkipAPtr2

				ldy #1
				lda (ZPPtr2),y
				ldy #S.MITEM.pICON+1
				sta (ZPPtr1),y
				dey
				lda (ZPPtr2)
				sta (ZPPtr1),y

				lda #2					skip pICON
				jsr SkipAPtr2

				ldy #S.MITEM.pTEXT
				lda ZPPtr2
				sta (ZPPtr1),y
				iny
				lda ZPPtr2+1
				sta (ZPPtr1),y

				jsr SkipStrZPtr2		skip "TEXT\0"

				ldy #S.MITEM.S
				lda #0
				sta (ZPPtr1),y

.5				jsr MENU.NextMItem
				bra .1

.8				clc
				rts
*--------------------------------------
MENU.SetMenuWH	stz IY					Icon Margin W

				stz IE					Max Text W
				stz IE+1

				stz INE					Max Mod-Key W
				stz INE+1

				lda #2					2px Borders
				sta DY					Menu H
				stz DY+1

				jsr MENU.GetMItems

.1				lda (ZPPtr1)
				beq .3

				cmp #S.MITEM.T.SEP
				beq .2

				jsr MENU.GetMItemW

.2				jsr MENU.NextMItemDY
				bra .1
*--------------------------------------
.3				lda	#8+8				1px Border+7 (Checked) +7 (SubMenu)+1px Border
				clc

				adc IY					Icon W
				sta DX
				stz DX+1

				lda DX
				clc
				adc IE					Max Text W
				sta DX

				lda DX+1
				adc IE+1
				sta DX+1

				lda DX
				clc
				adc INE					Max Mod-Key W
				sta DX

				lda DX+1
				adc INE+1
				sta DX+1

				lda DX
				and #3
				beq .8

				eor #3
				inc
				pha
				clc
				adc DX
				sta DX
				bcc .4

				inc DX+1

.4				pla

				clc
				adc IE
				sta IE
				bcc .8

				inc IE+1

.8				jsr OBJ.SetDXDYWH

				ldy #S.MENU.iW
				lda IY
				sta (ZPpOBJ),y

				ldy #S.MENU.tW
				lda IE
				sta (ZPpOBJ),y
				iny
				lda IE+1
				sta (ZPpOBJ),y

				ldy	#S.MENU.kW
				lda INE
				sta (ZPpOBJ),y
				iny
				lda INE+1
				sta (ZPpOBJ),y

				rts
*--------------------------------------
MENU.SetMenuPos	ldy #S.OBJ.Y1
				lda (ZPpOBJ),y
				clc
				adc DY
				tax

				iny
				lda (ZPpOBJ),y
				adc DY+1

				pha
				txa

				ldy #S.WND.InnerH
				cmp (ZPpWND),y
				iny
				pla
				sbc (ZPpWND),y
				bcc .8

				ldy #S.OBJ.Y1

				lda (ZPpOBJ),y
*				sec
				sbc DY
				sta (ZPpOBJ),y

				iny

				lda (ZPpOBJ),y
				sbc DY+1
				sta (ZPpOBJ),y

.8				jmp OBJ.SetX2Y2
*--------------------------------------
MENU.GetMItemW	ldy #S.MITEM.pICON
				lda (ZPPtr1),y
				iny
				ora (ZPPtr1),y
				beq .1

				lda #20					Reserve space for Icon W + 4px SEP
				sta IY

.1				jsr MENU.GetpText
				ldx #FON.ID.SYSB
				jsr FON.GetTextWYAX

				ldx CB.Cache+S.CB.SrcW
				lda CB.Cache+S.CB.SrcW+1

				cpx IE
				pha
				sbc IE+1
				pla
				bcc .2

				stx IE					Set Max text W
				sta IE+1

.2				jsr MENU.GetKeyString
				beq .8

				ldx #FON.ID.SYSB
				jsr FON.GetTextWYAX

				ldx CB.Cache+S.CB.SrcW
				lda CB.Cache+S.CB.SrcW+1

				cpx INE

				pha
				sbc INE+1
				pla
				bcc .8

				stx INE					set Max Mod-key W
				sta INE+1

.8				rts
*--------------------------------------
MENU.FindMItemByID
				jsr MENU.GetMItems

.1				lda (ZPPtr1)
				beq .9

				cmp #S.MITEM.T.SEP
				beq .7

				txa
				ldy #S.MITEM.ID
				cmp (ZPPtr1),y
				beq .8

.7				jsr MENU.NextMItem
				bra .1

.8				clc
				rts

.9				sec
				rts
*--------------------------------------
MENU.CheckOver	ldy MENU.Stack.Cnt
				beq .9

.2				dey
				dey
				sty MENU.Stack.Idx
				>LDAX MENU.Stack,y

				jsr OBJ.ToGlobalAX
				jsr MSG.InClipRect
				bcs .3

				lda #PTR.T.ARROW
				jsr PTR.SetA

				jsr MSG.ToLocal

				ldy MENU.Stack.Idx
				>LDAX MENU.Stack,y
				jmp OBJ.EnterAX

.3				ldy MENU.Stack.Idx
				bne .2

				lda	MSG+S.MSG.S
				bit #S.XY.S.CLK
				beq .9

				jmp MENU.DestroyAll

.9				sec
				rts
*--------------------------------------
MENU.fEnter		jsr MENU.ResetDXDY		set to (1,1)

				stz D
				stz D+1

				ldy #S.OBJ.X2
				lda DX					X1
				clc
				adc (ZPpOBJ),y
				sta IE					IE = X2

				iny
				lda DX+1
				adc (ZPpOBJ),y
				sta IE+1

				jsr MENU.GetMItems

.1				lda (ZPPtr1)
				beq .4

				cmp #S.MITEM.T.SEP
				beq .2

				lda DY					Y1
				clc
				adc SYSFON.Hp1
				sta INE					INE = Y2
				lda DY+1
				adc #0
				sta INE+1

				>LDYAI DX				X1,X2,Y1,Y2
				jsr MOU.InRectYA
				bcc .3

				ldy #S.MITEM.S
				lda (ZPPtr1),y
				bpl .2

				and #S.MITEM.S.SELECTED^$ff
				sta (ZPPtr1),y			was selected before...

				jsr MENU.DrawMItem
				bcs .9








.2				jsr MENU.NextMItemDY
				bra .1
*--------------------------------------
.3				ldy #S.MITEM.S
				lda (ZPPtr1),y
				bmi .5					already selected, nothing to do

				ora #S.MITEM.S.SELECTED
				sta (ZPPtr1),y

				jsr MENU.DrawMItem
				bcs .9

				lda (ZPPtr1)
				cmp #S.MITEM.T.SUBMENU
				bne .32

				ldy #S.MITEM.pMENU
				lda (ZPPtr1),y
				sta D
				iny
				lda (ZPPtr1),y
				sta D+1

				lda DY
				sec
				sbc #1
				sta Counter
				lda DY+1
				sbc #0
				sta Counter+1
				bra .2
*--------------------------------------
.32				ldy #S.MITEM.ID
				lda (ZPPtr1),y
				sta MSG+S.MSG.ID
				bra .2
*--------------------------------------
.8				clc
.9				rts
*--------------------------------------
.4				jmp MENU.fEnterNewM
*--------------------------------------
.5				lda	MSG+S.MSG.S
				and #S.XY.S.CLK
				clc
				beq .80

				lda #S.MSG.T.ID
				sta MSG+S.MSG.T

				jmp MENU.DestroyAll

.80				stz MSG+S.MSG.T			#S.MSG.Y.IDLE

				rts
*--------------------------------------
MENU.fEnterNewM	lda D
				ora D+1
				beq .8

.1				ldx MENU.Stack.Idx
				inx
				inx
				cpx MENU.Stack.Cnt
				beq .2

				jsr MENU.Destroy
				bra .1

.2				ldy #S.OBJ.X2
				lda (ZPpOBJ),y
				clc
				adc #1
				sta DX
				iny
				lda (ZPpOBJ),y
				adc #0
				sta DX+1

				ldy #S.OBJ.Y1
				lda (ZPpOBJ),y
				clc
				adc Counter
				sta DY
				iny
				lda (ZPpOBJ),y
				adc Counter+1
				sta DY+1

				jsr OBJ.GetpWND

				>LDAX D
				jsr MENU.NewAX
				bcs .9

				jsr MENU.fPaint
				bcs .9

				stz MSG+S.MSG.T			#S.MSG.Y.IDLE

.8				clc

.9				rts
*--------------------------------------
MENU.fLeave		clc
				rts
*--------------------------------------
* ShowMenu(pMENU)
*--------------------------------------
MENU.Show		>GETSTKW
				>STAX ZPpOBJ

MENU.fPaint		lda #S.CB.CMD.GETRECTBUFSIZE
				sta CB.Cache+S.CB.CMD

				jsr CB.ZeroX1Y1

				jsr CB.SetX2Y2

				jsr CB.Write0
				bcs .9

				>LIBC Malloc
				bcs .9

				>STYA CB.Cache+S.CB.pDst
				phy
				ldy #S.MENU.pSAVEBUF+1
				sta (ZPpOBJ),y
				dey
				pla
				sta (ZPpOBJ),y

				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.SAVE
				sta CB.Cache+S.CB.OP

				jsr CB.ZeroSrcXSrcY

				jsr CB.GetObjSrcWH

				jsr CB.WriteInnerW
				bcs .9

				jsr DRAW.BordersIW
				bcs .9

				jsr MENU.ResetDXDY

				jsr MENU.GetMItems

.1				lda (ZPPtr1)
				beq .8

				jsr MENU.DrawMItem
				bcs .9

				jsr MENU.NextMItemDY
				bra .1

.8				clc

.9				rts
*--------------------------------------
MENU.DrawMItem	jsr MENU.ResetX1Y1X2

				lda (ZPPtr1)
				cmp #S.MITEM.T.SEP
				bne .1

				jsr CB.BorderLineH
				jmp CB.WriteInnerW
*--------------------------------------
.1				jsr MENU.IsSelected

				lda PREFS.MenuColor
				bcc .2

				eor #$ff

.2				jsr CB.FillRectMonoA

				lda CB.Cache+S.CB.Y1
				sec
				adc SYSFON.H
				sta CB.Cache+S.CB.Y2

				lda CB.Cache+S.CB.Y1+1
				adc #0
				sta CB.Cache+S.CB.Y2+1
				jsr CB.WriteInnerW
*--------------------------------------
				>INCW CB.Cache+S.CB.Y1

*	TODO : Checked

				lda #8
				jsr CB.AddA2X1

				jsr MENU.GetpIcon
				beq .3

				jsr BM.YA2CBCache
				jsr CB.WriteInnerW
*--------------------------------------
.3				ldy #S.MENU.iW
				lda (ZPpOBJ),y
				jsr CB.AddA2X1

				jsr MENU.GetpText
				ldx #FON.ID.SYSB
				jsr DRAW.TextYAXIW

				ldy #S.MENU.tW+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y
				jsr CB.AddAX2X1
*--------------------------------------
				lda (ZPPtr1)
				cmp #S.MITEM.T.ITEM
				bne .4

				jsr MENU.GetKeyString	C untouched
				beq .8

				ldx #FON.ID.SYSB
				jmp DRAW.TextYAXIW
*--------------------------------------
.4				cmp #S.MITEM.T.SUBMENU
				bne .8

				ldy #S.MENU.kW+1
				lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y

				jsr CB.AddAX2X1

				>LDYA L.BM.Right
				jsr BM.YA2CBCache

				stz CB.Cache+S.CB.COLOR

				jmp CB.WriteInnerW

.8				clc
				rts
*--------------------------------------
MENU.GetpIcon	ldy #S.MITEM.pICON+1
				lda (ZPPtr1),y
				bne .1

				dey
				lda (ZPPtr1),y
				beq .8

				tax
				ldy L.BMs,x
				lda L.BMs+1,x			NON ZERO

				rts

.1				pha
				dey
				lda (ZPPtr1),y
				tay
				pla						NON ZERO

.8				rts
*--------------------------------------
MENU.IsSelected	ldy #S.MITEM.S
				lda (ZPPtr1),y
				asl
				rts
*--------------------------------------
MENU.GetpText	ldy #S.MITEM.pTEXT

				lda (ZPPtr1),y
				pha
				iny
				lda (ZPPtr1),y
				ply
				rts
*--------------------------------------
* ZPPtr1 = pMITEM
*--------------------------------------
MENU.GetKeyString
				ldy #S.MITEM.KEY
				lda (ZPPtr1),y
				dey 					S.MITEM.KEYMOD
				ora (ZPPtr1),y
				beq .9

				ldx #0

				lda (ZPPtr1),y
				beq .3

				bit #KEYMOD.CTRL
				beq .1

				ldy #KEYMOD.S.CTRL
				jsr .4

.1				bit #KEYMOD.OA
				beq .2

				ldy #KEYMOD.S.OA
				jsr .4

.2				bit #KEYMOD.CA
				beq .3

				ldy #KEYMOD.S.CA
				jsr .4

.3				ldy #S.MITEM.KEY
				lda (ZPPtr1),y
				sta TEXTBUF,x
				stz TEXTBUF+1,x

				>LDYA L.TEXTBUF			NZ

				rts
*--------------------------------------
.4				pha

.5				lda KEYMOD.S,y
				sta TEXTBUF,x
				beq .6

				inx
				iny
				bra .5

.6				pla

.9				rts
*--------------------------------------
MENU.ResetDXDY	lda #1
				sta DX
				sta DY
				stz DX+1
				stz DY+1

				rts
*--------------------------------------
MENU.ResetX1Y1X2
				lda #1
				sta CB.Cache+S.CB.X1
				stz CB.Cache+S.CB.X1+1

				lda DY
				sta CB.Cache+S.CB.Y1
				lda DY+1
				sta CB.Cache+S.CB.Y1+1

				ldy #S.OBJ.W
				lda (ZPpOBJ),y
				sec
				sbc #2
				sta CB.Cache+S.CB.X2
				iny
				lda (ZPpOBJ),y
				sbc #0
				sta CB.Cache+S.CB.X2+1

				rts
*--------------------------------------
MENU.GetMItems	lda ZPpOBJ
				clc
				adc #S.MENU.Items
				sta ZPPtr1

				lda ZPpOBJ+1
				adc /S.MENU.Items
				sta ZPPtr1+1

				rts
*--------------------------------------
MENU.NextMItemDY
				lda (ZPPtr1)
				sec
				eor #S.MITEM.T.SEP
				beq .1

				lda SYSFON.Hp2
				clc

.1				adc DY
				sta DY
				bcc MENU.NextMItem

				inc DY+1

MENU.NextMItem	lda #S.MITEM
				jmp SkipAPtr1
*--------------------------------------
MENU.Destroy	clc
*			>DEBUG
				.HS B0					BCS
MENU.DestroyAll	sec
				ldx MENU.Stack.Cnt
				beq .8

				lda #S.CB.CMD.BITBLT
				sta CB.Cache+S.CB.CMD

				lda #S.CB.OP.RESTORE
				sta CB.Cache+S.CB.OP

				lda ZPpOBJ+1
				pha
				lda ZPpOBJ
				pha

.2				php

				ldx MENU.Stack.Cnt
				>LDYA MENU.Stack-2,x
				>STYA ZPpOBJ

				jsr CB.ZeroX1Y1

				jsr CB.ZeroSrcXSrcY

				jsr CB.GetObjSrcWH

				ldy #S.MENU.pSAVEBUF+1
				lda (ZPpOBJ),y
				beq .3

				sta CB.Cache+S.CB.pDst+1
				dey
				lda (ZPpOBJ),y
				sta CB.Cache+S.CB.pDst

				jsr CB.WriteInnerW

				ldy #S.MENU.pSAVEBUF
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				ply

				>LibC Free

.3				ldx MENU.Stack.Cnt
				>LDYA MENU.Stack-2,x
				>LibC Free

				plp						Destroy all ?

				dec MENU.Stack.Cnt
				dec MENU.Stack.Cnt

				bcc .7					no

				bne .2

.7				pla
				sta ZPpOBJ
				pla
				sta ZPpOBJ+1

.8				clc
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.menu
LOAD usr/src/lib/libgui.s
ASM
