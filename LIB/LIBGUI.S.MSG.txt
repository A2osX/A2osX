NEW
  AUTO 3,1
*--------------------------------------
MSG.Get			stz MSG+S.MSG.T

				lda A2osX.ASCREEN		GUI screen active ?
				cmp DCB.GFX+S.DCB.GFX.DEVID
				beq .10
				
				jmp .9					no, exit....

.10				lda hDevMouse
				beq .5

				>PUSHA
				>PUSHBI IOCTL.READ
				>PUSHW L.XY
				>SYSCALL2 IOCTL
				bcs .5

				ldx #S.XY-1
				ldy #S.MSG-1

.1				lda XY,x
				sta MSG,y
				dey
				dex
				bpl .1

				lda #S.MSG.T.XY
				sta MSG+S.MSG.T

				lda	XY+S.XY.S
				bit #S.MSG.S.MOVE
				beq .2

				>PUSHW L.XY
				jsr SetCursorPos

				lda	XY+S.XY.S

.2				bit #S.MSG.S.MOVE+S.MSG.S.CLK
				beq .5

				jsr MOU.CheckOverMenu
				bcc .8

				jsr MOU.CheckOverSysBut
				bcc .8

				jsr MOU.CheckOverWND
				bcc .8
*--------------------------------------
.5				lda KBD
				bpl .9
				
				sta KBDSTROBE
				and #$7F
				sta MSG+S.MSG.KEY
				
				lda #S.MSG.T.KEY
				sta MSG+S.MSG.T

.8				>PULLW ZPPtr1			pS.MSG

				ldy #S.MSG-1

.6				lda MSG,y
				sta (ZPPtr1),y
				dey
				bpl .6

				clc
				rts

.9				inc pStack				discard pS.MSG
				inc pStack

				sec						No User Msg
				rts	
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.msg
LOAD usr/src/lib/libgui.s
ASM
