NEW
  AUTO 3,1
*--------------------------------------
MSG.Get			stz MSG+S.MSG.T			#S.MSG.Y.IDLE

				lda A2osX.ASCREEN		GFX screen active ?
				cmp #9
				beq .1

				clc						no, exit....
				rts

.1				ldy hDevMouse
				beq .5

				>LDYA L.MSG.XY
				>STYA IOCTL+S.IOCTL.BUFPTR

				>SS
				>PUSHW hDevMouse
				>PUSHBI IOCTL.READ
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR
				bcs .5

				lda #S.MSG.T.XY
				sta MSG+S.MSG.T

				lda	MSG+S.MSG.S
				bit #S.XY.S.MOVE
				beq .2

				jsr PTR.Disable

				ldx #3

.10				lda MSG+S.MSG.X1,x
				sta PTR.Pos,x
				dex
				bpl .10

				jsr PTR.Enable

				lda	MSG+S.MSG.S

.2				bit #S.XY.S.MOVE+S.XY.S.CLK
				beq .5

*			bit #S.XY.S.CLK
*			beq .55
*			>DEBUG
*.55			

				jsr MENU.CheckOver
				bcc .8

				jsr WND.CheckOver
				bcc .8
*--------------------------------------
.5				lda IO.KBD
				bpl .8					exit with IDLE

				sta IO.KBDSTROBE
				and #$7F
				sta MSG+S.MSG.KEY

				lda #S.MSG.T.KEY
				sta MSG+S.MSG.T
*--------------------------------------
.8				>GETSTKW
				>STAX R1				pS.MSG

				ldy #S.MSG-1

.6				lda MSG,y
				sta (R1),y
				dey
				bpl .6

				clc
.9				rts
*--------------------------------------
MSG.InClipRect	ldy #0 					#S.RECT.X1

				lda MSG+S.MSG.X1
				cmp CLIP.Rect,y
				iny
				lda MSG+S.MSG.X1+1
				sbc CLIP.Rect,y
				bcc .9

				iny						#S.RECT.Y1

				lda MSG+S.MSG.Y1
				cmp CLIP.Rect,y
				iny
				lda MSG+S.MSG.Y1+1
				sbc CLIP.Rect,y
				bcc .9

				iny						#S.RECT.X2

				lda CLIP.Rect,y
				cmp MSG+S.MSG.X1
				iny
				lda CLIP.Rect,y
				sbc MSG+S.MSG.X1+1
				bcc .9

				iny						#S.RECT.Y2

				lda CLIP.Rect,y
				cmp MSG+S.MSG.Y1
				iny
				lda CLIP.Rect,y
				sbc MSG+S.MSG.Y1+1
				bcc .9

				clc
				rts

.9				sec
				rts
*--------------------------------------
MSG.ToLocal		lda MSG+S.MSG.X1
				sec
				sbc CLIP.Rect
				sta MSG+S.MSG.X1

				lda MSG+S.MSG.X1+1
				sbc CLIP.Rect+1
				sta MSG+S.MSG.X1+1

				lda MSG+S.MSG.Y1
				sec
				sbc CLIP.Rect+2
				sta MSG+S.MSG.Y1

				lda MSG+S.MSG.Y1+1
				sbc CLIP.Rect+3
				sta MSG+S.MSG.Y1+1

				rts
*--------------------------------------
MSG.InOBJ		ldy #S.OBJ.X1

				lda MSG+S.MSG.X1
				cmp (ZPpOBJ),y
				iny
				lda MSG+S.MSG.X1+1
				sbc (ZPpOBJ),y
				bcc .9

				iny						#S.OBJ.Y1

				lda MSG+S.MSG.Y1
				cmp (ZPpOBJ),y
				iny
				lda MSG+S.MSG.Y1+1
				sbc (ZPpOBJ),y
				bcc .9

				iny						#S.OBJ.X2

				lda (ZPpOBJ),y
				cmp MSG+S.MSG.X1
				iny
				lda (ZPpOBJ),y
				sbc MSG+S.MSG.X1+1
				bcc .9

				iny						#S.OBJ.Y2

				lda (ZPpOBJ),y
				cmp MSG+S.MSG.Y1
				iny
				lda (ZPpOBJ),y
				sbc MSG+S.MSG.Y1+1
				bcc .9

				clc
				rts

.9				sec
				rts
*--------------------------------------
MSG.ToLocalY	lda MSG+S.MSG.X1		#S.OBJ.X1, #S.WND.InnerX1

				sec
				sbc (ZPpOBJ),y
				sta MSG+S.MSG.X1

				iny

				lda MSG+S.MSG.X1+1
				sbc (ZPpOBJ),y
				sta MSG+S.MSG.X1+1

				iny						#S.OBJ.Y1, #S.WND.InnerY1

				lda MSG+S.MSG.Y1
				sec
				sbc (ZPpOBJ),y
				sta MSG+S.MSG.Y1

				iny

				lda MSG+S.MSG.Y1+1
				sbc (ZPpOBJ),y
				sta MSG+S.MSG.Y1+1

				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.msg
LOAD usr/src/lib/libgui.s
ASM
