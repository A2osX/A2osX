NEW
  AUTO 3,1
*--------------------------------------
* NewLabel(pWND, pTEXT, iX, iY)
*--------------------------------------
LABEL.New		>GETSTKW
				>STAX DY

				>GETSTKW 2
				>STAX DX

				>GETSTKW 6
				>STAX ZPpWND

				>GETSTKW 4

LABEL.NewAX		>STAX ZPPtr1			pTEXT

				lda #S.OBJ.T.LABEL
				jsr OBJ.New
				bcs .9

*				>STYA ZPpOBJ

				>LDAX ZPpWND
				jsr OBJ.SetParentAX

				jsr OBJ.SetDXDYX1Y1

				>LDAX hSYSFON
				>STAX CB.Cache+S.CB.hFont

				lda SYSFON.H
				sta CB.Cache+S.CB.SrcH
				stz CB.Cache+S.CB.SrcH+1

				ldy #S.LABEL.pTEXT+1
				lda ZPPtr1+1
				sta (ZPpOBJ),y
				tax
				dey
				lda ZPPtr1
				sta (ZPpOBJ),y

				jsr FON.GetTextSizeAX
				bcs .9

				ldx #3
				ldy #S.OBJ.W+3

.1				lda DX,x
				sta (ZPpOBJ),y
				dey
				dex
				bpl .1

				jsr OBJ.SetX2Y2

				jsr WND.AddNext.I

				>LDYA ZPpOBJ

*				clc

.9				rts
*--------------------------------------
LABEL.fEnter
LABEL.fLeave	clc
				rts
*--------------------------------------
LABEL.fPaint	jsr CB.ZeroX1Y1

*				lda #C.WHITE
*				jsr CB.FillRectMonoA
*				jsr CB.WriteP
*				bcs .9

				>LDAX hSYSFON
				>STAX CB.Cache+S.CB.hFont

				lda SYSFON.H
				sta CB.Cache+S.CB.SrcH
				stz CB.Cache+S.CB.SrcH+1

				lda #C.BLACK
				sta CB.Cache+S.CB.COLOR

				ldy #S.LABEL.pTEXT
				jsr OBJ.GetPropAtY

				jmp FON.DrawTextML

.9				rts
*--------------------------------------

*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.label
LOAD usr/src/lib/libgui.s
ASM
