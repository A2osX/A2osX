NEW
  AUTO 3,1
*--------------------------------------
* A = Object Class
*--------------------------------------
OBJ.New			pha
				tax
				ldy OBJ.SizeOf,x
				lda #0

				>LIBC Malloc
				bcs .9

				>STYA ZPpOBJ

				pla
				sta (ZPpOBJ)

				tay
				lda OBJ.SizeOf,y
				tay
				dey
				lda #0

.1				sta (ZPpOBJ),y
				dey
				bne .1

				ldy #S.PS.PID
				jsr A2osX.GetPSy
				ldy #S.OBJ.hOWNERPID
				sta (ZPpOBJ),y

				ldy #S.PS.PID+1
				jsr A2osX.GetPSy
				ldy #S.OBJ.hOWNERPID+1
				sta (ZPpOBJ),y

				>LDYA ZPpOBJ

*				clc

				rts

.9				plx

				rts
*--------------------------------------
OBJ.EnterAX		>STAX ZPpOBJ

				jsr .1
				bcs .9

				ldy #S.OBJ.fEnter
				bra OBJ.JmpfY

.1				lda (ZPpOBJ)
				tax
				>LDYA ZPpOBJ
				jmp (J.OBJ.fEnter,x)

.9				rts
*--------------------------------------
OBJ.LeaveAX		>STAX ZPpOBJ

				jsr .1
				bcs .9

				ldy #S.OBJ.fLeave
				bra OBJ.JmpfY

.1				lda (ZPpOBJ)
				tax
				>LDYA ZPpOBJ
				jmp (J.OBJ.fLeave,x)

.9				rts
*--------------------------------------
OBJ.Paint		>GETSTKW

OBJ.PaintAX		>STAX ZPpOBJ

OBJ.PaintpOBJ	jsr .1
				bcs .9

				ldy #S.OBJ.fPaint
				bra OBJ.JmpfY

.1				lda (ZPpOBJ)
				tax
				>LDYA ZPpOBJ
				jmp (J.OBJ.fPaint,x)

.9				rts
*--------------------------------------
OBJ.JmpfY		lda (ZPpOBJ),y
				sta R1
				iny
				lda (ZPpOBJ),y
				sta R1+1
				ora R1
				bne .8

				clc
				rts

.8				>LDYA ZPpOBJ
				jmp (R1)
*--------------------------------------
* pOBJ, Prop8, Value16
*--------------------------------------
OBJ.SetProp		>GETSTKW 3
				>STAX ZPpOBJ

				>GETSTKB 2				Prop8
				tay

				lda (ZPpOBJ),y
				pha						old value
				iny
				lda (ZPpOBJ),y
				pha

				inc pStack

				lda (pStack)
				sta (ZPpOBJ),y

				dec pStack

				dey
				lda (pStack)
				sta (ZPpOBJ),y

				pla
				ply

				rts
*--------------------------------------
* pOBJ, Prop8
*--------------------------------------
OBJ.GetProp		>GETSTKW 1
				>STAX ZPpOBJ

				>GETSTKB

OBJ.GetPropAtY	lda (ZPpOBJ),y
				pha
				iny
				ora (ZPpOBJ),y
				clc
				bne .1

				sec

.1				lda (ZPpOBJ),y
				ply

				rts
*--------------------------------------
OBJ.SetDXDYX1Y1	ldx #3
				ldy #S.OBJ.X1+3

.1				lda DX,x
				sta (ZPpOBJ),y
				dey
				dex
				bpl .1

				rts
*--------------------------------------
OBJ.SetDXDYWH	ldx #3
				ldy #S.OBJ.W+3

.1				lda DX,x
				sta (ZPpOBJ),y
				dey
				dex
				bpl .1

				rts
*--------------------------------------
* X2 = X1 + W - 1
* Y2 = Y1 + H - 1
*--------------------------------------
OBJ.SetX2Y2		ldy #S.OBJ.W+1
				jsr OBJ.GetAXAtYm1
				clc
				ldy #S.OBJ.X1
				adc (ZPpOBJ),y
				pha
				txa
				iny
				adc (ZPpOBJ),y
				ldy #S.OBJ.X2+1
				sta (ZPpOBJ),y
				dey
				pla
				sta (ZPpOBJ),y
*--------------------------------------
				ldy #S.OBJ.H+1
				jsr OBJ.GetAXAtYm1
				clc
				ldy #S.OBJ.Y1
				adc (ZPpOBJ),y
				pha
				txa
				iny
				adc (ZPpOBJ),y
				ldy #S.OBJ.Y2+1
				sta (ZPpOBJ),y
				dey
				pla
				sta (ZPpOBJ),y

				rts
*--------------------------------------
OBJ.ClrStatus	ldy #S.OBJ.S
				eor #$ff
				and (ZPpOBJ),y
				sta (ZPpOBJ),y
				rts
*--------------------------------------
OBJ.SetStatus	ldy #S.OBJ.S
				ora (ZPpOBJ),y
				sta (ZPpOBJ),y
				rts
*--------------------------------------
OBJ.SetParentAX ldy #S.OBJ.pPARENT
OBJ.SetAXAtY	sta (ZPpOBJ),y
				iny
				txa
				sta (ZPpOBJ),y
				rts
*--------------------------------------
OBJ.GetAXAtYm1	lda (ZPpOBJ),y
				tax
				dey
				lda (ZPpOBJ),y
				bne .1

				dex

.1				dec

				rts
*--------------------------------------
OBJ.GetpWND		ldy #S.OBJ.pPARENT
				lda (ZPpOBJ),y
				sta ZPpWND
				iny
				lda (ZPpOBJ),y
				sta ZPpWND+1
				rts
*--------------------------------------
OBJ.GetpNext	ldy #S.OBJ.pNext+1
				lda (ZPpOBJ),y
				beq .9

				tax
				dey
				lda (ZPpOBJ),y
				clc
				rts

.9				sec
				rts
*--------------------------------------
OBJ.ToGlobalAX	>STAX ZPpOBJ
				>STAX R1

				ldy #S.OBJ.X1+7
				ldx #7

.1				lda (ZPpOBJ),y
				sta CLIP.Rect,x
				dey
				dex
				bpl .1

.2				ldy #S.OBJ.pPARENT+1
				lda (R1),y
				beq .8

				tax
				dey
				lda (R1),y
				>STAX R1

				lda (R1)
				bne .3
				
				ldy #S.WND.InnerX1
				jsr .7

.3				ldy #S.OBJ.X1
				jsr .7

				bra .2

.7				phy

				clc
				lda (R1),y
				adc CLIP.Rect
				sta CLIP.Rect
				iny
				lda (R1),y
				adc CLIP.Rect+1
				sta CLIP.Rect+1

				iny
				
				clc
				lda (R1),y
				adc CLIP.Rect+2
				sta CLIP.Rect+2
				iny
				lda (R1),y
				adc CLIP.Rect+3
				sta CLIP.Rect+3

				ply
				
				clc
				lda (R1),y
				adc CLIP.Rect+4
				sta CLIP.Rect+4
				iny
				lda (R1),y
				adc CLIP.Rect+5
				sta CLIP.Rect+5

				iny
				
				clc
				lda (R1),y
				adc CLIP.Rect+6
				sta CLIP.Rect+6
				iny
				lda (R1),y
				adc CLIP.Rect+7
				sta CLIP.Rect+7

.8				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.obj
LOAD usr/src/lib/libgui.s
ASM
