NEW
  AUTO 3,1
*--------------------------------------
MOU.Init		>SS
				>PUSHW L.DEVNAME.MOUSE
				>PUSHBI 0
				>LIBC Open
				>SR
				bcc .1

				inc DEVNAME.MOUSE+8
				lda DEVNAME.MOUSE+8
				cmp #'8'
				bcc MOU.Init

				lda #MLI.E.NODEV

*				sec

				rts

.1				>STYA hDevMouse

*				clc

				rts
*--------------------------------------
* make sure mouse X,Y always in Screen
*--------------------------------------
MOU.Setup		>LDYA L.PTR.SaveBuf
				>STYA CB.Ptr+S.CB.pDst

				lda CLIP.Screen+S.RECT.X2+1
				sta MouseClamp+3
				lsr
				sta PTR.Pos+S.POINT.X+1

				lda CLIP.Screen+S.RECT.X2
				sta MouseClamp+2
				ror
				sta PTR.Pos+S.POINT.X

				lda CLIP.Screen+S.RECT.Y2+1
				sta MouseClamp+7
				lsr
				sta PTR.Pos+S.POINT.Y+1

				lda CLIP.Screen+S.RECT.Y2
				sta MouseClamp+6
				ror
				sta PTR.Pos+S.POINT.Y

				>LDYA L.MouseClamp
				>STYA IOCTL+S.IOCTL.BUFPTR
				lda #8
				sta IOCTL+S.IOCTL.BYTECNT
				stz IOCTL+S.IOCTL.BYTECNT+1

				>SS
				>PUSHW hDevMouse
				>PUSHBI IOCTL.WRITE
				>PUSHW L.IOCTL
				>LIBC IOCTL
				>SR

.9				rts
*--------------------------------------
MOU.Quit		ldy hDevMouse
				beq .8

				lda hDevMouse

				>LIBC Close

.8				clc
				rts
*--------------------------------------
MOU.ResizeWND

				clc
				rts
*--------------------------------------
MOU.MoveWND		lda	MSG+S.MSG.S
				bit #S.XY.S.DRAGSTART
				beq .8

				lda ZPpOBJ
				clc
				adc #S.OBJ.X1
				tay
				lda ZPpOBJ+1
				adc /S.OBJ.X1

				jmp PTR.SetRectYA

.8				lda #PTR.T.MOVE
				jmp PTR.SetA
*--------------------------------------
MOU.InRectYA	>STYA R1

				ldy #0 					#S.RECT.X1

				lda MSG+S.MSG.X1
				cmp (R1),y
				iny
				lda MSG+S.MSG.X1+1
				sbc (R1),y
				bcc .9

				iny						#S.RECT.Y1

				lda MSG+S.MSG.Y1
				cmp (R1),y
				iny
				lda MSG+S.MSG.Y1+1
				sbc (R1),y
				bcc .9

				iny						#S.RECT.X2

				lda (R1),y
				cmp MSG+S.MSG.X1
				iny
				lda (R1),y
				sbc MSG+S.MSG.X1+1
				bcc .9

				iny						#S.RECT.Y2

				lda (R1),y
				cmp MSG+S.MSG.Y1
				iny
				lda (R1),y
				sbc MSG+S.MSG.Y1+1
				bcc .9

				clc
				rts

.9				sec
				rts
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.mou
LOAD usr/src/lib/libgui.s
ASM
