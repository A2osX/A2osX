NEW
  AUTO 3,1
*--------------------------------------
* NewBitmap (pWND, pBM, iX, iY)
*--------------------------------------
BITMAP.New		>GETSTKW
				>STAX DY

				>GETSTKW 2
				>STAX DX

				>GETSTKW 6
				>STAX ZPpWND

				>GETSTKW 4

BITMAP.NewAX	>STAX ZPpBM

				lda #S.OBJ.T.BITMAP
				jsr OBJ.New
				bcs .9					ZPpOBJ

*				>STYA ZPpOBJ

				>LDAX ZPpWND
				jsr OBJ.SetParentAX

				jsr OBJ.SetDXDYX1Y1

				ldy #S.BITMAP.pBM
				lda ZPpBM
				sta (ZPpOBJ),y
				iny
				lda ZPpBM+1
				sta (ZPpOBJ),y

				jsr WND.AddNext.I

				>LDYA ZPpOBJ

*				clc

.9				rts
*--------------------------------------
BITMAP.fEnter
BITMAP.fLeave
				clc
				rts
*--------------------------------------
* ZPpWND
* ZPpOBJ
*--------------------------------------
BITMAP.fPaint	jsr CB.ZeroX1Y1

				ldy #S.BITMAP.pBM
				lda (ZPpOBJ),y
				pha
				iny
				lda (ZPpOBJ),y
				beq .1

				ply
				bra .2

.1				plx

				>LDYA L.BMs,x

.2				jsr BM.YA2CBCache

				jmp CB.WriteP
*--------------------------------------
MAN
SAVE usr/src/lib/libgui.s.bitmap
LOAD usr/src/lib/libgui.s
ASM
